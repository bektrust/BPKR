
#Область ПрограммныйИнтерфейс

// Рассчитывает сумму НДС (Поступления) исходя из суммы и флагов налогообложения
//
// Параметры: 
//  Всего            	- число, сумма включая налоги от которой надо рассчитывать налоги, 
//  СуммаВключаетНалоги - булево, признак включения НДС и НСП в сумму ("внутри" или "сверху"),
//  СтавкаНДС        	- число, процентная ставка НДС,
//  СтавкаНДС        	- число, процентная ставка НСП,
//
// Возвращаемое значение:
//  Число, полученная сумма НДС
//
Функция РассчитатьСуммуНДСПоступления(Всего, СуммаВключаетНалоги, СтавкаНДС, СтавкаНСП) Экспорт
	
	СуммаНДС = ?(СуммаВключаетНалоги,
		Всего * СтавкаНДС / 100,
		Всего * СтавкаНДС / (100 + СтавкаНДС + СтавкаНСП));

	Возврат СуммаНДС;

КонецФункции // РассчитатьСуммуНДС()

// Рассчитывает сумму НДС исходя из суммы и флагов налогообложения
//
// Параметры: 
//  Сумма            - число, сумма от которой надо рассчитывать налоги, 
//  СуммаВключаетНалоги - булево, признак включения НДС и НСП в сумму ("внутри" или "сверху"),
//  СтавкаНДС        - число , процентная ставка НДС,
//  СтавкаНДС        - число , процентная ставка НСП,
//
// Возвращаемое значение:
//  Число, полученная сумма НДС
//
Функция РассчитатьСуммуНДС(Сумма, СуммаВключаетНалоги, СтавкаНДС, СтавкаНСП) Экспорт
	
	СуммаНДС = ?(СуммаВключаетНалоги, 
		Сумма - Сумма / ((СтавкаНДС + СтавкаНСП + 100) / (100 + СтавкаНСП)),
		Сумма * СтавкаНДС / 100);

	Возврат СуммаНДС;

КонецФункции // РассчитатьСуммуНДС()

// Рассчитывает сумму НСП исходя из суммы и флагов налогообложения
//
// Параметры: 
//  Сумма            - число, сумма от которой надо рассчитывать налоги, 
//  СуммаВключаетНалоги - булево, признак включения НДС и НСП в сумму ("внутри" или "сверху"),
//  СтавкаНДС        - число , процентная ставка НДС,
//  СтавкаНДС        - число , процентная ставка НСП,
//
// Возвращаемое значение:
//  Число, полученная сумма НСП
//
Функция РассчитатьСуммуНСП(Сумма, СуммаВключаетНалоги, СтавкаНДС, СтавкаНСП) Экспорт
	
	СуммаНСП = ?(СуммаВключаетНалоги, 
		Сумма - Сумма / ((СтавкаНДС + СтавкаНСП + 100) / (100 + СтавкаНДС)),
		Сумма * СтавкаНСП / 100);

	Возврат СуммаНСП;

КонецФункции // РассчитатьСуммуНСП()

// Производит пересчет цен при изменении флагов учета налогов.
// Пересчет зависит от способа заполнения цен, при заполнении По ценам номенклатуры (при продаже) 
// хочется избегать ситуаций, когда компания  «теряет деньги» при пересчете налогов. 
// Поэтому если в документе флаг "Сумма включает налоги" выключен, то цены должны браться напрямую из справочника, 
// потому что хочется продавать по той же цене, независимо от режима налогообложения. 
// Например, если отпускная цена задана с налогами для избежания ошибок округления, то это не значит, 
// что при отпуске без налогов мы должны продать дешевле. Если же флаг учета налога в документе включен, 
// то цены должны пересчитываться при подстановке в документ: 
// налог должен включаться или не включаться в зависимости от флага включения налога в типе цен.
// При заполнении по ценам контрагентов (при покупке) хочется хранить цены поставщиков. 
// Поэтому нужно пересчитывать всегда по установленным флагам в документе и в типе цен. 
// Это гарантирует, что при записи цен в регистр и последующем их чтении, 
// например, при заполнении следующего документа, мы с точностью до ошибок округления при пересчете 
// получим те же самые цены.
// Параметры: 
//  Цена                - число, пересчитываемое значение цены, 
//  ЦенаВключаетНалоги  - булево, определяет содержит ли переданное значение цены НДС и НСП,
//  СуммаВключаетНалоги - булево, определяет должно ли новое значение цены включать НДС и НСП,
//  СтавкаНДС           - число, ставка НДС, 
//  СтавкаНСП           - число, ставка НСП, 
//
// Возвращаемое значение:
//  Число, новое значение цены.
//
Функция ПересчитатьЦенуПриИзмененииФлаговНалогов(Цена, ЦенаВключаетНалоги, СуммаВключаетНалоги, СтавкаНДС, СтавкаНСП) Экспорт

	// Инициализация переменных
	НадоВключитьНалоги  = Ложь;
	НадоИсключитьНалоги = Ложь;
	НоваяЦена		 = Цена;

	Если СуммаВключаетНалоги
		И (НЕ ЦенаВключаетНалоги) Тогда
		
		// Надо добавлять НДС       
		НадоВключитьНалоги = Истина;
	ИначеЕсли (НЕ СуммаВключаетНалоги)
		И ЦенаВключаетНалоги  Тогда
		
		// Надо исключать НДС       
		НадоИсключитьНалоги = Истина;
	КонецЕсли;
		
	Если НадоИсключитьНалоги Тогда
		НоваяЦена = (НоваяЦена * 100) / (100 + СтавкаНДС + СтавкаНСП);
	КонецЕсли;

	Если НадоВключитьНалоги Тогда
		НоваяЦена = (НоваяЦена * (100 + СтавкаНДС + СтавкаНСП)) / 100;
	КонецЕсли;

	Возврат НоваяЦена;

КонецФункции // ПересчитатьЦенуПриИзмененииФлаговНалогов()

#КонецОбласти




