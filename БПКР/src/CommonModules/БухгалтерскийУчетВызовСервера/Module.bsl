
#Область ПрограммныйИнтерфейс

#Область СчетаФактуры

///////////////////////////////////////////////////////////////////////////////// 
// ПРОЦЕДУРЫ И ФУНКЦИИ ПОЛУЧЕНИЯ СЕРИИ И НОМЕРА СЧЕТОВ-ФАКТУР

// Функция - Сформировать список серий СФ
//
// Параметры:
//  Организация	 - СправочникСсылка.Организации	 - Организация, из которой подбираются серии бланков СФ
//  ДобавитьДПБУ - Булево						 - Признак добавления к результату "ДПБУ" (по умолчанию: Ложь)
// 
// Возвращаемое значение:
//   - массив - серий бланков СФ
//
Функция СформироватьСписокСерийСФ(Организация, ДобавитьДПБУ = Ложь) Экспорт
	// В данном запросе мы не используем отбор по периоду, т.к.
	// если проводить документ задним числом, то при отборе по периоду
	// запрос может вернуть серию бланка, ВСЕ номера которой уже 
	// использованы другими документами (числами позже).
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	БланкиСчетовФактурОстатки.СерияБланкаСФ КАК СерияБланкаСФ
		|ИЗ
		|	РегистрНакопления.БланкиСчетовФактур.Остатки(
		|			,
		|			Организация = &Организация) КАК БланкиСчетовФактурОстатки
		|ГДЕ
		|	БланкиСчетовФактурОстатки.КоличествоОстаток > 0";
	
	Если ДобавитьДПБУ Тогда
		Запрос.Текст = Запрос.Текст +
		"
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	""ДПБУ"""
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Организация", Организация);
		
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("СерияБланкаСФ");
КонецФункции

// Функция - Сформировать список номеров бланков СФ
//
// Параметры:
//  Организация		 - СправочникСсылка.Организации	 - Организация, из которой подбираются серии бланков СФ.
//  СерияБланкаСФ	 - Строка						 - Наименование серии, из которой подбираются номера бланков СФ.
//  НомерБланкаСФ	 - Строка						 - Номер бланка, начиная с которого подбираются бланки (по умолчанию "0").
// 
// Возвращаемое значение:
//   - СписокЗначений - номеров бланков СФ
//
Функция СформироватьСписокНомеровБланковСФ(Организация, СерияБланкаСФ, НомерБланкаСФ = "0") Экспорт
	
	СписокНомеров = Новый СписокЗначений;

	// В данном запросе мы не используем отбор по периоду, т.к.
	// если проводить документ задним числом, то при отборе по периоду
	// запрос может вернуть номер бланка, который уже использован
	// в другом документе (числом позже).
	// Так как длина номеров в рамках одной серри может быть разная
	// добавим лидирующие нули до длины 6 для получения самых коротких в начале списка.
	Запрос = Новый Запрос;
	Запрос.Текст =	
		"ВЫБРАТЬ ПЕРВЫЕ 10
		|	БланкиСчетовФактурОстатки.НомерБланкаСФ КАК НомерБланкаСФ
		|ИЗ
		|	РегистрНакопления.БланкиСчетовФактур.Остатки(
		|			,
		|			Организация = &Организация
		|				И СерияБланкаСФ = &СерияБланкаСФ
		|				И НомерБланкаСФ >= &НомерБланкаСФ) КАК БланкиСчетовФактурОстатки
		|ГДЕ
		|	БланкиСчетовФактурОстатки.КоличествоОстаток > 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВЫБОР
		|		КОГДА БланкиСчетовФактурОстатки.НомерБланкаСФ ПОДОБНО ""______""
		|			ТОГДА БланкиСчетовФактурОстатки.НомерБланкаСФ
		|		КОГДА БланкиСчетовФактурОстатки.НомерБланкаСФ ПОДОБНО ""_____""
		|			ТОГДА ""0"" + БланкиСчетовФактурОстатки.НомерБланкаСФ
		|		КОГДА БланкиСчетовФактурОстатки.НомерБланкаСФ ПОДОБНО ""____""
		|			ТОГДА ""00"" + БланкиСчетовФактурОстатки.НомерБланкаСФ
		|		КОГДА БланкиСчетовФактурОстатки.НомерБланкаСФ ПОДОБНО ""___""
		|			ТОГДА ""000"" + БланкиСчетовФактурОстатки.НомерБланкаСФ
		|		КОГДА БланкиСчетовФактурОстатки.НомерБланкаСФ ПОДОБНО ""__""
		|			ТОГДА ""0000"" + БланкиСчетовФактурОстатки.НомерБланкаСФ
		|		КОГДА БланкиСчетовФактурОстатки.НомерБланкаСФ ПОДОБНО ""_""
		|			ТОГДА ""00000"" + БланкиСчетовФактурОстатки.НомерБланкаСФ
		|	КОНЕЦ";
	Запрос.УстановитьПараметр("СерияБланкаСФ", СерияБланкаСФ);
	Запрос.УстановитьПараметр("НомерБланкаСФ", НомерБланкаСФ);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда 
		Возврат СписокНомеров;
	КонецЕсли;	
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СписокНомеров.Добавить(Выборка.НомерБланкаСФ, Выборка.НомерБланкаСФ);		
	КонецЦикла; 
	
	Если СписокНомеров.Количество() = 10 Тогда 
		СписокНомеров.Добавить("000000", НСтр("ru = '<Выбрать другой номер>'"));
	КонецЕсли;	
	
	Возврат СписокНомеров;
КонецФункции

#КонецОбласти

// Функция - Проверяет запись учетной политики для организации в указанном периоде
//
// Параметры:
//  Организация									 - СправочникСсылка.Организации - Организация, по которой будет осуществлена проверка
//  Период										 - Дата - Период, за который нужно проверить существование
//  ВыводитьСообщениеОбОтсутствииУчетнойПолитики - Булево - Признак вывода сообщения пользователю об ошибке
//  ДокументСсылка								 - ДокументСсылка - Прикреплять сообщение об ошибке к объекту
// 
// Возвращаемое значение:
//  Результат - Булево - Признак существования учетной политики
//
Функция УчетнаяПолитикаСуществует(Организация, Период, ВыводитьСообщениеОбОтсутствииУчетнойПолитики = Ложь, ДокументСсылка = Неопределено) Экспорт
	Возврат БухгалтерскийУчетВызовСервераПовтИсп.УчетнаяПолитикаСуществует(Организация, НачалоМесяца(Период), ВыводитьСообщениеОбОтсутствииУчетнойПолитики, ДокументСсылка);
КонецФункции

// Функция возвращает возможные счета учета расчетов с контрагентом
//
// Параметры:
//  Организация - по которой выполняется поиск
//  Контрагент  - по которому выполняется поиск
//  Договор     - по которому выполняется поиск
//
// Возвращаемое значение:
//  Структура - структура содержащая счета учета расчетов
// 
// Алгоритм выпорки
// 1. Организация, Контрагент, Договор. 
// 2. Организация, Контрагент, Договор(пустая). 
// 3. Организация, ГруппаКонтрагентов по иерархии, Догвор(пустая). 
// 4. Организация(пустая), Контрагент, Договор(пустая). 
// 5. Организация(пустая), ГруппаКонтрагентов по иерархии, Договор(пустая). 
// 6. Организация, Контрагент(пустая), Договор(пустая). 
// 7. Организация(пустая), Контрагент(пустая), Договор(пустая).
//
Функция ПолучитьСчетаРасчетовСКонтрагентом(Организация, Контрагент, Договор) Экспорт
	Возврат БухгалтерскийУчетСервер.ПолучитьСчетаРасчетовСКонтрагентом(Организация, Контрагент, Договор);
КонецФункции // ПолучитьСчетаРасчетовСКонтрагентом()

// Функция возвращает значение по умолчанию для передаваемого пользователя и настройки.
//
// Параметры:
//  Настройка         - Строка - вид настройки, значение по умолчанию которой необходимо получить
//  ИмяПользователяИБ - Строка - имя пользователя ИБ программы, настройка которого
//				   запрашивается, если параметр не передается настройка возвращается для текущего пользователя
//
// Возвращаемое значение:
//  Значение по умолчанию для настройки.
//
Функция ПолучитьЗначениеПоУмолчанию(Настройка, ИмяПользователяИБ = Неопределено) Экспорт
	Возврат БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию(Настройка, ИмяПользователяИБ);
КонецФункции // ПолучитьЗначениеПоУмолчанию()

Процедура СформироватьАдрес(КонтактнаяИнформация, СоответствиеАдреса, АдресФакт = Ложь) Экспорт
	
	Если УправлениеКонтактнойИнформациейКлиентСервер.ЭтоКонтактнаяИнформацияВJSON(КонтактнаяИнформация) Тогда
		//ПолученныйАдрес = УправлениеКонтактнойИнформациейСлужебный.СтрокуJSONВСтруктуру(КонтактнаяИнформация);
		//НаселенныйПунктДетально = РаботаСАдресамиКлиентСервер.ОписаниеНовойКонтактнойИнформации(Перечисления.ТипыКонтактнойИнформации.Адрес);
		НаселенныйПунктДетально = УправлениеКонтактнойИнформациейСлужебный.JSONВКонтактнуюИнформациюПоПолям(КонтактнаяИнформация, Перечисления.ТипыКонтактнойИнформации.Адрес);
	Иначе 
		Возврат;
	КонецЕсли;	
	
	Если АдресФакт = Ложь Тогда	
	//Юридический Адрес	
	СоответствиеАдреса["Страна"] = НаселенныйПунктДетально.country; 
		
	СоответствиеАдреса["Индекс"] = НаселенныйПунктДетально.ZIPcode;
	СоответствиеАдреса["Регион"] = НаселенныйПунктДетально.area;
	СоответствиеАдреса["Район"] = НаселенныйПунктДетально.district;
	СоответствиеАдреса["Город"] = СокрЛП(НаселенныйПунктДетально.city + " " + НаселенныйПунктДетально.cityType);
	СоответствиеАдреса["НаселенныйПункт"] = НаселенныйПунктДетально.locality;
	СоответствиеАдреса["Улица"] = СокрЛП(НаселенныйПунктДетально.street + " " + НаселенныйПунктДетально.streetType);
	СоответствиеАдреса["Дом"] = НаселенныйПунктДетально.houseNumber; 
	СоответствиеАдреса["Корпус"] = ?(НаселенныйПунктДетально.buildings.Количество() = 0, "", НаселенныйПунктДетально.buildings[0].Number);
	СоответствиеАдреса["Квартира"] = ?(НаселенныйПунктДетально.apartments.Количество() = 0, "", НаселенныйПунктДетально.apartments[0].Number);
	Иначе
	//Фактический Адрес
	СоответствиеАдреса["СтранаФакт"] = НаселенныйПунктДетально.country;
		
	СоответствиеАдреса["ИндексФакт"] = НаселенныйПунктДетально.ZIPcode;
    СоответствиеАдреса["РегионФакт"] = НаселенныйПунктДетально.area;
    СоответствиеАдреса["РайонФакт"] = НаселенныйПунктДетально.district;
	СоответствиеАдреса["ГородФакт"] = СокрЛП(НаселенныйПунктДетально.city + " " + НаселенныйПунктДетально.cityType);
    СоответствиеАдреса["НаселенныйПунктФакт"] = НаселенныйПунктДетально.locality;
    СоответствиеАдреса["УлицаФакт"] = СокрЛП(НаселенныйПунктДетально.street + " " + НаселенныйПунктДетально.streetType);
    СоответствиеАдреса["ДомФакт"] = НаселенныйПунктДетально.houseNumber;
    СоответствиеАдреса["КорпусФакт"] = ?(НаселенныйПунктДетально.buildings.Количество() = 0, "", НаселенныйПунктДетально.buildings[0].Number);
	СоответствиеАдреса["КвартираФакт"] = ?(НаселенныйПунктДетально.apartments.Количество() = 0, "", НаселенныйПунктДетально.apartments[0].Number);
	КонецЕсли;	
КонецПроцедуры

// Возвращает служебную информацию для письма о новых возможностях
//
Функция СлужебнаяИнформацияДляПисьма(Тег) Экспорт
	
	Разделитель = "-------------------------------------------------------------------------------------";
	ВступительныйТекст = НСтр("ru = 'Спасибо, что помогаете развивать приложение 1С:Бухшалтерия! 
	|Мы обязательно прочитаем письмо и рассмотрим ваше пожелание, но вероятнее всего на письмо не ответим. 
	|Если у вас есть вопрос по работе приложения, требующий ответа, то зарегистрируйте обращение на линию консультации.'")
	+ Символы.ПС + Символы.ПС;

	Шаблон = НСтр("ru = 'Просьба не удалять данную информацию:
	|Конфигурация: [СинонимКонфигурации] ([ВерсияКонфигурации])
	|Платформа: 1С:Предприятие ([ВерсияПлатформы])
	|Тег: [Тег]'");
	
	СисИнфо = Новый СистемнаяИнформация;
	ВставляемыеЗначения = Новый Структура;
	ВставляемыеЗначения.Вставить("СинонимКонфигурации",	Метаданные.Синоним);
	ВставляемыеЗначения.Вставить("ВерсияКонфигурации",	Метаданные.Версия);
	ВставляемыеЗначения.Вставить("ВерсияПлатформы",		СисИнфо.ВерсияПриложения);
	ВставляемыеЗначения.Вставить("Тег",					Тег);
	ИнформацияОПрограмме = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(Шаблон, ВставляемыеЗначения);
	
	СлужебнаяИнформацияДляПисьма = Разделитель + Символы.ПС + ВступительныйТекст + Символы.ПС + ИнформацияОПрограмме;
	Возврат СлужебнаяИнформацияДляПисьма;
	
КонецФункции

#КонецОбласти