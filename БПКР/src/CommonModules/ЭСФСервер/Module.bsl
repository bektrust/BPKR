
#Область ПрограммныйИнтерфейс

// Получает сведения ЭСФ для документа
//
// Параметры:
//  Организация			 - СправочникСсылка.Организации - ссылка на организацию	  
//  ДокументОснование	 - ДокументСсылка.РеализацияТоваровУслуг - ссылка на документ, для котрого нужно получить представление
// 
// Возвращаемое значение:
//  СведенияОбЭСФ - Структура
//
Функция СведенияОбЭСФПоДокументуОснованию(Организация, ДокументОснование, ПредставлениеЭСФ = "") Экспорт
	
	СведенияОбЭСФ = Новый Структура;
	СведенияОбЭСФ.Вставить("ПредставлениеЭСФ", ПредставлениеЭСФ);
	СведенияОбЭСФ.Вставить("Ссылка", Неопределено);
	СведенияОбЭСФ.Вставить("Дата", Дата(1, 1, 1));
	СведенияОбЭСФ.Вставить("ДатаПоставки", Дата(1, 1, 1));
	СведенияОбЭСФ.Вставить("НомерЭСФ", "");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СведенияОбЭСФ.Регистратор КАК Ссылка,
		|	СведенияОбЭСФ.Дата КАК Дата,
		|	СведенияОбЭСФ.ДатаПоставки КАК ДатаПоставки,
		|	СведенияОбЭСФ.НомерЭСФ КАК НомерЭСФ
		|ИЗ
		|	РегистрСведений.СведенияОбЭСФ КАК СведенияОбЭСФ
		|ГДЕ
		|	СведенияОбЭСФ.Организация = &Организация
		|	И СведенияОбЭСФ.ДокументОснование = &ДокументОснование";
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		СведенияОбЭСФ.ПредставлениеЭСФ = ВыборкаДетальныеЗаписи.Ссылка;
		ЗаполнитьЗначенияСвойств(СведенияОбЭСФ, ВыборкаДетальныеЗаписи);
	КонецЕсли;
	
	Возврат СведенияОбЭСФ;	
	
КонецФункции

Функция СведенияОбЭСФНоНомеруЭСФ(Организация, НомерЭСФ) Экспорт 

	Результат = Новый Структура;
	Результат.Вставить("ДокументОснование", Неопределено);
	Результат.Вставить("ДокументСсылка", Неопределено);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СведенияОбЭСФ.ДокументОснование КАК ДокументОснование,
		|	СведенияОбЭСФ.Регистратор КАК ДокументСсылка
		|ИЗ
		|	РегистрСведений.СведенияОбЭСФ КАК СведенияОбЭСФ
		|ГДЕ
		|	СведенияОбЭСФ.Организация = &Организация
		|	И СведенияОбЭСФ.НомерЭСФ = &НомерЭСФ";
	Запрос.УстановитьПараметр("НомерЭСФ", НомерЭСФ);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Результат, ВыборкаДетальныеЗаписи);
	КонецЦикла;
	
	Возврат Результат;

КонецФункции // СведенияОбЭСФНоНомеруЭСФ()


#КонецОбласти


