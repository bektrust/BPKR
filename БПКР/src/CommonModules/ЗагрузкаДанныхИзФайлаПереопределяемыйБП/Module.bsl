///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2021, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

Функция СопоставитьНоменклатуруДоПервогоСовпадения(ЗагружаемыеДанные) Экспорт 
	
	ТекстыЗапроса = Новый Массив;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ДанныеДляСопоставления.Штрихкод КАК СТРОКА(200)) КАК Штрихкод,
		|	ВЫРАЗИТЬ(ДанныеДляСопоставления.Артикул КАК СТРОКА(25)) КАК Артикул,
		|	ДанныеДляСопоставления.Номенклатура КАК НоменклатураНаименование,
		|	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
		|ПОМЕСТИТЬ ДанныеДляСопоставления
		|ИЗ
		|	&ДанныеДляСопоставления КАК ДанныеДляСопоставления";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// По ШтрихКоду.
	ТекстЗапроса =	
		"ВЫБРАТЬ
		|	ШтрихкодыНоменклатуры.Номенклатура КАК Ссылка,
		|	ШтрихкодыНоменклатуры.Штрихкод КАК Штрихкод,
		|	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
		|ПОМЕСТИТЬ СопоставленнаяНоменклатураПоШтрихкоду
		|ИЗ
		|	ДанныеДляСопоставления КАК ДанныеДляСопоставления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
		|		ПО ДанныеДляСопоставления.Штрихкод = ШтрихкодыНоменклатуры.Штрихкод
		|			И (ДанныеДляСопоставления.Штрихкод <> """")
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Идентификатор";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
		
	// Отсечение найденых по ШтрихКоду.
	ТекстЗапроса =	
		"ВЫБРАТЬ
		|	ДанныеДляСопоставления.Артикул КАК Артикул,
		|	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
		|ПОМЕСТИТЬ ДанныеДляСопоставленияПоАртикулу
		|ИЗ
		|	ДанныеДляСопоставления КАК ДанныеДляСопоставления
		|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленнаяНоменклатураПоШтрихкоду КАК СопоставленнаяНоменклатураПоШтрихкоду
		|		ПО ДанныеДляСопоставления.Идентификатор = СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор
		|ГДЕ
		|	СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор ЕСТЬ NULL
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Идентификатор";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
		
	// По Артикулу.
	ТекстЗапроса =	
		"ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК Ссылка,
		|	Номенклатура.Артикул КАК Артикул,
		|	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
		|ПОМЕСТИТЬ СопоставленнаяНоменклатураПоАртикулу
		|ИЗ
		|	ДанныеДляСопоставленияПоАртикулу КАК ДанныеДляСопоставления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
		|		ПО ДанныеДляСопоставления.Артикул = Номенклатура.Артикул
		|			И (ДанныеДляСопоставления.Артикул <> """")
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Идентификатор";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
		
	// Отсечение найденых по ШтрихКоду и Артикулу.
	ТекстЗапроса =	
		"ВЫБРАТЬ
		|	ДанныеДляСопоставления.НоменклатураНаименование КАК НоменклатураНаименование,
		|	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
		|ПОМЕСТИТЬ ДанныеДляСопоставленияПоНаименованию
		|ИЗ
		|	ДанныеДляСопоставления КАК ДанныеДляСопоставления
		|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленнаяНоменклатураПоШтрихкоду КАК СопоставленнаяНоменклатураПоШтрихкоду
		|		ПО ДанныеДляСопоставления.Идентификатор = СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор
		|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу
		|		ПО ДанныеДляСопоставления.Идентификатор = СопоставленнаяНоменклатураПоАртикулу.Идентификатор
		|ГДЕ
		|	СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор ЕСТЬ NULL
		|	И СопоставленнаяНоменклатураПоАртикулу.Идентификатор ЕСТЬ NULL
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Идентификатор";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
		
	// По наименованию.
	ТекстЗапроса =	
		"ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК Ссылка,
		|	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
		|ПОМЕСТИТЬ СопоставленнаяНоменклатураПоНаименованию
		|ИЗ
		|	ДанныеДляСопоставленияПоНаименованию КАК ДанныеДляСопоставления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
		|		ПО (Номенклатура.Наименование = (ВЫРАЗИТЬ(ДанныеДляСопоставления.НоменклатураНаименование КАК СТРОКА(500))))
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Идентификатор";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
		
	// Отсечение найденых по ШтрихКоду, Артикулу, Наименованию.
	ТекстЗапроса =	
		"ВЫБРАТЬ
		|	ДанныеДляСопоставления.НоменклатураНаименование КАК НоменклатураНаименование,
		|	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
		|ПОМЕСТИТЬ ДанныеДляСопоставленияПоПолномуНаименованию
		|ИЗ
		|	ДанныеДляСопоставления КАК ДанныеДляСопоставления
		|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленнаяНоменклатураПоШтрихкоду КАК СопоставленнаяНоменклатураПоШтрихкоду
		|		ПО ДанныеДляСопоставления.Идентификатор = СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор
		|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу
		|		ПО ДанныеДляСопоставления.Идентификатор = СопоставленнаяНоменклатураПоАртикулу.Идентификатор
		|		ЛЕВОЕ СОЕДИНЕНИЕ СопоставленнаяНоменклатураПоНаименованию КАК СопоставленнаяНоменклатураПоНаименованию
		|		ПО ДанныеДляСопоставления.Идентификатор = СопоставленнаяНоменклатураПоНаименованию.Идентификатор
		|ГДЕ
		|	СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор ЕСТЬ NULL
		|	И СопоставленнаяНоменклатураПоАртикулу.Идентификатор ЕСТЬ NULL
		|	И СопоставленнаяНоменклатураПоНаименованию.Идентификатор ЕСТЬ NULL
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Идентификатор";
	ТекстыЗапроса.Добавить(ТекстЗапроса);	
		
	// По полному наименованию.
	ТекстЗапроса =	
		"ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК Ссылка,
		|	ДанныеДляСопоставления.Идентификатор КАК Идентификатор
		|ПОМЕСТИТЬ СопоставленнаяНоменклатураПоПолномуНаименованию
		|ИЗ
		|	ДанныеДляСопоставленияПоПолномуНаименованию КАК ДанныеДляСопоставления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
		|		ПО (Номенклатура.НаименованиеПолное = (ВЫРАЗИТЬ(ДанныеДляСопоставления.НоменклатураНаименование КАК СТРОКА(1000))))
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Идентификатор";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
		
	// Порядок сопоставления:
	// Штрихкод, Артикул, Наименование, НаименованиеПолное
	ТекстЗапроса =	
		"ВЫБРАТЬ
		|	МАКСИМУМ(СопоставленнаяНоменклатураПоШтрихкоду.Ссылка) КАК Ссылка,
		|	СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор КАК Идентификатор,
		|	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор) КАК Количество
		|ИЗ
		|	СопоставленнаяНоменклатураПоШтрихкоду КАК СопоставленнаяНоменклатураПоШтрихкоду
		|
		|СГРУППИРОВАТЬ ПО
		|	СопоставленнаяНоменклатураПоШтрихкоду.Идентификатор
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	МАКСИМУМ(СопоставленнаяНоменклатураПоАртикулу.Ссылка),
		|	СопоставленнаяНоменклатураПоАртикулу.Идентификатор,
		|	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоАртикулу.Идентификатор)
		|ИЗ
		|	СопоставленнаяНоменклатураПоАртикулу КАК СопоставленнаяНоменклатураПоАртикулу
		|
		|СГРУППИРОВАТЬ ПО
		|	СопоставленнаяНоменклатураПоАртикулу.Идентификатор
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	МАКСИМУМ(СопоставленнаяНоменклатураПоНаименованию.Ссылка),
		|	СопоставленнаяНоменклатураПоНаименованию.Идентификатор,
		|	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоНаименованию.Идентификатор)
		|ИЗ
		|	СопоставленнаяНоменклатураПоНаименованию КАК СопоставленнаяНоменклатураПоНаименованию
		|
		|СГРУППИРОВАТЬ ПО
		|	СопоставленнаяНоменклатураПоНаименованию.Идентификатор
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	МАКСИМУМ(СопоставленнаяНоменклатураПоПолномуНаименованию.Ссылка),
		|	СопоставленнаяНоменклатураПоПолномуНаименованию.Идентификатор,
		|	КОЛИЧЕСТВО(СопоставленнаяНоменклатураПоПолномуНаименованию.Идентификатор)
		|ИЗ
		|	СопоставленнаяНоменклатураПоПолномуНаименованию КАК СопоставленнаяНоменклатураПоПолномуНаименованию
		|
		|СГРУППИРОВАТЬ ПО
		|	СопоставленнаяНоменклатураПоПолномуНаименованию.Идентификатор";
	ТекстыЗапроса.Добавить(ТекстЗапроса);

	Запрос = Новый Запрос;
	Запрос.Текст = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());

	Запрос.УстановитьПараметр("ДанныеДляСопоставления", ЗагружаемыеДанные);
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаНоменклатура = РезультатЗапроса.Выгрузить();
	ТаблицаНоменклатура.Индексы.Добавить("Идентификатор");
	
	Возврат ТаблицаНоменклатура;
		
КонецФункции

#КонецОбласти