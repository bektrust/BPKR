///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// См.НастройкиПрограммыПереопределяемый.НастройкиПользователейИПравПриСозданииНаСервере
Процедура НастройкиПользователейИПравПриСозданииНаСервере(Форма) Экспорт
	Если Форма.Параметры.Свойство("ФокусНаФлагеИспользоватьДатыЗапретаИзменения")
		И Форма.Параметры.ФокусНаФлагеИспользоватьДатыЗапретаИзменения Тогда
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма.Элементы, "ГруппаДатыЗапретаИзменения")  Тогда
			
			Форма.Элементы.ГруппаДатыЗапретаИзменения.Показать();
			Форма.Элементы.ГруппаДатыЗапретаИзменения.ЦветФона = ЦветаСтиля.БыстрыеОтборыФонГруппы;
			
			ОбщегоНазначенияКлиентСервер. УстановитьСвойствоЭлементаФормы(Форма.Элементы, 
				"ГруппаДатыЗапретаИзмененияИспользование", "Видимость", Истина);
			
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма.Элементы, "ИспользоватьДатыЗапретаИзменения")  Тогда
				Форма.ТекущийЭлемент = Форма.Элементы.ИспользоватьДатыЗапретаИзменения;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры

// См.НастройкиПрограммыПереопределяемый.ИнтернетПоддержкаИСервисыПриСозданииНаСервере
Процедура ИнтернетПоддержкаИСервисыПриСозданииНаСервере(Форма) Экспорт
	Форма.Элементы.ГруппаОткрытьОписаниеИзмененийСистемы.Видимость = Ложь;
	
	// Переопределение элементов для автозагрузки патчей.
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма.Элементы, "ГруппаОбновлениеПрограммыИсправления")  Тогда
		Форма.Элементы.ГруппаОбновлениеПрограммыИсправления.ЦветФона = ЦветаСтиля.БыстрыеОтборыФонГруппы;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма.Элементы, "БИПЗагружатьИУстанавливатьИсправленияАвтоматически")  Тогда
		ЭлементАвтозагрузкаПатчей = Форма.Элементы.БИПЗагружатьИУстанавливатьИсправленияАвтоматически;
		ЭлементАвтозагрузкаПатчей.Заголовок = НСтр("ru = 'Загружать и устанавливать исправления (быстрые обновления) автоматически'");
		ЭлементАвтозагрузкаПатчей.ШрифтЗаголовка = Новый Шрифт(,, Истина); // полужирный
		ЭлементАвтозагрузкаПатчей.ВысотаЗаголовка = 2;
		Если Форма.Параметры.Свойство("ФокусНаФлагеАвтозагрузкаПатчей")
				И Форма.Параметры.ФокусНаФлагеАвтозагрузкаПатчей Тогда
			Форма.ТекущийЭлемент = ЭлементАвтозагрузкаПатчей;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// См.НастройкиПрограммыПереопределяемый.ОрганайзерПриСозданииНаСервере
Процедура ОрганайзерПриСозданииНаСервере(Форма) Экспорт
	МоиЗадачи.НастроитьФормуОрганайзер(Форма);
КонецПроцедуры

// См.НастройкиПрограммыПереопределяемый.СинхронизацияДанныхПриСозданииНаСервере
Процедура СинхронизацияДанныхПриСозданииНаСервере(Форма) Экспорт
	Если Форма.Параметры.Свойство("ФокусНаФлагеИспользоватьДатыЗапрета")
		И Форма.Параметры.ФокусНаФлагеИспользоватьДатыЗапрета Тогда
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма.Элементы, "ГруппаДатыЗапретаЗагрузки")  Тогда
			
			Форма.Элементы.ГруппаДатыЗапретаЗагрузки.Показать();
			Форма.Элементы.ГруппаДатыЗапретаЗагрузки.ЦветФона = ЦветаСтиля.БыстрыеОтборыФонГруппы;
			
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы,
				"ГруппаДатыЗапретаЗагрузкиИспользование", "Видимость", Истина);
			
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма.Элементы, "ИспользоватьДатыЗапретаЗагрузки")  Тогда
				Форма.ТекущийЭлемент = Форма.Элементы.ИспользоватьДатыЗапретаЗагрузки;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры

#КонецОбласти