///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2020, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Расширение события ПриСозданииНаСервере.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма для расширения.
//
Процедура ПриСозданииНаСервере(Форма) Экспорт
	
КонецПроцедуры

// Расширение события ПослеЗаписиНаСервере.
//
// Параметры:
//  Форма			 - ФормаКлиентскогоПриложения	 - форма для расширения.
//  ТекущийОбъект	 - 								 - см. справку к событию
//  ПараметрыЗаписи	 - 								 - см. справку к событию
//
Процедура ПослеЗаписиНаСервере(Форма, ТекущийОбъект, ПараметрыЗаписи) Экспорт
	
	Объект = Форма.Объект;

	Если ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") 
		Или ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
		Ценообразование.ОбновитьЦеныНоменклатуры(Объект.Ссылка, 
			Перечисления.СпособыЗаполненияЦен.ПоЗакупочнымЦенам,
			Объект.ВалютаДокумента,
			Объект.СуммаВключаетНалоги);
		ИначеЕсли ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.ПоступлениеВПереработку")
			Или ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.АвансовыйОтчет") Тогда 
		Ценообразование.ОбновитьЦеныНоменклатуры(Объект.Ссылка, 
			Перечисления.СпособыЗаполненияЦен.ПоЗакупочнымЦенам,
			Объект.ВалютаДокумента,
			Истина);			
	ИначеЕсли ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.ОприходованиеТоваров") Тогда 
		ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
		Ценообразование.ОбновитьЦеныНоменклатуры(Объект.Ссылка, 
			Перечисления.СпособыЗаполненияЦен.ПоЗакупочнымЦенам,
			ВалютаРегламентированногоУчета,
			Истина);
	ИначеЕсли ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.РеализацияТоваровУслуг") 
		Или ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.РеализацияУслугПоПереработке")
		Или ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.РозничнаяПродажа")
		Или ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.АктОбОказанииПроизводственныхУслуг") Тогда 
		Ценообразование.ОбновитьЦеныНоменклатуры(Объект.Ссылка, 
			Перечисления.СпособыЗаполненияЦен.ПоПродажнымЦенам,
			Объект.ВалютаДокумента,
			Объект.СуммаВключаетНалоги);
	ИначеЕсли ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.ОтчетПроизводстваЗаСмену")
		Или ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.ПоступлениеИзПереработки") Тогда 
		ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
		Ценообразование.ОбновитьЦеныНоменклатуры(Объект.Ссылка, 
			Перечисления.СпособыЗаполненияЦен.ПоПлановымЦенам,
			ВалютаРегламентированногоУчета,
			Истина);
	КонецЕсли;	

КонецПроцедуры

Процедура УстановитьЗаголовокФормыПоступление(Форма) Экспорт
	
	Объект = Форма.Объект;

	ТекстЗаголовка = НСтр("ru = 'Поступление'");
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Товары Тогда
		ТекстЗаголовка = ТекстЗаголовка + НСтр("ru = ' товаров: Накладная'");
	ИначеЕсли Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Услуги Тогда
		ТекстЗаголовка = ТекстЗаголовка + НСтр("ru = ' услуг: Акт'");
	ИначеЕсли Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ОсновныеСредства Тогда
		ТекстЗаголовка = ТекстЗаголовка + НСтр("ru = ' основных средств'");
	Иначе
		ТекстЗаголовка = ТекстЗаголовка + ": " + Строка(Объект.ВидОперации);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ТекстЗаголовка = ТекстЗаголовка + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru=' %1 от %2'"), Объект.Номер, Объект.Дата);
	Иначе
		ТекстЗаголовка = ТекстЗаголовка + НСтр("ru = ' (создание)'");
	КонецЕсли;
	
	Форма.Заголовок = ТекстЗаголовка;

КонецПроцедуры

#КонецОбласти
