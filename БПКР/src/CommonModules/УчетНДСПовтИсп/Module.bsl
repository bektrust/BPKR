
#Область ПрограммныйИнтерфейс

// Функция - Получить ставку НДС, Нужно использовать процедуру УчетНДС.ПолучитьСтавкуНДС
//
// Параметры:
//  Период		 - Дата	- дата на которую нужно получить значения ставки
//  СтавкаНДС	 - СправочникСсылка.СтавкиНДС - ссылка на справочник ставки НДС
// 
// Возвращаемое значение:
//  Ставка - число - размер ставки
//
Функция ПолучитьСтавкуНДС(Период, СтавкаНДС) Экспорт
	Ставка = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтавкиНДССрезПоследних.Ставка КАК Ставка
		|ИЗ
		|	РегистрСведений.СтавкиНДС.СрезПоследних(&Период, СтавкаНДС = &СтавкаНДС) КАК СтавкиНДССрезПоследних";
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("СтавкаНДС", СтавкаНДС);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда 
		Ставка = ВыборкаДетальныеЗаписи.Ставка;
	КонецЕсли;
	
	Возврат Ставка;
КонецФункции // ПолучитьСтавкуНДС()

// Функция - Получить ставку НСП, Нужно использовать процедуру УчетНДС.ПолучитьСтавкуНСП
//
// Параметры:
//  Период		 - Дата	- дата на которую нужно получить значения ставки
//  Организация	 - СправочникСсылка.Организации - ссылка на справочник Организации
//  СтавкаНСП	 - СправочникСсылка.СтавкиНСП - ссылка на справочник ставки НСП
// 
// Возвращаемое значение:
//   Ставка - число - размер ставки
//
Функция ПолучитьСтавкуНСП(Период, Организация, СтавкаНСП) Экспорт
	
	Ставка = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтавкиНСПСрезПоследних.Период КАК Период,
		|	СтавкиНСПСрезПоследних.Ставка КАК Ставка
		|ИЗ
		|	РегистрСведений.СтавкиНСП.СрезПоследних(
		|			&Период,
		|			Организация = &Организация
		|				И СтавкаНСП = &СтавкаНСП) КАК СтавкиНСПСрезПоследних";	
	Запрос.УстановитьПараметр("Период", Период);	
	Запрос.УстановитьПараметр("Организация", Организация);	
	Запрос.УстановитьПараметр("СтавкаНСП", СтавкаНСП);	
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда 
		Ставка = ВыборкаДетальныеЗаписи.Ставка;
	КонецЕсли;
	
	Возврат Ставка;
КонецФункции // ПолучитьСтавкуНСП()

// Функция - Получить ставки НДС с нулевыми значениями,  Нужно использовать процедуру УчетНДС.ПолучитьСтавкиНДССНулевымиЗначениями
//
// Параметры:
//  Период		 - Дата	- Дата на которую нужно получить значения ставки
// Возвращаемое значение:
//   - Таблица значений -
Функция ПолучитьСтавкиНДССНулевымиЗначениями(Период) Экспорт

	Запрос = Новый Запрос();
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтавкиНДССрезПоследних.СтавкаНДС КАК СтавкаНДС
		|ИЗ
		|	РегистрСведений.СтавкиНДС.СрезПоследних(&Период, ) КАК СтавкиНДССрезПоследних
		|ГДЕ
		|	СтавкиНДССрезПоследних.Ставка = 0";		
	Запрос.УстановитьПараметр("Период", Период);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти

