
#Область ПрограммныйИнтерфейс

// Функция возвращает текст запроса для расчета налоговой амортизации в документе ЗакрытиеМесяца.
//
Функция ТекстЗапросаПоРасчетуНалоговойАмортизацииВДокументеЗакрытиеМесяца() Экспорт
	
	ТекстыЗапроса = Новый Массив;
	ТекстыЗапроса.Добавить(ТекстЗапроса_ВременнаяТаблицаНалоговаяАмортизация());
	ТекстыЗапроса.Добавить(ТекстЗапроса_ВременнаяТаблицаДвижениеОС());
	ТекстыЗапроса.Добавить(ТекстЗапроса_УничтожитьВременнуюТаблицу("ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС"));
	//ТекстыЗапроса.Добавить(ТекстЗапроса_ВременнаяТаблицаДвижениеОСРезультат());
	//ТекстыЗапроса.Добавить(ТекстЗапроса_УничтожитьВременнуюТаблицу("ВременнаяТаблицаДвижениеОС"));
	ТекстыЗапроса.Добавить(ТекстЗапроса_ВременнаяТаблицаЗатратыНаРемонт());
	ТекстыЗапроса.Добавить(ТекстЗапроса_ВременнаяТаблицаСписокГрупп());
	ТекстыЗапроса.Добавить(ТекстЗапроса_ВременнаяТаблицаПолныйСписок());
	ТекстыЗапроса.Добавить(ТекстЗапроса_ВременнаяТаблицаКоличествоВГруппах());
	ТекстыЗапроса.Добавить(ТекстЗапроса_УничтожитьВременнуюТаблицу("ВременнаяТаблицаСписокОС"));
	ТекстыЗапроса.Добавить(ТекстЗапроса_ДляДокументаЗакрытиеМесяца());
	
	Возврат СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
КонецФункции 

// Функция возвращает текст запроса для расчета налоговой амортизации в мониторе по налогу на прибыль.
//
Функция ТекстЗапросаПоРасчетуНалоговойАмортизацииВМонитореНП() Экспорт
	
	ТекстыЗапроса = Новый Массив;
	ТекстыЗапроса.Добавить(ТекстЗапроса_ВременнаяТаблицаНалоговаяАмортизация());
	ТекстыЗапроса.Добавить(ТекстЗапроса_ВременнаяТаблицаДвижениеОСМониторНП());
	ТекстыЗапроса.Добавить(ТекстЗапроса_УничтожитьВременнуюТаблицу("ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС"));
	//ТекстыЗапроса.Добавить(ТекстЗапроса_ВременнаяТаблицаДвижениеОСРезультат());
	//ТекстыЗапроса.Добавить(ТекстЗапроса_УничтожитьВременнуюТаблицу("ВременнаяТаблицаДвижениеОС"));
	ТекстыЗапроса.Добавить(ТекстЗапроса_ВременнаяТаблицаСписокГрупп_МониторНП());
	ТекстыЗапроса.Добавить(ТекстЗапроса_ЗатратыНаРемонт_МониторНП());
	ТекстыЗапроса.Добавить(ТекстЗапроса_ДляМонитораНП());
	
	Возврат СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());

КонецФункции

// Функция возвращает текст запроса ВременнаяТаблицаЗатратыНаРемонт.
// 
Функция ТекстЗапроса_ЗатратыНаРемонт_МониторНП()
	// Затраты на ремонт.
	ТекстЗапроса =	
		"ВЫБРАТЬ
		|	ПараметрыУчетаОС.ГруппаНУ КАК ГруппаНУ,
		|	ПараметрыУчетаОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ХозрасчетныйОбороты.СуммаОборот КАК ЗатратыНаРемонт,
		|	ХозрасчетныйОбороты.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ ВременнаяТаблицаЗатратыНаРемонт
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(
		|			&НачалоГода,
		|			&КонецГода,
		|			Авто,
		|			Счет В (&СчетаЗатратНаРемонтОС),
		|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства),
		|			Организация = &Организация,
		|			,
		|			) КАК ХозрасчетныйОбороты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыУчетаОС.СрезПоследних(
		|				&КонецГода,
		|				Организация = &Организация) КАК ПараметрыУчетаОС
		|		ПО ХозрасчетныйОбороты.Субконто1 = ПараметрыУчетаОС.ОсновноеСредство
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПараметрыУчетаОСВАренде.ГруппаНУ КАК ГруппаНУ,
		|	ПараметрыУчетаОСВАренде.ОсновноеСредство КАК ОсновноеСредство,
		|	ХозрасчетныйОбороты.СуммаОборот КАК ЗатратыНаРемонт,
		|	ХозрасчетныйОбороты.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(
		|			&НачалоГода,
		|			&КонецГода,
		|			Авто,
		|			Счет В (&СчетаЗатратНаРемонтОС),
		|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства),
		|			Организация = &Организация,
		|			,
		|			) КАК ХозрасчетныйОбороты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыУчетаОСВАренде.СрезПоследних(
		|				&КонецГода,
		|				Организация = &Организация) КАК ПараметрыУчетаОСВАренде
		|		ПО ХозрасчетныйОбороты.Субконто1 = ПараметрыУчетаОСВАренде.ОсновноеСредство
		|ГДЕ
		|	НЕ ПараметрыУчетаОСВАренде.ГруппаНУ = ЗНАЧЕНИЕ(Справочник.ГруппыНалоговогоУчетаОС.ПустаяСсылка)";
		
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция возвращает текст запроса для расчета налоговой выверки в документе ЗакрытиеМесяца и обработке МониторНалогаНаПрибыль.
//
Функция ТекстЗапросаПоРасчетуНалоговойВыверки() Экспорт
	// 1. Доходы по бухгалтерскому учету.
	// 2. Расходы по бухгалтерскому учету.
	// 3. Постоянные разницы - доходы.
	// 4. Постоянные разницы - расходы.
	// 5. Амортизация бухгалтерская.
	// 6. Прочие временные разницы.
	// 7. Прочие временные разницы - доходы
	// 8. Прочие временные разницы - расходы
	// 9. Суммы КР – доходы
	// 10. Суммы КР – расходы
	// 11. Затраты на ремонт
	// 12 - 15. Сверка. Временные таблицы: даты принятия/снятия с учета ОС
	// 16. Сверка. затраты на ремонт ОС
	// 17. Сверка. Свод по документам ОС
	// 18. СуммаТекущийРасчетНалогаНаПрибыль из РС ПлатежиПоНалогуНаПрибыль
	// 19. УбытокПодлежащийПереносу - расход из ЗМ
	
	Текст = 
		"ВЫБРАТЬ
		|	1 КАК Порядок,
		|	-ХозрасчетныйОбороты.СуммаОборот КАК Сумма,
		|	ХозрасчетныйОбороты.Счет КАК Счет,
		|	ХозрасчетныйОбороты.Субконто1 КАК Субконто1,
		|	ХозрасчетныйОбороты.Регистратор КАК Документ
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Авто,
		|			Счет.Временный
		|				И Счет.Вид = ЗНАЧЕНИЕ(ВидСчета.Пассивный),
		|			,
		|			Организация = &Организация,
		|			НЕ КорСчет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СводДоходовИРасходов),
		|			) КАК ХозрасчетныйОбороты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	2 КАК Порядок,
		|	ХозрасчетныйОбороты.СуммаОборот КАК Сумма,
		|	ХозрасчетныйОбороты.Счет КАК Счет,
		|	ХозрасчетныйОбороты.Субконто1 КАК Субконто1,
		|	ХозрасчетныйОбороты.Регистратор КАК Документ
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Авто,
		|			Счет.Временный
		|				И Счет.Вид = ЗНАЧЕНИЕ(ВидСчета.Активный)
		|				И НЕ Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасходыДоходыПоНалогуНаПрибыль),
		|			,
		|			Организация = &Организация,
		|			НЕ КорСчет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СводДоходовИРасходов),
		|			) КАК ХозрасчетныйОбороты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	3 КАК Порядок,
		|	КорректировкиНУОбороты.СуммаОборот КАК Сумма,
		|	КорректировкиНУОбороты.Счет КАК Счет,
		|	КорректировкиНУОбороты.ВидУчета КАК ВидУчета,
		|	КорректировкиНУОбороты.Регистратор КАК Документ
		|ИЗ
		|	РегистрНакопления.КорректировкиНУ.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Авто,
		|			Организация = &Организация
		|				И Счет.Вид = ЗНАЧЕНИЕ(ВидСчета.Пассивный)
		|				И ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.ПР)
		|				И НЕ АмортизацияОС) КАК КорректировкиНУОбороты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	4 КАК Порядок,
		|	КорректировкиНУОбороты.СуммаОборот КАК Сумма,
		|	КорректировкиНУОбороты.Счет КАК Счет,
		|	КорректировкиНУОбороты.ВидУчета КАК ВидУчета,
		|	КорректировкиНУОбороты.Регистратор КАК Документ
		|ИЗ
		|	РегистрНакопления.КорректировкиНУ.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Авто,
		|			Организация = &Организация
		|				И Счет.Вид = ЗНАЧЕНИЕ(ВидСчета.Активный)
		|				И ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.ПР)
		|				И НЕ АмортизацияОС) КАК КорректировкиНУОбороты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	5 КАК Порядок,
		|	ХозрасчетныйОборотыДтКт.Регистратор КАК Документ,
		|	ХозрасчетныйОборотыДтКт.СчетДт КАК СчетДт,
		|	ХозрасчетныйОборотыДтКт.СчетКт КАК СчетКт,
		|	ХозрасчетныйОборотыДтКт.СчетДт.Временный КАК СчетДтВременный,
		|	ХозрасчетныйОборотыДтКт.СубконтоКт1 КАК ОсновноеСредство,
		|	ХозрасчетныйОборотыДтКт.СуммаОборот КАК Сумма
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Авто,
		|			,
		|			,
		|			СчетКт В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОсновныеСредства), ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НематериальныеАктивы))
		|				И СчетКт.Вид = ЗНАЧЕНИЕ(ВидСчета.Пассивный),
		|			,
		|			Организация = &Организация) КАК ХозрасчетныйОборотыДтКт
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	6 КАК Порядок,
		|	КорректировкиНУОбороты.ВидУчета КАК ВидУчета,
		|	СУММА(КорректировкиНУОбороты.СуммаОборот) КАК Сумма,
		|	КорректировкиНУОбороты.Регистратор КАК Документ,
		|	КорректировкиНУОбороты.Счет КАК Счет,
		|	ВЫБОР
		|		КОГДА КорректировкиНУОбороты.Счет.Вид = ЗНАЧЕНИЕ(ВидСчета.Активный)
		|			ТОГДА КорректировкиНУОбороты.СуммаОборот
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Доходы,
		|	ВЫБОР
		|		КОГДА КорректировкиНУОбороты.Счет.Вид = ЗНАЧЕНИЕ(ВидСчета.Пассивный)
		|			ТОГДА КорректировкиНУОбороты.СуммаОборот
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Расходы
		|ИЗ
		|	РегистрНакопления.КорректировкиНУ.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Авто,
		|			Организация = &Организация
		|				И ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.ВР)
		|				И НЕ Счет В (&СчетаУчетаИсключения)
		|				И НЕ АмортизацияОС) КАК КорректировкиНУОбороты
		|
		|СГРУППИРОВАТЬ ПО
		|	КорректировкиНУОбороты.ВидУчета,
		|	КорректировкиНУОбороты.Счет,
		|	КорректировкиНУОбороты.Регистратор,
		|	ВЫБОР
		|		КОГДА КорректировкиНУОбороты.Счет.Вид = ЗНАЧЕНИЕ(ВидСчета.Активный)
		|			ТОГДА КорректировкиНУОбороты.СуммаОборот
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА КорректировкиНУОбороты.Счет.Вид = ЗНАЧЕНИЕ(ВидСчета.Пассивный)
		|			ТОГДА КорректировкиНУОбороты.СуммаОборот
		|		ИНАЧЕ 0
		|	КОНЕЦ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	7 КАК Порядок,
		|	ЕСТЬNULL(СУММА(КорректировкиНУОбороты.СуммаОборот), 0) КАК Сумма
		|ИЗ
		|	РегистрНакопления.КорректировкиНУ.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Авто,
		|			Организация = &Организация
		|				И ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.ВР)
		|				И Счет.Вид = ЗНАЧЕНИЕ(ВидСчета.Пассивный)
		|				И НЕ АмортизацияОС) КАК КорректировкиНУОбороты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	8 КАК Порядок,
		|	ЕСТЬNULL(СУММА(КорректировкиНУОбороты.СуммаОборот), 0) КАК Сумма
		|ИЗ
		|	РегистрНакопления.КорректировкиНУ.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Авто,
		|			Организация = &Организация
		|				И ВидУчета = ЗНАЧЕНИЕ(Перечисление.ВидыУчета.ВР)
		|				И Счет.Вид = ЗНАЧЕНИЕ(ВидСчета.Активный)
		|				И НЕ АмортизацияОС) КАК КорректировкиНУОбороты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	9 КАК Порядок,
		|	ХозрасчетныйОбороты.Счет КАК Счет,
		|	ХозрасчетныйОбороты.Регистратор КАК Документ,
		|	-ХозрасчетныйОбороты.СуммаОборот КАК Сумма
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Авто, Счет В (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДоходыОтОперационныхКурсовыхРазниц), ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДоходОтКурсовыхРазницПоОперациямВИностраннойВалюте)), , Организация = &Организация, НЕ КорСчет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СводДоходовИРасходов), ) КАК ХозрасчетныйОбороты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	10 КАК Порядок,
		|	ХозрасчетныйОбороты.Счет КАК Счет,
		|	ХозрасчетныйОбороты.Регистратор КАК Документ,
		|	ХозрасчетныйОбороты.СуммаОборот КАК Сумма
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Авто, Счет В (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.УбыткиОтОперационныхКурсовыхРазниц), ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.УбыткиОтКурсовыхРазницПоОперациямВИностраннойВалюте)), , Организация = &Организация, НЕ КорСчет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СводДоходовИРасходов), ) КАК ХозрасчетныйОбороты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	11 КАК Порядок,
		|	СУММА(НалоговаяАмортизация.ЗатратыНаРемонт) КАК СуммаЗатрат
		|ИЗ
		|	РегистрСведений.НалоговаяАмортизация КАК НалоговаяАмортизация
		|ГДЕ
		|	НалоговаяАмортизация.Организация = &Организация
		|	И НалоговаяАмортизация.Год МЕЖДУ &НачалоПериода И &КонецПериода
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	12 КАК Порядок,
		|	ТаблицаОСДатаПринятия.ОсновноеСредство КАК ОсновноеСредство,
		|	ТаблицаОСДатаПринятия.Период КАК ДатаПринятия,
		|	ЕСТЬNULL(ТаблицаОСДатаСнятия.Период, &КонецПериода) КАК ДатаСнятия,
		|	ЕСТЬNULL(ПараметрыУчетаОССрезПоследних.ГруппаНУ, ЗНАЧЕНИЕ(Справочник.ГруппыНалоговогоУчетаОС.ПустаяСсылка)) КАК ГруппаНУ
		|ПОМЕСТИТЬ ВременнаяТаблицаОССДатамиПринятияИСнятия
		|ИЗ
		|	РегистрСведений.СостоянияОС.СрезПоследних(
		|			&КонецПериода,
		|			Организация = &Организация
		|				И Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийОС.ПринятоКУчету)) КАК ТаблицаОСДатаПринятия
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияОС.СрезПоследних(
		|				&КонецПериода,
		|				Организация = &Организация
		|					И НЕ Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийОС.ПринятоКУчету)
		|					И НЕ Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийОС.Поступило)) КАК ТаблицаОСДатаСнятия
		|		ПО ТаблицаОСДатаПринятия.ОсновноеСредство = ТаблицаОСДатаСнятия.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыУчетаОС.СрезПоследних(&КонецПериода, Организация = &Организация) КАК ПараметрыУчетаОССрезПоследних
		|		ПО ТаблицаОСДатаПринятия.ОсновноеСредство = ПараметрыУчетаОССрезПоследних.ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	13 КАК Порядок,
		|	ВременнаяТаблицаОССДатамиПринятияИСнятия.ОсновноеСредство КАК ОсновноеСредство
		|ПОМЕСТИТЬ ВременнаяТаблицаИсключенияОС
		|ИЗ
		|	ВременнаяТаблицаОССДатамиПринятияИСнятия КАК ВременнаяТаблицаОССДатамиПринятияИСнятия
		|ГДЕ
		|	ВременнаяТаблицаОССДатамиПринятияИСнятия.ГруппаНУ <> ЗНАЧЕНИЕ(Справочник.ГруппыНалоговогоУчетаОС.ПустаяСсылка)
		|	И ВременнаяТаблицаОССДатамиПринятияИСнятия.ДатаПринятия <= &НачалоПериода
		|	И ВременнаяТаблицаОССДатамиПринятияИСнятия.ДатаСнятия >= &КонецПериода
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	14 КАК Порядок,
		|	ВременнаяТаблицаОССДатамиПринятияИСнятия.ОсновноеСредство КАК ОсновноеСредство,
		|	ВременнаяТаблицаОССДатамиПринятияИСнятия.ДатаПринятия КАК ДатаПринятия,
		|	ВременнаяТаблицаОССДатамиПринятияИСнятия.ДатаСнятия КАК ДатаСнятия
		|ПОМЕСТИТЬ ВременнаяТаблицаОССПериодомДействия
		|ИЗ
		|	ВременнаяТаблицаОССДатамиПринятияИСнятия КАК ВременнаяТаблицаОССДатамиПринятияИСнятия
		|ГДЕ
		|	ВременнаяТаблицаОССДатамиПринятияИСнятия.ГруппаНУ <> ЗНАЧЕНИЕ(Справочник.ГруппыНалоговогоУчетаОС.ПустаяСсылка)
		|	И (ВременнаяТаблицаОССДатамиПринятияИСнятия.ДатаПринятия > &НачалоПериода
		|			ИЛИ ВременнаяТаблицаОССДатамиПринятияИСнятия.ДатаСнятия < &КонецПериода)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	15 КАК Порядок,
		|	ОсновныеСредства.Ссылка КАК ОсновноеСредство
		|ПОМЕСТИТЬ ВременнаяТаблицаОсновныеСредства
		|ИЗ
		|	Справочник.ОсновныеСредства КАК ОсновныеСредства
		|ГДЕ
		|	НЕ ОсновныеСредства.Ссылка В
		|				(ВЫБРАТЬ
		|					ВременнаяТаблицаИсключенияОС.ОсновноеСредство
		|				ИЗ
		|					ВременнаяТаблицаИсключенияОС)
		|	И НЕ ОсновныеСредства.Ссылка В
		|				(ВЫБРАТЬ
		|					ВременнаяТаблицаОССПериодомДействия.ОсновноеСредство
		|				ИЗ
		|					ВременнаяТаблицаОССПериодомДействия)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	16 КАК Порядок,
		|	ХозрасчетныйОбороты.Регистратор КАК Документ,
		|	ХозрасчетныйОбороты.Период КАК Период,
		|	ХозрасчетныйОбороты.Субконто1 КАК ОсновноеСредство,
		|	ХозрасчетныйОбороты.СуммаОборот КАК СуммаОборот,
		|	ХозрасчетныйОбороты.Счет КАК Счет
		|ПОМЕСТИТЬ ВременнаяТаблицаДокументыОС
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Регистратор, Счет В (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ЗатратыНаРемонтИОбслуживаниеОсновныхСредств), ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РемонтИОбслуживаниеОсновныхСредств)), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства), Организация = &Организация, , ) КАК ХозрасчетныйОбороты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыУчетаОС.СрезПоследних(&КонецПериода, Организация = &Организация) КАК ПараметрыУчетаОС
		|		ПО ХозрасчетныйОбороты.Субконто1 = ПараметрыУчетаОС.ОсновноеСредство
		|ГДЕ
		|	ПараметрыУчетаОС.ГруппаНУ = ЗНАЧЕНИЕ(Справочник.ГруппыНалоговогоУчетаОС.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	16,
		|	ХозрасчетныйОбороты.Регистратор,
		|	ХозрасчетныйОбороты.Период,
		|	ХозрасчетныйОбороты.Субконто1,
		|	ХозрасчетныйОбороты.СуммаОборот,
		|	ХозрасчетныйОбороты.Счет
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Регистратор, Счет В (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ЗатратыНаРемонтИОбслуживаниеОсновныхСредств), ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РемонтИОбслуживаниеОсновныхСредств)), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства), Организация = &Организация, , ) КАК ХозрасчетныйОбороты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияОС.СрезПоследних(&КонецПериода, Организация = &Организация) КАК СостоянияОССрезПоследних
		|		ПО ХозрасчетныйОбороты.Субконто1 = СостоянияОССрезПоследних.ОсновноеСредство
		|ГДЕ
		|	СостоянияОССрезПоследних.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийОС.ПринятоКУчету)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	17 КАК Порядок,
		|	ВременнаяТаблицаДокументыОС.Документ КАК Документ,
		|	ВременнаяТаблицаДокументыОС.СуммаОборот КАК Сумма,
		|	ВременнаяТаблицаДокументыОС.Счет.Код КАК СчетКод
		|ИЗ
		|	ВременнаяТаблицаДокументыОС КАК ВременнаяТаблицаДокументыОС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаОССПериодомДействия КАК ВременнаяТаблицаОССПериодомДействия
		|		ПО ВременнаяТаблицаДокументыОС.ОсновноеСредство = ВременнаяТаблицаОССПериодомДействия.ОсновноеСредство
		|			И (ВременнаяТаблицаДокументыОС.Период < ВременнаяТаблицаОССПериодомДействия.ДатаПринятия
		|				ИЛИ ВременнаяТаблицаДокументыОС.Период > ВременнаяТаблицаОССПериодомДействия.ДатаСнятия)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	17,
		|	ВременнаяТаблицаДокументыОС.Документ,
		|	ВременнаяТаблицаДокументыОС.СуммаОборот,
		|	ВременнаяТаблицаДокументыОС.Счет.Код
		|ИЗ
		|	ВременнаяТаблицаДокументыОС КАК ВременнаяТаблицаДокументыОС
		|ГДЕ
		|	ВременнаяТаблицаДокументыОС.ОсновноеСредство В
		|			(ВЫБРАТЬ
		|				ВременнаяТаблицаОсновныеСредства.ОсновноеСредство
		|			ИЗ
		|				ВременнаяТаблицаОсновныеСредства)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	17,
		|	ХозрасчетныйОбороты.Регистратор,
		|	ХозрасчетныйОбороты.СуммаОборот,
		|	ХозрасчетныйОбороты.Счет.Код
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Регистратор,
		|			Счет В (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ЗатратыНаРемонтИОбслуживаниеОсновныхСредств), ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РемонтИОбслуживаниеОсновныхСредств)),
		|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства),
		|			Организация = &Организация
		|				И Субконто1 = ЗНАЧЕНИЕ(Справочник.ОсновныеСредства.ПустаяСсылка),
		|			,
		|			) КАК ХозрасчетныйОбороты
		|ГДЕ
		|	НЕ ХозрасчетныйОбороты.Регистратор ССЫЛКА Документ.ЗакрытиеМесяца
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	18 КАК Порядок,
		|	ПлатежиПоНалогуНаПрибыльСрезПоследних.Сумма КАК Сумма
		|ИЗ
		|	РегистрСведений.ПлатежиПоНалогуНаПрибыль.СрезПоследних(
		|			&Период3кв,
		|			Организация = &Организация
		|				И ГОД(Период) = &ТекущийГод
		|				И Сумма > 0) КАК ПлатежиПоНалогуНаПрибыльСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	УбыткиПрошлыхЛетОбороты.СуммаРасход КАК Сумма
		|ИЗ
		|	РегистрНакопления.УбыткиПрошлыхЛет.Обороты(&КонецПериода, КОНЕЦПЕРИОДА(&КонецПериода, ГОД), Авто, ) КАК УбыткиПрошлыхЛетОбороты
		|ГДЕ
		|	УбыткиПрошлыхЛетОбороты.Регистратор ССЫЛКА Документ.ЗакрытиеМесяца";
		
	Возврат Текст;
	
КонецФункции // ()

#КонецОбласти

#Область ПроцедурыРасчетаНалоговойАмортизации

// Функция возвращает текст запроса ВременнаяТаблицаНалоговаяАмортизация.
// 
Функция ТекстЗапроса_ВременнаяТаблицаНалоговаяАмортизация()
	// Данные Налоговой амортизации за пред.год. 
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	НалоговаяАмортизация.ГруппаНУ КАК ГруппаНУ,
		|	НалоговаяАмортизация.ОсновноеСредство КАК ОсновноеСредство,
		|	НалоговаяАмортизация.БалансоваяСтоимостьНаКонецГода КАК БалансоваяСтоимостьНаКонецПрошлыйГод,
		|	НалоговаяАмортизация.АмортизацияЗаГод КАК АмортизацияЗаПрошлыйГод,
		|	НалоговаяАмортизация.СуммаПоступленияОстаток КАК СуммаПоступленияОстатокПрошлыйГод,
		|	НалоговаяАмортизация.СуммаВыбытияОстаток КАК СуммаВыбытияОстатокПрошлыйГод,
		|	НалоговаяАмортизация.БазаПрямолинейнойАмортизации КАК БазаПрямолинейнойАмортизацииПрошлыйГод
		|ПОМЕСТИТЬ ВременнаяТаблицаНалоговаяАмортизация
		|ИЗ
		|	РегистрСведений.НалоговаяАмортизация КАК НалоговаяАмортизация
		|ГДЕ
		|	НалоговаяАмортизация.Организация = &Организация
		|	И НалоговаяАмортизация.Год = &НачалоПрошлогоГода
		|	И (НалоговаяАмортизация.ГруппаНУ = &ГруппаНУ
		|			ИЛИ &ГруппаНУ = ЗНАЧЕНИЕ(Справочник.ГруппыНалоговогоУчетаОС.ПустаяСсылка))
		|	И (НалоговаяАмортизация.ОсновноеСредство = &ОсновноеСредство
		|			ИЛИ &ОсновноеСредство = ЗНАЧЕНИЕ(Справочник.ОсновныеСредства.ПустаяСсылка))";
	
	Возврат ТекстЗапроса;
	
КонецФункции 

// Функция возвращает текст запроса ВременнаяТаблицаДвижениеОС.
// 
Функция ТекстЗапроса_ВременнаяТаблицаДвижениеОС()
	// Поступление ОС.
	// Выбытие ОС.	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДвижениеОСНУОбороты.ГруппаНУ КАК ГруппаНУ,
		|	ДвижениеОСНУОбороты.ОсновноеСредство КАК ОсновноеСредство,
		|	ДвижениеОСНУОбороты.Регистратор КАК Регистратор,
		|	МЕСЯЦ(ДвижениеОСНУОбороты.ПериодМесяц) КАК МесяцПоступления,
		|	0 КАК МесяцВыбытия,
		|	ДвижениеОСНУОбороты.ПоступлениеОборот КАК СуммаПоступления,
		|	0 КАК СуммаВыбытия,
		|	ДвижениеОСНУОбороты.ТекущееПоступлениеОборот КАК СуммаПоступленияНУ,
		|	ДвижениеОСНУОбороты.ТекущееВыбытиеОборот КАК СуммаВыбытияНУ
		|ПОМЕСТИТЬ ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС
		|ИЗ
		|	РегистрНакопления.ДвижениеОСНУ.Обороты(
		|			&НачалоГода,
		|			&КонецГода,
		|			Авто,
		|			Организация = &Организация
		|				И (ГруппаНУ = &ГруппаНУ
		|					ИЛИ &ГруппаНУ = ЗНАЧЕНИЕ(Справочник.ГруппыНалоговогоУчетаОС.ПустаяСсылка))
		|				И (ОсновноеСредство = &ОсновноеСредство
		|					ИЛИ &ОсновноеСредство = ЗНАЧЕНИЕ(Справочник.ОсновныеСредства.ПустаяСсылка))) КАК ДвижениеОСНУОбороты
		|ГДЕ
		//|	ДвижениеОСНУОбороты.ПоступлениеОборот >= &МинимумСтоимостиОСДляНУ
		|		ВЫБОР
		|			КОГДА ДвижениеОСНУОбороты.Регистратор ССЫЛКА Документ.МодернизацияОС
		|				ТОГДА ДвижениеОСНУОбороты.ПоступлениеОборот > 0
		|			ИНАЧЕ ДвижениеОСНУОбороты.ПоступлениеОборот >= &МинимумСтоимостиОСДляНУ
		|		КОНЕЦ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДвижениеОСНУОбороты.ГруппаНУ,
		|	ДвижениеОСНУОбороты.ОсновноеСредство,
		|	ДвижениеОСНУОбороты.Регистратор,
		|	0,
		|	МЕСЯЦ(ДвижениеОСНУОбороты.ПериодМесяц),
		|	0,
		|	ДвижениеОСНУОбороты.ВыбытиеОборот,
		|	ДвижениеОСНУОбороты.ТекущееПоступлениеОборот,
		|	ДвижениеОСНУОбороты.ТекущееВыбытиеОборот
		|ИЗ
		|	РегистрНакопления.ДвижениеОСНУ.Обороты(
		|			&НачалоГода,
		|			&КонецГода,
		|			Авто,
		|			Организация = &Организация
		|				И (ГруппаНУ = &ГруппаНУ
		|					ИЛИ &ГруппаНУ = ЗНАЧЕНИЕ(Справочник.ГруппыНалоговогоУчетаОС.ПустаяСсылка))
		|				И (ОсновноеСредство = &ОсновноеСредство
		|					ИЛИ &ОсновноеСредство = ЗНАЧЕНИЕ(Справочник.ОсновныеСредства.ПустаяСсылка))) КАК ДвижениеОСНУОбороты
		|ГДЕ
		|	ДвижениеОСНУОбороты.ВыбытиеОборот > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС.ГруппаНУ КАК ГруппаНУ,
		|	ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС.Регистратор КАК Регистратор,
		|	СУММА(ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС.МесяцПоступления) КАК МесяцПоступления,
		|	СУММА(ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС.МесяцВыбытия) КАК МесяцВыбытия,
		|	СУММА(ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС.СуммаПоступления) КАК СуммаПоступления,
		|	СУММА(ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС.СуммаВыбытия) КАК СуммаВыбытия,
		|	ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС.СуммаПоступленияНУ КАК СуммаПоступленияНУ,
		|	ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС.СуммаВыбытияНУ КАК СуммаВыбытияНУ
		|ПОМЕСТИТЬ ВременнаяТаблицаДвижениеОС
		|ИЗ
		|	ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС КАК ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС.ГруппаНУ,
		|	ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС.ОсновноеСредство,
		|	ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС.Регистратор,
		|	ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС.СуммаПоступленияНУ,
		|	ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС.СуммаВыбытияНУ";
		
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция возвращает текст запроса ВременнаяТаблицаДвижениеОС для Монитор НП.
// 
Функция ТекстЗапроса_ВременнаяТаблицаДвижениеОСМониторНП()
	// Поступление ОС.
	// Выбытие ОС.	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДвижениеОСНУОбороты.ГруппаНУ КАК ГруппаНУ,
		|	ДвижениеОСНУОбороты.ОсновноеСредство КАК ОсновноеСредство,
		|	ДвижениеОСНУОбороты.Регистратор КАК Регистратор,
		|	МЕСЯЦ(ДвижениеОСНУОбороты.ПериодМесяц) КАК МесяцПоступления,
		|	0 КАК МесяцВыбытия,
		|	ДвижениеОСНУОбороты.ПоступлениеОборот КАК СуммаПоступления,
		|	0 КАК СуммаВыбытия,
		|	ДвижениеОСНУОбороты.ТекущееПоступлениеОборот КАК СуммаПоступленияНУ,
		|	0 КАК СуммаВыбытияНУ
		|ПОМЕСТИТЬ ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС
		|ИЗ
		|	РегистрНакопления.ДвижениеОСНУ.Обороты(
		|			&НачалоГода,
		|			&КонецГода,
		|			Авто,
		|			Организация = &Организация
		|				И (ГруппаНУ = &ГруппаНУ
		|					ИЛИ &ГруппаНУ = ЗНАЧЕНИЕ(Справочник.ГруппыНалоговогоУчетаОС.ПустаяСсылка))
		|				И (ОсновноеСредство = &ОсновноеСредство
		|					ИЛИ &ОсновноеСредство = ЗНАЧЕНИЕ(Справочник.ОсновныеСредства.ПустаяСсылка))) КАК ДвижениеОСНУОбороты
		|ГДЕ
		|	ДвижениеОСНУОбороты.ПоступлениеОборот >= &МинимумСтоимостиОСДляНУ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДвижениеОСНУОбороты.ГруппаНУ КАК ГруппаНУ,
		|	ДвижениеОСНУОбороты.ОсновноеСредство КАК ОсновноеСредство,
		|	ДвижениеОСНУОбороты.Регистратор КАК Регистратор,
		|	МЕСЯЦ(ДвижениеОСНУОбороты.ПериодМесяц) КАК МесяцПоступления,
		|	0 КАК МесяцВыбытия,
		|	0 КАК СуммаПоступления,
		|	0 КАК СуммаВыбытия,
		|	0 КАК СуммаПоступленияНУ,
		|	ДвижениеОСНУОбороты.ТекущееВыбытиеОборот КАК СуммаВыбытияНУ
		|ИЗ
		|	РегистрНакопления.ДвижениеОСНУ.Обороты(
		|			&НачалоГода,
		|			&КонецГода,
		|			Авто,
		|			Организация = &Организация) КАК ДвижениеОСНУОбороты
		|ГДЕ
		|	ДвижениеОСНУОбороты.ПоступлениеОборот >= &МинимумСтоимостиОСДляНУ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДвижениеОСНУОбороты.ГруппаНУ,
		|	ДвижениеОСНУОбороты.ОсновноеСредство,
		|	ДвижениеОСНУОбороты.Регистратор,
		|	0,
		|	МЕСЯЦ(ДвижениеОСНУОбороты.ПериодМесяц),
		|	0,
		|	0,
		|	ДвижениеОСНУОбороты.ТекущееПоступлениеОборот,
		|	0
		|ИЗ
		|	РегистрНакопления.ДвижениеОСНУ.Обороты(
		|			&НачалоГода,
		|			&КонецГода,
		|			Авто,
		|			Организация = &Организация
		|				И (ГруппаНУ = &ГруппаНУ
		|					ИЛИ &ГруппаНУ = ЗНАЧЕНИЕ(Справочник.ГруппыНалоговогоУчетаОС.ПустаяСсылка))
		|				И (ОсновноеСредство = &ОсновноеСредство
		|					ИЛИ &ОсновноеСредство = ЗНАЧЕНИЕ(Справочник.ОсновныеСредства.ПустаяСсылка))) КАК ДвижениеОСНУОбороты
		|ГДЕ
		|	ДвижениеОСНУОбороты.ВыбытиеОборот > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДвижениеОСНУОбороты.ГруппаНУ,
		|	ДвижениеОСНУОбороты.ОсновноеСредство,
		|	ДвижениеОСНУОбороты.Регистратор,
		|	0,
		|	МЕСЯЦ(ДвижениеОСНУОбороты.ПериодМесяц),
		|	0,
		|	ДвижениеОСНУОбороты.ВыбытиеОборот,
		|	0,
		|	ДвижениеОСНУОбороты.ТекущееВыбытиеОборот
		|ИЗ
		|	РегистрНакопления.ДвижениеОСНУ.Обороты(
		|			&НачалоГода,
		|			&КонецГода,
		|			Авто,
		|			Организация = &Организация) КАК ДвижениеОСНУОбороты
		|ГДЕ
		|	ДвижениеОСНУОбороты.ВыбытиеОборот > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС.ГруппаНУ КАК ГруппаНУ,
		|	ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС.Регистратор КАК Регистратор,
		|	СУММА(ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС.МесяцПоступления) КАК МесяцПоступления,
		|	СУММА(ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС.МесяцВыбытия) КАК МесяцВыбытия,
		|	СУММА(ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС.СуммаПоступления) КАК СуммаПоступления,
		|	СУММА(ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС.СуммаВыбытия) КАК СуммаВыбытия,
		|	ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС.СуммаПоступленияНУ КАК СуммаПоступленияНУ,
		|	ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС.СуммаВыбытияНУ КАК СуммаВыбытияНУ
		|ПОМЕСТИТЬ ВременнаяТаблицаДвижениеОС
		|ИЗ
		|	ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС КАК ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС.ГруппаНУ,
		|	ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС.ОсновноеСредство,
		|	ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС.Регистратор,
		|	ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС.СуммаПоступленияНУ,
		|	ВременнаяТаблицаДвижениеПоступлениеВыбытиеОС.СуммаВыбытияНУ";
		
	Возврат ТекстЗапроса;
	
КонецФункции

//// Функция возвращает текст запроса ВременнаяТаблицаДвижениеОСРезультат.
//// 
//Функция ТекстЗапроса_ВременнаяТаблицаДвижениеОСРезультат()
//	// Расчет полных месяцев.
//	ТекстЗапроса = 
//		"ВЫБРАТЬ
//		|	ВременнаяТаблицаДвижениеОС.ГруппаНУ КАК ГруппаНУ,
//		|	ВременнаяТаблицаДвижениеОС.ОсновноеСредство КАК ОсновноеСредство,
//		|	ВременнаяТаблицаДвижениеОС.Регистратор КАК Регистратор,
//		|	ВременнаяТаблицаДвижениеОС.МесяцПоступления КАК МесяцПоступления,
//		|	ВременнаяТаблицаДвижениеОС.МесяцВыбытия КАК МесяцВыбытия,
//		|	ВременнаяТаблицаДвижениеОС.СуммаПоступления КАК СуммаПоступления,
//		|	ВременнаяТаблицаДвижениеОС.СуммаВыбытия КАК СуммаВыбытия,
//		|	ВременнаяТаблицаДвижениеОС.СуммаПоступленияНУ КАК СуммаПоступленияНУ,
//		|	ВременнаяТаблицаДвижениеОС.СуммаВыбытияНУ КАК СуммаВыбытияНУ,		
//		|	ВЫБОР
//		|		КОГДА ВременнаяТаблицаДвижениеОС.МесяцВыбытия = ВременнаяТаблицаДвижениеОС.МесяцПоступления
//		|			ТОГДА 0
//		|		КОГДА ВременнаяТаблицаДвижениеОС.МесяцВыбытия = 0
//		|			ТОГДА 12 - ВременнаяТаблицаДвижениеОС.МесяцПоступления
//		|		КОГДА НЕ ВременнаяТаблицаДвижениеОС.МесяцВыбытия = ВременнаяТаблицаДвижениеОС.МесяцПоступления
//		|			ТОГДА ВременнаяТаблицаДвижениеОС.МесяцВыбытия - ВременнаяТаблицаДвижениеОС.МесяцПоступления - 1
//		|		ИНАЧЕ 12
//		|	КОНЕЦ КАК КоличествоПолныхМесяцев
//		|ПОМЕСТИТЬ ВременнаяТаблицаДвижениеОСРезультат
//		|ИЗ
//		|	ВременнаяТаблицаДвижениеОС КАК ВременнаяТаблицаДвижениеОС";
//		
//	Возврат ТекстЗапроса;
//	
//КонецФункции

// Функция возвращает текст запроса ВременнаяТаблицаЗатратыНаРемонт.
// 
Функция ТекстЗапроса_ВременнаяТаблицаЗатратыНаРемонт()
	// Затраты на ремонт.
	// 1 пакет - основные средства
	// 2 пакет - основные средства в аренде
	ТекстЗапроса =	
		"ВЫБРАТЬ
		|	ПараметрыУчетаОС.ГруппаНУ КАК ГруппаНУ,
		|	ПараметрыУчетаОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ХозрасчетныйОбороты.СуммаОборот КАК ЗатратыНаРемонт
		|ПОМЕСТИТЬ ВременнаяТаблицаЗатратыНаРемонт
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(
		|			&НачалоГода,
		|			&КонецГода,
		|			,
		|			Счет В (&СчетаЗатратНаРемонтОС),
		|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства),
		|			Организация = &Организация
		|				И (Субконто1 = &ОсновноеСредство
		|					ИЛИ &ОсновноеСредство = ЗНАЧЕНИЕ(Справочник.ОсновныеСредства.ПустаяСсылка)),
		|			,
		|			) КАК ХозрасчетныйОбороты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыУчетаОС.СрезПоследних(
		|				&КонецГода,
		|				Организация = &Организация
		|					И (ГруппаНУ = &ГруппаНУ
		|						ИЛИ &ГруппаНУ = ЗНАЧЕНИЕ(Справочник.ГруппыНалоговогоУчетаОС.ПустаяСсылка))
		|					И (ОсновноеСредство = &ОсновноеСредство
		|						ИЛИ &ОсновноеСредство = ЗНАЧЕНИЕ(Справочник.ОсновныеСредства.ПустаяСсылка))) КАК ПараметрыУчетаОС
		|		ПО ХозрасчетныйОбороты.Субконто1 = ПараметрыУчетаОС.ОсновноеСредство
		|ГДЕ
		|	НЕ ПараметрыУчетаОС.ГруппаНУ = ЗНАЧЕНИЕ(Справочник.ГруппыНалоговогоУчетаОС.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПараметрыУчетаОСВАренде.ГруппаНУ,
		|	ПараметрыУчетаОСВАренде.ОсновноеСредство,
		|	ХозрасчетныйОбороты.СуммаОборот
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(
		|			&НачалоГода,
		|			&КонецГода,
		|			,
		|			Счет В (&СчетаЗатратНаРемонтОС),
		|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства),
		|			Организация = &Организация
		|				И (Субконто1 = &ОсновноеСредство
		|					ИЛИ &ОсновноеСредство = ЗНАЧЕНИЕ(Справочник.ОсновныеСредства.ПустаяСсылка)),
		|			,
		|			) КАК ХозрасчетныйОбороты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыУчетаОСВАренде.СрезПоследних(
		|				&КонецГода,
		|				Организация = &Организация
		|					И (ГруппаНУ = &ГруппаНУ
		|						ИЛИ &ГруппаНУ = ЗНАЧЕНИЕ(Справочник.ГруппыНалоговогоУчетаОС.ПустаяСсылка))
		|					И (ОсновноеСредство = &ОсновноеСредство
		|						ИЛИ &ОсновноеСредство = ЗНАЧЕНИЕ(Справочник.ОсновныеСредства.ПустаяСсылка))) КАК ПараметрыУчетаОСВАренде
		|		ПО ХозрасчетныйОбороты.Субконто1 = ПараметрыУчетаОСВАренде.ОсновноеСредство
		|ГДЕ
		|	НЕ ПараметрыУчетаОСВАренде.ГруппаНУ = ЗНАЧЕНИЕ(Справочник.ГруппыНалоговогоУчетаОС.ПустаяСсылка)";
		
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция возвращает текст запроса ВременнаяТаблицаСписокГрупп.
// 
Функция ТекстЗапроса_ВременнаяТаблицаСписокГрупп()
	// Объединение данных.
	// Определение детализации расчета:
	// * для 5-й группы расчет для каждого ОС
	// * для групп 1,2,3,4,6 расчет для всей группы	
	ТекстЗапроса = 	
		"ВЫБРАТЬ
		|	ВременнаяТаблицаДвижениеОС.ГруппаНУ КАК ГруппаНУ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаДвижениеОС.ГруппаНУ = ЗНАЧЕНИЕ(Справочник.ГруппыНалоговогоУчетаОС.Группа5)
		|			ТОГДА ВременнаяТаблицаДвижениеОС.ОсновноеСредство
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ОсновныеСредства.ПустаяСсылка)
		|	КОНЕЦ КАК ОсновноеСредство,
		|	СУММА(ВременнаяТаблицаДвижениеОС.СуммаПоступления) КАК СуммаПоступления,
		|	СУММА(ВременнаяТаблицаДвижениеОС.СуммаВыбытия) КАК СуммаВыбытия,
		|	СУММА(ВременнаяТаблицаДвижениеОС.СуммаПоступленияНУ) КАК СуммаПоступленияНУ,
		|	СУММА(ВременнаяТаблицаДвижениеОС.СуммаВыбытияНУ) КАК СуммаВыбытияНУ,
		|	0 КАК БалансоваяСтоимостьНаКонецПрошлыйГод,
		|	0 КАК АмортизацияЗаПрошлыйГод,
		|	0 КАК СуммаПоступленияОстатокПрошлыйГод,
		|	0 КАК СуммаВыбытияОстатокПрошлыйГод,
		|	0 КАК БазаПрямолинейнойАмортизацииПрошлыйГод,
		|	0 КАК ЗатратыНаРемонт
		|ПОМЕСТИТЬ ВременнаяТаблицаСписокГрупп
		|ИЗ
		|	ВременнаяТаблицаДвижениеОС КАК ВременнаяТаблицаДвижениеОС
		|
		|СГРУППИРОВАТЬ ПО
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаДвижениеОС.ГруппаНУ = ЗНАЧЕНИЕ(Справочник.ГруппыНалоговогоУчетаОС.Группа5)
		|			ТОГДА ВременнаяТаблицаДвижениеОС.ОсновноеСредство
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ОсновныеСредства.ПустаяСсылка)
		|	КОНЕЦ,
		|	ВременнаяТаблицаДвижениеОС.ГруппаНУ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаНалоговаяАмортизация.ГруппаНУ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаНалоговаяАмортизация.ГруппаНУ = ЗНАЧЕНИЕ(Справочник.ГруппыНалоговогоУчетаОС.Группа5)
		|			ТОГДА ВременнаяТаблицаНалоговаяАмортизация.ОсновноеСредство
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ОсновныеСредства.ПустаяСсылка)
		|	КОНЕЦ,
		|	0,
		|	0,
		|	0,
		|	0,
		|	ВременнаяТаблицаНалоговаяАмортизация.БалансоваяСтоимостьНаКонецПрошлыйГод,
		|	ВременнаяТаблицаНалоговаяАмортизация.АмортизацияЗаПрошлыйГод,
		|	ВременнаяТаблицаНалоговаяАмортизация.СуммаПоступленияОстатокПрошлыйГод,
		|	ВременнаяТаблицаНалоговаяАмортизация.СуммаВыбытияОстатокПрошлыйГод,
		|	ВременнаяТаблицаНалоговаяАмортизация.БазаПрямолинейнойАмортизацииПрошлыйГод,
		|	0
		|ИЗ
		|	ВременнаяТаблицаНалоговаяАмортизация КАК ВременнаяТаблицаНалоговаяАмортизация
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаЗатратыНаРемонт.ГруппаНУ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаЗатратыНаРемонт.ГруппаНУ = ЗНАЧЕНИЕ(Справочник.ГруппыНалоговогоУчетаОС.Группа5)
		|			ТОГДА ВременнаяТаблицаЗатратыНаРемонт.ОсновноеСредство
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ОсновныеСредства.ПустаяСсылка)
		|	КОНЕЦ,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	ВременнаяТаблицаЗатратыНаРемонт.ЗатратыНаРемонт
		|ИЗ
		|	ВременнаяТаблицаЗатратыНаРемонт КАК ВременнаяТаблицаЗатратыНаРемонт";	
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция возвращает текст запроса ВременнаяТаблицаПолныйСписок.
// 
Функция ТекстЗапроса_ВременнаяТаблицаПолныйСписок()
	// Расчет сумм поступления и выбытия ОС по группам НУ.
	ТекстЗапроса =  
		"ВЫБРАТЬ
		|	ВременнаяТаблицаСписокГрупп.ГруппаНУ КАК ГруппаНУ,
		|	ВременнаяТаблицаСписокГрупп.ОсновноеСредство КАК ОсновноеСредство,
		|	СУММА(ВременнаяТаблицаСписокГрупп.СуммаПоступления) КАК СуммаПоступления,
		|	СУММА(ВременнаяТаблицаСписокГрупп.СуммаВыбытия) КАК СуммаВыбытия,
		|	СУММА(ВременнаяТаблицаСписокГрупп.СуммаПоступленияНУ) КАК СуммаПоступленияНУ,
		|	СУММА(ВременнаяТаблицаСписокГрупп.СуммаВыбытияНУ) КАК СуммаВыбытияНУ,		
		|	СУММА(ВременнаяТаблицаСписокГрупп.БалансоваяСтоимостьНаКонецПрошлыйГод) КАК БалансоваяСтоимостьНаКонецПрошлыйГод,
		|	СУММА(ВременнаяТаблицаСписокГрупп.АмортизацияЗаПрошлыйГод) КАК АмортизацияЗаПрошлыйГод,
		|	СУММА(ВременнаяТаблицаСписокГрупп.СуммаПоступленияОстатокПрошлыйГод) КАК СуммаПоступленияОстатокПрошлыйГод,
		|	СУММА(ВременнаяТаблицаСписокГрупп.СуммаВыбытияОстатокПрошлыйГод) КАК СуммаВыбытияОстатокПрошлыйГод,
		|	СУММА(ВременнаяТаблицаСписокГрупп.БазаПрямолинейнойАмортизацииПрошлыйГод) КАК БазаПрямолинейнойАмортизацииПрошлыйГод,
		|	СУММА(ВременнаяТаблицаСписокГрупп.ЗатратыНаРемонт) КАК ЗатратыНаРемонт
		|ПОМЕСТИТЬ ВременнаяТаблицаПолныйСписок
		|ИЗ
		|	ВременнаяТаблицаСписокГрупп КАК ВременнаяТаблицаСписокГрупп
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаСписокГрупп.ГруппаНУ,
		|	ВременнаяТаблицаСписокГрупп.ОсновноеСредство";
		
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция возвращает текст запроса ВременнаяТаблицаКоличествоВГруппах.
// 
Функция ТекстЗапроса_ВременнаяТаблицаКоличествоВГруппах()
	// Количество ОС в группах.
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПараметрыУчетаОССрезПоследних.ГруппаНУ КАК ГруппаНУ,
		|	ПараметрыУчетаОССрезПоследних.ОсновноеСредство КАК ОсновноеСредство,
		|	ПараметрыУчетаОССрезПоследних.ДатаВводаВЭксплуатацию КАК ДатаВводаВЭксплуатацию
		|ПОМЕСТИТЬ ВременнаяТаблицаСписокОС
		|ИЗ
		|	РегистрСведений.ПараметрыУчетаОС.СрезПоследних(
		|			&КонецГода,
		|			(ГруппаНУ = &ГруппаНУ
		|				ИЛИ &ГруппаНУ = ЗНАЧЕНИЕ(Справочник.ГруппыНалоговогоУчетаОС.ПустаяСсылка))
		|				И (ОсновноеСредство = &ОсновноеСредство
		|					ИЛИ &ОсновноеСредство = ЗНАЧЕНИЕ(Справочник.ОсновныеСредства.ПустаяСсылка))) КАК ПараметрыУчетаОССрезПоследних
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияОС.СрезПоследних(
		|				&КонецГода,
		|				ОсновноеСредство = &ОсновноеСредство
		|					ИЛИ &ОсновноеСредство = ЗНАЧЕНИЕ(Справочник.ОсновныеСредства.ПустаяСсылка)) КАК СостоянияОССрезПоследних
		|		ПО ПараметрыУчетаОССрезПоследних.ОсновноеСредство = СостоянияОССрезПоследних.ОсновноеСредство
		|ГДЕ
		|	НЕ ПараметрыУчетаОССрезПоследних.ГруппаНУ = ЗНАЧЕНИЕ(Справочник.ГруппыНалоговогоУчетаОС.ПустаяСсылка)
		|	И СостоянияОССрезПоследних.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийОС.ПринятоКУчету)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаСписокОС.ГруппаНУ КАК ГруппаНУ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаСписокОС.ГруппаНУ = ЗНАЧЕНИЕ(Справочник.ГруппыНалоговогоУчетаОС.Группа5)
		|			ТОГДА ВременнаяТаблицаСписокОС.ОсновноеСредство
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ОсновныеСредства.ПустаяСсылка)
		|	КОНЕЦ КАК ОсновноеСредство,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВременнаяТаблицаСписокОС.ОсновноеСредство) КАК КоличествоВГруппах,
		|	МАКСИМУМ(ВременнаяТаблицаСписокОС.ДатаВводаВЭксплуатацию) КАК ДатаВводаВЭксплуатацию
		|ПОМЕСТИТЬ ВременнаяТаблицаКоличествоВГруппах
		|ИЗ
		|	ВременнаяТаблицаСписокОС КАК ВременнаяТаблицаСписокОС
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаСписокОС.ГруппаНУ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаСписокОС.ГруппаНУ = ЗНАЧЕНИЕ(Справочник.ГруппыНалоговогоУчетаОС.Группа5)
		|			ТОГДА ВременнаяТаблицаСписокОС.ОсновноеСредство
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ОсновныеСредства.ПустаяСсылка)
		|	КОНЕЦ";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция возвращает текст итогового запроса.
// 
Функция ТекстЗапроса_ДляДокументаЗакрытиеМесяца()
	// Результат.
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ВременнаяТаблицаПолныйСписок.ГруппаНУ КАК ГруппаНУ,
		|	ВременнаяТаблицаПолныйСписок.ГруппаНУ.Код КАК ГруппаНУКод,
		|	ВременнаяТаблицаПолныйСписок.ОсновноеСредство КАК ОсновноеСредство,
		|	НормыАмортизацииГруппНалоговогоУчетаОССрезПоследних.НормаАмортизации КАК НормаАмортизации,
		|	НормыАмортизацииГруппНалоговогоУчетаОССрезПоследних.НормаАмортизацииДополнительная КАК НормаАмортизацииДополнительная,
		|	ВременнаяТаблицаПолныйСписок.СуммаПоступления КАК СуммаПоступления,
		|	ВременнаяТаблицаПолныйСписок.СуммаВыбытия КАК СуммаВыбытия,
		
		|	ВременнаяТаблицаПолныйСписок.СуммаПоступленияНУ КАК СуммаПоступленияНУ,
		|	ВременнаяТаблицаПолныйСписок.СуммаВыбытияНУ КАК СуммаВыбытияНУ,
		
		|	ВременнаяТаблицаПолныйСписок.БалансоваяСтоимостьНаКонецПрошлыйГод КАК БалансоваяСтоимостьНаКонецПрошлыйГод,
		|	ВременнаяТаблицаПолныйСписок.АмортизацияЗаПрошлыйГод КАК АмортизацияЗаПрошлыйГод,

		|	ВременнаяТаблицаПолныйСписок.СуммаПоступленияОстатокПрошлыйГод КАК СуммаПоступленияОстатокПрошлыйГод,
		|	ВременнаяТаблицаПолныйСписок.СуммаВыбытияОстатокПрошлыйГод КАК СуммаВыбытияОстатокПрошлыйГод,
		|	ВременнаяТаблицаПолныйСписок.БазаПрямолинейнойАмортизацииПрошлыйГод КАК БазаПрямолинейнойАмортизацииПрошлыйГод,
		
		|	ВременнаяТаблицаПолныйСписок.ЗатратыНаРемонт КАК ЗатратыНаРемонт,
		
		|	ЕСТЬNULL(ВременнаяТаблицаКоличествоВГруппах.ДатаВводаВЭксплуатацию, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаВводаВЭксплуатацию,
		|	ЕСТЬNULL(ВременнаяТаблицаКоличествоВГруппах.КоличествоВГруппах, 0) КАК КоличествоВГруппах
		|ИЗ
		|	ВременнаяТаблицаПолныйСписок КАК ВременнаяТаблицаПолныйСписок
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКоличествоВГруппах КАК ВременнаяТаблицаКоличествоВГруппах
		|		ПО ВременнаяТаблицаПолныйСписок.ГруппаНУ = ВременнаяТаблицаКоличествоВГруппах.ГруппаНУ
		|			И ВременнаяТаблицаПолныйСписок.ОсновноеСредство = ВременнаяТаблицаКоличествоВГруппах.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НормыАмортизацииГруппНалоговогоУчетаОС.СрезПоследних(&КонецГода, ) КАК НормыАмортизацииГруппНалоговогоУчетаОССрезПоследних
		|		ПО ВременнаяТаблицаПолныйСписок.ГруппаНУ = НормыАмортизацииГруппНалоговогоУчетаОССрезПоследних.ГруппаНалоговогоУчетаОС
		|
		|УПОРЯДОЧИТЬ ПО
		|	ГруппаНУКод";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция возвращает текст запроса ТекстРазделителяЗапросовПакета.
// 
Функция ТекстЗапроса_УничтожитьВременнуюТаблицу(ИмяВременнойТаблицы)
	
	Возврат "УНИЧТОЖИТЬ " + ИмяВременнойТаблицы;	
	
КонецФункции

#КонецОбласти

#Область ПроцедурыРасчетаНалоговойАмортизацииДляМонитораНП

// Функция возвращает текст запроса ВременнаяТаблицаСписокГрупп.
// 
Функция ТекстЗапроса_ВременнаяТаблицаСписокГрупп_МониторНП()
	ТекстЗапроса = 	
		"ВЫБРАТЬ
		|	ВременнаяТаблицаДвижениеОС.ГруппаНУ КАК ГруппаНУ,
		|	ВременнаяТаблицаДвижениеОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ВременнаяТаблицаДвижениеОС.Регистратор КАК Регистратор,
		|	ВременнаяТаблицаДвижениеОС.СуммаПоступления КАК СуммаПоступления,
		|	ВременнаяТаблицаДвижениеОС.СуммаВыбытия КАК СуммаВыбытия,
		|	ВременнаяТаблицаДвижениеОС.СуммаПоступленияНУ КАК СуммаПоступленияНУ,
		|	ВременнаяТаблицаДвижениеОС.СуммаВыбытияНУ КАК СуммаВыбытияНУ
		|ПОМЕСТИТЬ ВременнаяТаблицаСписокГрупп
		|ИЗ
		|	ВременнаяТаблицаДвижениеОС КАК ВременнаяТаблицаДвижениеОС";	
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция возвращает текст запроса ВременнаяТаблицаПолныйСписок.
// 
Функция ТекстЗапроса_ДляМонитораНП()
	// Расчет сумм поступления и выбытия ОС по группам НУ.
	ТекстЗапроса =  
		"ВЫБРАТЬ
		|	ВременнаяТаблицаСписокГрупп.ГруппаНУ КАК ГруппаНУ,
		|	ВременнаяТаблицаСписокГрупп.ОсновноеСредство КАК ОсновноеСредство,
		|	ВременнаяТаблицаСписокГрупп.Регистратор КАК Документ,
		|	ВременнаяТаблицаСписокГрупп.СуммаПоступления КАК СуммаПоступления,
		|	ВременнаяТаблицаСписокГрупп.СуммаВыбытия КАК СуммаВыбытия,
		|	ВременнаяТаблицаСписокГрупп.СуммаПоступленияНУ КАК СуммаПоступленияНУ,
		|	ВременнаяТаблицаСписокГрупп.СуммаВыбытияНУ КАК СуммаВыбытияНУ,
		|	0 КАК ЗатратыНаРемонт
		|ПОМЕСТИТЬ ВременнаяТаблицаСводМониторНП
		|ИЗ
		|	ВременнаяТаблицаСписокГрупп КАК ВременнаяТаблицаСписокГрупп
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаЗатратыНаРемонт.ГруппаНУ,
		|	ВременнаяТаблицаЗатратыНаРемонт.ОсновноеСредство,
		|	ВременнаяТаблицаЗатратыНаРемонт.Регистратор,
		|	0,
		|	0,
		|	0,
		|	0,
		|	ВременнаяТаблицаЗатратыНаРемонт.ЗатратыНаРемонт
		|ИЗ
		|	ВременнаяТаблицаЗатратыНаРемонт КАК ВременнаяТаблицаЗатратыНаРемонт
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаСводМониторНП.ГруппаНУ КАК ГруппаНУ,
		|	ВременнаяТаблицаСводМониторНП.ОсновноеСредство КАК ОсновноеСредство,
		|	ВременнаяТаблицаСводМониторНП.Документ КАК Документ,
		|	СУММА(ВременнаяТаблицаСводМониторНП.СуммаПоступления) КАК СуммаПоступления,
		|	СУММА(ВременнаяТаблицаСводМониторНП.СуммаВыбытия) КАК СуммаВыбытия,
		|	СУММА(ВременнаяТаблицаСводМониторНП.СуммаПоступленияНУ) КАК СуммаПоступленияНУ,
		|	СУММА(ВременнаяТаблицаСводМониторНП.СуммаВыбытияНУ) КАК СуммаВыбытияНУ,
		|	СУММА(ВременнаяТаблицаСводМониторНП.ЗатратыНаРемонт) КАК ЗатратыНаРемонт
		|ИЗ
		|	ВременнаяТаблицаСводМониторНП КАК ВременнаяТаблицаСводМониторНП
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаСводМониторНП.ОсновноеСредство,
		|	ВременнаяТаблицаСводМониторНП.Документ,
		|	ВременнаяТаблицаСводМониторНП.ГруппаНУ
		|
		|УПОРЯДОЧИТЬ ПО
		|	ГруппаНУ,
		|	ОсновноеСредство,
		|	Документ";
		
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти