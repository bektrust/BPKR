
#Область ОбработчикиСобытийФормы

// Процедура - обработчик события ПриСозданииНаСервере.
// В процедуре осуществляется
// - инициализация реквизитов формы,
// - установка параметров функциональных опций формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	СписокЗначений = Новый СписокЗначений;
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Организации.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Организации КАК Организации";
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СписокЗначений.Добавить(ВыборкаДетальныеЗаписи.Ссылка);
	КонецЦикла;
	СписокОрганизаций = СписокЗначений;
	
	//БанковскиеВыписки.Параметры.УстановитьЗначениеПараметра("ВалютаРегламентированногоУчета", 
	//	ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета());
		
	УстановитьУсловноеОформление();
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	
	Организация = Настройки.Получить("Организация");
	БанковскийСчет = Настройки.Получить("БанковскийСчет");
	Контрагент = Настройки.Получить("Контрагент");
	
	Если ЗначениеЗаполнено(Организация) Тогда
		НовыйПараметр = Новый ПараметрВыбора("Отбор.Владелец", Организация);
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(НовыйПараметр);
		НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
		Элементы.БанковскийСчет.ПараметрыВыбора = НовыеПараметры;
	Иначе
		НовыйМассив = Новый Массив();
		Для каждого Элемент Из СписокОрганизаций Цикл
			НовыйМассив.Добавить(Элемент.Значение);
		КонецЦикла;
		ФиксированныйМассивОрганизации = Новый ФиксированныйМассив(НовыйМассив);
		НовыйПараметр = Новый ПараметрВыбора("Отбор.Владелец", ФиксированныйМассивОрганизации);
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(НовыйПараметр);
		НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
		Элементы.БанковскийСчет.ПараметрыВыбора = НовыеПараметры;
	КонецЕсли;
	
	РаботаСОтборамиКлиентСервер.УстановитьЭлементОтбораСписка(БанковскиеВыписки, "Организация", Организация, ЗначениеЗаполнено(Организация));
	РаботаСОтборамиКлиентСервер.УстановитьЭлементОтбораСписка(БанковскиеВыписки, "Контрагент", Контрагент, ЗначениеЗаполнено(Контрагент));
	РаботаСОтборамиКлиентСервер.УстановитьЭлементОтбораСписка(БанковскиеВыписки, "БанковскийСчет", БанковскийСчет, ЗначениеЗаполнено(БанковскийСчет));
	
КонецПроцедуры // ПриЗагрузкеДанныхИзНастроекНаСервере()

// Процедура - обработчик события ПриИзменении поля ввода БанковскийСчет.
//
&НаКлиенте
Процедура БанковскийСчетПриИзменении(Элемент)
	
	РаботаСОтборамиКлиентСервер.УстановитьЭлементОтбораСписка(БанковскиеВыписки, "БанковскийСчет", БанковскийСчет, ЗначениеЗаполнено(БанковскийСчет));
	
КонецПроцедуры // БанковскийСчетПриИзменении()

// Процедура - обработчик события ПриИзменении поля ввода Контрагент.
//
&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	РаботаСОтборамиКлиентСервер.УстановитьЭлементОтбораСписка(БанковскиеВыписки, "Контрагент", Контрагент, ЗначениеЗаполнено(Контрагент));
	
КонецПроцедуры // КонтрагентПриИзменении()

// Процедура - обработчик события ПриИзменении поля ввода Организация.
//
&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Организация) Тогда
		
		НовыйПараметр = Новый ПараметрВыбора("Отбор.Владелец", Организация);
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(НовыйПараметр);
		НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
		Элементы.БанковскийСчет.ПараметрыВыбора = НовыеПараметры;
		
	Иначе
		
		НовыйМассив = Новый Массив();
		Для каждого Элемент Из СписокОрганизаций Цикл
			НовыйМассив.Добавить(Элемент.Значение);
		КонецЦикла;
		ФиксированныйМассивОрганизации = Новый ФиксированныйМассив(НовыйМассив);
		НовыйПараметр = Новый ПараметрВыбора("Отбор.Владелец", ФиксированныйМассивОрганизации);
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(НовыйПараметр);
		НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
		Элементы.БанковскийСчет.ПараметрыВыбора = НовыеПараметры;
		
	КонецЕсли;
	
	РаботаСОтборамиКлиентСервер.УстановитьЭлементОтбораСписка(БанковскиеВыписки, "Организация", Организация, ЗначениеЗаполнено(Организация));
	
КонецПроцедуры // ОрганизацияПриИзменении()

&НаКлиенте
Процедура БанковскиеВыпискиПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("Подключаемый_ОбработатьАктивизациюСтрокиСписка", 0.2, Истина);
	
КонецПроцедуры // БанковскиеВыпискиПриАктивизацииСтроки()

// Процедура - обработчик события ОбработкаОповещения.
//
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ПлатежноеПоручениеВходящее" 
		Или ИмяСобытия = "Запись_ОплатаПлатежнойКартой"Тогда
		Подключаемый_ОбработатьАктивизациюСтрокиСписка();
	КонецЕсли;
	
КонецПроцедуры // ОбработкаОповещения()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура настройки условного оформления форм и динамических списков .
//
&НаСервере
Процедура УстановитьУсловноеОформление()

	БанковскиеВыписки.УсловноеОформление.Элементы.Очистить();
	
	// Таблица БанковскиеВыписки.
	Элемент = БанковскиеВыписки.УсловноеОформление.Элементы.Добавить();
	// Поля.	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, "СуммаПриход");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, "СуммаРасход");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, "Валюта");
	// Условие.	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Элемент.Отбор, "СуммаРасход", ВидСравненияКомпоновкиДанных.НеРавно, 0);
	// Оформление.
	Элемент.Оформление.УстановитьЗначениеПараметра("ВыделятьОтрицательные", Истина);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработатьАктивизациюСтрокиСписка()
	
	ТекущаяСтрока = Элементы.БанковскиеВыписки.ТекущиеДанные;
	Если ТекущаяСтрока <> Неопределено Тогда
		СтруктураДанные = ПолучитьДанныеПоБанковскомуСчету(ТекущаяСтрока.Дата, ТекущаяСтрока.БанковскийСчет);
		ЗаполнитьЗначенияСвойств(ЭтаФорма, СтруктураДанные);
		Элементы.СостояниеБанковскогоСчета.Заголовок = Формат(ТекущаяСтрока.Дата, "ДЛФ=D");
	Иначе
		ИнформацияСуммаВалКонечныйОстаток = 0;
		ИнформацияСуммаВалНачальныйОстаток = 0;
		ИнформацияСуммаВалПриход = 0;
		ИнформацияСуммаВалРасход = 0;
		Элементы.СостояниеБанковскогоСчета.Заголовок = "";
	КонецЕсли;
	
КонецПроцедуры // ОбработатьАктивизациюСтрокиСписка()

&НаСервереБезКонтекста
Функция ПолучитьДанныеПоБанковскомуСчету(Период, БанковскийСчет)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ХозрасчетныйОстаткиИОбороты.СуммаНачальныйОстаток КАК ИнформацияСуммаВалНачальныйОстаток,
		|	ХозрасчетныйОстаткиИОбороты.СуммаОборотДт КАК ИнформацияСуммаВалПриход,
		|	ХозрасчетныйОстаткиИОбороты.СуммаОборотКт КАК ИнформацияСуммаВалРасход,
		|	ХозрасчетныйОстаткиИОбороты.СуммаКонечныйОстаток КАК ИнформацияСуммаВалКонечныйОстаток
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(&НачалоПериода, &КонецПериода, День, , Счет = &Счет, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ДенежныеСредства), Субконто1 = &БанковскийСчет) КАК ХозрасчетныйОстаткиИОбороты";
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(Период));
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(Период));
	Запрос.УстановитьПараметр("Счет", БанковскийСчет.СчетУчета);
	Запрос.УстановитьПараметр("БанковскийСчет", БанковскийСчет);
	
	ВыборкаРезультата = Запрос.Выполнить().Выбрать();
	
	Если ВыборкаРезультата.Следующий() Тогда
		СтруктураВозврата = Новый Структура("ИнформацияСуммаВалНачальныйОстаток, ИнформацияСуммаВалКонечныйОстаток, ИнформацияСуммаВалПриход, ИнформацияСуммаВалРасход");
		ЗаполнитьЗначенияСвойств(СтруктураВозврата, ВыборкаРезультата);
		Возврат СтруктураВозврата;
	Иначе
		Возврат Новый Структура(
			"ИнформацияСуммаВалКонечныйОстаток, ИнформацияСуммаВалНачальныйОстаток, ИнформацияСуммаВалПриход, ИнформацияСуммаВалРасход",
			0,0,0,0);
	КонецЕсли;
	
КонецФункции // ПолучитьДанныеПобанковскомуСчету()

#КонецОбласти