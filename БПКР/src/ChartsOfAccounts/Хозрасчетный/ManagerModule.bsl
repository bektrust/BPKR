#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

// Возвращает описание блокируемых реквизитов.
//
// Возвращаемое значение:
//  Массив - содержит строки в формате ИмяРеквизита[;ИмяЭлементаФормы,...]
//           где ИмяРеквизита - имя реквизита объекта, ИмяЭлементаФормы - имя элемента формы,
//           связанного с реквизитом.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;
	
	БлокируемыеРеквизиты.Добавить("Код");
	БлокируемыеРеквизиты.Добавить("Родитель");
	БлокируемыеРеквизиты.Добавить("ПарныйСчет");
	БлокируемыеРеквизиты.Добавить("Наименование");
	БлокируемыеРеквизиты.Добавить("Вид");
	БлокируемыеРеквизиты.Добавить("Временный");
	БлокируемыеРеквизиты.Добавить("Забалансовый");
	БлокируемыеРеквизиты.Добавить("ЗапретитьИспользоватьВПроводках");
	БлокируемыеРеквизиты.Добавить("Валютный");
	БлокируемыеРеквизиты.Добавить("Количественный");
	БлокируемыеРеквизиты.Добавить("ВидыСубконто");
	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции

// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Процедура - Заполнить по умолчанию
// Выполняет первоночальное заполнение
Процедура ЗаполнитьПоУмолчанию() Экспорт 
	// Если счет является родителем- то этот счет группа
	// у группы не может быть субконто		
	
	// получение всех родителей
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	Хозрасчетный.Родитель КАК Ссылка,
		|	Хозрасчетный.Родитель.Код КАК РодительКод
		|ИЗ
		|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
		|ГДЕ
		|	НЕ Хозрасчетный.Родитель.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	РодительКод";
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ПланСчетовОбъект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();	
		ПланСчетовОбъект.ЗапретитьИспользоватьВПроводках = Истина;
		
		Попытка
			ПланСчетовОбъект.Записать();
		Исключение
		КонецПопытки;
	КонецЦикла;
	
	// Заполнение парных счетов.
	МассивЭлементов = Новый Массив; 	
	КлассификаторXML = ПланыСчетов.Хозрасчетный.ПолучитьМакет("СписокПарныхСчетов").ПолучитьТекст();
	КлассификаторТаблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные; 
	
	Для Каждого СтрокаТаблицыЗначений Из КлассификаторТаблица Цикл
		КодСчетаУчета = СокрЛП(СтрокаТаблицыЗначений.КодСчетаУчета);
		КодПарныйСчет = СокрЛП(СтрокаТаблицыЗначений.КодПарногоСчетаУчета);
		
		// Получение ссылок
		СчетУчета = ПланыСчетов.Хозрасчетный.НайтиПоКоду(КодСчетаУчета);
		СчетУчетаПарный = ПланыСчетов.Хозрасчетный.НайтиПоКоду(КодПарныйСчет);
		Если СчетУчета.Пустая() ИЛИ СчетУчетаПарный.Пустая() Тогда
			Продолжить;
		КонецЕсли;
		
		// Получение объектов
		СчетУчетаОбъект = СчетУчета.ПолучитьОбъект();
		СчетУчетаОбъект.ПарныйСчет = СчетУчетаПарный;
		СчетУчетаОбъектПарный = СчетУчетаПарный.ПолучитьОбъект();
		СчетУчетаОбъектПарный.ПарныйСчет = СчетУчета;
		
		Попытка
			СчетУчетаОбъект.Записать();
			СчетУчетаОбъектПарный.Записать();
		Исключение
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось завершить запись по причине: %1'"), 
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
		КонецПопытки;
	КонецЦикла;	

	// Заполнение временных счетов.
	СписокВременныхСчетов = Новый СписокЗначений;
	
	МассивЭлементов = Новый Массив; 	
	КлассификаторXML = ПланыСчетов.Хозрасчетный.ПолучитьМакет("СписокВременныхСчетов").ПолучитьТекст();
	КлассификаторТаблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные; 
	
	Для Каждого СтрокаТаблицыЗначений Из КлассификаторТаблица Цикл
		КодСчетаУчета = СокрЛП(СтрокаТаблицыЗначений.КодСчетаУчета);
		
		// Получение ссылок
		СчетУчета = ПланыСчетов.Хозрасчетный.НайтиПоКоду(КодСчетаУчета);
		Если СчетУчета.Пустая() Тогда
			Продолжить;
		КонецЕсли;
		
		СписокВременныхСчетов.Добавить(СчетУчета);
	КонецЦикла;	

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Хозрасчетный.Ссылка КАК Ссылка
		|ИЗ
		|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
		|ГДЕ
		|	Хозрасчетный.Ссылка В ИЕРАРХИИ(&СписокВременныхСчетов)";
	Запрос.УстановитьПараметр("СписокВременныхСчетов", СписокВременныхСчетов);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СчетУчетаОбъект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
		СчетУчетаОбъект.Временный = Истина;

		Попытка
			СчетУчетаОбъект.Записать();
		Исключение
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось завершить запись временного счета учета по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка'"), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьПоУмолчанию()	

Функция ПолучитьСчетаИсключения() Экспорт
	
	МассивСчетовИсключений = Новый Массив;
	
	
	Возврат Новый ФиксированныйМассив(МассивСчетовИсключений);
	
КонецФункции

#КонецОбласти

#Область ИнтерфейсПечати

Функция ПечатьПланаСчетов(ВыводитьОписания = Ложь)
	
	Макет = ПланыСчетов.Хозрасчетный.ПолучитьМакет("Описание");
	
	Шапка  = Макет.ПолучитьОбласть("Шапка");
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.Вывести(Шапка);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаПланаСчетов.Ссылка КАК Ссылка,
		|	ТаблицаПланаСчетов.ЭтоГруппа КАК ЭтоГруппа,
		|	ТаблицаПланаСчетов.Ссылка.Код КАК Код,
		|	ТаблицаПланаСчетов.Ссылка.Наименование КАК Наименование,
		|	ТаблицаПланаСчетов.Ссылка.Валютный КАК Валютный,
		|	ТаблицаПланаСчетов.Ссылка.Количественный КАК Количественный,
		|	ТаблицаПланаСчетов.Ссылка.Забалансовый КАК Забалансовый,
		|	ТаблицаПланаСчетов.Ссылка.Вид КАК Вид,
		|	ТаблицаПланаСчетов.Ссылка.ВидыСубконто.(
		|		НомерСтроки КАК НомерСтроки,
		|		ВидСубконто.Наименование КАК Наименование,
		|		ТолькоОбороты КАК ТолькоОбороты
		|	) КАК ВидыСубконто
		|ИЗ
		|	(ВЫБРАТЬ
		|		ПланСчетов1.Ссылка КАК Ссылка,
		|		ВЫБОР
		|			КОГДА КОЛИЧЕСТВО(ПланСчетов2.Ссылка) > 0
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ КАК ЭтоГруппа
		|	ИЗ
		|		ПланСчетов.Хозрасчетный КАК ПланСчетов1
		|			ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный КАК ПланСчетов2
		|			ПО ПланСчетов1.Ссылка = ПланСчетов2.Родитель
		|	ГДЕ
		|		ПланСчетов1.ПометкаУдаления = ЛОЖЬ
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ПланСчетов1.Ссылка) КАК ТаблицаПланаСчетов
		|
		|УПОРЯДОЧИТЬ ПО
		|	ТаблицаПланаСчетов.Ссылка.Порядок";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ЭтоГруппа Тогда
			Строка = Макет.ПолучитьОбласть("Группа");
		Иначе
			Строка = Макет.ПолучитьОбласть("Строка");
		КонецЕсли;
		
		Строка.Параметры.Заполнить(Выборка);
		
		Если Выборка.Вид = ВидСчета.Активный Тогда
			Строка.Параметры.Активность = "А";
		ИначеЕсли Выборка.Вид = ВидСчета.Пассивный Тогда
			Строка.Параметры.Активность = "П";
		Иначе
			Строка.Параметры.Активность = "АП";
		КонецЕсли;
		
		ВидыСубконто = Выборка.ВидыСубконто.Выбрать();
		Пока ВидыСубконто.Следующий() Цикл
			Строка.Параметры["Субконто" + ВидыСубконто.НомерСтроки] = ?(ВидыСубконто.ТолькоОбороты, "(об) ", "") + ВидыСубконто.Наименование;
		КонецЦикла;
		
		ТабДокумент.Вывести(Строка);
		
		Если ВыводитьОписания Тогда
			
			Попытка
				Описание = Макет.ПолучитьОбласть(ПланыСчетов[Выборка.Ссылка.Метаданные().Имя].ПолучитьИмяПредопределенного(Выборка.Ссылка));
				ТабДокумент.Вывести(Описание);
			Исключение
				// Запись в журнал регистрации не требуется
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ТабДокумент.ФиксацияСверху = 2;
	
	Возврат ТабДокумент;
	
КонецФункции

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Устанавливаем признак доступности печати покомплектно.
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Ложь;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПростойСписок") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ПростойСписок", "Простой список", ПечатьПланаСчетов());                                            
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СПодробнымиОписаниями") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "СПодробнымиОписаниями", "С подробными описаниями", ПечатьПланаСчетов(Истина));                                            
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	// При вводе кода счета с цифровой клавиатуры заменяем запятую на точку
	Если ТипЗнч(Параметры.СтрокаПоиска) = Тип("Строка") Тогда
		Параметры.СтрокаПоиска = СтрЗаменить(Параметры.СтрокаПоиска, ",", ".");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли