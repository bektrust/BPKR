#Область ОбработчикиСобытийФормы

// Процедура - обработчик события ПриСозданииНаСервере.
// В процедуре осуществляется
// - инициализация реквизитов формы,
// - установка параметров функциональных опций формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Установка реквизитов формы.
	ИмяТабличнойЧасти = "СтрокиОтчета";
	
	УстановитьУсловноеОформление();

КонецПроцедуры

// Процедура - обработчик события ПослеЗаписиНаСервере.
//
&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.КонтрольВеденияУчета
	КонтрольВеденияУчета.ПослеЗаписиНаСервере(ТекущийОбъект);
	// Конец СтандартныеПодсистемы.КонтрольВеденияУчета
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВсеСтрокиПриИзменении(Элемент)
	Если ВсеСтроки Тогда 
		Элементы.НастройкиСтрок.ОтборСтрок = Неопределено;
	Иначе 
		БухгалтерскийУчетКлиент.УстановитьОтборНаПодчиненнуюТабличнуюЧасть(ЭтаФорма, "НастройкиСтрок");
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицФормы

// Процедура - обработчик события ПриАктивизации строки табличной части СтрокиОтчета.
//
&НаКлиенте
Процедура СтрокиОтчетаПриАктивизацииСтроки(Элемент)
	Если НЕ ВсеСтроки Тогда
		БухгалтерскийУчетКлиент.УстановитьОтборНаПодчиненнуюТабличнуюЧасть(ЭтаФорма, "НастройкиСтрок");
	КонецЕсли;	
КонецПроцедуры

// Процедура - обработчик события ПередУдалением строки табличной части СтрокиОтчета.
//
&НаКлиенте
Процедура СтрокиОтчетаПередУдалением(Элемент, Отказ)
	БухгалтерскийУчетКлиент.УдалитьСтрокиПодчиненнойТабличнойЧасти(ЭтаФорма, "НастройкиСтрок");
КонецПроцедуры

// Процедура - обработчик события ПриНачалеРедактирования строки табличной части СтрокиОтчета.
//
&НаКлиенте
Процедура СтрокиОтчетаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда
		БухгалтерскийУчетКлиент.ДобавитьКлючСвязиВСтрокуТабличнойЧасти(ЭтаФорма);
		
		Если НЕ ВсеСтроки Тогда
			БухгалтерскийУчетКлиент.УстановитьОтборНаПодчиненнуюТабличнуюЧасть(ЭтаФорма, "НастройкиСтрок");
		КонецЕсли;	
	КонецЕсли;
КонецПроцедуры

// Процедура - обработчик события ПередНачаломДобавления строки табличной части НастройкиСтрок.
//
&НаКлиенте
Процедура НастройкиСтрокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = БухгалтерскийУчетКлиент.ПередНачаломДобавленияВПодчиненнуюТабличнуюЧасть(ЭтаФорма, Элемент.Имя);
	
	Если НЕ Отказ И ВсеСтроки Тогда 
		ТекстСообщения = НСтр("ru = 'Для добавления строки необходимо отключить режим ""Все строки"".'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЕсли;
	
	Если НЕ Отказ И Элементы.СтрокиОтчета.ТекущиеДанные.ЭтоЗаголовок Тогда 
		ТекстСообщения = НСтр("ru = 'Для заголовков и итоговых строк настройки не задаются. Необходимо настраивать детальные строки отчета.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЕсли;	
		
КонецПроцедуры

// Процедура - обработчик события ПриНачалеРедактирования строки табличной части НастройкиСтрок.
//
&НаКлиенте
Процедура НастройкиСтрокПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда
		БухгалтерскийУчетКлиент.ДобавитьКлючСвязиВСтрокуПодчиненнойТабличнойЧасти(ЭтаФорма, Элемент.Имя);
		
		СтрокаТабличнойЧасти = Элемент.ТекущиеДанные;
		СтрокаТабличнойЧасти.КодСтроки = Элементы.СтрокиОтчета.ТекущиеДанные.КодСтроки;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	Если Объект.СтрокиОтчета.Количество() > 0 Тогда 
		ОписаниеОповещения = Новый ОписаниеОповещения("ОтветНаВопросЗаполнитьНастройки", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Табличные части будут перезаполнены настройками по умолчанию. Продолжить выполнение операции?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60);	
	Иначе
		ЗаполнитьНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьНастройки(Команда)	
	ПроверитьНастройкиНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиРезультатовИнтерактивныхДействий

&НаКлиенте
Процедура ОтветНаВопросЗаполнитьНастройки(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Объект.СтрокиОтчета.Очистить();
		Объект.НастройкиСтрок.Очистить();
		
        ЗаполнитьНаСервере();
    КонецЕсли;
КонецПроцедуры // ЗаполнитьПоОснованию()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура настройки условного оформления форм и динамических списков .
//
&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	// Таблица СтрокиОтчета.
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("СтрокиОтчета");

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.СтрокиОтчета.ЭтоЗаголовок");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЦветаСтиля.ЦветФонаШапкиОтчета);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()
	СправочникОбъект = РеквизитФормыВЗначение("Объект");
	СправочникОбъект.ЗаполнитьНастройки();
	ЗначениеВРеквизитФормы(СправочникОбъект, "Объект");
	Модифицированность = Истина;
КонецПроцедуры

// Процедура проверяет заполненные настройки в зависимости от выбранной группы настроек. 
//
&НаСервере
Процедура ПроверитьНастройкиНаСервере()

	СтруктураТаблиц = Новый Структура();
	СтруктураТаблиц.Вставить("СтрокиОтчета", Объект.СтрокиОтчета.Выгрузить());
	СтруктураТаблиц.Вставить("НастройкиСтрок", Объект.НастройкиСтрок.Выгрузить());
	
	МассивСообщений = Справочники.НастройкиНалоговойДекларации.ПолучитьДанныеПроверкиНастроек(СтруктураТаблиц);	
							
	Для Каждого СтрокаМассива Из МассивСообщений Цикл		
		ОбщегоНазначения.СообщитьПользователю(СтрокаМассива);
	КонецЦикла;
	
КонецПроцедуры // ПроверитьНастройкиНаСервере()

#КонецОбласти
