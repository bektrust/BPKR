#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает описание блокируемых реквизитов.
//
// Возвращаемое значение:
//  Массив - содержит строки в формате ИмяРеквизита[;ИмяЭлементаФормы,...]
//           где ИмяРеквизита - имя реквизита объекта, ИмяЭлементаФормы - имя элемента формы,
//           связанного с реквизитом.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;
	
	БлокируемыеРеквизиты.Добавить("ВидОсновногоДоговора");
	БлокируемыеРеквизиты.Добавить("СтранаРезидентства");
	БлокируемыеРеквизиты.Добавить("ПризнакСтраны");
	БлокируемыеРеквизиты.Добавить("СЭЗ");
	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции

// Возвращает список реквизитов, которые не нужно редактировать
// с помощью обработки группового изменения объектов.
//
Функция РеквизитыНеРедактируемыеВГрупповойОбработке() Экспорт
	
	Результат = Новый Массив;
	
	Результат.Добавить("ИНН");
	Результат.Добавить("КодПоОКПО");
	Результат.Добавить("ОсновнойБанковскийСчет");
	
	Результат.Добавить("КонтактнаяИнформация.*");
	
	Возврат Результат;
	
КонецФункции

// Функция возвращает массив подчиненных контрагентов
//
// Параметры:
//  ГоловнойКонтрагент	 - СправочникСсылка.Контрагент	 - Головной контрагент, по которому нужно получить подчиненные контрагенты
// 
// Возвращаемое значение:
//  Контрагенты - Массив
//
Функция ПодчиненныеКонтрагенты(ГоловнойКонтрагент) Экспорт 

	Контрагенты = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Контрагенты.Ссылка КАК Контрагент
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.ГоловнойКонтрагент = &ГоловнойКонтрагент
		|	И НЕ Контрагенты.ПометкаУдаления";
	Запрос.УстановитьПараметр("ГоловнойКонтрагент", ГоловнойКонтрагент);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда 
		Контрагенты = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Контрагент");
	КонецЕсли;	

	Возврат Контрагенты
	
КонецФункции // ПодчиненныеКонтрагенты()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
	
// Функция определяет наличие дублей у контрагента.
// ИНН - ИНН проверяемого контрагента, Тип - Строка(12)
// Ссылка - Сам проверяемый контрагент, Тип - СправочникСсылка.Контрагенты
Функция ПроверитьДублиСправочникаКонтрагентыПоИНН(Знач ИНН, ИсключаяСсылку = Неопределено, ПроверкаПриЗаписи = Ложь) Экспорт
	
	Дубли = Новый Массив;
	
	Запрос = Новый Запрос;
	// Если при записи элемента ничего не нашлось в регистре дублей, 
	// или при интерактивной проверке выполним поиск дублей по справочнику Контрагенты
	Если Дубли.Количество() = 0 Тогда
		
		Запрос.Текст = 	"ВЫБРАТЬ
		               	|	Контрагенты.Ссылка
		               	|ИЗ
		               	|	Справочник.Контрагенты КАК Контрагенты
		               	|ГДЕ
		               	|	НЕ Контрагенты.ЭтоГруппа
		               	|	И НЕ Контрагенты.Ссылка = &Ссылка
		               	|	И Контрагенты.ИНН = &ИНН";
		Запрос.УстановитьПараметр("ИНН", СокрЛП(ИНН));
		Запрос.УстановитьПараметр("Ссылка", ИсключаяСсылку);
		
		ВыборкаДублей = Запрос.Выполнить().Выбрать();
		
		Пока ВыборкаДублей.Следующий() Цикл
			Дубли.Добавить(ВыборкаДублей.Ссылка);
		КонецЦикла;
		
	КонецЕсли;
			
	Возврат Дубли;
	
КонецФункции	

#КонецОбласти

#КонецЕсли