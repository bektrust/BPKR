#Область ОписаниеПеременных

&НаКлиенте
Перем ВыполняетсяОперация;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ФискальноеУстройство") Тогда
		ФискальноеУстройство = Параметры.ФискальноеУстройство;
	КонецЕсли;
	
	Если Параметры.Свойство("Организация") Тогда
		Организация = Параметры.Организация;
	КонецЕсли;

	Если Параметры.Свойство("ТипОперации") Тогда
		ТипОперации = Параметры.ТипОперации;
		Если ТипОперации = 1 Тогда
			Заголовок = НСтр("ru='Регистрация фискального накопителя'")
		ИначеЕсли ТипОперации = 3 Тогда
			Заголовок = НСтр("ru='Закрытие фискального накопителя'");
		Иначе
			Заголовок = НСтр("ru='Изменение параметров регистрации фискального накопителя'");
		КонецЕсли;
		Элементы.ТипОперации.Видимость = Ложь;
	Иначе
		ТипОперации = 2;
		Элементы.ТипОперации.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ВыполняетсяОперация = Ложь;
	
	Доступность = Ложь;
	
	ОповещениеПриЗавершении = Новый ОписаниеОповещения("ПолучениеПараметровФискальногоУстройства_Завершение", ЭтотОбъект);
	ОборудованиеЧекопечатающиеУстройстваКлиент.НачатьПолучениеПараметровФискальногоУстройства(ОповещениеПриЗавершении, УникальныйИдентификатор, ФискальноеУстройство);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучениеПараметровФискальногоУстройства_Завершение(РезультатВыполнения, Параметры) Экспорт
	
	ОчиститьСообщения();
	
	Если РезультатВыполнения.Результат Тогда
		ПараметрыККТ = РезультатВыполнения.ПараметрыККТ;
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыККТ);
		КодСистемыНалогообложения = ПараметрыККТ.КодСистемыНалогообложения; 
	Иначе
		ТекстСообщения = РезультатВыполнения.ОписаниеОшибки;
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли; 
	
	ОбновитьСостояниеФормы();
	
	Если ТипОперации = 1 Тогда // Регистрация ФН
		
		Если РезультатВыполнения.Результат И ПараметрыККТ.ПризнакФискализации Тогда
			Элементы.ОперацияПродолжить.Видимость = Ложь;
			ТекстСообщения = НСтр("ru='Фискальный накопитель уже зарегистрирован. Воспользуйтесь функцией изменение параметров регистрации фискального накопителя.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;                                                            
		
		Если НЕ ПризнакАвтономногоРежима Тогда 
			ПараметрыРегистрации = Новый Структура("ОрганизацияНазвание, ОрганизацияИНН, АдресПроведенияРасчетов");
			МенеджерОборудованияВызовСервераПереопределяемый.ЗаполнитьРеквизитыОрганизацииДляРегистрацииФН(Организация, ПараметрыРегистрации); 
			
			Если Не ПустаяСтрока(ПараметрыРегистрации.ОрганизацияНазвание) Тогда
				ОрганизацияНазвание = ПараметрыРегистрации.ОрганизацияНазвание;
			КонецЕсли; 
			Если Не ПустаяСтрока(ПараметрыРегистрации.ОрганизацияИНН) Тогда
				ОрганизацияИНН = ПараметрыРегистрации.ОрганизацияИНН;
			КонецЕсли; 
			Если Не ПустаяСтрока(ПараметрыРегистрации.АдресПроведенияРасчетов) Тогда
				АдресПроведенияРасчетов = ПараметрыРегистрации.АдресПроведенияРасчетов;
			КонецЕсли;
			
		КонецЕсли;	
		
	КонецЕсли;

	Если НЕ ПризнакАвтономногоРежима Тогда 
		Если РезультатВыполнения.Результат Тогда
			//Элементы.ОперацияПродолжить.Видимость = Ложь;
			ТекстСообщения = НСтр("ru='Подключаемое оборудование используется в онлаин режиме все параметры заполняются из ОФД.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;                                                            
	КонецЕсли;
	
	ОповещениеПриЗавершении = Новый ОписаниеОповещения("ПолучениеТекущегоСостоянияФискальногоУстройства_Завершение", ЭтотОбъект);
	ОборудованиеЧекопечатающиеУстройстваКлиент.НачатьПолучениеТекущегоСостоянияФискальногоУстройства(ОповещениеПриЗавершении, УникальныйИдентификатор, ФискальноеУстройство);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучениеТекущегоСостоянияФискальногоУстройства_Завершение(РезультатВыполнения, Параметры) Экспорт
	
	Доступность = Истина;
	
	Если РезультатВыполнения.Результат И РезультатВыполнения.СтатусСмены <> 1 Тогда
		Элементы.ОперацияПродолжить.Видимость = Ложь;
		ТекстСообщения = НСтр("ru='Операция доступна только при закрытой смене на фискальном устройстве. Закройте смену и повторите операцию.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПродолжитьОперациюПередПродолжением(РезультатВопроса, ПараметрыЗаписи) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ВыполняетсяОперация = Истина;
		ВыполнитьОперацию();
	КонецЕсли;
        
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьОперацию(Команда)
	
	Если ТипОперации = 3 И Не ВыполняетсяОперация Тогда
		Оповещение = Новый ОписаниеОповещения("ПродолжитьОперациюПередПродолжением", ЭтотОбъект);
		Сообщение = НСтр("ru='Закрытие фискального накопителя - необратимая операция.
		|В дальнейшем фискальный накопитель нельзя будет использовать.
		|Вы уверенны что хотите продолжить операцию?'");
		ПоказатьВопрос(Оповещение, Сообщение, РежимДиалогаВопрос.ДаНет);
		Возврат;
	КонецЕсли;
	
	ВыполнитьОперацию();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОперацию()
	
	Если Элементы.ГруппаРеквизитыОрганизации.Видимость Тогда
		Если Не ЗначениеЗаполнено(КодСистемыНалогообложения) Тогда
			ПоказатьПредупреждение(, НСтр("ru='Не указан не один код налогообложения.'"));
			Возврат;
		КонецЕсли;	
	КонецЕсли;
	
	ПараметрыНастройки = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыОперацииФискализацииНакопителя();
	ЗаполнитьЗначенияСвойств(ПараметрыНастройки, ЭтотОбъект);
	
	ПараметрыНастройки.КодСистемыНалогообложения = КодСистемыНалогообложения;
	
	
	
	Кассир = "";
	СтандартнаяОбработка = Истина;
	МенеджерОборудованияКлиентСерверПереопределяемый.ОбработкаЗаполненияИмяКассира(Кассир, СтандартнаяОбработка); 
	ПараметрыНастройки.Кассир = ?(Не СтандартнаяОбработка, Кассир, НСтр("ru='Администратор'")); 
	
	Закрыть(ПараметрыНастройки);  
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацияОтмена(Команда)
	
	Закрыть(Неопределено); 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСостояниеФормы()
	
	
	Элементы.ГруппаОператорФискальныхДанных.Видимость = ТипОперации <> 2;
	Элементы.ГруппаРеквизитыОрганизации.Видимость = ТипОперации <> 2;
	Элементы.ГруппаНастройкиККТ.Видимость = ТипОперации <> 2;
	
	
	Элементы.ОрганизацияИНН.ТолькоПросмотр = НЕ (ТипОперации = 1);
	Элементы.РегистрационныйНомерККТ.ТолькоПросмотр = НЕ (ТипОперации = 1);
	
КонецПроцедуры



&НаКлиенте
Процедура ТипОперацииПриИзменении(Элемент)
	
	ОбновитьСостояниеФормы();
	
КонецПроцедуры

#КонецОбласти