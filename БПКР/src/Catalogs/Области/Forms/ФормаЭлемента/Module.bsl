
#Область ПроцедурыОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Дата = ЗаполнитьМаксимальнойДатойПоОбласти();
	
	СозданиеНового = Объект.Ссылка.Пустая();
	
	СтруктураОбъектов = Новый Структура;
	СтруктураОбъектов.Вставить("Период", Дата);
	СтруктураОбъектов.Вставить("Область", Объект.Ссылка);
	
	РедактированиеПериодическихСведений.ПрочитатьНаборЗаписейПоСтруктуре(ЭтаФорма, "СтавкиЗемельногоНалогаПоОбластям", СтруктураОбъектов);
		
	УстановитьФункциональныеОпцииФормы();
	
КонецПроцедуры

// Процедура - обработчик события ПриОткрытии.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьПредставлениеВсехСтрок();
	
КонецПроцедуры

// Процедура - обработчик события ПриЗаписиНаСервере.
//
&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ДополнительныеСвойства = Новый Структура;
	
	Если СозданиеНового Тогда
		ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
	КонецЕсли;
	
	СтруктураОбъектов = Новый Структура;
	СтруктураОбъектов.Вставить("Период", Дата);
	СтруктураОбъектов.Вставить("Область", ТекущийОбъект.Ссылка);
		
	РедактированиеПериодическихСведений.ЗаписатьНаборЗаписейПоСтруктуре(
		ЭтаФорма,
		"СтавкиЗемельногоНалогаПоОбластям",
		СтруктураОбъектов,
		"ПредставлениеИнтервала",
		ДополнительныеСвойства);
				
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиШапкиФормы

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	Дата = НачалоМесяца(Дата);
	
	ДатаПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	Если ТребуетсяНастройкаАвторизацияИнтернетПоддержки() Тогда
		ТекстВопроса = НСтр("ru='Для заполнения ставок
			|необходимо подключиться к Интернет-поддержке пользователей.
			|Подключиться сейчас?'");
		ОписаниеОповещения = Новый ОписаниеОповещения("ПриПодключенииИнтернетПоддержки", ЭтотОбъект);
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Подключиться'"));
		Кнопки.Добавить(КодВозвратаДиалога.Отмена);
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки);
	Иначе
		ЗаполнитьСтавкиЗемельногоНалогаФрагмент();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыОбработчикиСобытийНабораЗаписейОбласти

// Процедура - обработчик события "ПриНачалеРедактирования" строки в наборе записей регистра сведений "Области".
//
&НаКлиенте
Процедура ОбластиНаборЗаписейПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда 
		// получаем текущую строку
		СтрокаТабличнойЧасти = Элемент.ТекущиеДанные;
		
		// не удалось получить- возвращаемся
		Если СтрокаТабличнойЧасти = Неопределено Тогда 
			Возврат;
		КонецЕсли;
		
		СтрокаТабличнойЧасти.Период = НачалоМесяца(Дата);
	КонецЕсли;
КонецПроцедуры

// Процедура - обработчик события "ПриОкончанииРедактирования" строки в наборе записей регистра сведений "Области".
//
&НаКлиенте
Процедура ОбластиНаборЗаписейПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	СтавкиЗемельногоНалогаПоОбластямНаборЗаписей.Сортировать("НижняяГраница");
	ОбновитьПредставлениеВсехСтрок();
КонецПроцедуры

// Процедура - обработчик события "ПриИзменении" для колонки "НижняяГраница" строки в наборе записей регистра сведений "Области".
//
&НаКлиенте
Процедура ОбластиНаборЗаписейНижняяГраницаПриИзменении(Элемент)
	
	ТекСтрЭлемент = Элементы.СтавкиЗемельногоНалогаПоОбластямНаборЗаписей.ТекущиеДанные;
	ТекСтрЭлемент.ПредставлениеИнтервала = ПолучитьПредставлениеИнтервала(СтавкиЗемельногоНалогаПоОбластямНаборЗаписей, ТекСтрЭлемент);
	
КонецПроцедуры

// Процедура - обработчик события "ПослеУдаления" ТЧ набора записей регистра сведений "Области".
//
&НаКлиенте
Процедура ОбластиНаборЗаписейПослеУдаления(Элемент)
	ОбновитьПредставлениеВсехСтрок();
КонецПроцедуры

&НаКлиенте
Процедура ОбластиНаборЗаписейПериодПриИзменении(Элемент)
	ТекДанные = Элементы.СтавкиЗемельногоНалогаПоОбластямНаборЗаписей.ТекущиеДанные;
	ТекДанные.Период = НачалоМесяца(ТекДанные.Период);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура устанавливает функциональные опции формы документа.
//
&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()
	Период = ТекущаяДатаСеанса();
	Организация = Справочники.Организации.ОрганизацияПоУмолчанию();
	ОбщегоНазначенияБПКлиентСервер.УстановитьПараметрыФункциональныхОпцийФормыДокумента(ЭтаФорма, Организация, Период);
КонецПроцедуры

&НаКлиенте
// Возвращает текстовое представление интервала.
//
Функция ПолучитьПредставлениеИнтервала(Таблица, ТекущаяСтрока)

	МаксНижняяГраница = Неопределено;
	СледующаяСтрока = Неопределено;
	Для Каждого СтрокаТЧ Из Таблица Цикл
		Если (МаксНижняяГраница = Неопределено Или МаксНижняяГраница >= СтрокаТЧ.НижняяГраница)
		   И ТекущаяСтрока.НижняяГраница <= СтрокаТЧ.НижняяГраница
		   И СтрокаТЧ <> ТекущаяСтрока
		   И ТекущаяСтрока.Период = СтрокаТЧ.Период Тогда
			СледующаяСтрока = СтрокаТЧ;
			МаксНижняяГраница = СтрокаТЧ.НижняяГраница;
		КонецЕсли;
	КонецЦикла;

	Если СледующаяСтрока = Неопределено Тогда
		ПредставлениеИнтервала = СтрШаблон(НСтр("ru = 'От %1'"), СокрЛП(ТекущаяСтрока.НижняяГраница));
	ИначеЕсли ТекущаяСтрока.НижняяГраница = СледующаяСтрока.НижняяГраница Тогда
		ПредставлениеИнтервала = НСтр("ru = 'ОШИБКА: такая нижняя граница уже есть.'");
	Иначе
		ПредставлениеИнтервала = СтрШаблон(НСтр("ru = 'От %1 до %2'"), 
									СокрЛП(ТекущаяСтрока.НижняяГраница), 
									СокрЛП(СледующаяСтрока.НижняяГраница));
	КонецЕсли;

	Возврат ПредставлениеИнтервала;

КонецФункции // ПолучитьПредставлениеИнтервала()

// Процедура обновляет значение в колонке ПредставлениеИнтервала набора регистра сведений Области.
//
&НаКлиенте
Процедура ОбновитьПредставлениеВсехСтрок()

	Для каждого ТекСтр Из СтавкиЗемельногоНалогаПоОбластямНаборЗаписей Цикл
		ТекСтр.ПредставлениеИнтервала = ПолучитьПредставлениеИнтервала(СтавкиЗемельногоНалогаПоОбластямНаборЗаписей, ТекСтр);
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСтавкиЗемельногоНалогаФрагмент()
	ЗаполнитьСтавкиЗемельногоНалогаНаКлиенте();	
КонецПроцедуры // ЗаполнитьСтавкиЗемельногоНалога()

&НаКлиенте
Процедура ЗаполнитьСтавкиЗемельногоНалогаНаКлиенте()
	
	ОписаниеОшибки = "";
	ЗаполнитьСтавкиЗемельногоНалогаНаСервере(ОписаниеОшибки);
	ОбновитьПредставлениеВсехСтрок();
	Модифицированность = Истина;
	
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		
		ОбработчикЗавершенияОбработкиОшибки = Новый ОписаниеОповещения(
			"ОбработатьОшибкуЗаполнитьСтавкиЗемельногоНалога",
			ЭтотОбъект);
		ДополнительныеПараметрыОбработкиОшибки =
			РаботаСКонтрагентамиКлиентБП.НовыйДополнительныеПараметрыОбработкиОшибки();
		ДополнительныеПараметрыОбработкиОшибки.ПредставлениеДействия    = НСтр("ru = 'Загрузка ставок земельного налога'");
		ДополнительныеПараметрыОбработкиОшибки.ИдентификаторМестаВызова = "ratesLandTaxRegions";
		ДополнительныеПараметрыОбработкиОшибки.Форма                    = ЭтотОбъект;
		
		РаботаСКонтрагентамиКлиентБП.ОбработатьОшибку(
			ОписаниеОшибки,
			ОбработчикЗавершенияОбработкиОшибки,
			ДополнительныеПараметрыОбработкиОшибки);
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьСтавкиЗемельногоНалогаНаКлиенте()

&НаКлиенте
Процедура ОбработатьОшибкуЗаполнитьСтавкиЗемельногоНалога(Результат, ДополнительныеПараметры) Экспорт
	Если Результат.ПовторитьДействие Тогда
		ЗаполнитьСтавкиЗемельногоНалогаНаКлиенте();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтавкиЗемельногоНалогаНаСервере(ОписаниеОшибки)
		
	Результат = РаботаСКонтрагентамиБП.РеквизитыСтавкиНалогов("ratesLandTaxRegions");
	
	Если ЗначениеЗаполнено(Результат.ОписаниеОшибки) Тогда 
		Возврат;
	КонецЕсли;	
	
	// Получение из XML.
	КлассификаторXML = Результат.КлассификаторXML;
	ТаблицаСтавкиЗемельногоНалога = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;
	ТаблицаСтавкиЗемельногоНалога.Индексы.Добавить("КодОбласти");
	
	ПериодЗаполнения = ОбщегоНазначенияБПСервер.ПолучитьПериодИзXML(КлассификаторXML);
	
	НаборЗаписей = РегистрыСведений.СтавкиЗемельногоНалогаПоОбластям.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Область.Установить(Объект.Ссылка);
	НаборЗаписей.Отбор.Период.Установить(ПериодЗаполнения);
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();
	
	НайденныеСтроки = ТаблицаСтавкиЗемельногоНалога.НайтиСтроки(Новый Структура("КодОбласти", Объект.Код));
	Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл 
		
		Запись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, НайденнаяСтрока);
		Запись.Период = ПериодЗаполнения;
		Запись.Область = Объект.Ссылка;
		
	КонецЦикла;

	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей, Истина, Истина, Истина);
	
	Дата = ПериодЗаполнения;
	
	СтруктураОбъектов = Новый Структура;
	СтруктураОбъектов.Вставить("Период", Дата);
	СтруктураОбъектов.Вставить("Область", Объект.Ссылка);
	
	РедактированиеПериодическихСведений.ПрочитатьНаборЗаписейПоСтруктуре(ЭтаФорма, "СтавкиЗемельногоНалогаПоОбластям", СтруктураОбъектов);
	
КонецПроцедуры // ЗаполнитьСтавкиЗемельногоНалогаНаСервере()

&НаСервере
Функция ЗаполнитьМаксимальнойДатойПоОбласти()
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СтавкиЗемельногоНалогаПоОбластям.Период КАК Период
		|ИЗ
		|	РегистрСведений.СтавкиЗемельногоНалогаПоОбластям КАК СтавкиЗемельногоНалогаПоОбластям
		|ГДЕ
		|	СтавкиЗемельногоНалогаПоОбластям.Область = &Область
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период УБЫВ";
	
	Запрос.УстановитьПараметр("Область", Объект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат НачалоМесяца(ТекущаяДатаСеанса());
	Иначе	
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Период;
	КонецЕсли;	
		
КонецФункции	

&НаСервере
Процедура ДатаПриИзмененииНаСервере()
	СтруктураОбъектов = Новый Структура;
	СтруктураОбъектов.Вставить("Период", Дата);
	СтруктураОбъектов.Вставить("Область", Объект.Ссылка);
	
	РедактированиеПериодическихСведений.ПрочитатьНаборЗаписейПоСтруктуре(ЭтаФорма, "СтавкиЗемельногоНалогаПоОбластям", СтруктураОбъектов);
КонецПроцедуры	

#КонецОбласти

#Область ОбработчикиБиблиотек

// ИнтернетПоддержкаПользователей
&НаСервере
Функция ТребуетсяНастройкаАвторизацияИнтернетПоддержки()
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей") Тогда
		МодульИнтернетПоддержкаПользователей = ОбщегоНазначения.ОбщийМодуль("ИнтернетПоддержкаПользователей");
		Возврат Не МодульИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки();
	КонецЕсли;
	Возврат Ложь;
КонецФункции

&НаКлиенте
Процедура ПриПодключенииИнтернетПоддержки(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтернетПоддержкаПользователей") Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПодключенияИнтернетПоддержки", ЭтотОбъект, ДополнительныеПараметры);
		МодульИнтернетПоддержкаПользователейКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ИнтернетПоддержкаПользователейКлиент");
		МодульИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(ОписаниеОповещения, ЭтотОбъект);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеПодключенияИнтернетПоддержки(Результат, ДополнительныеПараметры) Экспорт
	Если Не (ТипЗнч(Результат) = Тип("Структура")
		И ЗначениеЗаполнено(Результат.Логин)) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСтавкиЗемельногоНалогаФрагмент();	
КонецПроцедуры

// Конец ИнтернетПоддержкаПользователей

#КонецОбласти


