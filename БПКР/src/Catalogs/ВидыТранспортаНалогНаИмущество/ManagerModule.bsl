#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаполнитьСтавкиВидовТранспорта(ДополнительныеПараметры) Экспорт

	Результат = РаботаСКонтрагентамиБП.РеквизитыСтавкиНалогов("ratesTransportTypes");
	
	Если ЗначениеЗаполнено(Результат.ОписаниеОшибки) Тогда 
		Возврат;
	КонецЕсли;	
	
	// Получение из XML.
	КлассификаторXML = Результат.КлассификаторXML;
	ТаблицаШкалаСтоимости = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;
	ТаблицаШкалаСтоимости.Индексы.Добавить("КодВидаТранспорта");
	
	ПериодЗаполнения = ОбщегоНазначенияБПСервер.ПолучитьПериодИзXML(КлассификаторXML);
	
	НаборЗаписей = РегистрыСведений.ШкалаОценкиСтоимостиВидовТранспорта.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ВидТранспорта.Установить(ДополнительныеПараметры.Ссылка);
	НаборЗаписей.Отбор.Период.Установить(ПериодЗаполнения);
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();
	
	НайденныеСтроки = ТаблицаШкалаСтоимости.НайтиСтроки(Новый Структура("КодВидаТранспорта", ДополнительныеПараметры.Код));
	Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл 
		
		Запись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, НайденнаяСтрока);
		Запись.Период = ПериодЗаполнения;
		Запись.ВидТранспорта = ДополнительныеПараметры.Ссылка;
		
	КонецЦикла;

	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей, Истина, Истина, Истина);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

