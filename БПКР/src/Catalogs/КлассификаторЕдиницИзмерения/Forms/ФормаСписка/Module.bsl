
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
// Процедура - обработчик события ПередНачаломДобавления табличного поля Список
//
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	ТекстВопроса = НСтр("ru = 'Имеется возможность добавить единицу измерения из классификатора. Добавить из классификатора?
		|(Да - добавить из классификатора; Нет - добавить самостоятельно; Отмена - отменить действияе)'");
		
	ПоказатьВопрос(Новый ОписаниеОповещения("СписокПередНачаломДобавленияЗавершение", ЭтотОбъект),
		ТекстВопроса,
		РежимДиалогаВопрос.ДаНетОтмена
	);
	Отказ = Истина;
	Возврат;
	
КонецПроцедуры // СписокПередНачаломДобавления()

&НаКлиенте
Процедура СписокПередНачаломДобавленияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Ответ = Результат;
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		ПодобратьИзКлассификатораКлиент();
		
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		
		ОткрытьФорму("Справочник.КлассификаторЕдиницИзмерения.Форма.ФормаЭлемента");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
// Обработчик команды КомандаПодборИзКлассификатора.
//
Процедура ПодобратьИзКлассификатора(Команда)
	
	ПодобратьИзКлассификатораКлиент();

КонецПроцедуры // ПодобратьИзКлассификатора()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПодобратьИзКлассификатораКлиент()

	ОбработчикОповещения = Новый ОписаниеОповещения("ПодборИзКлассификатораОповещение", ЭтотОбъект, Неопределено);
	Режим = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	
	ОткрытьФорму("Справочник.КлассификаторЕдиницИзмерения.Форма.КлассификаторЕдиницИзмерения",,,,,, ОбработчикОповещения, Режим);

КонецПроцедуры // ПодобратьИзКлассификатораКлиент()

&НаКлиенте
Процедура ПодборИзКлассификатораОповещение(Результат, ДополнительныеПараметры) Экспорт
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

#КонецОбласти
