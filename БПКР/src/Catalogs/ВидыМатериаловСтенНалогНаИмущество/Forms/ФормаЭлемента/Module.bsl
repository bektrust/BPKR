
#Область ПроцедурыОбработчикиСобытийФормы

// Процедура - обработчик события ПриСозданииНаСервере.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Дата = ЗаполнитьМаксимальнойДатойПоВидуМатериала();
	
	СозданиеНового = Объект.Ссылка.Пустая();
	
	СтруктураОбъектов = Новый Структура;
	СтруктураОбъектов.Вставить("Период", Дата);
	СтруктураОбъектов.Вставить("ВидМатериалаСтен", Объект.Ссылка);
	
	РедактированиеПериодическихСведений.ПрочитатьНаборЗаписейПоСтруктуре(ЭтаФорма, "ШкалаОценкиСтоимостиВидовМатериаловСтен", СтруктураОбъектов);
			
КонецПроцедуры

// Процедура - обработчик события ПриОткрытии.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьПредставлениеВсехСтрок();
	
КонецПроцедуры

// Процедура - обработчик события ПриЗаписиНаСервере.
//
&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ДополнительныеСвойства = Новый Структура;
	
	Если СозданиеНового Тогда
		ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
	КонецЕсли;
	
	СтруктураОбъектов = Новый Структура;
	СтруктураОбъектов.Вставить("Период", Дата);
	СтруктураОбъектов.Вставить("ВидМатериалаСтен", ТекущийОбъект.Ссылка);
		
	РедактированиеПериодическихСведений.ЗаписатьНаборЗаписейПоСтруктуре(
		ЭтаФорма,
		"ШкалаОценкиСтоимостиВидовМатериаловСтен",
		СтруктураОбъектов,
		"ПредставлениеИнтервала",
		ДополнительныеСвойства);
				
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиШапкиФормы

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	Дата = НачалоМесяца(Дата);
	
	ДатаПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	Если ТребуетсяНастройкаАвторизацияИнтернетПоддержки() Тогда
		ТекстВопроса = НСтр("ru='Для заполнения ставок
			|необходимо подключиться к Интернет-поддержке пользователей.
			|Подключиться сейчас?'");
		ОписаниеОповещения = Новый ОписаниеОповещения("ПриПодключенииИнтернетПоддержки", ЭтотОбъект);
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Подключиться'"));
		Кнопки.Добавить(КодВозвратаДиалога.Отмена);
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки);
	Иначе
		ЗаполнитьСтавкиЗемельногоНалогаНаКлиенте();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыОбработчикиСобытийНабораЗаписей

&НаКлиенте
Процедура ШкалаОценкиСтоимостиВидовМатериаловСтенНаборЗаписейПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда 
		// получаем текущую строку
		СтрокаТабличнойЧасти = Элемент.ТекущиеДанные;
		
		// не удалось получить- возвращаемся
		Если СтрокаТабличнойЧасти = Неопределено Тогда 
			Возврат;
		КонецЕсли;
		
		СтрокаТабличнойЧасти.Период = НачалоМесяца(Дата);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ШкалаОценкиСтоимостиВидовМатериаловСтенНаборЗаписейПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	ШкалаОценкиСтоимостиВидовМатериаловСтенНаборЗаписей.Сортировать("НижняяГраница");
	ОбновитьПредставлениеВсехСтрок();
КонецПроцедуры

&НаКлиенте
Процедура ШкалаОценкиСтоимостиВидовМатериаловСтенНаборЗаписейНижняяГраницаПриИзменении(Элемент)
	ТекСтрЭлемент = Элементы.ШкалаОценкиСтоимостиВидовМатериаловСтенНаборЗаписей.ТекущиеДанные;
	ТекСтрЭлемент.ПредставлениеИнтервала = ПолучитьПредставлениеИнтервала(ШкалаОценкиСтоимостиВидовМатериаловСтенНаборЗаписей, ТекСтрЭлемент); 
КонецПроцедуры

&НаКлиенте
Процедура ШкалаОценкиСтоимостиВидовМатериаловСтенНаборЗаписейПослеУдаления(Элемент)
	ОбновитьПредставлениеВсехСтрок();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
// Возвращает текстовое представление интервала.
//
Функция ПолучитьПредставлениеИнтервала(ТабличнаяЧасть, ТекущаяСтрока)

	МаксНижняяГраница = Неопределено;
	СледующаяСтрока = Неопределено;
	Для Каждого СтрокаТЧ Из ТабличнаяЧасть Цикл
		Если (МаксНижняяГраница = Неопределено Или МаксНижняяГраница >= СтрокаТЧ.НижняяГраница)
		   И ТекущаяСтрока.НижняяГраница <= СтрокаТЧ.НижняяГраница
		   И СтрокаТЧ <> ТекущаяСтрока Тогда
			СледующаяСтрока = СтрокаТЧ;
			МаксНижняяГраница = СтрокаТЧ.НижняяГраница;
		КонецЕсли;
	КонецЦикла;

	Если СледующаяСтрока = Неопределено Тогда
		ПредставлениеИнтервала = СтрШаблон(НСтр("ru = 'От %1'"), СокрЛП(ТекущаяСтрока.НижняяГраница));
	ИначеЕсли ТекущаяСтрока.НижняяГраница = СледующаяСтрока.НижняяГраница Тогда
		ПредставлениеИнтервала = НСтр("ru = 'ОШИБКА: такая нижняя граница уже есть.'");
	Иначе
		ПредставлениеИнтервала = СтрШаблон(НСтр("ru = 'От %1 до %2'"), 
									СокрЛП(ТекущаяСтрока.НижняяГраница), 
									СокрЛП(СледующаяСтрока.НижняяГраница));
	КонецЕсли;

	Возврат ПредставлениеИнтервала;

КонецФункции // ПолучитьПредставлениеИнтервала()

// Процедура обновляет значение в колонке ПредставлениеИнтервала табличной части ШкалаОценкиСтоимости.
//
&НаКлиенте
Процедура ОбновитьПредставлениеВсехСтрок()

	Для каждого ТекСтр Из ШкалаОценкиСтоимостиВидовМатериаловСтенНаборЗаписей Цикл
		ТекСтр.ПредставлениеИнтервала = ПолучитьПредставлениеИнтервала(ШкалаОценкиСтоимостиВидовМатериаловСтенНаборЗаписей, ТекСтр);
	КонецЦикла;

КонецПроцедуры

&НаСервере
Функция ЗаполнитьМаксимальнойДатойПоВидуМатериала()
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ШкалаОценкиСтоимостиВидовМатериаловСтен.Период КАК Период
		|ИЗ
		|	РегистрСведений.ШкалаОценкиСтоимостиВидовМатериаловСтен КАК ШкалаОценкиСтоимостиВидовМатериаловСтен
		|ГДЕ
		|	ШкалаОценкиСтоимостиВидовМатериаловСтен.ВидМатериалаСтен = &ВидМатериалаСтен
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период УБЫВ";
	
	Запрос.УстановитьПараметр("ВидМатериалаСтен", Объект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат НачалоМесяца(ТекущаяДатаСеанса());
	Иначе	
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Период;
	КонецЕсли;	
		
КонецФункции	

&НаКлиенте
Процедура ЗаполнитьСтавкиЗемельногоНалогаНаКлиенте()
	
	ОписаниеОшибки = "";
	ЗаполнитьСтавкиЗемельногоНалогаНаСервере(ОписаниеОшибки);
	ОбновитьПредставлениеВсехСтрок();
	Модифицированность = Истина;
	
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		
		ОбработчикЗавершенияОбработкиОшибки = Новый ОписаниеОповещения(
			"ОбработатьОшибкуЗаполнитьСтавкиЗемельногоНалога",
			ЭтотОбъект);
		ДополнительныеПараметрыОбработкиОшибки =
			РаботаСКонтрагентамиКлиентБП.НовыйДополнительныеПараметрыОбработкиОшибки();
		ДополнительныеПараметрыОбработкиОшибки.ПредставлениеДействия    = НСтр("ru = 'Загрузка ставок видов материалов стен'");
		ДополнительныеПараметрыОбработкиОшибки.ИдентификаторМестаВызова = "ratesWallMaterial";
		ДополнительныеПараметрыОбработкиОшибки.Форма                    = ЭтотОбъект;
		
		РаботаСКонтрагентамиКлиентБП.ОбработатьОшибку(
			ОписаниеОшибки,
			ОбработчикЗавершенияОбработкиОшибки,
			ДополнительныеПараметрыОбработкиОшибки);
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьСтавкиЗемельногоНалогаНаКлиенте()

&НаКлиенте
Процедура ОбработатьОшибкуЗаполнитьСтавкиЗемельногоНалога(Результат, ДополнительныеПараметры) Экспорт
	Если Результат.ПовторитьДействие Тогда
		ЗаполнитьСтавкиЗемельногоНалогаНаКлиенте();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтавкиЗемельногоНалогаНаСервере(ОписаниеОшибки)
		
	Результат = РаботаСКонтрагентамиБП.РеквизитыСтавкиНалогов("ratesWallMaterial");
	
	Если ЗначениеЗаполнено(Результат.ОписаниеОшибки) Тогда 
		Возврат;
	КонецЕсли;	
	
	// Получение из XML.
	КлассификаторXML = Результат.КлассификаторXML;
	ТаблицаШкалаСтоимости = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;
	ТаблицаШкалаСтоимости.Индексы.Добавить("КодВидаМатериала");
	
	ПериодЗаполнения = ОбщегоНазначенияБПСервер.ПолучитьПериодИзXML(КлассификаторXML);
	
	НаборЗаписей = РегистрыСведений.ШкалаОценкиСтоимостиВидовМатериаловСтен.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ВидМатериалаСтен.Установить(Объект.Ссылка);
	НаборЗаписей.Отбор.Период.Установить(ПериодЗаполнения);
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();
	
	НайденныеСтроки = ТаблицаШкалаСтоимости.НайтиСтроки(Новый Структура("КодВидаМатериала", Объект.Код));
	Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл 
		
		Запись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, НайденнаяСтрока);
		Запись.Период = ПериодЗаполнения;
		Запись.ВидМатериалаСтен = Объект.Ссылка;
		
	КонецЦикла;

	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей, Истина, Истина, Истина);
	
	Дата = ПериодЗаполнения;
	
	СтруктураОбъектов = Новый Структура;
	СтруктураОбъектов.Вставить("Период", Дата);
	СтруктураОбъектов.Вставить("ВидМатериалаСтен", Объект.Ссылка);
	
	РедактированиеПериодическихСведений.ПрочитатьНаборЗаписейПоСтруктуре(ЭтаФорма, "ШкалаОценкиСтоимостиВидовМатериаловСтен", СтруктураОбъектов);
	
КонецПроцедуры // ЗаполнитьСтавкиЗемельногоНалогаНаСервере()

&НаСервере
Процедура ДатаПриИзмененииНаСервере()
	СтруктураОбъектов = Новый Структура;
	СтруктураОбъектов.Вставить("Период", Дата);
	СтруктураОбъектов.Вставить("ВидМатериалаСтен", Объект.Ссылка);
	
	РедактированиеПериодическихСведений.ПрочитатьНаборЗаписейПоСтруктуре(ЭтаФорма, "ШкалаОценкиСтоимостиВидовМатериаловСтен", СтруктураОбъектов);
КонецПроцедуры	

#КонецОбласти

#Область ОбработчикиБиблиотек

// ИнтернетПоддержкаПользователей
&НаСервере
Функция ТребуетсяНастройкаАвторизацияИнтернетПоддержки()
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей") Тогда
		МодульИнтернетПоддержкаПользователей = ОбщегоНазначения.ОбщийМодуль("ИнтернетПоддержкаПользователей");
		Возврат Не МодульИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки();
	КонецЕсли;
	Возврат Ложь;
КонецФункции

&НаКлиенте
Процедура ПриПодключенииИнтернетПоддержки(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтернетПоддержкаПользователей") Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПодключенияИнтернетПоддержки", ЭтотОбъект, ДополнительныеПараметры);
		МодульИнтернетПоддержкаПользователейКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ИнтернетПоддержкаПользователейКлиент");
		МодульИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(ОписаниеОповещения, ЭтотОбъект);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеПодключенияИнтернетПоддержки(Результат, ДополнительныеПараметры) Экспорт
	Если Не (ТипЗнч(Результат) = Тип("Структура")
		И ЗначениеЗаполнено(Результат.Логин)) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСтавкиЗемельногоНалогаНаКлиенте();	
КонецПроцедуры

// Конец ИнтернетПоддержкаПользователей

#КонецОбласти

