
#Область ОбработчикиСобытийФормы 

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДанныеМакета = Справочники.КлассификаторТНВЭД.ПолучитьМакет("КлассификаторТНВЭД");
	ДанныеМакета.ФиксацияСверху = 1; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДанныеМакетаВыбор(Элемент, Область, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОбработатьВыбранныеДанные();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьДанные(Команда)
	
	ОбработатьВыбранныеДанные();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработатьВыбранныеДанные()

	Если Элементы.ДанныеМакета.ТекущаяОбласть = Неопределено
		Или ПустаяСтрока(Элементы.ДанныеМакета.ТекущаяОбласть.Текст) Тогда
		Возврат;
	КонецЕсли;
	
	ИмяОбластиСтрока = Лев(Элементы.ДанныеМакета.ТекущаяОбласть.Имя,
		СтрНайти(Элементы.ДанныеМакета.ТекущаяОбласть.Имя, "C") - 1);
	НомерСтроки = Прав(ИмяОбластиСтрока, СтрДлина(ИмяОбластиСтрока) - 1);
	
	ДанныеЭлемента = Новый Структура;
	ДанныеЭлемента.Вставить("Код",          ДанныеМакета.Область(НомерСтроки, 1).Текст);
	ДанныеЭлемента.Вставить("Наименование", ДанныеМакета.Область(НомерСтроки, 2).Текст);
	
	Если СтрДлина(ДанныеЭлемента.Код) < 10 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Могут быть выбраны только данные товарных подсубпозиций'"),,
			НСтр("ru = 'Ошибка'"));
		Возврат;
	КонецЕсли;
	
	Если Не ДобавитьЭлементКлассификатораИзМакета(ДанныеЭлемента) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Элемент с выбранным кодом уже добавлен'"),, НСтр("ru = 'Ошибка'"));
	КонецЕсли;
	
	Оповестить("ЗавершениеПодбораИзМакетаДанныхТНВЭД", ДанныеЭлемента);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДобавитьЭлементКлассификатораИзМакета(ДанныеЭлемента)

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИСТИНА КАК ЭлементСуществует
	|ИЗ
	|	Справочник.КлассификаторТНВЭД КАК КлассификаторТНВЭД
	|ГДЕ
	|	КлассификаторТНВЭД.Код = &Код";
	
	Запрос.УстановитьПараметр("Код", ДанныеЭлемента.Код);
	
	Если Не Запрос.Выполнить().Пустой() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЭлементКлассификатора = Справочники.КлассификаторТНВЭД.СоздатьЭлемент();
	ЭлементКлассификатора.Код = ДанныеЭлемента.Код;
	ЭлементКлассификатора.Наименование = ДанныеЭлемента.Наименование;
	ЭлементКлассификатора.Записать();
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти



