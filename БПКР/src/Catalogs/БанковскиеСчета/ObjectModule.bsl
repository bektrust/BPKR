#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события ОбработкаЗаполнения объекта.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	ВалютаДенежныхСредств = Константы.ВалютаРегламентированногоУчета.Получить();
	ВидСчета = "Расчетный";
	ВариантВыводаМесяца = Перечисления.ВариантыВыводаМесяцаВДатеДокумента.Числом;	
КонецПроцедуры

// Процедура - обработчик события ОбработкаПроверкиЗаполнения объекта.
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если ТипЗнч(Владелец) = Тип("СправочникСсылка.Контрагенты") 
		Или ТипЗнч(Владелец) = Тип("СправочникСсылка.ФизическиеЛица") Тогда 
		МассивНепроверяемыхРеквизитов.Добавить("СчетУчета");
		МассивНепроверяемыхРеквизитов.Добавить("Префикс");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
					   
	ВыполнитьПредварительныйКонтроль(Отказ);	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура выполняет контроль противоречий.
//
Процедура ВыполнитьПредварительныйКонтроль(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	БанковскиеСчета.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.БанковскиеСчета КАК БанковскиеСчета
		|ГДЕ
		|	БанковскиеСчета.НомерСчета = &НомерСчета
		|	И БанковскиеСчета.ВалютаДенежныхСредств = &ВалютаДенежныхСредств
		|	И БанковскиеСчета.Ссылка <> &Ссылка
		|	И ВЫБОР
		|			КОГДА ТИПЗНАЧЕНИЯ(БанковскиеСчета.Владелец) <> ТИП(Справочник.ФизическиеЛица)
		|				ТОГДА ТИПЗНАЧЕНИЯ(БанковскиеСчета.Владелец) = &ТипЗначенияВладелец
		|		КОНЕЦ
		|	И НЕ БанковскиеСчета.ПометкаУдаления";
	Запрос.УстановитьПараметр("ВалютаДенежныхСредств", ВалютаДенежныхСредств);
	Запрос.УстановитьПараметр("НомерСчета", НомерСчета);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	// Разрешаем создавать банковский счет с одинаковым р/с и валютой, если владельцы разные (Организация и Контрагент).
	Запрос.УстановитьПараметр("ТипЗначенияВладелец", ТипЗнч(Владелец));

	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Количество() > 0 Тогда	
		ТекстСообщения = НСтр(
			"ru = 'Банковский счет с такими номером и валютой уже существует.'");
		БухгалтерскийУчетСервер.СообщитьОбОшибке(
			ЭтотОбъект,
			ТекстСообщения,
			,
			,
			"НомерСчета",
			Отказ);		
	КонецЕсли;	

	Если ВАрхиве Тогда 
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Контрагенты.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Контрагенты КАК Контрагенты
			|ГДЕ
			|	Контрагенты.ОсновнойБанковскийСчет = &БанковскийСчет";
		Запрос.УстановитьПараметр("БанковскийСчет", Ссылка);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Если Выборка.Количество() > 0 Тогда	
			ТекстСообщения = НСтр(
				"ru = 'Банковский счет назначен основным.
				|Если предполагается больше не использовать текущий банковсикй счет, то необходимо предварительно назначить основным другой банковский счет.'");
			БухгалтерскийУчетСервер.СообщитьОбОшибке(ЭтотОбъект, ТекстСообщения,,,, Отказ);		
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли