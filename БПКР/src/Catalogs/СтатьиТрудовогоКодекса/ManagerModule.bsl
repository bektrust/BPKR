#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	#Область ПрограммныйИнтерфейс

Функция ПолучитьСтатьюТрудовогоКодексаПоУмолчанию(Пользователь = Неопределено) Экспорт
	
	ЗначениеПоУмолчанию = Справочники.СтатьиТрудовогоКодекса.ПустаяСсылка();
	
	ОсновнаяСтатьяТрудовогоКодекса = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ОсновнаяСтатьяТрудовогоКодекса", "ЗначениеПоУмолчанию",, Пользователь);
	
	Если ТипЗнч(ОсновнаяСтатьяТрудовогоКодекса) = ТипЗнч(ЗначениеПоУмолчанию) Тогда
		Если ОбщегоНазначения.СсылкаСуществует(ОсновнаяСтатьяТрудовогоКодекса) Тогда
			ЗначениеПоУмолчанию = ОсновнаяСтатьяТрудовогоКодекса;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЗначениеПоУмолчанию;

КонецФункции

#КонецОбласти	

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПриНастройкеНачальногоЗаполненияЭлементов(Настройки) Экспорт
	
	Настройки.ПриНачальномЗаполненииЭлемента = Ложь;
	Настройки.ИмяКлючевогоРеквизита = "Наименование";
	
КонецПроцедуры

// Вызывается при начальном заполнении справочника.
//
// Параметры:
//  КодыЯзыков - Массив - список языков конфигурации. Актуально для мультиязычных конфигураций.
//  Элементы   - ТаблицаЗначений - данные заполнения. Состав колонок соответствует набору реквизитов
//                                 справочника ПапкиФайлов.
//  ТабличныеЧасти - Структура - описание табличных частей объекта, где:
//   * Ключ - Строка - имя табличной части;
//   * Значение - ТаблицаЗначений - табличная часть в виде таблицы значений, структуру которой
//                                  необходимо скопировать перед заполнением. Например:
//                                  Элемент.Ключи = ТабличныеЧасти.Ключи.Скопировать();
//                                  ЭлементТЧ = Элемент.Ключи.Добавить();
//                                  ЭлементТЧ.ИмяКлюча = "Первичный";
//
Процедура ПриНачальномЗаполненииЭлементов(КодыЯзыков, Элементы, ТабличныеЧасти) Экспорт
	
	СправочникМенеджер = Справочники.СтатьиТрудовогоКодекса;
	
	КлассификаторXML = СправочникМенеджер.ПолучитьМакет("МакетЗаполнения").ПолучитьТекст();
	КлассификаторТаблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;
	
	Для Каждого СтрокаТаблицыЗначений Из КлассификаторТаблица Цикл
		Элемент = Элементы.Добавить();
		ЗаполнитьЗначенияСвойств(Элемент, СтрокаТаблицыЗначений);
	КонецЦикла;
	
КонецПроцедуры

// Получить статью по собственному желанию
// 
// Возвращаемое значение:
//  Стятья - статья, содержащая 82
//
Функция ПолучитьСтатьюПоСобственномуЖеланию() Экспорт 
	
	Статья = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СтатьиТрудовогоКодекса.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.СтатьиТрудовогоКодекса КАК СтатьиТрудовогоКодекса
		|ГДЕ
		|	СтатьиТрудовогоКодекса.Наименование ПОДОБНО ""%82%""";
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда 
		Статья = ВыборкаДетальныеЗаписи.Ссылка;	
	КонецЕсли;

	Возврат Статья;
	
КонецФункции // ПолучитьСтатьюПоСобственномуЖеланию()

#КонецОбласти

#КонецЕсли

