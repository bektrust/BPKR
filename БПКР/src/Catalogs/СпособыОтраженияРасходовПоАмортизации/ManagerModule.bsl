#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура - Заполнить по умолчанию
// Выполняет первоночальное заполнение
Процедура ЗаполнитьПоУмолчанию() Экспорт 
	СправочникМенеджер = Справочники.СпособыОтраженияРасходовПоАмортизации;
	
	КлассификаторXML = СправочникМенеджер.ПолучитьМакет("МакетЗаполнения").ПолучитьТекст();
	КлассификаторТаблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;     
	
	Для Каждого СтрокаТаблицыЗначений Из КлассификаторТаблица Цикл
		Наименование = СокрЛП(СтрокаТаблицыЗначений.Наименование);
		Коэффициент = СокрЛП(СтрокаТаблицыЗначений.Коэффициент);
		СчетЗатрат = ПланыСчетов.Хозрасчетный.НайтиПоКоду(СокрЛП(СтрокаТаблицыЗначений.СчетЗатрат));
		ТипСубконто1 = СокрЛП(СтрокаТаблицыЗначений.ТипСубконто1);
		Субконто1 = Справочники[ТипСубконто1][СокрЛП(СтрокаТаблицыЗначений.Субконто1)];
		
		СправочникСсылка = СправочникМенеджер.НайтиПоНаименованию(Наименование, Истина);
		
		Если СправочникСсылка.Пустая() Тогда
			СправочникОбъект = СправочникМенеджер.СоздатьЭлемент();
		Иначе
			СправочникОбъект = СправочникСсылка.ПолучитьОбъект();
		КонецЕсли;	
		
		СправочникОбъект.Наименование = Наименование;
		СправочникОбъект.Способы.Очистить();
		
		СтрокаТабличнойЧасти = СправочникОбъект.Способы.Добавить();
		СтрокаТабличнойЧасти.СчетЗатрат = СчетЗатрат;
		СтрокаТабличнойЧасти.Субконто1 = Субконто1;
		СтрокаТабличнойЧасти.Коэффициент = Коэффициент;
		
		БухгалтерскийУчетСервер.ЗаписатьСправочникОбъект(СправочникОбъект);
	КонецЦикла;
КонецПроцедуры // ЗаполнитьПоУмолчанию()	

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПриНастройкеНачальногоЗаполненияЭлементов(Настройки) Экспорт
	
	Настройки.ПриНачальномЗаполненииЭлемента = Ложь;
	Настройки.ИмяКлючевогоРеквизита = "Наименование";
	
КонецПроцедуры

// Вызывается при начальном заполнении справочника.
//
// Параметры:
//  КодыЯзыков - Массив - список языков конфигурации. Актуально для мультиязычных конфигураций.
//  Элементы   - ТаблицаЗначений - данные заполнения. Состав колонок соответствует набору реквизитов
//                                 справочника ПапкиФайлов.
//  ТабличныеЧасти - Структура - описание табличных частей объекта, где:
//   * Ключ - Строка - имя табличной части;
//   * Значение - ТаблицаЗначений - табличная часть в виде таблицы значений, структуру которой
//                                  необходимо скопировать перед заполнением. Например:
//                                  Элемент.Ключи = ТабличныеЧасти.Ключи.Скопировать();
//                                  ЭлементТЧ = Элемент.Ключи.Добавить();
//                                  ЭлементТЧ.ИмяКлюча = "Первичный";
//
Процедура ПриНачальномЗаполненииЭлементов(КодыЯзыков, Элементы, ТабличныеЧасти) Экспорт
	
	СправочникМенеджер = Справочники.СпособыОтраженияРасходовПоАмортизации;
	
	КлассификаторXML = СправочникМенеджер.ПолучитьМакет("МакетЗаполнения").ПолучитьТекст();
	КлассификаторТаблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;
	
	Для Каждого СтрокаТаблицыЗначений Из КлассификаторТаблица Цикл
		ЗначенияЗаполнения = Новый Структура;
		ЗначенияЗаполнения.Вставить("Наименование", СтрокаТаблицыЗначений.Наименование);
		ЗначенияЗаполнения.Вставить("СчетЗатрат", ПланыСчетов.Хозрасчетный.НайтиПоКоду(СтрокаТаблицыЗначений.СчетЗатрат));
		ЗначенияЗаполнения.Вставить("Субконто1", Справочники[СтрокаТаблицыЗначений.ТипСубконто1][СтрокаТаблицыЗначений.Субконто1]);
		ЗначенияЗаполнения.Вставить("Коэффициент", СтрокаТаблицыЗначений.Коэффициент);
		
		// Добавление строка табличной части.
		Способы = ТабличныеЧасти.Способы.Скопировать();
		СтрокаТабличнойЧасти = Способы.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ЗначенияЗаполнения);
		ЗначенияЗаполнения.Вставить("Способы", Способы);
		
		Элемент = Элементы.Добавить();
		ЗаполнитьЗначенияСвойств(Элемент, ЗначенияЗаполнения);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

