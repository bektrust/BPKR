
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НоменклатурнаяГруппа", Объект.Ссылка);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Номенклатура.Код КАК Код,
		|	Номенклатура.Ссылка КАК Номенклатура
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.НоменклатурнаяГруппа.Ссылка = &НоменклатурнаяГруппа
		|
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура";
	ТаблицаТоваров = Запрос.Выполнить().Выгрузить();
	
	СписокГруппы.Загрузить(ТаблицаТоваров);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ПараметрыЗаписи.Вставить("ИспользоватьОднуНоменклатурнуюГруппуПередЗаписью", 
		БухгалтерскийУчетВызовСервераПовтИсп.ИспользоватьОднуНоменклатурнуюГруппу());
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	СписокНоменклатуры = СписокГруппы.Выгрузить().ВыгрузитьКолонку("Номенклатура");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекущаяНоменклатурнаяГруппа", Объект.Ссылка);
	Запрос.УстановитьПараметр("СписокНоменклатуры", СписокНоменклатуры);
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	Номенклатура.Ссылка КАК НоменклатураСсылка,
		|	ВЫБОР
		|		КОГДА НЕ Номенклатура.НоменклатурнаяГруппа = &ТекущаяНоменклатурнаяГруппа
		|			ТОГДА &ТекущаяНоменклатурнаяГруппа
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НоменклатурныеГруппы.ПустаяСсылка)
		|	КОНЕЦ КАК НоменклатурнаяГруппа
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	(Номенклатура.Ссылка В (&СписокНоменклатуры)
		|				И НЕ Номенклатура.НоменклатурнаяГруппа = &ТекущаяНоменклатурнаяГруппа
		|			ИЛИ НЕ Номенклатура.Ссылка В (&СписокНоменклатуры)
		|				И Номенклатура.НоменклатурнаяГруппа = &ТекущаяНоменклатурнаяГруппа)
		|	И НЕ Номенклатура.ЭтоГруппа";
	НачатьТранзакцию();
	
	НоменклатураСсылка = Справочники.Номенклатура.ПустаяСсылка();
	
	Попытка
		
		ТаблицаТоваров = Запрос.Выполнить().Выбрать();
		Пока ТаблицаТоваров.Следующий() Цикл
			
			НоменклатураСсылка = ТаблицаТоваров.НоменклатураСсылка;
			
			НоменклатураОбъект = НоменклатураСсылка.ПолучитьОбъект();
			НоменклатураОбъект.НоменклатурнаяГруппа = ТаблицаТоваров.НоменклатурнаяГруппа;
			НоменклатураОбъект.Записать();
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ШаблонСообщения = НСтр("ru = 'Не удалось обновить элемент справочника.
                                |%1'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Установка номенклатурной группы'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()), 
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Справочники.Номенклатура,
			НоменклатураСсылка, 
			ТекстСообщения);
		ВызватьИсключение ТекстСообщения;
		
	КонецПопытки;
	
	ПараметрыЗаписи.Вставить("ИспользоватьОднуНоменклатурнуюГруппуПослеЗаписи", 
		БухгалтерскийУчетВызовСервераПовтИсп.ИспользоватьОднуНоменклатурнуюГруппу());

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Перем ИспользоватьОднуНоменклатурнуюГруппуПередЗаписью, ИспользоватьОднуНоменклатурнуюГруппуПослеЗаписи;
	
	ОбновитьИнтерфейс = Ложь;
	
	Если ПараметрыЗаписи.Свойство("ИспользоватьОднуНоменклатурнуюГруппуПередЗаписью", ИспользоватьОднуНоменклатурнуюГруппуПередЗаписью)
		И ПараметрыЗаписи.Свойство("ИспользоватьОднуНоменклатурнуюГруппуПослеЗаписи", ИспользоватьОднуНоменклатурнуюГруппуПослеЗаписи) Тогда
		
		Если ИспользоватьОднуНоменклатурнуюГруппуПередЗаписью <> ИспользоватьОднуНоменклатурнуюГруппуПослеЗаписи Тогда
			ОбновитьИнтерфейс = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОбновитьИнтерфейс Тогда
		ОбновитьИнтерфейс();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти