#Область ОбработчикиСобытийФормы

// Процедура - обработчик события ПриСозданииНаСервере.
// В процедуре осуществляется
// - инициализация реквизитов формы,
// - установка параметров функциональных опций формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущаяДата = НачалоДня(ТекущаяДатаСеанса());
	Список.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить("ТекущаяДата", ТекущаяДата);
	
КонецПроцедуры

// Процедура - обработчик события ОбработкаОповещения.
//
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененаСтавкаНДС" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицФормы

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	Перем ТекущаяДата;
	
	Если Не Настройки.ДополнительныеСвойства.Свойство("ТекущаяДата", ТекущаяДата) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СтавкиНДС.СтавкаНДС КАК СтавкаНДС,
		|	СтавкиНДС.Ставка КАК Ставка
		|ИЗ
		|	РегистрСведений.СтавкиНДС.СрезПоследних(&КонецПериода, СтавкаНДС В (&Ставки)) КАК СтавкиНДС";
	Запрос.УстановитьПараметр("Ставки", Строки.ПолучитьКлючи());
	Запрос.УстановитьПараметр("КонецПериода", ТекущаяДата);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СтрокаСписка = Строки[Выборка.СтавкаНДС];
		СтрокаСписка.Данные["Ставка"] = Выборка.Ставка;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ПоказатьПредупреждение(,НСтр("ru = 'Добавление новых элементов не предусмотрено.'"));
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти