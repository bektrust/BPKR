#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МожноРедактировать = ПравоДоступа("Редактирование", Метаданные.Справочники.СпецификацииНоменклатуры);
	Элементы.СписокКонтекстноеМенюИзменитьВыделенные.Видимость = МожноРедактировать;
	
	// Это список всех спецификаций. 
	Если Не Параметры.Отбор.Свойство("Владелец") Тогда
		Элементы.СписокОсновная.Видимость = Ложь;
		Элементы.ФормаИспользоватьКакОсновную.Видимость = Ложь;
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);

КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьКакОсновную(Команда)
	
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыСообщения = Новый Структура;
	ПараметрыСообщения.Вставить("ОсновнаяСпецификация", Элементы.Список.ТекущиеДанные.Ссылка);
	ПараметрыСообщения.Вставить("Номенклатура",         Элементы.Список.ТекущиеДанные.Владелец);
	Модифицированность = Ложь;
	Оповестить("НазначенаПоКнопкеОсновнаяСпецификацияВСпискеСпецификаций", ПараметрыСообщения);
	
КонецПроцедуры

&НаКлиенте
Процедура СвернутьРазвернутьПанельОтборов(Элемент)
	
	НовоеЗначениеВидимость = НЕ Элементы.ФильтрыНастройкиИДопИнфо.Видимость;
	РаботаСОтборамиКлиент.СвернутьРазвернутьПанельОтборов(ЭтотОбъект, НовоеЗначениеВидимость);	
	Элементы.ПраваяПанель.Ширина = ?(НовоеЗначениеВидимость, 40, 0);
		
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыбрать(Команда)
	
	ОбработатьВыбор();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	УстановитьОтбор();
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ЗаданОтборПоВладельцу(Список.Отбор.Элементы) Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьФормуСпецификации(Ложь, Ложь, Элемент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Группа Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗаданОтборПоВладельцу(Список.Отбор.Элементы) Тогда
		Отказ = Истина;
		ОткрытьФормуСпецификации(Истина, Копирование, Элемент);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура настройки условного оформления форм и динамических списков .
//
&НаСервере
Процедура УстановитьУсловноеОформление()

	Список.Группировка.Элементы.Очистить();
	
	Если Не Параметры.Отбор.Свойство("Владелец") Тогда
		ЭлементГруппировки = Список.Группировка.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
		ЭлементГруппировки.Использование = Истина;
		ЭлементГруппировки.Поле = Новый ПолеКомпоновкиДанных("Владелец");
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтбор()
	
	СтрокаТабличнойЧасти = Элементы.Список.ТекущиеДанные;
	
	Если СтрокаТабличнойЧасти = Неопределено
		Или НЕ СтрокаТабличнойЧасти.Свойство("Ссылка") Тогда 
		СсылкаСпецификация = ПредопределенноеЗначение("Справочник.СпецификацииНоменклатуры.ПустаяСсылка");
	Иначе 
		СсылкаСпецификация = СтрокаТабличнойЧасти.Ссылка;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ИсходныеКомплектующие, "Ссылка", СсылкаСпецификация,,,Истина); 
	
КонецПроцедуры 

&НаКлиенте
Функция ЗаданОтборПоВладельцу(Элементы)
	
	ПолеВладелец = Новый ПолеКомпоновкиДанных("Владелец");
	
	Для Каждого СтрокаОтбора Из Элементы Цикл
		Если СтрокаОтбора.ЛевоеЗначение = ПолеВладелец И ПодходитДляЗначенияЗаполнения(СтрокаОтбора) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

&НаКлиенте
Функция ПодходитДляЗначенияЗаполнения(СтрокаОтбора)
	Возврат СтрокаОтбора.Использование
		И СтрокаОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно
		И ТипЗнч(СтрокаОтбора.ЛевоеЗначение) = Тип("ПолеКомпоновкиДанных") 
		И ТипЗнч(СтрокаОтбора.ПравоеЗначение) <> Тип("ПолеКомпоновкиДанных");
КонецФункции

&НаКлиенте
Процедура ОткрытьФормуСпецификации(ДобавлениеНового, Копирование, Элемент)
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("СписокСОтборомПоВладельцу", Истина);
	
	Если Копирование Тогда
		ПараметрыОткрытия.Вставить("ЗначениеКопирования", Элемент.ТекущаяСтрока);
	КонецЕсли;
	
	Если ДобавлениеНового Тогда
		
		Отбор = Новый Структура;
		
		Для Каждого СтрокаОтбора Из Список.Отбор.Элементы Цикл
			
			Если ПодходитДляЗначенияЗаполнения(СтрокаОтбора) Тогда
				
				Отбор.Вставить(Строка(СтрокаОтбора.ЛевоеЗначение), СтрокаОтбора.ПравоеЗначение);
				
			КонецЕсли;
			
		КонецЦикла;
		
		ПараметрыОткрытия.Вставить("ЗначенияЗаполнения", Отбор);
		
	Иначе
		ПараметрыОткрытия.Вставить("Ключ", Элемент.ТекущаяСтрока);
	КонецЕсли;
	
	ОткрытьФорму("Справочник.СпецификацииНоменклатуры.ФормаОбъекта", ПараметрыОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыбор()
	
	Если Элементы.Список.ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОповеститьОВыборе(Элементы.Список.ТекущиеДанные.Ссылка);
			
КонецПроцедуры

#КонецОбласти
