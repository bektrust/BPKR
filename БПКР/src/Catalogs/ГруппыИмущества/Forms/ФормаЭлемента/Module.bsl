#Область ОбработчикиСобытийФормы

// Процедура - обработчик события ПриСозданииНаСервере.
// В процедуре осуществляется
// - инициализация реквизитов формы,
// - установка параметров функциональных опций формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	СозданиеНового = Объект.Ссылка.Пустая();
	РедактированиеПериодическихСведений.ПрочитатьЗаписьДляРедактированияВФорме(ЭтаФорма, "СтавкиНалогаНаИмущество", Объект.Ссылка);
	РедактированиеПериодическихСведений.ПрочитатьНаборЗаписей(ЭтаФорма, "СтавкиНалогаНаИмущество", Объект.Ссылка);
	
	Период = ТекущаяДатаСеанса();
	
	УстановитьДоступность();
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ДополнительныеСвойства = Новый Структура;
	
	Если СозданиеНового Тогда
		ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
	КонецЕсли;
	
	РедактированиеПериодическихСведений.ЗаписатьНаборЗаписей(
		ЭтаФорма,
		"СтавкиНалогаНаИмущество",
		ТекущийОбъект.Ссылка,
		,
		ДополнительныеСвойства); 	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСтавкиНалогаНаИмуществоНаборЗаписей

&НаКлиенте
Процедура СтавкиГруппыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда 
		// получаем текущую строку
		СтрокаТабличнойЧасти = Элемент.ТекущиеДанные;
		
		// не удалось получить- возвращаемся
		Если СтрокаТабличнойЧасти = Неопределено Тогда 
			Возврат;
		КонецЕсли;
		
		СтрокаТабличнойЧасти.ГруппаНалогаНаИмущество = Объект.Ссылка;
		СтрокаТабличнойЧасти.Период = Период;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьДоступность()
	
	Если Объект.Ссылка = Справочники.ГруппыИмущества.ГИ4с ИЛИ Объект.Ссылка = Справочники.ГруппыИмущества.ГИ4нс Тогда 
		Элементы.СтавкиНалогаНаИмуществоНаборЗаписей.Доступность = Ложь; 
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

