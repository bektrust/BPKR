
#Область ПроцедурыОбработчикиСобытийФормы

// Процедура - обработчик события ПриСозданииНаСервере.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Дата = ЗаполнитьМаксимальнойДатойПоГруппеНалоговогоУчетаОС();
	
	СозданиеНового = Объект.Ссылка.Пустая();
	
	СтруктураОбъектов = Новый Структура;
	СтруктураОбъектов.Вставить("Период", Дата);
	СтруктураОбъектов.Вставить("ГруппаНалоговогоУчетаОС", Объект.Ссылка);
	
	РедактированиеПериодическихСведений.ПрочитатьНаборЗаписейПоСтруктуре(ЭтаФорма, "НормыАмортизацииГруппНалоговогоУчетаОС", СтруктураОбъектов);
			
КонецПроцедуры

// Процедура - обработчик события ПриЗаписиНаСервере.
//
&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ДополнительныеСвойства = Новый Структура;
	
	Если СозданиеНового Тогда
		ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
	КонецЕсли;
	
	СтруктураОбъектов = Новый Структура;
	СтруктураОбъектов.Вставить("Период", Дата);
	СтруктураОбъектов.Вставить("ГруппаНалоговогоУчетаОС", ТекущийОбъект.Ссылка);
		
	РедактированиеПериодическихСведений.ЗаписатьНаборЗаписейПоСтруктуре(
		ЭтаФорма,
		"НормыАмортизацииГруппНалоговогоУчетаОС",
		СтруктураОбъектов,
		,
		ДополнительныеСвойства);
				
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиШапкиФормы

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	Дата = НачалоМесяца(Дата);
	
	ДатаПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	Если ТребуетсяНастройкаАвторизацияИнтернетПоддержки() Тогда
		ТекстВопроса = НСтр("ru='Для заполнения ставок
			|необходимо подключиться к Интернет-поддержке пользователей.
			|Подключиться сейчас?'");
		ОписаниеОповещения = Новый ОписаниеОповещения("ПриПодключенииИнтернетПоддержки", ЭтотОбъект);
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Подключиться'"));
		Кнопки.Добавить(КодВозвратаДиалога.Отмена);
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки);
	Иначе
		ЗаполнитьНормыАмортизацииНаКлиенте();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ЗаполнитьМаксимальнойДатойПоГруппеНалоговогоУчетаОС()
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	НормыАмортизацииГруппНалоговогоУчетаОС.Период КАК Период
		|ИЗ
		|	РегистрСведений.НормыАмортизацииГруппНалоговогоУчетаОС КАК НормыАмортизацииГруппНалоговогоУчетаОС
		|ГДЕ
		|	НормыАмортизацииГруппНалоговогоУчетаОС.ГруппаНалоговогоУчетаОС = &ГруппаНалоговогоУчетаОС
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период УБЫВ";
	
	Запрос.УстановитьПараметр("ГруппаНалоговогоУчетаОС", Объект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат НачалоМесяца(ТекущаяДатаСеанса());
	Иначе	
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Период;
	КонецЕсли;	
		
КонецФункции	

&НаСервере
Процедура ДатаПриИзмененииНаСервере()
	СтруктураОбъектов = Новый Структура;
	СтруктураОбъектов.Вставить("Период", Дата);
	СтруктураОбъектов.Вставить("ГруппаНалоговогоУчетаОС", Объект.Ссылка);
	
	РедактированиеПериодическихСведений.ПрочитатьНаборЗаписейПоСтруктуре(ЭтаФорма, "НормыАмортизацииГруппНалоговогоУчетаОС", СтруктураОбъектов);
КонецПроцедуры	

&НаКлиенте
Процедура ЗаполнитьНормыАмортизацииНаКлиенте()
	
	ОписаниеОшибки = "";
	ЗаполнитьНормыАмортизацииНаСервере(ОписаниеОшибки);
	Модифицированность = Истина;
	
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		
		ОбработчикЗавершенияОбработкиОшибки = Новый ОписаниеОповещения(
			"ОбработатьОшибкуЗаполнитьСтавкиЗемельногоНалога",
			ЭтотОбъект);
		ДополнительныеПараметрыОбработкиОшибки =
			РаботаСКонтрагентамиКлиентБП.НовыйДополнительныеПараметрыОбработкиОшибки();
		ДополнительныеПараметрыОбработкиОшибки.ПредставлениеДействия    = НСтр("ru = 'Загрузка норм амортизации'");
		ДополнительныеПараметрыОбработкиОшибки.ИдентификаторМестаВызова = "ratesDepreciation";
		ДополнительныеПараметрыОбработкиОшибки.Форма                    = ЭтотОбъект;
		
		РаботаСКонтрагентамиКлиентБП.ОбработатьОшибку(
			ОписаниеОшибки,
			ОбработчикЗавершенияОбработкиОшибки,
			ДополнительныеПараметрыОбработкиОшибки);
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьСтавкиЗемельногоНалогаНаКлиенте()

&НаКлиенте
Процедура ОбработатьОшибкуЗаполнитьСтавкиЗемельногоНалога(Результат, ДополнительныеПараметры) Экспорт
	Если Результат.ПовторитьДействие Тогда
		ЗаполнитьНормыАмортизацииНаКлиенте();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНормыАмортизацииНаСервере(ОписаниеОшибки)
		
	Результат = РаботаСКонтрагентамиБП.РеквизитыСтавкиНалогов("ratesDepreciation");
	
	Если ЗначениеЗаполнено(Результат.ОписаниеОшибки) Тогда 
		Возврат;
	КонецЕсли;	
	
	// Получение из XML.
	КлассификаторXML = Результат.КлассификаторXML;
	ТаблицаНормАмортизации = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;
	ТаблицаНормАмортизации.Индексы.Добавить("КодГруппы");
	
	ПериодЗаполнения = ОбщегоНазначенияБПСервер.ПолучитьПериодИзXML(КлассификаторXML);
	
	НаборЗаписей = РегистрыСведений.НормыАмортизацииГруппНалоговогоУчетаОС.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ГруппаНалоговогоУчетаОС.Установить(Объект.Ссылка);
	НаборЗаписей.Отбор.Период.Установить(ПериодЗаполнения);
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();
	
	НайденныеСтроки = ТаблицаНормАмортизации.НайтиСтроки(Новый Структура("КодГруппы", Объект.Код));
	Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл 
		
		Запись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, НайденнаяСтрока);
		Запись.Период = ПериодЗаполнения;
		Запись.ГруппаНалоговогоУчетаОС = Объект.Ссылка;
		
	КонецЦикла;

	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей, Истина, Истина, Истина);
	
	Дата = ПериодЗаполнения;
	
	СтруктураОбъектов = Новый Структура;
	СтруктураОбъектов.Вставить("Период", Дата);
	СтруктураОбъектов.Вставить("ГруппаНалоговогоУчетаОС", Объект.Ссылка);
	
	РедактированиеПериодическихСведений.ПрочитатьНаборЗаписейПоСтруктуре(ЭтаФорма, "НормыАмортизацииГруппНалоговогоУчетаОС", СтруктураОбъектов);
	
КонецПроцедуры 

#КонецОбласти

#Область ОбработчикиБиблиотек

// ИнтернетПоддержкаПользователей
&НаСервере
Функция ТребуетсяНастройкаАвторизацияИнтернетПоддержки()
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей") Тогда
		МодульИнтернетПоддержкаПользователей = ОбщегоНазначения.ОбщийМодуль("ИнтернетПоддержкаПользователей");
		Возврат Не МодульИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки();
	КонецЕсли;
	Возврат Ложь;
КонецФункции

&НаКлиенте
Процедура ПриПодключенииИнтернетПоддержки(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтернетПоддержкаПользователей") Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПодключенияИнтернетПоддержки", ЭтотОбъект, ДополнительныеПараметры);
		МодульИнтернетПоддержкаПользователейКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ИнтернетПоддержкаПользователейКлиент");
		МодульИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(ОписаниеОповещения, ЭтотОбъект);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеПодключенияИнтернетПоддержки(Результат, ДополнительныеПараметры) Экспорт
	Если Не (ТипЗнч(Результат) = Тип("Структура")
		И ЗначениеЗаполнено(Результат.Логин)) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьНормыАмортизацииНаКлиенте();	
КонецПроцедуры

// Конец ИнтернетПоддержкаПользователей

#КонецОбласти
