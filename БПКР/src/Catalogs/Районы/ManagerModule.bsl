#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьСтавкиЗемельногоНалога(ДополнительныеПараметры) Экспорт

	Результат = РаботаСКонтрагентамиБП.РеквизитыСтавкиНалогов("ratesLandTaxDistricts");
	
	Если ЗначениеЗаполнено(Результат.ОписаниеОшибки) Тогда 
		Возврат;
	КонецЕсли;	
	
	// Получение из XML.
	КлассификаторXML = Результат.КлассификаторXML;
	ТаблицаСтавкиЗемельногоНалога = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;
	ТаблицаСтавкиЗемельногоНалога.Индексы.Добавить("КодРайона");
	
	ПериодЗаполнения = ОбщегоНазначенияБПСервер.ПолучитьПериодИзXML(КлассификаторXML);
		
	НаборЗаписей = РегистрыСведений.СтавкиЗемельногоНалогаПоРайонам.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Район.Установить(ДополнительныеПараметры.Ссылка);
	НаборЗаписей.Отбор.Период.Установить(ПериодЗаполнения);
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();
	
	НайденныеСтроки = ТаблицаСтавкиЗемельногоНалога.НайтиСтроки(Новый Структура("КодРайона", ДополнительныеПараметры.Код));
	Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл 
		
		Запись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, НайденнаяСтрока);
		Запись.Период = ПериодЗаполнения;
		Запись.Район = ДополнительныеПараметры.Ссылка;
		Запись.ВидЗемельногоУчастка = Перечисления.ВидыЗемельныхУчастков[НайденнаяСтрока.ВидЗемельногоУчастка];
		
	КонецЦикла;

	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей, Истина, Истина, Истина);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
