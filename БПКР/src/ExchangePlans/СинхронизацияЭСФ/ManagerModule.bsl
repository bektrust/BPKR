#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращет узел плана обмена.
//
// Параметры
//
// Возвращаемое значение:
//   ПланОбменаСсылка.СинхронизацияЭСФ - узел плана обмена "СинхронизацияЭСФ"
//
Функция УзелПланаОбменаСинхронизацияЭСФ() Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	КодЯзыка = ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка();
	
	УзелСинхронизацияЭСФ = ПланыОбмена.СинхронизацияЭСФ.НайтиПоКоду("002");
	
	Если ЗначениеЗаполнено(УзелСинхронизацияЭСФ) Тогда
		Возврат УзелСинхронизацияЭСФ;
	КонецЕсли;
	
	ГлавныйУзелОбмена = ПланыОбмена.СинхронизацияЭСФ.ЭтотУзел();
	
	Если Не ЗначениеЗаполнено(ГлавныйУзелОбмена.Наименование) Тогда
		
		ГлавныйУзелОбъект = ГлавныйУзелОбмена.ПолучитьОбъект();
		ГлавныйУзелОбъект.Наименование = НСтр("ru='Центральный'", КодЯзыка);
		Попытка
			ГлавныйУзелОбъект.Записать();
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	Если УзелСинхронизацияЭСФ.Пустая() Тогда
		
		УзелСинхронизацияЭСФ = ПланыОбмена.СинхронизацияЭСФ.СоздатьУзел();
		УзелСинхронизацияЭСФ.Код = "002";
		УзелСинхронизацияЭСФ.Наименование =
			НСтр("ru='Синхронизация ЭСФ'", КодЯзыка);
		Попытка
			УзелСинхронизацияЭСФ.Записать();
			Возврат УзелСинхронизацияЭСФ.Ссылка;
		Исключение
			// Сообщение пользоватею
			ТекстСообщения = НСтр("ru = 'Не найден узел плана обмена для регистрации изменений номенклатуры.
											|Попробуйте снять и заново установить настройку ""Проверка номенклатуры"" в ""Настройки"" - ""Параметры учета""'");				
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			
			// Причина ошибки в журнал регистрации
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось завершить запись узала плана обмена ""Синхронизация ЭСФ"" по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка'"), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
		КонецПопытки;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

#КонецОбласти

#КонецЕсли
