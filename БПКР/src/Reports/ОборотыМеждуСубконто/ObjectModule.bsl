#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВариантыОтчетов

// Настройки общей формы отчета подсистемы "Варианты отчетов".

//
// Параметры:
//   Форма - УправляемаяФорма, Неопределено - Форма отчета или форма настроек отчета.
//       Неопределено когда вызов без контекста.
//   КлючВарианта - Строка, Неопределено - Имя предопределенного
//       или уникальный идентификатор пользовательского варианта отчета.
//       Неопределено когда вызов без контекста.
//   Настройки - Структура - см. возвращаемое значение
//       ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию().
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	Настройки.События.ПередЗагрузкойНастроекВКомпоновщик = Истина;
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВариантыОтчетов

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	БухгалтерскиеОтчетыВызовСервера.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ);
	
	Для Каждого ВидСубконто Из СписокВидовСубконто Цикл
		Если НЕ ЗначениеЗаполнено(ВидСубконто.Значение) Тогда
			ТекстСообщения = НСтр("ru = 'Список видов субконто не заполнен'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "Отчет.СписокВидовСубконто",, Отказ);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ВидСубконто Из СписокВидовКорСубконто Цикл
		Если НЕ ЗначениеЗаполнено(ВидСубконто.Значение) Тогда
			ТекстСообщения = НСтр("ru = 'Список видов кор. субконто не заполнен'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "Отчет.СписокВидовКорСубконто",, Отказ);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	МаксКоличествоСубконто = БухгалтерскийУчетСервер.МаксимальноеКоличествоСубконто();
	Если СписокВидовСубконто.Количество() > МаксКоличествоСубконто Тогда
		ТекстСообщения = НСтр("ru = 'Выбрано слишком много видов субконто, максимально допустимо %1'");
		ОбщегоНазначения.СообщитьПользователю(СтрШаблон(ТекстСообщения, МаксКоличествоСубконто),, "Отчет.СписокВидовСубконто",, Отказ);
	КонецЕсли;
	
	Если СписокВидовКорСубконто.Количество() > МаксКоличествоСубконто Тогда
		ТекстСообщения = НСтр("ru = 'Выбрано слишком много видов кор. субконто, максимально допустимо %1'");
		ОбщегоНазначения.СообщитьПользователю(СтрШаблон(ТекстСообщения, МаксКоличествоСубконто),, "Отчет.СписокВидовКорСубконто",, Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли