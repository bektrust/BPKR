#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура СформироватьОтчет(Знач ПараметрыОтчета, АдресХранилища) Экспорт
	
	ПараметрыОтчета.СписокСформированныхЛистов.Очистить();
	
	СписокСформированныхЛистов = Новый СписокЗначений;
	СформироватьЛистыКассовйКниги(ПараметрыОтчета, СписокСформированныхЛистов);
	
	ПоместитьВоВременноеХранилище(СписокСформированныхЛистов, АдресХранилища);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьЛистыКассовйКниги(ПараметрыОтчета, СписокСформированныхЛистов)
	
	ДокументРезультат = Новый ТабличныйДокумент;
	ДокументРезультат.КлючПараметровПечати = "КассоваяКнига_КассоваяКнига";
	
	Макет = ПолучитьМакет("КассоваяКнига");
	
	ОбластьВкладнойЛистОтчет 			= Макет.ПолучитьОбласть("ВкладнойЛист|Отчет");
	ОбластьОтчетКассираОтчет 			= Макет.ПолучитьОбласть("ОтчетКассира|Отчет");
	ОбластьШапкаОтчет 					= Макет.ПолучитьОбласть("Шапка|Отчет");
	ОбластьОстатокНаНДОтчет 			= Макет.ПолучитьОбласть("ОстатокНаНД|Отчет");
	ОбластьСтрокаОтчет 					= Макет.ПолучитьОбласть("Строка|Отчет");
	ОбластьОборотОтчет 					= Макет.ПолучитьОбласть("Оборот|Отчет");
	ОбластьКонечныйОстатокОтчет 		= Макет.ПолучитьОбласть("КонечныйОстаток|Отчет");
	ОбластьПодвалОтчет 					= Макет.ПолучитьОбласть("Подвал|Отчет");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	НомераЛистовКассовойКнигиСрезПоследних.Период,
		|	НомераЛистовКассовойКнигиСрезПоследних.Касса,
		|	НомераЛистовКассовойКнигиСрезПоследних.НомерЛиста
		|ИЗ
		|	РегистрСведений.НомераЛистовКассовойКниги.СрезПоследних(&Дата, Касса = &Касса) КАК НомераЛистовКассовойКнигиСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СУММА(ТиповойОстатки.СуммаОстаток) КАК СуммаОстаток
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(
		|			&ДатаНач,
		|			Счет В ИЕРАРХИИ (&СчетаКассы),
		|			&ВидСубконтоКасса,
		|			Организация = &Организация
		|				И Субконто1 = &Касса
		|				И (НЕ &ВВалюте
		|					ИЛИ Валюта = &Валюта)) КАК ТиповойОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	НАЧАЛОПЕРИОДА(ТиповойОбороты.Период, ДЕНЬ) КАК Период,
		|	ТиповойОбороты.Регистратор КАК Документ,
		|	ТиповойОбороты.Регистратор.Дата КАК ДатаДокумента,
		|	ТиповойОбороты.Регистратор.Номер КАК НомерДокумента,
		|	ПРЕДСТАВЛЕНИЕ(ТиповойОбороты.Регистратор) КАК ПредставлениеДокумента,
		|	ЕСТЬNULL(ТиповойОбороты.СуммаОборотДт, 0) КАК Приход,
		|	ЕСТЬNULL(ТиповойОбороты.СуммаОборотКт, 0) КАК Расход,
		|	ВЫБОР
		|		КОГДА ТиповойОбороты.Регистратор ССЫЛКА Документ.ПриходныйКассовыйОрдер
		|			ТОГДА ВЫРАЗИТЬ(ТиповойОбороты.Регистратор.ПринятоОт КАК СТРОКА(1000))
		|		КОГДА ТиповойОбороты.Регистратор ССЫЛКА Документ.РасходныйКассовыйОрдер
		|			ТОГДА ВЫРАЗИТЬ(ТиповойОбороты.Регистратор.Выдать КАК СТРОКА(1000))
		|	КОНЕЦ КАК Контрагент,
		|	ПРЕДСТАВЛЕНИЕ(ТиповойОбороты.КорСчет) КАК ПредставлениеСчета,
		|	ВЫБОР
		|		КОГДА &ВыводитьОснования
		|			ТОГДА ВЫБОР
		|					КОГДА ТиповойОбороты.Регистратор ССЫЛКА Документ.ПриходныйКассовыйОрдер
		|							ИЛИ ТиповойОбороты.Регистратор ССЫЛКА Документ.РасходныйКассовыйОрдер
		|						ТОГДА ВЫРАЗИТЬ(ТиповойОбороты.Регистратор.Основание КАК СТРОКА(1000))
		|				КОНЕЦ
		|	КОНЕЦ КАК Основание,
		|	ВЫБОР
		|		КОГДА ТиповойОбороты.Регистратор ССЫЛКА Документ.ПриходныйКассовыйОрдер
		|			ТОГДА 1
		|		КОГДА ТиповойОбороты.Регистратор ССЫЛКА Документ.РасходныйКассовыйОрдер
		|			ТОГДА 2
		|		ИНАЧЕ 3
		|	КОНЕЦ КАК ИндексСортировкиВида
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(
		|			&ДатаНач,
		|			&ДатаКон,
		|			Регистратор,
		|			Счет В ИЕРАРХИИ (&СчетаКассы),
		|			&ВидСубконтоКасса,
		|			Организация = &Организация
		|				И Субконто1 = &Касса
		|				И (НЕ &ВВалюте
		|					ИЛИ Валюта = &Валюта),
		|			,
		|			) КАК ТиповойОбороты
		|
		|УПОРЯДОЧИТЬ ПО ТиповойОбороты.Регистратор.Дата
		|	
		|ИТОГИ
		|	СУММА(Приход),
		|	СУММА(Расход)
		|ПО
		|	Период,
		|	Документ
		|АВТОУПОРЯДОЧИВАНИЕ";
		
	Касса       = ПараметрыОтчета.Касса;
	Организация = ПараметрыОтчета.Организация;
		
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	ВалютаДенежныхСредств          = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Касса, "ВалютаДенежныхСредств");
		
	СписокСчетовКассы = Новый СписокЗначений();
	СписокСчетовКассы.Добавить(ПланыСчетов.Хозрасчетный.ДенежныеСредстваВКассе);
	ОтчетВВалюте = НЕ ВалютаДенежныхСредств.Пустая() И НЕ ВалютаДенежныхСредств = ВалютаРегламентированногоУчета;
	ИмяРесурса   = ?(ОтчетВВалюте, ".ВалютнаяСумма", ".Сумма");
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, ".Сумма", ИмяРесурса);
	
	Если НЕ ПараметрыОтчета.РазбиватьЛистыПоДням Тогда		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "НАЧАЛОПЕРИОДА(ТиповойОбороты.Период, ДЕНЬ)", "&ДатаНач");
	КонецЕсли;
	
	Если ПараметрыОтчета.СортироватьПоВиду И ПараметрыОтчета.СортироватьПоНомеру Тогда
		БлокСортировки = "УПОРЯДОЧИТЬ ПО  ИндексСортировкиВида, ТиповойОбороты.Регистратор.Номер";
	ИначеЕсли ПараметрыОтчета.СортироватьПоВиду Тогда
		БлокСортировки = "УПОРЯДОЧИТЬ ПО  ИндексСортировкиВида";
	ИначеЕсли ПараметрыОтчета.СортироватьПоНомеру Тогда
		БлокСортировки = "УПОРЯДОЧИТЬ ПО ТиповойОбороты.Регистратор.Номер";
	Иначе
		БлокСортировки = "УПОРЯДОЧИТЬ ПО НАЧАЛОПЕРИОДА(ТиповойОбороты.Регистратор.Дата, ДЕНЬ), ДатаДокумента";
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "УПОРЯДОЧИТЬ ПО ТиповойОбороты.Регистратор.Дата", БлокСортировки);
	
	Запрос.УстановитьПараметр("Валюта"           , ВалютаДенежныхСредств);
	Запрос.УстановитьПараметр("ВВалюте"          , ОтчетВВалюте);
	Запрос.УстановитьПараметр("Дата"             , ПараметрыОтчета.НачалоПериода - 86400);
	Запрос.УстановитьПараметр("ДатаКон"          , КонецДня(ПараметрыОтчета.КонецПериода));
	Запрос.УстановитьПараметр("ДатаНач"          , ПараметрыОтчета.НачалоПериода);
	Запрос.УстановитьПараметр("Касса"            , Касса);
	Запрос.УстановитьПараметр("Организация"      , Организация);
	Запрос.УстановитьПараметр("СчетаКассы"       , СписокСчетовКассы);
	Запрос.УстановитьПараметр("ВидСубконтоКасса" , ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ДенежныеСредства);
	Запрос.УстановитьПараметр("ВыводитьОснования", ПараметрыОтчета.ВыводитьОснования);

	Результат = Запрос.ВыполнитьПакет();
	
	// Получим номер листа кассовой книги
	НомерЛиста = 0;
	НачалоГода = НачалоГода(ПараметрыОтчета.НачалоПериода);
	Если ПараметрыОтчета.ПересчитатьНомера Тогда
		Номера = РегистрыСведений.НомераЛистовКассовойКниги.СоздатьНаборЗаписей();
		Номера.Отбор.Касса.Значение		 = Касса;
		Номера.Отбор.Касса.Использование = Истина;
		Номера.Прочитать();
		Номера.Очистить();
		Если ПравоДоступа("Изменение", Номера.Метаданные()) Тогда
			Номера.Записать();
		КонецЕсли;
	ИначеЕсли ПараметрыОтчета.НачалоПериода <> НачалоГода Тогда
		Номера = Результат[0].Выгрузить();
		Если Номера.Количество() > 0 Тогда
			Если Номера[0].Период >= НачалоГода Тогда
				НомерЛиста = Номера[0].НомерЛиста;			
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;
	
	// Определим начальный остаток по кассе
	ТекущийОстаток = 0;
	Если НЕ Результат[1].Пустой() Тогда
		ВыборкаПоОстаткам = Результат[1].Выбрать();
		ВыборкаПоОстаткам.Следующий();
		ТекущийОстаток = ВыборкаПоОстаткам.СуммаОстаток;
	КонецЕсли;	

    // Формирование листов отчета
	Если Результат[2].Пустой() Тогда
		// Выводим только шапку таблицы
		НомерЛиста = НомерЛиста + 1;

		ДатаЛиста = ПараметрыОтчета.НачалоПериода;
		ВывестиШапкуОтчета(ДокументРезультат, ДатаЛиста, НомерЛиста, ТекущийОстаток, ОбластьВкладнойЛистОтчет, ОбластьОтчетКассираОтчет, ОбластьШапкаОтчет, ОбластьОстатокНаНДОтчет, ПараметрыОтчета);
		ВывестиПодвалОтчета(ДокументРезультат,ТекущийОстаток, ДатаЛиста, НомерЛиста, 0, 0,
							ОбластьОборотОтчет, ОбластьКонечныйОстатокОтчет,ОбластьПодвалОтчет, ПараметрыОтчета)

	Иначе
		// Были движения за период
		ВыборкаПериоды = Результат[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПериоды.Следующий() Цикл
			Если ВыборкаПериоды.Приход = 0 И ВыборкаПериоды.Расход = 0 Тогда
				Продолжить;
			КонецЕсли;
			ДатаЛиста = ВыборкаПериоды.Период;
			КоличествоРасходныхДокументов = 0;				
			КоличествоПриходныхДокументов = 0;
			
			НомерЛиста = НомерЛиста + 1;
			// Вывод Шапки листа
			ВывестиШапкуОтчета(ДокументРезультат, ДатаЛиста, НомерЛиста, 
				ТекущийОстаток, ОбластьВкладнойЛистОтчет, ОбластьОтчетКассираОтчет, ОбластьШапкаОтчет, ОбластьОстатокНаНДОтчет, ПараметрыОтчета);	
						
			// По документам
			ВыборкаДокументы = ВыборкаПериоды.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			КоличествоСтрок = ВыборкаДокументы.Количество();
			НомерСтроки = 0;
			Пока ВыборкаДокументы.Следующий() Цикл
				Если ВыборкаДокументы.Приход = 0 И ВыборкаДокументы.Расход = 0 Тогда
					Продолжить;
				КонецЕсли;
				НомерСтроки = НомерСтроки + 1;
				
				// Проверим вывод
				МассивСтрок = Новый Массив;
				МассивСтрок.Добавить(ОбластьСтрокаОтчет);
				// Если строка последняя, то нужно проверить не только его помещение на листе,
				// но и сроки по оборотам, остатку и подвалу
				Если НомерСтроки = КоличествоСтрок Тогда					
					МассивСтрок.Добавить(ОбластьОборотОтчет);
					МассивСтрок.Добавить(ОбластьКонечныйОстатокОтчет);
					МассивСтрок.Добавить(ОбластьПодвалОтчет);
				КонецЕсли;
				
				Если НЕ ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ДокументРезультат, МассивСтрок) Тогда
					НомерЛиста = НомерЛиста + 1;
					ОбластьШапкаОтчет.Параметры.ТекстНомерЛиста = "Лист " + НомерЛиста;
					
					ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
					ВывестиДанныеВОтчет(ДокументРезультат, ОбластьШапкаОтчет, ОбластьШапкаОтчет, ПараметрыОтчета);
				КонецЕсли;
				
				Если ПараметрыОтчета.НомераДокументовПоПараметрамУчета Тогда
					НомерДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(СокрЛП(ВыборкаДокументы.НомерДокумента));
				Иначе
				    НомерДокумента = СокрЛП(ВыборкаДокументы.НомерДокумента);
				КонецЕсли;
				
				ПредставлениеДокумента = СокрЛП(ВыборкаДокументы.Документ.Метаданные().Синоним) + " № " + НомерДокумента + НСтр("ru = ' от '") + Формат(ВыборкаДокументы.ДатаДокумента, "ДФ=дд.ММ.гг");			
				ОбластьСтрокаОтчет.Параметры.Заполнить(ВыборкаДокументы);
				ОбластьСтрокаОтчет.Параметры.НомерДокПечатнойФормы = ПредставлениеДокумента;
				
				ОбластьСтрокаОтчет.Параметры.Контрагент = СокрЛП(ВыборкаДокументы.Контрагент) + " "+ СокрЛП(ВыборкаДокументы.Основание);
				
				// По счетам
				ВыборкаСчета = ВыборкаДокументы.Выбрать();
				СчетаСтрокой = "";
				Пока ВыборкаСчета.Следующий() Цикл
					СчетаСтрокой = СчетаСтрокой + "," + СокрЛП(ВыборкаСчета.ПредставлениеСчета);
				КонецЦикла;	
				// Обрезаем первую запятую
				СчетаСтрокой = Сред(СчетаСтрокой, 2);
				ОбластьСтрокаОтчет.Параметры.КоррСчет = СчетаСтрокой;
				
				ВывестиДанныеВОтчет(ДокументРезультат, ОбластьСтрокаОтчет, ОбластьСтрокаОтчет, ПараметрыОтчета);
				
				Если ВыборкаДокументы.Расход <> 0 Тогда
					КоличествоРасходныхДокументов = КоличествоРасходныхДокументов + 1;				
				Иначе				
					КоличествоПриходныхДокументов = КоличествоПриходныхДокументов + 1;
				КонецЕсли;	
			КонецЦикла;// Выборка периодов
			// итоги за день - Обороты
			ОбластьОборотОтчет.Параметры.Заполнить(ВыборкаПериоды);
			
			// итоги за день - Остаток
			ТекущийОстаток = ТекущийОстаток + ВыборкаПериоды.Приход - ВыборкаПериоды.Расход;		
			ОбластьКонечныйОстатокОтчет.Параметры.ОстатокКонец = ТекущийОстаток;		
				
			ВывестиПодвалОтчета(ДокументРезультат,ТекущийОстаток, ДатаЛиста, НомерЛиста, КоличествоПриходныхДокументов, КоличествоРасходныхДокументов,
							ОбластьОборотОтчет, ОбластьКонечныйОстатокОтчет, ОбластьПодвалОтчет, ПараметрыОтчета)
										
		КонецЦикла; // Выборка периодов
						
	КонецЕсли;
	
	ДокументРезультат.ТолькоПросмотр = Истина;
	СписокСформированныхЛистов.Добавить(ДокументРезультат, "Листы");
	
	Если ПараметрыОтчета.ПечататьТитульныйЛист Тогда
		ЛистовЗаГод = НомерЛиста;
		СформироватьТитульныйЛист(ЛистовЗаГод, ПараметрыОтчета, СписокСформированныхЛистов);
	КонецЕсли;	
	
КонецПроцедуры

// Формирование обложки книги
Процедура СформироватьТитульныйЛист(ЛистовЗаГод, ПараметрыОтчета, СписокСформированныхЛистов)
	
	// Печать обложки и титульного листа
	Т1			   		  	= Новый ТабличныйДокумент;
	Т1.КлючПараметровПечати = "КассоваяКнига_Обложка";
	Т1.ОриентацияСтраницы 	= ОриентацияСтраницы.Ландшафт;
	Макет1 		   		  	= ПолучитьМакет("Обложка");
	ОбластьОбложка		  	= Макет1.ПолучитьОбласть("Обложка");
	КонецПериода         	= ПараметрыОтчета.КонецПериода;
	
	СведенияОбОрганизации 	= БухгалтерскийУчетСервер.ПолучитьСведенияОбОрганизации(ПараметрыОтчета.Организация, КонецПериода);
	
	ОбластьОбложка.Параметры.НазваниеОрганизации = СведенияОбОрганизации.НаименованиеПолное;
	ОбластьОбложка.Параметры.КодОКПО             = СведенияОбОрганизации.ОКПО;
	ОбластьОбложка.Параметры.НадписьОбложка 	 = НСтр("ru = ' на '") + Формат(Год(КонецПериода), "ЧГ=0") + НСтр("ru = ' г.'");
	Т1.Вывести(ОбластьОбложка);
	
	// Последний лист кассовой книги	
	ОбластьПослДеньГода = Макет1.ПолучитьОбласть("ПослДеньГода");
	
	Руководители = БухгалтерскийУчетСервер.ОтветственныеЛицаОрганизацийРуководители(ПараметрыОтчета.Организация, КонецПериода);
	
	ОбластьПослДеньГода.Параметры.ГлБухгалтер  = Руководители.ГлавныйБухгалтер;
	ОбластьПослДеньГода.Параметры.Руководитель = Руководители.Руководитель;
	Т1.Вывести(ОбластьПослДеньГода);
	Т1.ОтображатьСетку	   = Ложь;
	Т1.ОтображатьЗаголовки = Ложь;
	
	Т1.ТолькоПросмотр = Истина;
	
	СписокСформированныхЛистов.Добавить(Т1, "Обложка и титульный лист");
	
КонецПроцедуры

// Выводит данные в табличный документ
// в соответствии снастройкой печати разделов кассовой книги.
Процедура ВывестиДанныеВОтчет(ДокументРезультат, ОбластьВкладнойЛист, ОбластьОтчетКассира, ПараметрыОтчета)
	
	// Вывод раздела "Вкладной лист кассовой книги"
	Если ПараметрыОтчета.ПечататьВкладнойЛист Тогда							
		ДокументРезультат.Вывести(ОбластьВкладнойЛист);
	КонецЕсли;
	
	// Вывод раздела "Отчет кассира"
	Если ПараметрыОтчета.ПечататьОтчетКассира Тогда							
		Если ПараметрыОтчета.ПечататьВкладнойЛист Тогда					
			ДокументРезультат.Присоединить(ОбластьОтчетКассира);		
		Иначе
			ДокументРезультат.Вывести(ОбластьОтчетКассира);		
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ВывестиДанныеВОтчет()

// Процедура - выводит шапку таблицы
//
Процедура ВывестиШапкуОтчета(ДокументРезультат, ДатаЛиста, НомерЛиста, ТекущийОстаток, ОбластьВкладнойЛистОтчет, ОбластьОтчетКассираОтчет, ОбластьШапкаОтчет, ОбластьОстатокНаНДОтчет, ПараметрыОтчета)
		
	// Вывод Шапки листа
	ДанныеКассы = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПараметрыОтчета.Касса, "Наименование, ВалютаДенежныхСредств");
	
	ПериодЛистаСтрокой = НСтр("ru = 'КАССА'") + ?(ПараметрыОтчета.ВыводитьНазваниеКассы, ": " + СокрЛП(ДанныеКассы.Наименование),"")+ " " + ?(ПараметрыОтчета.РазбиватьЛистыПоДням, НСтр("ru = 'за '") + Формат(ДатаЛиста, "ДЛФ=D"),
						 НСтр("ru = 'за период с '") + Формат(ПараметрыОтчета.НачалоПериода, "ДЛФ=D") + НСтр("ru = ' по '") + Формат(ПараметрыОтчета.КонецПериода, "ДЛФ=D"));		
						 
	ОбластьВкладнойЛистОтчет.Параметры.ЗаголовокЛиста = ПериодЛистаСтрокой;							
	ОбластьОтчетКассираОтчет.Параметры.ЗаголовокЛиста = ПериодЛистаСтрокой;
	
	ВывестиДанныеВОтчет(ДокументРезультат, ОбластьВкладнойЛистОтчет, ОбластьОтчетКассираОтчет, ПараметрыОтчета);
	
	ОбластьШапкаОтчет.Параметры.ТекстНомерЛиста 	= НСтр("ru = 'Лист '") + НомерЛиста;
	ОбластьШапкаОтчет.Параметры.ВалютаПредставление = ДанныеКассы.ВалютаДенежныхСредств.Наименование;
	
	Если ПараметрыОтчета.ВыводитьНаименованиеОрганизации Тогда 
		ОбластьШапкаОтчет.Параметры.ЗаголовокЛиста = ПараметрыОтчета.Организация.НаименованиеПолное;
	КонецЕсли;	
	
	ВывестиДанныеВОтчет(ДокументРезультат, ОбластьШапкаОтчет, ОбластьШапкаОтчет, ПараметрыОтчета);
	
	ОбластьОстатокНаНДОтчет.Параметры.ОстатокНачало = ТекущийОстаток;	
	ВывестиДанныеВОтчет(ДокументРезультат, ОбластьОстатокНаНДОтчет, ОбластьОстатокНаНДОтчет, ПараметрыОтчета);
	
КонецПроцедуры  // ВывестиШапкуОтчета

// Процедура - выводит подвал таблицы
//
Процедура ВывестиПодвалОтчета(ДокументРезультат,ТекущийОстаток, ДатаЛиста, НомерЛиста, КоличествоПриходныхДокументов, КоличествоРасходныхДокументов,
	ОбластьОборотОтчет, ОбластьКонечныйОстатокОтчет,ОбластьПодвалОтчет, ПараметрыОтчета)
	
	// итоги за день - Обороты	
	ВывестиДанныеВОтчет(ДокументРезультат, ОбластьОборотОтчет, ОбластьОборотОтчет, ПараметрыОтчета);
	
	// итоги за день - Остаток
	ОбластьКонечныйОстатокОтчет.Параметры.ОстатокКонец = ТекущийОстаток;		
	ВывестиДанныеВОтчет(ДокументРезультат, ОбластьКонечныйОстатокОтчет, ОбластьКонечныйОстатокОтчет, ПараметрыОтчета);
	
	// итоги за день - подписи
	ОбластьПодвалОтчет.Параметры.НадписьКолПриходных = ?(КоличествоПриходныхДокументов > 0, ЧислоПрописью(КоличествоПриходныхДокументов,"НД=Ложь",",,,,,,,,0")," - ");																
	ОбластьПодвалОтчет.Параметры.НадписьКолРасходных = ?(КоличествоРасходныхДокументов > 0, ЧислоПрописью(КоличествоРасходныхДокументов,"НД=Ложь",",,,,,,,,0")," - ");	
	
	Руководители = ПолучитьОтветственныхЛиц(ДатаЛиста, ПараметрыОтчета);
	
	ОбластьПодвалОтчет.Параметры.ГлБухгалтер = Руководители.ГлавныйБухгалтер;
	ОбластьПодвалОтчет.Параметры.Кассир 	 = Руководители.Кассир;
	
	ВывестиДанныеВОтчет(ДокументРезультат, ОбластьПодвалОтчет, ОбластьПодвалОтчет, ПараметрыОтчета);
	
	ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
	
	// обновление номера листа
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Дата", ДатаЛиста);
	Запрос.УстановитьПараметр("Касса", ПараметрыОтчета.Касса);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Касса,
	|	НомерЛиста
	|ИЗ
	|	РегистрСведений.НомераЛистовКассовойКниги.СрезПоследних(&Дата, Касса = &Касса) КАК НомераЛистовКассовойКнигиСрезПоследних";
	ТекНомера = Запрос.Выполнить().Выгрузить();
	
	Если ТекНомера.Количество() = 0 ИЛИ ТекНомера[0].НомерЛиста <> НомерЛиста Тогда
		
		Номера = РегистрыСведений.НомераЛистовКассовойКниги.СоздатьНаборЗаписей();
		
		Номера.Отбор.Касса.Значение 			= ПараметрыОтчета.Касса; 
		Номера.Отбор.Касса.Использование  		= Истина;
		
		Номера.Отбор.Период.Значение 			= ДатаЛиста; 
		Номера.Отбор.Период.Использование 		= Истина; 
		
		НоваяЗапись = Номера.Добавить();
		
		НоваяЗапись.Касса		= ПараметрыОтчета.Касса;
		НоваяЗапись.Период 		= ДатаЛиста;
		НоваяЗапись.НомерЛиста 	= НомерЛиста;				
		Если ПравоДоступа("Изменение", Номера.Метаданные()) Тогда
			Номера.Записать();
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры  // ВывестиПодвалОтчета

// Возвращает структуру значений
// ГлавныйБухгалтер и Кассир с учетом данных регистров "Ответственные лица" и "Ответственные лица организаций"
Функция ПолучитьОтветственныхЛиц(ДатаЛиста, ПараметрыОтчета)
	
	СтруктураРезультата = Новый Структура("ГлавныйБухгалтер, Кассир");
	
	ОтборПоКассе = Новый Структура();
	Если ЗначениеЗаполнено(ПараметрыОтчета.Касса) Тогда 
		ОтборПоКассе.Вставить("Касса", ПараметрыОтчета.Касса);
	КонецЕсли;
	
	Руководители = БухгалтерскийУчетСервер.ОтветственныеЛицаОрганизацийРуководители(ПараметрыОтчета.Организация, ПараметрыОтчета.КонецПериода,ОтборПоКассе);
	СтруктураРезультата.ГлавныйБухгалтер = Руководители.ГлавныйБухгалтер;
	СтруктураРезультата.Кассир = Руководители.Кассир;
	
    Возврат СтруктураРезультата;
	
КонецФункции	


#КонецОбласти

#КонецЕсли
