///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2018, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ПроверитьВерсиюИРежимСовместимостиПлатформы();
	
	ЗаполнитьСписокТиповОбъектов();

	РежимОткрытияОкна = РежимОткрытияОкнаФормы.Независимый;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Перенумеровать(Команда)
	Отказ = Ложь;
	
	Если НЕ ПроверитьЗаполнение() Тогда 
		Отказ = Истина;
	КонецЕсли;
	
	Если Отказ Тогда 
		Возврат
	КонецЕсли;	
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтветНаВопросСформировать", ЭтотОбъект);
	ТекстВопроса = НСтр("ru = 'Будет выполнена перенумерация справочника. Продолжить?'");
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПредставлениеИзменяемыхОбъектовПриИзменении(Элемент)
	ВыбранныйТип = Элементы.ПредставлениеИзменяемыхОбъектов.СписокВыбора.НайтиПоЗначению(ПредставлениеИзменяемыхОбъектов);
	Если ВыбранныйТип = Неопределено Тогда
		Для Каждого Тип Из Элементы.ПредставлениеИзменяемыхОбъектов.СписокВыбора Цикл
			Если СтрНайти(НРег(Тип.Представление), НРег(ПредставлениеИзменяемыхОбъектов)) = 1 Тогда
				ВыбранныйТип = Тип;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ВыбранныйТип = Неопределено Тогда
		ВидыИзменяемыхОбъектов = "";
	Иначе
		ВидыИзменяемыхОбъектов = ВыбранныйТип.Значение;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеИзменяемыхОбъектовАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	ПолеВводаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиРезультатовИнтерактивныхДействий

&НаКлиенте
Процедура ОтветНаВопросСформировать(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Результат = ПеренумероватьНаСервере();
		
		Если Результат.Отказ Тогда 
			ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ТекстСообщения);
		Иначе
			ТекстОповещения = НСтр("ru = 'Перенумерация справочника завершена.'");
			ТекстПояснения = СтрШаблон(НСтр("ru = 'Обработано %1 элементов.'"), Результат.Количество);
			ПоказатьОповещениеПользователя(ТекстОповещения,, ТекстПояснения, БиблиотекаКартинок.Информация32);
			ОбновитьИнтерфейс();
		КонецЕсли;	
	КонецЕсли; 
КонецПроцедуры
	
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокТиповОбъектов()
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ЗаполнитьКоллекциюДоступныхДляИзмененияОбъектов(Элементы.ПредставлениеИзменяемыхОбъектов.СписокВыбора, Ложь);
КонецПроцедуры

&НаСервере
Процедура ПроверитьВерсиюИРежимСовместимостиПлатформы()
	
	Информация = Новый СистемнаяИнформация;
	Если Не (Лев(Информация.ВерсияПриложения, 3) = "8.3"
		И (Метаданные.РежимСовместимости = Метаданные.СвойстваОбъектов.РежимСовместимости.НеИспользовать
		Или (Метаданные.РежимСовместимости <> Метаданные.СвойстваОбъектов.РежимСовместимости.Версия8_1
		И Метаданные.РежимСовместимости <> Метаданные.СвойстваОбъектов.РежимСовместимости.Версия8_2_13
		И Метаданные.РежимСовместимости <> Метаданные.СвойстваОбъектов.РежимСовместимости["Версия8_2_16"]
		И Метаданные.РежимСовместимости <> Метаданные.СвойстваОбъектов.РежимСовместимости["Версия8_3_1"]
		И Метаданные.РежимСовместимости <> Метаданные.СвойстваОбъектов.РежимСовместимости["Версия8_3_2"]))) Тогда
		
		ВызватьИсключение НСтр("ru = 'Обработка предназначена для запуска на версии платформы
			|1С:Предприятие 8.3 с отключенным режимом совместимости или выше'");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеВводаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	ДанныеВыбора = ДанныеВыбора(Текст, Элемент.СписокВыбора);
	СтандартнаяОбработка = Не ЗначениеЗаполнено(Текст);
КонецПроцедуры

&НаКлиенте
Функция ДанныеВыбора(Строка, СписокВыбора)
	
	Результат = Новый СписокЗначений;
	
	Если Не ЗначениеЗаполнено(Строка) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Для Каждого Элемент Из СписокВыбора Цикл
		ПредставлениеЭлемента = Элемент.Представление;
		
		СтрокаПоиска = ПредставлениеЭлемента;
		
		ФорматированныеСтроки = Новый Массив;
		Для Каждого Подстрока Из СтрРазделить(Строка, " ", Ложь) Цикл
			Позиция = СтрНайти(НРег(СтрокаПоиска), НРег(Подстрока));
			Если Позиция = 0 Тогда
				ФорматированныеСтроки = Неопределено;
				Прервать;
			КонецЕсли;
			
			ПодстрокаДоВхождения = Лев(СтрокаПоиска, Позиция - 1);
			ПодстрокаВхождения = Сред(СтрокаПоиска, Позиция, СтрДлина(Подстрока));
			СтрокаПоиска = Сред(СтрокаПоиска, Позиция + СтрДлина(Подстрока));
			
			ФорматированныеСтроки.Добавить(ПодстрокаДоВхождения);
			ФорматированныеСтроки.Добавить(Новый ФорматированнаяСтрока(ПодстрокаВхождения, Новый Шрифт( , , Истина), Новый Цвет(0,128,0)));
		КонецЦикла;
		
		Если Не ЗначениеЗаполнено(ФорматированныеСтроки) Тогда
			Продолжить;
		КонецЕсли;
		
		ФорматированныеСтроки.Добавить(СтрокаПоиска);
		СтрокаСПодсветкой = Новый ФорматированнаяСтрока(ФорматированныеСтроки);
		
		Результат.Добавить(Элемент.Значение, СтрокаСПодсветкой);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПеренумероватьНаСервере()

	Результат = Новый Структура("ТекстСообщения, Отказ, Количество", "", Ложь, 0);
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТаблицаСправочника.Ссылка КАК Ссылка
		|ИЗ
		|	&ВидыИзменяемыхОбъектов КАК ТаблицаСправочника";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВидыИзменяемыхОбъектов", ВидыИзменяемыхОбъектов);
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда 
		Результат.ТекстСообщения = НСтр("ru = 'Нет справочников соответствующих указанным параметрам.'");
		Результат.Отказ = Истина;
		Возврат Результат;
	КонецЕсли;	
	
	НачатьТранзакцию();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	// Предварительный сброс всех номеров.
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СправочникОбъект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
		СправочникОбъект.УстановитьНовыйКод("§");	
		
		БухгалтерскийУчетСервер.ЗаписатьСправочникОбъект(СправочникОбъект,, Результат.Отказ);

		Если Результат.Отказ Тогда 
			ОтменитьТранзакцию();
			Результат.ТекстСообщения = НСтр("ru = 'Не удалось завершить запись элемента справочника. Подробнее см. Журнал регистрации.'");
			Возврат Результат;
		КонецЕсли;	
	КонецЦикла;
	
	// Установка нового номера
	ВыборкаДетальныеЗаписи.Сбросить();
	ТекущийНомер = 0;
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ТекущийНомер = ТекущийНомер + 1;

		СправочникОбъект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
		СправочникОбъект.УстановитьНовыйКод("0");	
		
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СправочникОбъект);

		Если Результат.Отказ Тогда 
			ОтменитьТранзакцию();
			Результат.ТекстСообщения = НСтр("ru = 'Не удалось завершить запись элемента справочника. Подробнее см. Журнал регистрации.'");
			Возврат Результат;
		КонецЕсли;	
	КонецЦикла;
	
	ЗафиксироватьТранзакцию();
	
	Результат.Количество = ВыборкаДетальныеЗаписи.Количество();
	Возврат Результат;
	
КонецФункции

#КонецОбласти
