#Область ОписаниеПеременных

&НаКлиенте
Перем ПараметрыОбработчикаОжидания;

#КонецОбласти

#Область ОбработчикиСобытийФормы

// Процедура - обработчик события ПриСозданииНаСервере.
// В процедуре осуществляется
// - инициализация реквизитов формы,
// - установка параметров функциональных опций формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ЭтоОсновнаяФормаПрайсЛистов.Пометка = (БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяФормаПрайсЛиста") = Перечисления.ОсновнаяФормаПрайсЛиста.ФормированиеПрайсЛистов);
	
	Если Параметры.Свойство("ОткрытьОсновнойРабочийПрайсЛист") Тогда
		
		ПрайсЛист = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнойРабочийПрайсЛист");
		
	Иначе
		
		Параметры.Свойство("ПрайсЛист", ПрайсЛист);
		
	КонецЕсли;
	
	АвтоматическиФормироватьОбновлятьПрайсЛист = (ЗначениеЗаполнено(ПрайсЛист) И БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("АвтоматическиФормироватьОбновлятьПрайсЛист"));
	
	КэшЗначений = Новый Структура;
	КэшЗначений.Вставить("АвтоматическиФормироватьОбновлятьПрайсЛист", АвтоматическиФормироватьОбновлятьПрайсЛист);
	КэшЗначений.Вставить("УИДЗамера", Неопределено);
	ОбновитьКэшЗначений();
	
КонецПроцедуры

// Процедура - обработчик события ПриОткрытии.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(ПрайсЛист) Тогда
		
		СформироватьПрайсЛистНаКлиенте();
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события ОбработкаВыбора.
//
&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ВРег(ИсточникВыбора.ИмяФормы) = ВРег("ОбщаяФорма.СохранениеПечатнойФормы") Тогда
		
		Если ВыбранноеЗначение <> Неопределено 
			И ВыбранноеЗначение <> КодВозвратаДиалога.Отмена Тогда
			
			ФайлыВоВременномХранилище = ПоместитьТабличныеДокументыВоВременноеХранилище(ВыбранноеЗначение);
			Если ВыбранноеЗначение.ВариантСохранения = "СохранитьВПапку" Тогда
				
				СохранитьПечатныеФормыВПапку(ФайлыВоВременномХранилище, ВыбранноеЗначение.ПапкаДляСохранения);
				
			ИначеЕсли ВыбранноеЗначение.ВариантСохранения = "Присоединить" Тогда
				
				ЗаписанныеОбъекты = ПрисоединитьПечатныеФормыКОбъекту(ФайлыВоВременномХранилище, ВыбранноеЗначение.ОбъектДляПрикрепления);
				Если ЗаписанныеОбъекты.Количество() > 0 Тогда
					
					ОповеститьОбИзменении(ТипЗнч(ЗаписанныеОбъекты[0]));
					
				КонецЕсли;
				
				Для Каждого ЗаписанныйОбъект Из ЗаписанныеОбъекты Цикл
					
					Оповестить("Запись_ПрисоединенныйФайл", Новый Структура, ЗаписанныйОбъект);
					
				КонецЦикла;
				
				Состояние(НСтр("ru = 'Сохранение успешно завершено.'"));
				
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ВРег(ИсточникВыбора.ИмяФормы) = ВРег("ОбщаяФорма.ВыборФорматаВложений") Тогда
		
		Если ВыбранноеЗначение <> Неопределено 
			И ВыбранноеЗначение <> КодВозвратаДиалога.Отмена Тогда
			
			СписокВложений = ПоместитьТабличныеДокументыВоВременноеХранилище(ВыбранноеЗначение);
			
			ПараметрыНовогоПисьма = Новый Структура;
			
			Тема = НСтр("ru = 'Прайс-лист '") + ?(ЗначениеЗаполнено(КэшЗначений.Организация), КэшЗначений.ОрганизацияНаименование, "");
			ПараметрыНовогоПисьма.Вставить("Тема", Тема);
			
			Тело = ?(ЗначениеЗаполнено(КэшЗначений.ВыводитьДатуФормирования), НСтр("ru = 'Сформирован '") + Формат(КэшЗначений.ДатаФормирования, "ДЛФ=DD"), "");
			ПараметрыНовогоПисьма.Вставить("Текст", Тело);
			
			ПараметрыНовогоПисьма.Вставить("Вложения", СписокВложений);
			
			ДокументыОснования = Новый Массив;
			ДокументыОснования.Добавить(ПрайсЛист);
			ПараметрыНовогоПисьма.Вставить("ДокументыОснования", ДокументыОснования);
			
			РаботаСПочтовымиСообщениямиКлиент.СоздатьНовоеПисьмо(ПараметрыНовогоПисьма);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события ОбработкаОповещения.
//
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененаЦена" Тогда
		
		Если КэшЗначений.АвтоматическиФормироватьОбновлятьПрайсЛист Тогда
			
			СформироватьПрайсЛистНаКлиенте();
			
		Иначе
			
			ПрайсЛистНеактуален();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТабличныйДокументОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	Если ТипЗнч(Расшифровка) = Тип("Структура") Тогда
		
		Если Расшифровка.Свойство("ЭтоХарактеристика") Тогда
			
			СтандартнаяОбработка = Ложь;
			//ОткрытьФорму("Справочник.ХарактеристикиНоменклатуры.ФормаОбъекта", Новый Структура("Ключ", Расшифровка.Характеристика), ЭтаФорма);
			
		Иначе
			
			СтандартнаяОбработка = Ложь;
			ОбработатьРасшифровкуЯчейки(Расшифровка);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода ПрайсЛист.
//
&НаКлиенте
Процедура ПрайсЛистПриИзменении(Элемент)
	
	ОбновитьКэшЗначений();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сформировать(Команда)
	
	СформироватьПрайсЛистНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтоОсновнаяФормаПрайсЛистов(Команда)
	
	ИзменитьНастройкуОсновнойФормыПрайсЛистов();
	
КонецПроцедуры

// Процедура - обработчик команды Сохранить.
//
&НаКлиенте
Процедура Сохранить(Команда)
	
	СохранитьПечатныеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура Отправить(Команда)
	
	ОтправитьПечатныеФормыПоПочте();
	
КонецПроцедуры

&НаКлиенте
Процедура Добавить(Команда)
	
	ОбработатьКомандуКонтекстногоМеню("Добавить");
	
КонецПроцедуры

&НаКлиенте
Процедура Изменить(Команда)
	
	ОбработатьКомандуКонтекстногоМеню("Изменить");
	
КонецПроцедуры

&НаКлиенте
Процедура Исключить(Команда)
	
	ОбработатьКомандуКонтекстногоМеню("Исключить");
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключенияПрайсЛиста(Команда)
	
	Отбор = Новый Структура;
	Отбор.Вставить("ПрайсЛист", ПрайсЛист);
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Отбор", Отбор);
	
	ОткрытьФорму("РегистрСведений.ИсключенияПрайсЛистов.ФормаСписка", ПараметрыОткрытия, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПрайсЛистНеактуален()
	
	ОтображениеСостояния = Элементы.ТабличныйДокумент.ОтображениеСостояния;
	ОтображениеСостояния.Видимость = Истина;
	ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.НеИспользовать;
	ОтображениеСостояния.Картинка = БиблиотекаКартинок.ИнтернетПоддержкаВнимание;
	ОтображениеСостояния.Текст = НСтр("ru = 'Возможно данные прайс-листа изменились. 
		|Рекомендуется повторно сформировать прайс-лист.'");
	
КонецПроцедуры

&НаСервере
Функция ЗаписьОЦенеСуществует(Знач ДанныеРасшифровки, КэшЗначений)
	
	КлючЗаписи = Новый Структура("Период, Организация, ТипЦен, Номенклатура");
	
	Если ТипЗнч(ДанныеРасшифровки) <> Тип("Структура") Тогда
		
		ДанныеРасшифровки = Новый Структура;
		
	КонецЕсли;
	
	Если НЕ ДанныеРасшифровки.Свойство("Период", КлючЗаписи.Период) Тогда
		
		КлючЗаписи.Период = ТекущаяДатаСеанса();
		
	КонецЕсли;
	
	Если НЕ ДанныеРасшифровки.Свойство("Организация", КлючЗаписи.Организация) Тогда
		
		КлючЗаписи.Организация = КэшЗначений.Организация;
		
	КонецЕсли;
	
	Если НЕ ДанныеРасшифровки.Свойство("ТипЦен", КлючЗаписи.ТипЦен) Тогда
		
		КлючЗаписи.ТипЦен = КэшЗначений.ТипЦен;
		
	КонецЕсли;
	
	Если НЕ ДанныеРасшифровки.Свойство("Номенклатура", КлючЗаписи.Номенклатура) Тогда
		
		КлючЗаписи.Номенклатура = Справочники.Номенклатура.ПустаяСсылка();
		
	КонецЕсли;
		
	Возврат ЗаписьОЦенеСуществуетСервер(КлючЗаписи);
	
КонецФункции

&НаСервере
Функция ЗаписьОЦенеСуществуетСервер(КлючЗаписи)

	СтруктураВозврата = Новый Структура("ЗаписьСуществует, Период, Организация, ТипЦен, Номенклатура", Ложь);
	ЗаполнитьЗначенияСвойств(СтруктураВозврата, КлючЗаписи);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РС.Период,
	|	РС.Организация,
	|	РС.ТипЦен,
	|	РС.Номенклатура,
	|	РС.Валюта,
	|	РС.Цена
	|ИЗ
	|	РегистрСведений.ЦеныНоменклатуры КАК РС
	|ГДЕ
	|	РС.Период 			= &Период
	|	И РС.Организация 	= &Организация
	|	И РС.ТипЦен 		= &ТипЦен
	|	И РС.Номенклатура	= &Номенклатура";
	
	Запрос.УстановитьПараметр("Период", 		НачалоДня(КлючЗаписи.Период));
	Запрос.УстановитьПараметр("Организация", 	КлючЗаписи.Организация);
	Запрос.УстановитьПараметр("ТипЦен", 		КлючЗаписи.ТипЦен);
	Запрос.УстановитьПараметр("Номенклатура", 	КлючЗаписи.Номенклатура);
	
	ТаблицаРезультат = Запрос.Выполнить().Выгрузить();
	СтруктураВозврата.ЗаписьСуществует = (ТаблицаРезультат.Количество() > 0);
	
	Возврат СтруктураВозврата;	

КонецФункции

&НаКлиенте
// Процедура открывает запись реистра.
//
Процедура ОткрытьФормуЗаписиРегистра(ДанныеРасшифровки)
	
	КлючЗаписи = ЗаписьОЦенеСуществует(ДанныеРасшифровки, КэшЗначений);
	
	Если КлючЗаписи.ЗаписьСуществует Тогда
		
		КлючЗаписи.Удалить("ЗаписьСуществует");
		
		ПараметрыМассив = Новый Массив;
		ПараметрыМассив.Добавить(КлючЗаписи);
		
		КлючЗаписиРегистра = Новый("РегистрСведенийКлючЗаписи.ЦеныНоменклатуры", ПараметрыМассив);
		ОткрытьФорму("РегистрСведений.ЦеныНоменклатуры.ФормаЗаписи", Новый Структура("Ключ", КлючЗаписиРегистра));
		
	Иначе
		
		ОткрытьФорму("РегистрСведений.ЦеныНоменклатуры.ФормаЗаписи", Новый Структура("ЗначенияЗаполнения", КлючЗаписи));
		
	КонецЕсли; 
	
КонецПроцедуры // ОткрытьФормуЗаписиРегистра()

&НаКлиенте
Процедура ОбработатьРасшифровкуЯчейки(ДанныеРасшифровки)
	
	Если ДанныеРасшифровки.Свойство("ТипЦен")
		И ДанныеРасшифровки.Свойство("Номенклатура") Тогда
		
		ОткрытьФормуЗаписиРегистра(ДанныеРасшифровки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьНовуюЗаписьОЦене(ДанныеРасшифровки)
	
	ОткрытьФормуЗаписиРегистра(ДанныеРасшифровки);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьЗаписьОЦене(ДанныеРасшифровки)
	
	ОткрытьФормуЗаписиРегистра(ДанныеРасшифровки);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьЗаписьОЦене(ДанныеРасшифровки)
	
	Если ТипЗнч(ДанныеРасшифровки) = Тип("Структура") 
		И ДанныеРасшифровки.Свойство("Номенклатура") Тогда
		
		ДобавитьИсключение(ДанныеРасшифровки.Номенклатура, ПрайсЛист);
		
		ПрайсЛистНеактуален();
		
	ИначеЕсли ТипЗнч(ДанныеРасшифровки) = Тип("СправочникСсылка.Номенклатура") Тогда
		
		ДобавитьИсключение(ДанныеРасшифровки, ПрайсЛист);
		ПрайсЛистНеактуален();
		
	Иначе
		
		ТекстСообщения = НСтр("ru = 'Для исключения позиции из прайс-листа необходимо указать номенклатуру.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьИсключение(Номенклатура, ПрайсЛист)
	
	РегистрыСведений.ИсключенияПрайсЛистов.ДобавитьИсключение(Номенклатура, ПрайсЛист);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеФормированияПрайсЛиста()
	
	ОтображениеСостояния = Элементы.ТабличныйДокумент.ОтображениеСостояния;
	ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.НеИспользовать;
	ОтображениеСостояния.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьФормированиеПрайсЛиста()
	
	РезультатПроверки = ПроверитьВыполнениеНаСервере(ФоновоеЗаданиеИдентификатор, ФоновоеЗаданиеАдресХранилища);
	
	Если РезультатПроверки.ЗаданиеВыполнено Тогда		
		ПослеФормированияПрайсЛиста();
		
	Иначе
		
		ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("ПроверитьФормированиеПрайсЛиста", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьПрайсЛистНаКлиенте()
	
	КэшЗначений.УИДЗамера = Неопределено;	
	ДокументСформирован = Ложь;
	СформироватьПрайсЛистНаСервере(ДокументСформирован);
	
	Если ДокументСформирован Тогда		
		ПослеФормированияПрайсЛиста();		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьПрайсЛистНаСервере(ДокументСформирован)
	
	ПараметрыФЗ			= Новый Структура("ПрайсЛист", ПрайсЛист);
	ЗаданиеРезультат	= ДлительныеОперации.ЗапуститьВыполнениеВФоне(УникальныйИдентификатор, "Обработки.ФормированиеПрайсЛистов.СформироватьПЛ", ПараметрыФЗ, НСтр("ru = 'Формирование прайс-листа.'"));
	ДокументСформирован = ЗаданиеРезультат.ЗаданиеВыполнено;
	
	Если ДокументСформирован Тогда		
		Результат = ПолучитьИзВременногоХранилища(ЗаданиеРезультат.АдресХранилища);
		
		Если ТипЗнч(Результат) = Тип("ТабличныйДокумент") Тогда			
			ТабличныйДокумент = Результат;			
		КонецЕсли;
		
	Иначе		
		ФоновоеЗаданиеИдентификатор  = ЗаданиеРезультат.ИдентификаторЗадания;
		ФоновоеЗаданиеАдресХранилища = ЗаданиеРезультат.АдресХранилища;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьВыполнениеНаСервере(ФоновоеЗаданиеИдентификатор, ФоновоеЗаданиеАдресХранилища)
	
	РезультатПроверки = Новый Структура("ЗаданиеВыполнено, Значение", Ложь, Неопределено);
	Если ДлительныеОперации.ЗаданиеВыполнено(ФоновоеЗаданиеИдентификатор) Тогда
		
		РезультатПроверки.ЗаданиеВыполнено	= Истина;
		ТабличныйДокумент					= ПолучитьИзВременногоХранилища(ФоновоеЗаданиеАдресХранилища);
		
	КонецЕсли;
	
	Возврат РезультатПроверки;
	
КонецФункции

&НаСервере
Процедура ИзменитьНастройкуОсновнойФормыПрайсЛистов()
	
	Элементы.ЭтоОсновнаяФормаПрайсЛистов.Пометка = НЕ Элементы.ЭтоОсновнаяФормаПрайсЛистов.Пометка;
	ЗначениеНастройки = Перечисления.ОсновнаяФормаПрайсЛиста[?(Элементы.ЭтоОсновнаяФормаПрайсЛистов.Пометка, "ФормированиеПрайсЛистов", "СписокПрайсЛистов")];
	ОбщегоНазначенияБПСервер.УстановитьЗначениеПоУмолчанию("ОсновнаяФормаПрайсЛиста", ЗначениеНастройки);
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьПечатныеФормы()
	
	ОбъектыПечати = Новый СписокЗначений;
	ОбъектыПечати.Добавить(ПрайсЛист);
	
	ПараметрыСохранения = Новый Структура;
	ПараметрыСохранения.Вставить("ОбъектыПечати", ОбъектыПечати);
	
	ОткрытьФорму("ОбщаяФорма.СохранениеПечатнойФормы", ПараметрыСохранения, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПечатныеФормыПоПочте()
	
	ОткрытьФорму("ОбщаяФорма.ВыборФорматаВложений", , ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция ПоместитьТабличныеДокументыВоВременноеХранилище(НастройкиСохранения)
	Перем ЗаписьZipФайла, ИмяАрхива;
	
	Результат = Новый Массив;
	
	// подготовка архива
	Если НастройкиСохранения.УпаковатьВАрхив Тогда
		ИмяАрхива = ПолучитьИмяВременногоФайла("zip");
		ЗаписьZipФайла = Новый ЗаписьZipФайла(ИмяАрхива);
	КонецЕсли;
	
	// подготовка временной папки
	ИмяВременнойПапки = ПолучитьИмяВременногоФайла();
	СоздатьКаталог(ИмяВременнойПапки);
	ИспользованныеИменаФайлов = Новый Соответствие;
	
	ВыбранныеФорматыСохранения = НастройкиСохранения.ФорматыСохранения;
	ТаблицаФорматов = УправлениеПечатью.НастройкиФорматовСохраненияТабличногоДокумента();
	
	// сохранение печатных форм
	ПечатнаяФорма = ТабличныйДокумент;
	
	Если ПечатнаяФорма.ВысотаТаблицы = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	Для Каждого ТипФайла Из ВыбранныеФорматыСохранения Цикл
		НастройкиФормата = ТаблицаФорматов.НайтиСтроки(Новый Структура("ТипФайлаТабличногоДокумента", ТипФайла))[0];
		
		ИмяФайла = ПолучитьИмяВременногоФайлаДляПечатнойФормы("Price", НастройкиФормата.Расширение, ИспользованныеИменаФайлов);
		ПолноеИмяФайла = УникальноеИмяФайла(ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(ИмяВременнойПапки) + ИмяФайла);
		
		ПечатнаяФорма.Записать(ПолноеИмяФайла, ТипФайла);
		
		Если ТипФайла = ТипФайлаТабличногоДокумента.HTML Тогда
			ВставитьКартинкиВHTML(ПолноеИмяФайла);
		КонецЕсли;
		
		Если ЗаписьZipФайла <> Неопределено Тогда
			ЗаписьZipФайла.Добавить(ПолноеИмяФайла);
		Иначе
			ДвоичныеДанные = Новый ДвоичныеДанные(ПолноеИмяФайла);
			ПутьВоВременномХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанные, ЭтотОбъект.УникальныйИдентификатор);
			ОписаниеФайла = Новый Структура;
			ОписаниеФайла.Вставить("Представление", ИмяФайла);
			ОписаниеФайла.Вставить("АдресВоВременномХранилище", ПутьВоВременномХранилище);
			Если ТипФайла = ТипФайлаТабличногоДокумента.ANSITXT Тогда
				ОписаниеФайла.Вставить("Кодировка", "windows-1251");
			КонецЕсли;
			Результат.Добавить(ОписаниеФайла);
		КонецЕсли;
	КонецЦикла;
	
	// если архив подготовлен, записываем и помещаем его во временное хранилище
	Если ЗаписьZipФайла <> Неопределено Тогда
		ЗаписьZipФайла.Записать();
		ДвоичныеДанные = Новый ДвоичныеДанные(ИмяАрхива);
		ПутьВоВременномХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанные, ЭтотОбъект.УникальныйИдентификатор);
		ОписаниеФайла = Новый Структура;
		ОписаниеФайла.Вставить("Представление", НСтр("ru = 'Price.zip'"));
		ОписаниеФайла.Вставить("АдресВоВременномХранилище", ПутьВоВременномХранилище);
		Результат.Добавить(ОписаниеФайла);
	КонецЕсли;
	
	УдалитьФайлы(ИмяВременнойПапки);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПолучитьИмяВременногоФайлаДляПечатнойФормы(ИмяМакета, Расширение, ИспользованныеИменаФайлов)
	
	ШаблонИмениФайла = "%1%2.%3";
	
	ИмяВременногоФайла = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИмениФайла, ИмяМакета, "", Расширение));
		
	НомерИспользования = ?(ИспользованныеИменаФайлов[ИмяВременногоФайла] <> Неопределено,
							ИспользованныеИменаФайлов[ИмяВременногоФайла] + 1,
							1);
	
	ИспользованныеИменаФайлов.Вставить(ИмяВременногоФайла, НомерИспользования);
	
	// если имя уже было ранее использовано, прибавляем счетчик в конце имени
	Если НомерИспользования > 1 Тогда
		ИмяВременногоФайла = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонИмениФайла,
				ИмяМакета,
				" (" + НомерИспользования + ")",
				Расширение));
	КонецЕсли;
	
	Возврат ИмяВременногоФайла;
	
КонецФункции

&НаСервере
Процедура ВставитьКартинкиВHTML(ИмяФайлаHTML)
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент();
	ТекстовыйДокумент.Прочитать(ИмяФайлаHTML, КодировкаТекста.UTF8);
	ТекстHTML = ТекстовыйДокумент.ПолучитьТекст();
	
	ФайлHTML = Новый Файл(ИмяФайлаHTML);
	
	ИмяПапкиКартинок = ФайлHTML.ИмяБезРасширения + "_files";
	ПутьКПапкеКартинок = СтрЗаменить(ФайлHTML.ПолноеИмя, ФайлHTML.Имя, ИмяПапкиКартинок);
	
	// ожидается, что в папке будут только картинки
	ФайлыКартинок = НайтиФайлы(ПутьКПапкеКартинок, "*");
	
	Для Каждого ФайлКартинки Из ФайлыКартинок Цикл
		КартинкаТекстом = Base64Строка(Новый ДвоичныеДанные(ФайлКартинки.ПолноеИмя));
		КартинкаТекстом = "data:image/" + Сред(ФайлКартинки.Расширение,2) + ";base64," + Символы.ПС + КартинкаТекстом;
		
		ТекстHTML = СтрЗаменить(ТекстHTML, ИмяПапкиКартинок + "\" + ФайлКартинки.Имя, КартинкаТекстом);
	КонецЦикла;
		
	ТекстовыйДокумент.УстановитьТекст(ТекстHTML);
	ТекстовыйДокумент.Записать(ИмяФайлаHTML, КодировкаТекста.UTF8);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьПечатныеФормыВПапку(СписокФайловВоВременномХранилище, Знач Папка = "")
	
	#Если ВебКлиент Тогда
		Для Каждого ФайлДляЗаписи Из СписокФайловВоВременномХранилище Цикл
			ПолучитьФайл(ФайлДляЗаписи.АдресВоВременномХранилище, ФайлДляЗаписи.Представление);
		КонецЦикла;
		Возврат;
	#КонецЕсли
	
	Папка = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(Папка);
	Для Каждого ФайлДляЗаписи Из СписокФайловВоВременномХранилище Цикл
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(ФайлДляЗаписи.АдресВоВременномХранилище);
		ДвоичныеДанные.Записать(УникальноеИмяФайла(Папка + ФайлДляЗаписи.Представление));
	КонецЦикла;
	
	Состояние(НСтр("ru = 'Сохранение успешно завершено'"), , НСтр("ru = 'в папку:'") + " " + Папка);
	
КонецПроцедуры

&НаСервере
Функция ПрисоединитьПечатныеФормыКОбъекту(ФайлыВоВременномХранилище, ОбъектДляПрисоединения)
	
	Результат = Новый Массив;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПрисоединенныеФайлы") Тогда
		
		МодульПрисоединенныеФайлы = ОбщегоНазначения.ОбщийМодуль("ПрисоединенныеФайлы");
		Для Каждого Файл Из ФайлыВоВременномХранилище Цикл
			
			Результат.Добавить(МодульПрисоединенныеФайлы.ДобавитьФайл(ОбъектДляПрисоединения, 
				Файл.Представление, , , , Файл.АдресВоВременномХранилище, , ТекущаяДатаСеанса()));
				
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция УникальноеИмяФайла(ИмяФайла)
	
	Файл = Новый Файл(ИмяФайла);
	ИмяБезРасширения = Файл.ИмяБезРасширения;
	Расширение = Файл.Расширение;
	Папка = Файл.Путь;
	
	Счетчик = 1;
	Пока Файл.Существует() Цикл
		Счетчик = Счетчик + 1;
		Файл = Новый Файл(Папка + ИмяБезРасширения + " (" + Счетчик + ")" + Расширение);
	КонецЦикла;
	
	Возврат Файл.ПолноеИмя;
	
КонецФункции

&НаСервере
Процедура ОбновитьКэшЗначений()
	
	КэшЗначений.Вставить("Организация", 			ПрайсЛист.Организация);
	КэшЗначений.Вставить("ОрганизацияНаименование", ПрайсЛист.Организация.Наименование);
	КэшЗначений.Вставить("ВыводитьДатуФормирования",ПрайсЛист.ВыводитьДатуФормирования);
	КэшЗначений.Вставить("ДатаФормирования",		ПрайсЛист.ДатаФормирования);
	КэшЗначений.Вставить("ТипЦен",					?(ПрайсЛист.ТипыЦен.Количество()> 0, ПрайсЛист.ТипыЦен[0].ТипЦен, Справочники.ТипыЦенНоменклатуры.ПустаяСсылка()));
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьКомандуКонтекстногоМеню(ИмяКоманды)
	
	ВыделанныеОбласти = Элементы.ТабличныйДокумент.ПолучитьВыделенныеОбласти();
	Если ВыделанныеОбласти.Количество() > 0 Тогда
		
		ДанныеРасшифровки = ВыделанныеОбласти[0].Расшифровка;
		
		Если ИмяКоманды = "Добавить" Тогда
			
			ДобавитьНовуюЗаписьОЦене(ДанныеРасшифровки);
			
		КонецЕсли;
		
		Если ИмяКоманды = "Изменить"
			И ТипЗнч(ДанныеРасшифровки) = Тип("Структура") Тогда
			
			ИзменитьЗаписьОЦене(ДанныеРасшифровки); // поведение отличается от выбора ячейки в 2 клика!
			
		КонецЕсли;
		
		Если ИмяКоманды = "Исключить" Тогда
			
			ИсключитьЗаписьОЦене(ДанныеРасшифровки);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
