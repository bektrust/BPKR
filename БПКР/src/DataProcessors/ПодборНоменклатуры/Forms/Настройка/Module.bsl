
#Область ОбработчикиСобытийФормы

// Процедура - обработчик события ПриСозданииНаСервере.
// В процедуре осуществляется
// - инициализация реквизитов формы,
// - установка параметров функциональных опций формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Корзина = Параметры.Корзина;
	ЗапрашиватьКоличество 	= БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ЗапрашиватьКоличество");
	
	Если Корзина = "КорзинаРеализации" ИЛИ Корзина = "КорзинаПоступления" Тогда
		ЗапрашиватьЦену	= БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ЗапрашиватьЦену");
	КонецЕсли;
	
	Если Корзина = "КорзинаПоступления" Тогда
		ЗапрашиватьВсего = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ЗапрашиватьВсего");
	КонецЕсли;
	
	УстановитьВидимостьКодТНВЭД = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("УстановитьВидимостьКодТНВЭД");
	
	// Установить видимость и доступность элементов формы
	УстановитьВидимостьДоступностьЭлементов();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ИзменныеНастройки = Новый Структура;
	
	Если Модифицированность Тогда
		
		ИзменныеНастройки.Вставить("ЗапрашиватьКоличество", ЗапрашиватьКоличество);
		
		Если Корзина = "КорзинаРеализации" ИЛИ Корзина = "КорзинаПоступления" Тогда
			ИзменныеНастройки.Вставить("ЗапрашиватьЦену", ЗапрашиватьЦену);
		КонецЕсли;
		
		Если Корзина = "КорзинаПоступления" Тогда
			ИзменныеНастройки.Вставить("ЗапрашиватьВсего", ЗапрашиватьВсего);	
		КонецЕсли;	
		
		ИзменныеНастройки.Вставить("УстановитьВидимостьКодТНВЭД", УстановитьВидимостьКодТНВЭД);
		
		СохранитьНовыеНастройкиИОбновитьИнтерфейс(ИзменныеНастройки);
				
	КонецЕсли;
	
	Закрыть(ИзменныеНастройки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура СохранитьНовыеНастройкиИОбновитьИнтерфейс(ИзменныеНастройки)
	
	Для Каждого ЭлементНастройки Из ИзменныеНастройки Цикл
		
		ТекущееЗначениеНастройки = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию(ЭлементНастройки.Ключ);
		Если ЭлементНастройки.Значение <> ТекущееЗначениеНастройки Тогда
			
			ОбщегоНазначенияБПСервер.УстановитьЗначениеПоУмолчанию(ЭлементНастройки.Ключ, ЭлементНастройки.Значение);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Процедура устанавливает видимость и доступность элементов.
//
&НаСервере
Процедура УстановитьВидимостьДоступностьЭлементов()

	Элементы.ЗапрашиватьЦену.Видимость = Корзина = "КорзинаРеализации" ИЛИ Корзина = "КорзинаПоступления";
	Элементы.ЗапрашиватьВсего.Видимость = Корзина = "КорзинаПоступления";

КонецПроцедуры

#КонецОбласти
