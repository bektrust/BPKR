#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПроцедурыПроведенияДокумента

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаНачисления(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(ВременнаяТаблицаШапка.Дата, МЕСЯЦ) КАК ПериодРегистрации,
		|	ВременнаяТаблицаШапка.Дата КАК ПериодДействияНачало,
		|	ВременнаяТаблицаШапка.Дата КАК ПериодДействияКонец,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаНачисления.ВидРасчета КАК ВидРасчета,
		|	ВременнаяТаблицаНачисления.Валюта КАК Валюта,
		|	ВременнаяТаблицаНачисления.СпособОтражения КАК СпособОтражения,
		|	ВременнаяТаблицаНачисления.ФизЛицо КАК ФизЛицо,
		|	ВременнаяТаблицаНачисления.Подразделение КАК Подразделение,
		|	ВременнаяТаблицаНачисления.Размер КАК Размер,
		|	ВременнаяТаблицаНачисления.Результат КАК Результат,
		|	ВременнаяТаблицаНачисления.Часов КАК ОтработаноЧасов,
		|	ВременнаяТаблицаНачисления.ГрафикРаботы КАК ГрафикРаботы
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНачисления КАК ВременнаяТаблицаНачисления
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаНачисления.Результат <> 0";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаНачисления", РезультатЗапроса.Выгрузить());
КонецПроцедуры 

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДокумента.Дата КАК Дата,
		|	ТаблицаДокумента.Организация КАК Организация
		|ПОМЕСТИТЬ ВременнаяТаблицаШапка
		|ИЗ
		|	Документ.РаботаСверхурочно КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.ФизЛицо КАК ФизЛицо,
		|	ТаблицаДокумента.ВидРасчета КАК ВидРасчета,
		|	ТаблицаДокумента.Валюта КАК Валюта,
		|	ТаблицаДокумента.Часов КАК Часов,
		|	ТаблицаДокумента.ГрафикРаботы КАК ГрафикРаботы,
		|	ТаблицаДокумента.СпособОтражения КАК СпособОтражения,
		|	ТаблицаДокумента.Подразделение КАК Подразделение,
		|	ТаблицаДокумента.Размер КАК Размер,
		|	ТаблицаДокумента.Результат КАК Результат
		|ПОМЕСТИТЬ ВременнаяТаблицаНачисления
		|ИЗ
		|	Документ.РаботаСверхурочно.Сотрудники КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Выполнить();
	
	СформироватьТаблицаНачисления(ДокументСсылка, СтруктураДополнительныеСвойства);
КонецПроцедуры // ИнициализироватьДанныеДокумента()

#КонецОбласти

#Область ВерсионированиеОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
//@skip-warning
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область ИнтерфейсПечати

Функция ПечатнаяФормаПриказаОСверхурочнойРаботе(МассивОбъектов, ОбъектыПечати)
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	РаботаСверхурочно.Ссылка,
		|	РаботаСверхурочно.ВерсияДанных,
		|	РаботаСверхурочно.ПометкаУдаления,
		|	РаботаСверхурочно.Номер КАК НомерДокумента,
		|	РаботаСверхурочно.Дата КАК ДатаДокумента,
		|	РаботаСверхурочно.Проведен,
		|	РаботаСверхурочно.Организация,
		|	РаботаСверхурочно.Организация.НаименованиеПолное КАК НазваниеОрганизации,
		|	РаботаСверхурочно.Причина,
		|	РаботаСверхурочно.Комментарий,
		|	РаботаСверхурочно.Автор,
		|	РаботаСверхурочно.Сотрудники.(
		|		Ссылка,
		|		НомерСтроки,
		|		ФизЛицо,
		|		ВидРасчета,
		|	)
		|ИЗ
		|	Документ.РаботаСверхурочно КАК РаботаСверхурочно
		|ГДЕ
		|	РаботаСверхурочно.Ссылка В(&СписокДокументов)";
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("СписокДокументов", МассивОбъектов);
	
	Шапка = Запрос.Выполнить().Выбрать();
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "РаботаСверхурочно_ПриказОСверхурочнойРаботе";
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РаботаСверхурочно.ПФ_MXL_ПриказОСверхурочнойРаботе");
	
	Пока Шапка.Следующий() Цикл
		Если ТабличныйДокумент.ВысотаТаблицы > 0 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ДанныеПечати = Новый Структура;
		ДанныеПечати.Вставить("НазваниеОрганизации", Шапка.НазваниеОрганизации);
		ДанныеПечати.Вставить("НомерДокумента",ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Шапка.НомерДокумента));
		ДанныеПечати.Вставить("ДатаДокумента", Формат(Шапка.ДатаДокумента, "ДЛФ=D"));
		ДанныеПечати.Вставить("Причина", Шапка.Причина);
		
		МассивОбластейМакета = Новый Массив;
		МассивОбластейМакета.Добавить("Шапка");
		МассивОбластейМакета.Добавить("ДатаРаботы");
		МассивОбластейМакета.Добавить("Сотрудник");
		МассивОбластейМакета.Добавить("Подвал");
		МассивОбластейМакета.Добавить("Ознакомлен");
		
		Для Каждого ИмяОбласти Из МассивОбластейМакета Цикл
			ОбластьМакета = Макет.ПолучитьОбласть(ИмяОбласти);
			Если НЕ ИмяОбласти = "Сотрудник" Тогда
				ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, ДанныеПечати);
				ТабличныйДокумент.Вывести(ОбластьМакета);
			ИначеЕсли ИмяОбласти = "Сотрудник" Тогда  
				
				ВыборкаПоСотрудникам = Шапка.Сотрудники.Выбрать();
				
				Пока ВыборкаПоСотрудникам.Следующий() Цикл 
					ОбластьМакета.Параметры.Заполнить(ВыборкаПоСотрудникам);
					
					СведенияОФизЛице = БухгалтерскийУчетСервер.ПолучитьСведенияОФизЛице(Шапка.Организация, ВыборкаПоСотрудникам.ФизЛицо, Шапка.ДатаДокумента); 
					ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, СведенияОФизЛице, "Должность, ФИО"); 
					
					ТабличныйДокумент.Вывести(ОбластьМакета);
				КонецЦикла;	
			КонецЕсли;
		КонецЦикла;

		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
КонецФункции	

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт

	// печать Приказ о сверхурочной работе
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПриказОСверхурочнойРаботе") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм, 
			"ПриказОСверхурочнойРаботе",
			НСтр("ru = 'Приказ о сверхурочной работе'"), 
			ПечатнаяФормаПриказаОСверхурочнойРаботе(МассивОбъектов, ОбъектыПечати),
			,
			"Документ.РаботаСверхурочно.ПФ_MXL_ПриказОСверхурочнойРаботе");
	КонецЕсли;
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Приказ о сверхурочной работе
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПриказОСверхурочнойРаботе";
	КомандаПечати.Представление = НСтр("ru = 'Приказ о сверхурочной работе'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Обработчик = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Порядок = 1;

КонецПроцедуры

#КонецОбласти

#КонецЕсли
