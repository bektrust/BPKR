#Область ОбработчикиСобытийФормы

// Процедура - обработчик события ПриСозданииНаСервере.
// В процедуре осуществляется
// - инициализация реквизитов формы,
// - установка параметров функциональных опций формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Установка реквизитов формы.
	ДатаДокумента = Объект.Дата;
	
	Подразделение = Объект.Подразделение;
	РазовоеНачисление = Объект.РазовоеНачисление;
	
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();

	БухгалтерскийУчетКлиентСервер.УстановитьКартинкуДляКомментария(Элементы.СтраницаДополнительно, Объект.Комментарий);
	
	// Установить видимость и доступность элементов формы
	УстановитьВидимостьДоступностьЭлементов();
	
	УстановитьУсловноеОформление();

	// РаботаСФормами
	РаботаСФормамиСервер.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец РаботаСФормами
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.ГруппаВажныеКоманды;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

// Процедура - обработчик события ПриЧтенииНаСервере.
//
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// СтандартныеПодсистемы.ДатыЗапретаИзменений
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменений
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом 
КонецПроцедуры

// Процедура - обработчик события ПриОткрытии.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

// Процедура - обработчик события ОбработкаОповещения.
//
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ПодборСотрудникаПроизведен" 
		И ЗначениеЗаполнено(Параметр) 
		// Проверка на владельца формы
		И Источник <> Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000")
		И Источник = УникальныйИдентификатор Тогда
		
		АдресЗапасовВХранилище = Параметр;
		ПолучитьРезультатПодбораИзХранилища(АдресЗапасовВХранилище, "Начисления");
	ИначеЕсли ИмяСобытия = "ВыборНачисления" Тогда
		ЗаполнитьТабличнуюЧастьПоВидуНачисленияНаСервере(Параметр);
	КонецЕсли;
КонецПроцедуры

// Процедура - обработчик события ПослеЗаписиНаСервере.
//
&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)	
	
	// РаботаСФормами
	РаботаСФормамиСервер.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец РаботаСФормами
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

// Процедура - обработчик события ПослеЗаписи.
//
&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Процедура - обработчик события ПриИзменении поля ввода Дата.
// В процедуре определяется ситуация, когда при изменении своей даты документ 
// оказывается в другом периоде нумерации документов, и в этом случае
// присваивает документу новый уникальный номер.
// Переопределяет соответствующий параметр формы.
//
&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	// Обработка события изменения даты.
	ДатаПередИзменением = ДатаДокумента;
	ДатаДокумента = Объект.Дата;
	Если Объект.Дата <> ДатаПередИзменением Тогда
		СтруктураДанные = ПолучитьДанныеДатаПриИзменении(ДатаПередИзменением);
		Если СтруктураДанные.РазностьДат <> 0 Тогда
			Объект.Номер = "";
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Организация.
//
&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	// Обработка события изменения организации.
	Объект.Номер = "";
	БухгалтерскийУчетВызовСервера.УчетнаяПолитикаСуществует(Объект.Организация, ДатаДокумента, Истина, Объект.Ссылка);
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Комментарий.
//
&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	ПодключитьОбработчикОжидания("УстановитьПиктограммуКомментария", 0.1, Истина);
КонецПроцедуры

// Процедура - обработчик события НачалоВыбора поля ввода Комментарий.
//
&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Подразделение.
//
&НаКлиенте
Асинх Процедура ПодразделениеПриИзменении(Элемент)
	// Значение реквизита не изменилось
	Если Объект.Подразделение = Подразделение Тогда 
		Возврат;
	КонецЕсли;	

	Если Объект.Начисления.Количество() > 0 Тогда 
		ТекстВопроса = НСтр("ru = 'Табличная часть документа будет очищена. Продолжить выполнение операции?'");
		Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60);
		
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			// Отмена изменения - возврат прежних значений
			Объект.Подразделение = Подразделение;		  
			Возврат;
		КонецЕсли;

		Объект.Начисления.Очистить();
	КонецЕсли;	
		
	Подразделение = Объект.Подразделение;
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода РазовоеНачисление.
//
&НаКлиенте
Асинх Процедура РазовоеНачислениеПриИзменении(Элемент)	

	Если Объект.Начисления.Количество() > 0 Тогда 
		ТекстВопроса = НСтр("ru = 'Табличная часть документа будет очищена. Продолжить выполнение операции?'");
		Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60);
		
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			// Отмена изменения - возврат прежних значений
			Объект.РазовоеНачисление = РазовоеНачисление;
			Возврат;
		КонецЕсли;

		Объект.Начисления.Очистить();

	КонецЕсли;

	РазовоеНачисление = Объект.РазовоеНачисление;
	УстановитьВидимостьДоступностьЭлементов();
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода НачисленияФизЛицо.
//
&НаКлиенте
Процедура НачисленияФизЛицоПриИзменении(Элемент)
	// получаем текущую строку
	СтрокаТабличнойЧасти = Элементы.Начисления.ТекущиеДанные;
	
	// не удалось получить- возвращаемся
	Если СтрокаТабличнойЧасти = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ДополнитьСтрокиНаСервере(СтрокаТабличнойЧасти.ФизЛицо);
КонецПроцедуры

&НаКлиенте
Процедура НачисленияВидРасчетаПриИзменении(Элемент)
	СтрокаТабличнойЧасти = Элементы.Начисления.ТекущиеДанные;
	
	// Сброс валюты для процентных видов начислений.
	СпособРасчета = ПолучитьСпособРасчетаПоВидуНачисления(СтрокаТабличнойЧасти.ВидРасчета);
	Если СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаНачислений.Процентом")
		Или СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаНачислений.ГодоваяПремия") Тогда 
		
		СтрокаТабличнойЧасти.Валюта = ВалютаРегламентированногоУчета;
	КонецЕсли;	
	
	СтруктураОтбора = ПроведениеРасчетовПоЗарплатеКлиент.СтруктураОтбораСпособыОтраженияЗаработнойПлаты();
	ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТабличнойЧасти);
	СтрокаТабличнойЧасти.СпособОтражения = ПолучитьСпособОтражения(СтруктураОтбора);		

КонецПроцедуры

&НаКлиенте
Процедура НачисленияПодразделениеПриИзменении(Элемент)
	СтрокаТабличнойЧасти = Элементы.Начисления.ТекущиеДанные;
	
	СтруктураОтбора = ПроведениеРасчетовПоЗарплатеКлиент.СтруктураОтбораСпособыОтраженияЗаработнойПлаты();
	ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТабличнойЧасти);
	СтрокаТабличнойЧасти.СпособОтражения = ПолучитьСпособОтражения(СтруктураОтбора);		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодборСотрудников(Команда)
	СотрудникиКлиент.ОткрытьПодбор(ЭтаФорма, "Начисления");  
КонецПроцедуры

&НаКлиенте
Асинх Процедура Заполнить(Команда)
	Если Объект.Начисления.Количество() > 0 Тогда 
		ТекстВопроса = НСтр("ru = 'Табличная часть документа будет заполнена. Продолжить выполнение операции?'");
		Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60);
		
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;	
		
		Объект.Начисления.Очистить();
	КонецЕсли;
	
	ЗаполнитьТабличнуюЧастьНаСервере();
	ЗаполнитьТабличнуюЧастьЗавершение();
КонецПроцедуры

&НаКлиенте
Асинх Процедура Рассчитать(Команда)
	Отказ = Ложь;
	
	Если Объект.Начисления.Количество() = 0  Тогда
		ТекстСообщения = НСтр("ru = 'Не заполнена табличная часть ""Начисления"". Расчет документа отменен.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "Объект.Начисления",,Отказ);		
	КонецЕсли;
	
	Для Каждого СтрокаТабличнойЧасти Из Объект.Начисления Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.ФизЛицо) Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не заполнен Сотрудник в строке №%1. Расчет документа отменен.'"), СтрокаТабличнойЧасти.НомерСтроки);
			ПолеСообщения = СтрШаблон("Объект.Начисления[%1].ФизЛицо", СтрокаТабличнойЧасти.НомерСтроки-1);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,ПолеСообщения,,Отказ);		
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.ВидРасчета) Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не заполнен Вид начисления в строке №%1. Расчет документа отменен.'"), СтрокаТабличнойЧасти.НомерСтроки);
			ПолеСообщения = СтрШаблон("Объект.Начисления[%1].ВидРасчета", СтрокаТабличнойЧасти.НомерСтроки-1);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,ПолеСообщения,,Отказ);		
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.ДатаНачала) Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не заполнена Дата начала в строке №%1. Расчет документа отменен.'"), СтрокаТабличнойЧасти.НомерСтроки);
			ПолеСообщения = СтрШаблон("Объект.Начисления[%1].ДатаНачала", СтрокаТабличнойЧасти.НомерСтроки-1);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,ПолеСообщения,,Отказ);		
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.ДатаОкончания) Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не заполнена Дата окончания в строке №%1. Расчет документа отменен.'"), СтрокаТабличнойЧасти.НомерСтроки);
			ПолеСообщения = СтрШаблон("Объект.Начисления[%1].ДатаОкончания", СтрокаТабличнойЧасти.НомерСтроки-1);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,ПолеСообщения,,Отказ);		
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.ГрафикРаботы) Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не заполнен График работы в строке №%1. Расчет документа отменен.'"), СтрокаТабличнойЧасти.НомерСтроки);
			ПолеСообщения = СтрШаблон("Объект.Начисления[%1].ГрафикРаботы", СтрокаТабличнойЧасти.НомерСтроки-1);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,ПолеСообщения,,Отказ);		
		КонецЕсли;
	КонецЦикла;
	
	Если БухгалтерскийУчетВызовСервераПовтИсп.ВедетсяУчетЗаработнойПлатыВВалюте() Тогда 
		КонтрольЗаполненияВалюты(Отказ);		
	КонецЕсли;	
	
	Если Отказ Тогда 
		Возврат
	КонецЕсли;	
	
	Если Объект.Начисления.Количество() > 0 Тогда 
		ТекстВопроса = НСтр("ru = 'Табличная часть документа будет пересчитана. Продолжить выполнение операции?'");
		Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60);
				
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	РассчитатьТабличнуюЧастьНаКлиенте();
КонецПроцедуры

&НаКлиенте
Асинх Процедура ЗаполнитьПоВидуНачисления(Команда)
	
	Если Объект.Начисления.Количество() > 0 Тогда 
		ТекстВопроса = НСтр("ru = 'Табличная часть документа будет заполнена. Продолжить выполнение операции?'");
		Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60);
		
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьПоВидуНачисленияНаКлиенте();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицФормы

&НаКлиенте
Процедура НачисленияОтработаноДнейПриИзменении(Элемент)
	СтрокаТабличнойЧасти = Элементы.Начисления.ТекущиеДанные;
	СтрокаТабличнойЧасти.РучнаяКорректировка = Истина;
КонецПроцедуры

&НаКлиенте
Процедура НачисленияОтработаноЧасовПриИзменении(Элемент)
	СтрокаТабличнойЧасти = Элементы.Начисления.ТекущиеДанные;
	СтрокаТабличнойЧасти.РучнаяКорректировка = Истина;
КонецПроцедуры

&НаКлиенте
Процедура НачисленияРезультатПриИзменении(Элемент)
	СтрокаТабличнойЧасти = Элементы.Начисления.ТекущиеДанные;
	СтрокаТабличнойЧасти.РучнаяКорректировка = Истина;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура устанавливает видимость и доступность элементов.
//
&НаСервере
Процедура УстановитьВидимостьДоступностьЭлементов()
		
	ЭтоРазовоеНачисление = РазовоеНачисление;
	Элементы.НачисленияПодразделение.Видимость 			= Не ЭтоРазовоеНачисление;
	Элементы.НачисленияДолжность.Видимость 				= Не ЭтоРазовоеНачисление;
	Элементы.НачисленияГрафикРаботы.Видимость 			= Не ЭтоРазовоеНачисление;
	Элементы.НачисленияГруппаПериод.Видимость 			= Не ЭтоРазовоеНачисление;
	Элементы.НачисленияГруппаНорма.Видимость 			= Не ЭтоРазовоеНачисление;
	Элементы.НачисленияГруппаФакт.Видимость 			= Не ЭтоРазовоеНачисление;
	Элементы.НачисленияЗаполнить.Видимость 				= Не ЭтоРазовоеНачисление;
	Элементы.НачисленияРучнаяКорректировка.Видимость 	= Не ЭтоРазовоеНачисление;
	Элементы.НачисленияЗаполнитьПоВидуНачисления.Видимость = ЭтоРазовоеНачисление;
	
	Если ЭтоРазовоеНачисление Тогда 
		Элементы.НачисленияГруппаРазмерРезультат.Группировка = ГруппировкаКолонок.Горизонтальная;
		Элементы.ГруппаВидРасчетаОтражение.Группировка = ГруппировкаКолонок.Горизонтальная;
	Иначе 	
		Элементы.НачисленияГруппаРазмерРезультат.Группировка = ГруппировкаКолонок.Вертикальная;
		Элементы.ГруппаВидРасчетаОтражение.Группировка = ГруппировкаКолонок.Вертикальная;
	КонецЕсли;
КонецПроцедуры 

// Процедура настройки условного оформления форм и динамических списков .
//
&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	// Таблица Начисления.
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("Начисления");

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Начисления.РучнаяКорректировка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ИзмененноеЗначениеРеквизитаЦвет);
	
КонецПроцедуры

// Получает набор данных с сервера для процедуры ДатаПриИзменении.
//
&НаСервере
Функция ПолучитьДанныеДатаПриИзменении(ДатаПередИзменением)
	РазностьДат = БухгалтерскийУчетСервер.ПроверитьНомерДокумента(Объект.Ссылка, Объект.Дата, ДатаПередИзменением);
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("РазностьДат",	РазностьДат);
	
	Возврат СтруктураДанные;
КонецФункции // ПолучитьДанныеДатаПриИзменении()

// Процедура - Установить пиктограмму комментария.
//
&НаКлиенте
Процедура УстановитьПиктограммуКомментария()
	БухгалтерскийУчетКлиентСервер.УстановитьКартинкуДляКомментария(Элементы.СтраницаДополнительно, Объект.Комментарий);
КонецПроцедуры

// Функция возвращает результат возможности записи/отмены проведения документа перед Расчетом
//
// Параметры:
//  Действие - действие, при котором выполняется проверка
// Возвращаемое значение:
//   Булево - 
//
&НаКлиенте
Функция ЗаписатьДокументОтменивПроведение()
	Если Объект.Проведен Тогда
		ЗаписатьНаСервере(РежимЗаписиДокумента.ОтменаПроведения);		
	ИначеЕсли НЕ ЗначениеЗаполнено(Объект.Ссылка) Или Модифицированность Тогда 
		Объект.Дата = ДатаДокумента;
		ЗаписатьНаСервере(РежимЗаписиДокумента.Запись);		
	КонецЕсли;	
	
	Модифицированность = Ложь;

	Возврат НЕ Объект.Проведен И ЗначениеЗаполнено(Объект.Ссылка);
КонецФункции // ЗаписатьДокументОтменивПроведение()

// Процедура - Записать на сервере
//
// Параметры:
//  Режим	 - 	 - 
//
&НаСервере
Процедура ЗаписатьНаСервере(Режим)
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.Записать(Режим);
	ЗначениеВРеквизитФормы(Документ, "Объект");
КонецПроцедуры // ЗаписатьНаСервере()

// Процедура - Заполнить табличную часть на сервере
//
&НаСервере
Процедура ЗаполнитьТабличнуюЧастьНаСервере()
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.ЗаполнитьТабличнуюЧасть();
	ЗначениеВРеквизитФормы(Документ, "Объект");
	Модифицированность = Истина;
КонецПроцедуры // ЗаполнитьТабличнуюЧастьНаСервере()

&НаКлиенте
Процедура ЗаполнитьТабличнуюЧастьЗавершение()
	Если Объект.Начисления.Количество() > 0 Тогда 
		ТекстОповещения = НСтр("ru = 'Документ заполнен'");
		ПоказатьОповещениеПользователя(ТекстОповещения,,, БиблиотекаКартинок.Информация32);
	Иначе 
		ТекстСообщения = НСтр("ru = 'Нет данных для заполнения. Проверьте кадровые приказы.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);		
	КонецЕсли;	
КонецПроцедуры	

&НаКлиенте
Процедура ЗаполнитьПоВидуНачисленияНаКлиенте()
	ОткрытьФорму("Документ.НачислениеЗарплаты.Форма.ФормаВыбораВидаНачислений",,ЭтаФорма);
КонецПроцедуры // ЗаполнитьПоВидуНачисленияНаКлиенте()

&НаСервере
Процедура КонтрольЗаполненияВалюты(Отказ)

	Для Каждого СтрокаТабличнойЧасти Из Объект.Начисления Цикл
		Если НЕ СтрокаТабличнойЧасти.Валюта = ВалютаРегламентированногоУчета 
			И СтрокаТабличнойЧасти.ВидРасчета.СпособРасчета = Перечисления.СпособыРасчетаНачислений.Процентом Тогда 
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Для вида начисления ""%1"" указывается не верная валюта (нужно указать валюту регламентированного учета) в строке %2.'"), 
				СтрокаТабличнойЧасти.ВидРасчета,
				СтрокаТабличнойЧасти.НомерСтроки);
			ПолеСообщения = СтрШаблон("Объект.Начисления[%1].Валюта", СтрокаТабличнойЧасти.НомерСтроки-1);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,ПолеСообщения,,Отказ);		
		КонецЕсли;		
	КонецЦикла;	

КонецПроцедуры // КонтрольЗаполненияВалюты()

&НаКлиенте
Процедура РассчитатьТабличнуюЧастьНаКлиенте()
	
	Если НЕ ЗаписатьДокументОтменивПроведение() Тогда 
		Возврат;
	КонецЕсли;	

	ЕстьОшибки = Ложь;
	РассчитатьТабличнуюЧастьНаСервере(ЕстьОшибки);
	
	ТекстОповещения = НСтр("ru = 'Документ заполнен'");
	ТекстПояснения = ?(ЕстьОшибки, 
		НСтр("ru = 'При расчете возникли ошибки'"), 
		НСтр("ru = 'При расчете ошибок не обнаружено'"));
	ПоказатьОповещениеПользователя(ТекстОповещения, , ТекстПояснения, БиблиотекаКартинок.Информация32);
	
КонецПроцедуры // РассчитатьТабличнуюЧастьНаКлиенте()

// Процедура - Рассчитать табличную часть на сервере
//
&НаСервере
Процедура РассчитатьТабличнуюЧастьНаСервере(ЕстьОшибки)
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.РассчитатьТабличнуюЧасть(ЕстьОшибки);
	ЗначениеВРеквизитФормы(Документ, "Объект");	
	Модифицированность = Истина;
КонецПроцедуры

// Процедура получает результат подбора из временного хранилища
//
&НаСервере
Процедура ПолучитьРезультатПодбораИзХранилища(АдресЗапасовВХранилище, ИмяТабличнойЧасти)
	ТаблицаДляЗагрузки = ПолучитьИзВременногоХранилища(АдресЗапасовВХранилище);
	
	МассивФизЛиц = Новый Массив;
	
	Для Каждого СтрокаЗагрузки Из ТаблицаДляЗагрузки Цикл
		НайденныеСтроки = Объект[ИмяТабличнойЧасти].НайтиСтроки(Новый Структура("ФизЛицо", СтрокаЗагрузки.ФизЛицо));
		
		Если НайденныеСтроки.Количество() > 0 Тогда 
			Продолжить;
		КонецЕсли;	
		
		СтрокаТабличнойЧасти = Объект[ИмяТабличнойЧасти].Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, СтрокаЗагрузки);
		МассивФизЛиц.Добавить(СтрокаТабличнойЧасти.ФизЛицо);
	КонецЦикла;
	
	ДополнитьСтрокиНаСервере(МассивФизЛиц);
КонецПроцедуры // ПолучитьЗапасыИзХранилища()

// Процедура заполняет строки
//
// Параметры:
//  Сотрудники  - Массив - массив физ.лиц, по которым нужно заполнить строки
//
&НаСервере
Процедура ДополнитьСтрокиНаСервере(Сотрудники)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СотрудникиСрезПоследних.ФизЛицо,
		|	СотрудникиСрезПоследних.Подразделение,
		|	СотрудникиСрезПоследних.Должность,
		|	&НачалоПериода КАК ДатаНачала,
		|	&КонецПериода КАК ДатаОкончания,
		|	СотрудникиСрезПоследних.ГрафикРаботы
		|ПОМЕСТИТЬ ВременнаяТаблицаСотрудники
		|ИЗ
		|	РегистрСведений.Сотрудники.СрезПоследних(
		|			&КонецПериода,
		|			Организация = &Организация
		|				И ФизЛицо В (&Сотрудники)) КАК СотрудникиСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КалендариГрафиковРабот.ГрафикРаботы,
		|	СУММА(КалендариГрафиковРабот.ЗначениеДней) КАК ЗначениеДней,
		|	СУММА(КалендариГрафиковРабот.ЗначениеЧасов) КАК ЗначениеЧасов
		|ПОМЕСТИТЬ ВременнаяТаблицаГрафики
		|ИЗ
		|	РегистрСведений.КалендариГрафиковРабот КАК КалендариГрафиковРабот
		|ГДЕ
		|	КалендариГрафиковРабот.Год = &Год
		|	И КалендариГрафиковРабот.ГрафикРаботы В
		|			(ВЫБРАТЬ
		|				ВременнаяТаблицаСотрудники.ГрафикРаботы
		|			ИЗ
		|				ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники)
		|	И КалендариГрафиковРабот.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|
		|СГРУППИРОВАТЬ ПО
		|	КалендариГрафиковРабот.ГрафикРаботы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаСотрудники.ФизЛицо,
		|	ВременнаяТаблицаСотрудники.Подразделение,
		|	ВременнаяТаблицаСотрудники.Должность,
		|	ВременнаяТаблицаСотрудники.ДатаНачала,
		|	ВременнаяТаблицаСотрудники.ДатаОкончания,
		|	ВременнаяТаблицаСотрудники.ГрафикРаботы,
		|	ВременнаяТаблицаГрафики.ЗначениеДней,
		|	ВременнаяТаблицаГрафики.ЗначениеЧасов КАК НормаЧасов
		|ИЗ
		|	ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаГрафики КАК ВременнаяТаблицаГрафики
		|		ПО ВременнаяТаблицаСотрудники.ГрафикРаботы = ВременнаяТаблицаГрафики.ГрафикРаботы";
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("Год", Год(ДатаДокумента));
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(ДатаДокумента));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(ДатаДокумента));
	Запрос.УстановитьПараметр("Сотрудники", Сотрудники);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	СтруктураОтбора = ПроведениеРасчетовПоЗарплатеСервер.СтруктураОтбораСпособыОтраженияЗаработнойПлаты();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СтрокиТабличнойЧасти = Объект.Начисления.НайтиСтроки(Новый Структура("ФизЛицо", ВыборкаДетальныеЗаписи.ФизЛицо));
		
		Для Каждого СтрокаТабличнойЧасти Из СтрокиТабличнойЧасти Цикл
			ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ВыборкаДетальныеЗаписи);			
			ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаТабличнойЧасти);
	
			СтрокаТабличнойЧасти.СпособОтражения = ПолучитьСпособОтражения(СтруктураОтбора);		
		КонецЦикла;		
	КонецЦикла;
КонецПроцедуры // ДополнитьСтрокиНаСервере()

// Процедура - Заполнить табличную часть на сервере
//
&НаСервере
Процедура ЗаполнитьТабличнуюЧастьПоВидуНачисленияНаСервере(СтруктураДанные)
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.ЗаполнитьТабличнуюЧастьПоВидуНачисления(СтруктураДанные);
	ЗначениеВРеквизитФормы(Документ, "Объект");
КонецПроцедуры // ЗаполнитьТабличнуюЧастьНаСервере()

&НаСервереБезКонтекста
Функция ПолучитьСпособОтражения(СтруктураОтбора)
	Возврат ПроведениеРасчетовПоЗарплатеСервер.СпособОтраженияЗаработнойПлаты(СтруктураОтбора);		
КонецФункции // ПолучитьСпособОтражения()

&НаСервереБезКонтекста
Функция ПолучитьСпособРасчетаПоВидуНачисления(ВидНачисления)
	Возврат ВидНачисления.СпособРасчета;
КонецФункции // ПолучитьСпособРасчетаПоВидуНачисления()

#КонецОбласти

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ПодключаемыеКоманды

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
     ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
     ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры
 
&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
     ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
     ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

