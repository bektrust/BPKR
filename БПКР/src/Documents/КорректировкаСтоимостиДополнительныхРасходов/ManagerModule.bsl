#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПроцедурыПроведенияДокумента

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	// 1. Товары НДС (признак зачета НДС включен)
	// 2. ОС НДС (признак зачета НДС включен)
	// 3. Товары
	// 4. ОС
	// 5. НДС из шапки (признак зачета НДС отключен)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СодержаниеТовары", НСтр("ru = 'Поступление доп.расходов (товары)'")); 
	Запрос.УстановитьПараметр("СодержаниеОС", НСтр("ru = 'Поступление доп.расходов (ОС)'")); 
	Запрос.УстановитьПараметр("СодержаниеНДС", НСтр("ru = 'НДС'")); 
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	    "ВЫБРАТЬ
	    |	1 КАК Порядок,
	    |	ВременнаяТаблицаШапка.Дата КАК Период,
	    |	ВременнаяТаблицаШапка.Организация КАК Организация,
	    |	ВЫБОР
	    |		КОГДА ВременнаяТаблицаТовары.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Себестоимость)
	    |			ТОГДА ВременнаяТаблицаТовары.СчетУчета
	    |		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДС_ПодлежащийВозмещению)
	    |	КОНЕЦ КАК СчетДт,
	    |	ВременнаяТаблицаШапка.СчетРасчетов КАК СчетКт,
	    |	ВЫБОР
	    |		КОГДА ВременнаяТаблицаТовары.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Себестоимость)
	    |			ТОГДА ВременнаяТаблицаТовары.Номенклатура
	    |		ИНАЧЕ НЕОПРЕДЕЛЕНО
	    |	КОНЕЦ КАК СубконтоДт1,
	    |	ВЫБОР
	    |		КОГДА ВременнаяТаблицаТовары.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Себестоимость)
	    |			ТОГДА ВременнаяТаблицаТовары.Склад
	    |		ИНАЧЕ НЕОПРЕДЕЛЕНО
	    |	КОНЕЦ КАК СубконтоДт2,
	    |	ВЫБОР
	    |		КОГДА ВременнаяТаблицаТовары.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Себестоимость)
	    |				И ВременнаяТаблицаШапка.ВедетсяУчетПоПартиям
	    |			ТОГДА ВременнаяТаблицаТовары.ДокументПоступления
	    |		ИНАЧЕ НЕОПРЕДЕЛЕНО
	    |	КОНЕЦ КАК СубконтоДт3,
	    |	ВременнаяТаблицаШапка.Контрагент КАК СубконтоКт1,
	    |	ВременнаяТаблицаШапка.ДоговорКонтрагента КАК СубконтоКт2,
	    |	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	    |	ВЫРАЗИТЬ(ВременнаяТаблицаТовары.СуммаРасходовНДС * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2)) КАК Сумма,
	    |	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаДт,
	    |	ВременнаяТаблицаТовары.СуммаРасходовНДС КАК ВалютнаяСуммаДт,
	    |	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаКт,
	    |	ВременнаяТаблицаТовары.СуммаРасходовНДС КАК ВалютнаяСуммаКт,
	    |	&СодержаниеНДС КАК Содержание,
	    |	0 КАК КоличествоДт,
	    |	0 КАК КоличествоКт
	    |ПОМЕСТИТЬ ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС
	    |ИЗ
	    |	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
	    |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	    |		ПО (ИСТИНА)
	    |ГДЕ
	    |	НЕ ВременнаяТаблицаТовары.СуммаРасходовНДС = 0
	    |	И &УказыватьПризнакЗачетаНДС
	    |
	    |ОБЪЕДИНИТЬ ВСЕ
	    |
	    |ВЫБРАТЬ
	    |	2,
	    |	ВременнаяТаблицаШапка.Дата,
	    |	ВременнаяТаблицаШапка.Организация,
	    |	ВЫБОР
	    |		КОГДА ВременнаяТаблицаОС.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Себестоимость)
	    |			ТОГДА ВременнаяТаблицаОС.СчетУчета
	    |		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДС_ПодлежащийВозмещению)
	    |	КОНЕЦ,
	    |	ВременнаяТаблицаШапка.СчетРасчетов,
	    |	ВЫБОР
	    |		КОГДА ВременнаяТаблицаОС.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Себестоимость)
	    |			ТОГДА ВременнаяТаблицаОС.ОсновноеСредство
	    |		ИНАЧЕ НЕОПРЕДЕЛЕНО
	    |	КОНЕЦ,
	    |	НЕОПРЕДЕЛЕНО,
	    |	НЕОПРЕДЕЛЕНО,
	    |	ВременнаяТаблицаШапка.Контрагент,
	    |	ВременнаяТаблицаШапка.ДоговорКонтрагента,
	    |	НЕОПРЕДЕЛЕНО,
	    |	ВЫРАЗИТЬ(ВременнаяТаблицаОС.СуммаРасходовНДС * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2)),
	    |	ВременнаяТаблицаШапка.ВалютаДокумента,
	    |	ВременнаяТаблицаОС.СуммаРасходовНДС,
	    |	ВременнаяТаблицаШапка.ВалютаДокумента,
	    |	ВременнаяТаблицаОС.СуммаРасходовНДС,
	    |	&СодержаниеНДС,
	    |	0,
	    |	0
	    |ИЗ
	    |	ВременнаяТаблицаОС КАК ВременнаяТаблицаОС
	    |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	    |		ПО (ИСТИНА)
	    |ГДЕ
	    |	НЕ ВременнаяТаблицаОС.СуммаРасходовНДС = 0
	    |	И &УказыватьПризнакЗачетаНДС
	    |;
	    |
	    |////////////////////////////////////////////////////////////////////////////////
	    |ВЫБРАТЬ
	    |	3 КАК Порядок,
	    |	ВременнаяТаблицаШапка.Дата КАК Период,
	    |	ВременнаяТаблицаШапка.Организация КАК Организация,
	    |	ВременнаяТаблицаТовары.СчетУчета КАК СчетДт,
	    |	ВременнаяТаблицаШапка.СчетРасчетов КАК СчетКт,
	    |	ВременнаяТаблицаТовары.Номенклатура КАК СубконтоДт1,
	    |	ВременнаяТаблицаТовары.Склад КАК СубконтоДт2,
	    |	ВЫБОР
	    |		КОГДА ВременнаяТаблицаШапка.ВедетсяУчетПоПартиям
	    |			ТОГДА ВременнаяТаблицаТовары.ДокументПоступления
	    |		ИНАЧЕ НЕОПРЕДЕЛЕНО
	    |	КОНЕЦ КАК СубконтоДт3,
	    |	ВременнаяТаблицаШапка.Контрагент КАК СубконтоКт1,
	    |	ВременнаяТаблицаШапка.ДоговорКонтрагента КАК СубконтоКт2,
	    |	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	    |	ВЫБОР
	    |		КОГДА ВременнаяТаблицаТовары.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Себестоимость)
	    |				И &УказыватьПризнакЗачетаНДС
	    |			ТОГДА ВЫРАЗИТЬ((ВременнаяТаблицаТовары.СуммаРасходов + ВременнаяТаблицаТовары.СуммаРасходовНДС + ВременнаяТаблицаТовары.СуммаРасходовНСП) * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2))
	    |		ИНАЧЕ ВЫРАЗИТЬ((ВременнаяТаблицаТовары.СуммаРасходов + ВременнаяТаблицаТовары.СуммаРасходовНСП) * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2))
	    |	КОНЕЦ КАК Сумма,
	    |	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаДт,
	    |	ВЫБОР
	    |		КОГДА ВременнаяТаблицаТовары.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Себестоимость)
	    |				И &УказыватьПризнакЗачетаНДС
	    |			ТОГДА ВременнаяТаблицаТовары.СуммаРасходов + ВременнаяТаблицаТовары.СуммаРасходовНДС + ВременнаяТаблицаТовары.СуммаРасходовНСП
	    |		ИНАЧЕ ВременнаяТаблицаТовары.СуммаРасходов + ВременнаяТаблицаТовары.СуммаРасходовНСП
	    |	КОНЕЦ КАК ВалютнаяСуммаДт,
	    |	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаКт,
	    |	ВЫБОР
	    |		КОГДА ВременнаяТаблицаТовары.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Себестоимость)
	    |				И &УказыватьПризнакЗачетаНДС
	    |			ТОГДА ВременнаяТаблицаТовары.СуммаРасходов + ВременнаяТаблицаТовары.СуммаРасходовНДС + ВременнаяТаблицаТовары.СуммаРасходовНСП
	    |		ИНАЧЕ ВременнаяТаблицаТовары.СуммаРасходов + ВременнаяТаблицаТовары.СуммаРасходовНСП
	    |	КОНЕЦ КАК ВалютнаяСуммаКт,
	    |	&СодержаниеТовары КАК Содержание,
	    |	0 КАК КоличествоДт,
	    |	0 КАК КоличествоКт
	    |ИЗ
	    |	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
	    |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	    |		ПО (ИСТИНА)
	    |ГДЕ
	    |	НЕ ВременнаяТаблицаТовары.СуммаРасходов = 0
	    |
	    |ОБЪЕДИНИТЬ ВСЕ
	    |
	    |ВЫБРАТЬ
	    |	4,
	    |	ВременнаяТаблицаШапка.Дата,
	    |	ВременнаяТаблицаШапка.Организация,
	    |	ВременнаяТаблицаОС.СчетУчета,
	    |	ВременнаяТаблицаШапка.СчетРасчетов,
	    |	ВременнаяТаблицаОС.ОсновноеСредство,
	    |	НЕОПРЕДЕЛЕНО,
	    |	НЕОПРЕДЕЛЕНО,
	    |	ВременнаяТаблицаШапка.Контрагент,
	    |	ВременнаяТаблицаШапка.ДоговорКонтрагента,
	    |	НЕОПРЕДЕЛЕНО,
	    |	ВЫБОР
	    |		КОГДА ВременнаяТаблицаОС.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Себестоимость)
	    |				И &УказыватьПризнакЗачетаНДС
	    |			ТОГДА ВЫРАЗИТЬ((ВременнаяТаблицаОС.СуммаРасходов + ВременнаяТаблицаОС.СуммаРасходовНДС + ВременнаяТаблицаОС.СуммаРасходовНСП) * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2))
	    |		ИНАЧЕ ВЫРАЗИТЬ((ВременнаяТаблицаОС.СуммаРасходов + ВременнаяТаблицаОС.СуммаРасходовНСП) * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2))
	    |	КОНЕЦ,
	    |	ВременнаяТаблицаШапка.ВалютаДокумента,
	    |	ВЫБОР
	    |		КОГДА ВременнаяТаблицаОС.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Себестоимость)
	    |				И &УказыватьПризнакЗачетаНДС
	    |			ТОГДА ВременнаяТаблицаОС.СуммаРасходов + ВременнаяТаблицаОС.СуммаРасходовНДС + ВременнаяТаблицаОС.СуммаРасходовНСП
	    |		ИНАЧЕ ВременнаяТаблицаОС.СуммаРасходов + ВременнаяТаблицаОС.СуммаРасходовНСП
	    |	КОНЕЦ,
	    |	ВременнаяТаблицаШапка.ВалютаДокумента,
	    |	ВЫБОР
	    |		КОГДА ВременнаяТаблицаОС.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Себестоимость)
	    |				И &УказыватьПризнакЗачетаНДС
	    |			ТОГДА ВременнаяТаблицаОС.СуммаРасходов + ВременнаяТаблицаОС.СуммаРасходовНДС + ВременнаяТаблицаОС.СуммаРасходовНСП
	    |		ИНАЧЕ ВременнаяТаблицаОС.СуммаРасходов + ВременнаяТаблицаОС.СуммаРасходовНСП
	    |	КОНЕЦ,
	    |	&СодержаниеОС,
	    |	0,
	    |	0
	    |ИЗ
	    |	ВременнаяТаблицаОС КАК ВременнаяТаблицаОС
	    |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	    |		ПО (ИСТИНА)
	    |ГДЕ
	    |	НЕ ВременнаяТаблицаОС.СуммаРасходов = 0
	    |
	    |ОБЪЕДИНИТЬ ВСЕ
	    |
	    |ВЫБРАТЬ
	    |	5,
	    |	ВременнаяТаблицаШапка.Дата,
	    |	ВременнаяТаблицаШапка.Организация,
	    |	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДС_ПодлежащийВозмещению),
	    |	ВременнаяТаблицаШапка.СчетРасчетов,
	    |	НЕОПРЕДЕЛЕНО,
	    |	НЕОПРЕДЕЛЕНО,
	    |	НЕОПРЕДЕЛЕНО,
	    |	ВременнаяТаблицаШапка.Контрагент,
	    |	ВременнаяТаблицаШапка.ДоговорКонтрагента,
	    |	НЕОПРЕДЕЛЕНО,
	    |	ВЫРАЗИТЬ(ВременнаяТаблицаШапка.СуммаНДС * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2)),
	    |	ВременнаяТаблицаШапка.ВалютаДокумента,
	    |	ВременнаяТаблицаШапка.СуммаНДС,
	    |	ВременнаяТаблицаШапка.ВалютаДокумента,
	    |	ВременнаяТаблицаШапка.СуммаНДС,
	    |	&СодержаниеНДС,
	    |	0,
	    |	0
	    |ИЗ
	    |	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	    |ГДЕ
	    |	НЕ ВременнаяТаблицаШапка.СуммаНДС = 0
	    |	И НЕ &УказыватьПризнакЗачетаНДС
	    |
	    |ОБЪЕДИНИТЬ ВСЕ
	    |
	    |ВЫБРАТЬ
	    |	МАКСИМУМ(ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС.Порядок),
	    |	ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС.Период,
	    |	ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС.Организация,
	    |	ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС.СчетДт,
	    |	ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС.СчетКт,
	    |	ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС.СубконтоДт1,
	    |	ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС.СубконтоДт2,
	    |	ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС.СубконтоДт3,
	    |	ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС.СубконтоКт1,
	    |	ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС.СубконтоКт2,
	    |	ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС.СубконтоКт3,
	    |	СУММА(ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС.Сумма),
	    |	ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС.ВалютаДт,
	    |	СУММА(ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС.ВалютнаяСуммаДт),
	    |	ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС.ВалютаКт,
	    |	СУММА(ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС.ВалютнаяСуммаКт),
	    |	ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС.Содержание,
	    |	СУММА(ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС.КоличествоДт),
	    |	СУММА(ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС.КоличествоКт)
	    |ИЗ
	    |	ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС КАК ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС
	    |
	    |СГРУППИРОВАТЬ ПО
	    |	ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС.ВалютаКт,
	    |	ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС.Период,
	    |	ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС.Организация,
	    |	ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС.Содержание,
	    |	ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС.СубконтоКт1,
	    |	ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС.СубконтоДт2,
	    |	ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС.СубконтоКт3,
	    |	ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС.СубконтоДт1,
	    |	ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС.СчетДт,
	    |	ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС.СубконтоКт2,
	    |	ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС.СубконтоДт3,
	    |	ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС.СчетКт,
	    |	ВременнаяТаблицаПроводкиНДССПризнакомЗачетаНДС.ВалютаДт";
	
	Запрос.УстановитьПараметр("УказыватьПризнакЗачетаНДС", СтруктураДополнительныеСвойства.УчетнаяПолитика.УказыватьПризнакЗачетаНДСПриПоступлении);
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаХозрасчетный", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаПоступлениеТоваров(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.Контрагент КАК Контрагент,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента КАК Договор,
		|	ВременнаяТаблицаШапка.НоменклатураРасходов КАК Номенклатура,
		|	ВременнаяТаблицаШапка.ЗначениеСтавкиНДС КАК ЗначениеСтавкиНДС,
		|	ВременнаяТаблицаШапка.ЗначениеСтавкиНСП КАК ЗначениеСтавкиНСП,
		|	ВременнаяТаблицаШапка.БезналичныйРасчет КАК БезналичныйРасчет,
		|	ВременнаяТаблицаТовары.Склад КАК Склад,
		|	ВЫБОР
		|		КОГДА &УказыватьПризнакЗачетаНДС
		|			ТОГДА ВременнаяТаблицаТовары.ЗачетНДС
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.ПустаяСсылка)
		|	КОНЕЦ КАК ЗачетНДС,
		|	ВЫРАЗИТЬ(ВременнаяТаблицаТовары.СуммаРасходовНДС * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2)) КАК СуммаНДС,
		|	ВЫРАЗИТЬ((ВременнаяТаблицаТовары.СуммаРасходов + ВременнаяТаблицаТовары.СуммаРасходовНДС + ВременнаяТаблицаТовары.СуммаРасходовНСП) * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2)) КАК Сумма,
		|	ВЫРАЗИТЬ(ВременнаяТаблицаТовары.СуммаРасходовНСП * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2)) КАК СуммаНСП,
		|	0 КАК Количество
		|ПОМЕСТИТЬ ВременнаяТаблицаПоступлениеТоваров
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.Контрагент,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента,
		|	ВременнаяТаблицаШапка.НоменклатураРасходов,
		|	ВременнаяТаблицаШапка.ЗначениеСтавкиНДС,
		|	ВременнаяТаблицаШапка.ЗначениеСтавкиНСП,
		|	ВременнаяТаблицаШапка.БезналичныйРасчет,
		|	ВременнаяТаблицаОС.Склад,
		|	ВЫБОР
		|		КОГДА &УказыватьПризнакЗачетаНДС
		|			ТОГДА ВременнаяТаблицаОС.ЗачетНДС
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.ПустаяСсылка)
		|	КОНЕЦ,
		|	ВЫРАЗИТЬ(ВременнаяТаблицаОС.СуммаРасходовНДС * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2)),
		|	ВЫРАЗИТЬ((ВременнаяТаблицаОС.СуммаРасходов + ВременнаяТаблицаОС.СуммаРасходовНДС + ВременнаяТаблицаОС.СуммаРасходовНСП) * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2)),
		|	ВЫРАЗИТЬ(ВременнаяТаблицаОС.СуммаРасходовНСП * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2)),
		|	0
		|ИЗ
		|	ВременнаяТаблицаОС КАК ВременнаяТаблицаОС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаПоступлениеТоваров.Период КАК Период,
		|	ВременнаяТаблицаПоступлениеТоваров.Организация КАК Организация,
		|	ВременнаяТаблицаПоступлениеТоваров.Контрагент КАК Контрагент,
		|	ВременнаяТаблицаПоступлениеТоваров.Договор КАК Договор,
		|	ВременнаяТаблицаПоступлениеТоваров.Номенклатура КАК Номенклатура,
		|	ВременнаяТаблицаПоступлениеТоваров.ЗначениеСтавкиНДС КАК ЗначениеСтавкиНДС,
		|	ВременнаяТаблицаПоступлениеТоваров.ЗначениеСтавкиНСП КАК ЗначениеСтавкиНСП,
		|	ВременнаяТаблицаПоступлениеТоваров.БезналичныйРасчет КАК БезналичныйРасчет,
		|	ВременнаяТаблицаПоступлениеТоваров.Склад КАК Склад,
		|	ВременнаяТаблицаПоступлениеТоваров.ЗачетНДС КАК ЗачетНДС,
		|	СУММА(ВременнаяТаблицаПоступлениеТоваров.СуммаНДС) КАК СуммаНДС,
		|	СУММА(ВременнаяТаблицаПоступлениеТоваров.Сумма) КАК Сумма,
		|	СУММА(ВременнаяТаблицаПоступлениеТоваров.СуммаНСП) КАК СуммаНСП,
		|	СУММА(ВременнаяТаблицаПоступлениеТоваров.Количество) КАК Количество
		|ИЗ
		|	ВременнаяТаблицаПоступлениеТоваров КАК ВременнаяТаблицаПоступлениеТоваров
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаПоступлениеТоваров.Период,
		|	ВременнаяТаблицаПоступлениеТоваров.ЗначениеСтавкиНДС,
		|	ВременнаяТаблицаПоступлениеТоваров.Организация,
		|	ВременнаяТаблицаПоступлениеТоваров.Номенклатура,
		|	ВременнаяТаблицаПоступлениеТоваров.ЗначениеСтавкиНСП,
		|	ВременнаяТаблицаПоступлениеТоваров.Контрагент,
		|	ВременнаяТаблицаПоступлениеТоваров.Договор,
		|	ВременнаяТаблицаПоступлениеТоваров.ЗачетНДС,
		|	ВременнаяТаблицаПоступлениеТоваров.БезналичныйРасчет,
		|	ВременнаяТаблицаПоступлениеТоваров.Склад";
	Запрос.УстановитьПараметр("УказыватьПризнакЗачетаНДС", СтруктураДополнительныеСвойства.УчетнаяПолитика.УказыватьПризнакЗачетаНДСПриПоступлении);
	РезультатЗапроса = Запрос.Выполнить();
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПоступлениеТоваров", РезультатЗапроса.Выгрузить());
		
КонецПроцедуры 

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаСведенияОПоступлении(СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Ссылка КАК ДокументСсылка,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.Контрагент КАК Контрагент,
		|	ВЫБОР
		|		КОГДА &УказыватьПризнакЗачетаНДС
		|			ТОГДА ВременнаяТаблицаТовары.ЗачетНДС
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.ПустаяСсылка)
		|	КОНЕЦ КАК ЗачетНДС,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	ВременнаяТаблицаШапка.Дата КАК ДатаПоставки,
		|	"""" КАК СерияБланкаСФ,
		|	ВременнаяТаблицаШапка.НомерБланкаСФ КАК НомерБланкаСФ,
		|	ВЫРАЗИТЬ(ВременнаяТаблицаТовары.СуммаРасходов * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2)) КАК Сумма,
		|	ВЫРАЗИТЬ(ВременнаяТаблицаТовары.СуммаРасходовНДС * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2)) КАК СуммаНДС,
		|	ВЫРАЗИТЬ(ВременнаяТаблицаТовары.СуммаРасходовНСП * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2)) КАК СуммаНСП
		|ПОМЕСТИТЬ ВременнаяТаблицаСведенияОПоступлении
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаШапка.СуммаДопРасходов <> 0
		|	И ВременнаяТаблицаШапка.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.КР)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Ссылка,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.Контрагент,
		|	ВЫБОР
		|		КОГДА &УказыватьПризнакЗачетаНДС
		|			ТОГДА ВременнаяТаблицаОС.ЗачетНДС
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.ПустаяСсылка)
		|	КОНЕЦ,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента,
		|	ВременнаяТаблицаШапка.Дата,
		|	"""",
		|	ВременнаяТаблицаШапка.НомерБланкаСФ,
		|	ВЫРАЗИТЬ(ВременнаяТаблицаОС.СуммаРасходов * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2)),
		|	ВЫРАЗИТЬ(ВременнаяТаблицаОС.СуммаРасходовНДС * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2)),
		|	ВЫРАЗИТЬ(ВременнаяТаблицаОС.СуммаРасходовНСП * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2))
		|ИЗ
		|	ВременнаяТаблицаОС КАК ВременнаяТаблицаОС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаШапка.СуммаДопРасходов <> 0
		|	И ВременнаяТаблицаШапка.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.КР)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаСведенияОПоступлении.ДокументСсылка КАК ДокументСсылка,
		|	ВременнаяТаблицаСведенияОПоступлении.Организация КАК Организация,
		|	ВременнаяТаблицаСведенияОПоступлении.Контрагент КАК Контрагент,
		|	ВременнаяТаблицаСведенияОПоступлении.ЗачетНДС КАК ЗачетНДС,
		|	ВременнаяТаблицаСведенияОПоступлении.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	ВременнаяТаблицаСведенияОПоступлении.ДатаПоставки КАК ДатаПоставки,
		|	ВременнаяТаблицаСведенияОПоступлении.СерияБланкаСФ КАК СерияБланкаСФ,
		|	ВременнаяТаблицаСведенияОПоступлении.НомерБланкаСФ КАК НомерБланкаСФ,
		|	СУММА(ВременнаяТаблицаСведенияОПоступлении.Сумма) КАК Сумма,
		|	СУММА(ВременнаяТаблицаСведенияОПоступлении.СуммаНДС) КАК СуммаНДС,
		|	СУММА(ВременнаяТаблицаСведенияОПоступлении.СуммаНСП) КАК СуммаНСП
		|ИЗ
		|	ВременнаяТаблицаСведенияОПоступлении КАК ВременнаяТаблицаСведенияОПоступлении
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаСведенияОПоступлении.ДокументСсылка,
		|	ВременнаяТаблицаСведенияОПоступлении.ЗачетНДС,
		|	ВременнаяТаблицаСведенияОПоступлении.ДоговорКонтрагента,
		|	ВременнаяТаблицаСведенияОПоступлении.Контрагент,
		|	ВременнаяТаблицаСведенияОПоступлении.ДатаПоставки,
		|	ВременнаяТаблицаСведенияОПоступлении.СерияБланкаСФ,
		|	ВременнаяТаблицаСведенияОПоступлении.НомерБланкаСФ,
		|	ВременнаяТаблицаСведенияОПоступлении.Организация";			
	Запрос.УстановитьПараметр("УказыватьПризнакЗачетаНДС", СтруктураДополнительныеСвойства.УчетнаяПолитика.УказыватьПризнакЗачетаНДСПриПоступлении);
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСведенияОПоступлении", Запрос.Выполнить().Выгрузить());		
КонецПроцедуры 

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаРеестрПриобретенныхМатериальныхРесурсов(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаТовары.ЗачетНДС КАК ЗачетНДС,
		|	ВременнаяТаблицаТовары.СуммаРасходов КАК Сумма,
		|	ВременнаяТаблицаТовары.СуммаРасходовНДС КАК СуммаНДС
		|ПОМЕСТИТЬ ВременнаяТаблицаДанныеТабличныхЧастей
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаОС.ЗачетНДС,
		|	ВременнаяТаблицаОС.СуммаРасходов,
		|	ВременнаяТаблицаОС.СуммаРасходовНДС
		|ИЗ
		|	ВременнаяТаблицаОС КАК ВременнаяТаблицаОС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаДанныеТабличныхЧастей.ЗачетНДС КАК ЗачетНДС,
		|	СУММА(ВременнаяТаблицаДанныеТабличныхЧастей.Сумма) КАК Сумма,
		|	СУММА(ВременнаяТаблицаДанныеТабличныхЧастей.СуммаНДС) КАК СуммаНДС
		|ПОМЕСТИТЬ ВременнаяТаблицаДанныеТабличныхЧастейГруппировка
		|ИЗ
		|	ВременнаяТаблицаДанныеТабличныхЧастей КАК ВременнаяТаблицаДанныеТабличныхЧастей
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаДанныеТабличныхЧастей.ЗачетНДС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.Контрагент КАК Контрагент,
		|	ВременнаяТаблицаДанныеТабличныхЧастейГруппировка.ЗачетНДС КАК ЗачетНДС,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.ЭтоПоступлениеБезНДС
		|				ИЛИ ВременнаяТаблицаШапка.ЭтоПоступлениеПоЧеку
		|			ТОГДА ВременнаяТаблицаШапка.НомерБланкаСФ
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК НомерБланкаСФ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.ЭтоПоступлениеПоЧеку
		|			ТОГДА ""Чек""
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК СерияБланкаСФ,
		|	ВременнаяТаблицаДанныеТабличныхЧастейГруппировка.СуммаНДС * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК СуммаНДС,
		|	ВременнаяТаблицаДанныеТабличныхЧастейГруппировка.Сумма * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК Сумма
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаДанныеТабличныхЧастейГруппировка КАК ВременнаяТаблицаДанныеТабличныхЧастейГруппировка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаШапка.ПлательщикНДС
		|	И ВременнаяТаблицаШапка.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.КР)
		|	И НЕ ВременнаяТаблицаШапка.КонтрагентСЭЗ";
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстЗапроса;
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРеестрПриобретенныхМатериальныхРесурсов", Запрос.Выполнить().Выгрузить());		
КонецПроцедуры

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаРеестрЗакупок(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.Контрагент КАК Контрагент,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента КАК Договор,
		|	ВременнаяТаблицаШапка.НоменклатураРасходов КАК Номенклатура,
		|	ВременнаяТаблицаШапка.ЗначениеСтавкиНДС КАК ЗначениеСтавкиНДС,
		|	ВременнаяТаблицаШапка.ЗначениеСтавкиНСП КАК ЗначениеСтавкиНСП,
		|	ВЫРАЗИТЬ(ВременнаяТаблицаШапка.СуммаНДС * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2)) КАК СуммаНДС,
		|	ВЫРАЗИТЬ(ВременнаяТаблицаШапка.СуммаДопРасходов * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2)) КАК Сумма,
		|	ВЫРАЗИТЬ(ВременнаяТаблицаШапка.СуммаНСП * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК ЧИСЛО(15, 2)) КАК СуммаНСП,
		|	0 КАК Количество
		|ПОМЕСТИТЬ ВременнаяТаблицаДанные
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|ГДЕ
		|	ВременнаяТаблицаШапка.ПризнакСтраны <> ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ИмпортЭкспорт)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаДанные.Период КАК Период,
		|	ВременнаяТаблицаДанные.Организация КАК Организация,
		|	ВременнаяТаблицаДанные.Контрагент КАК Контрагент,
		|	ВременнаяТаблицаДанные.Договор КАК Договор,
		|	ВременнаяТаблицаДанные.Номенклатура КАК Номенклатура,
		|	ВременнаяТаблицаДанные.ЗначениеСтавкиНДС КАК ЗначениеСтавкиНДС,
		|	ВременнаяТаблицаДанные.ЗначениеСтавкиНСП КАК ЗначениеСтавкиНСП,
		|	ВременнаяТаблицаДанные.СуммаНДС КАК СуммаНДС,
		|	ВременнаяТаблицаДанные.Сумма - ВременнаяТаблицаДанные.СуммаНДС - ВременнаяТаблицаДанные.СуммаНСП КАК Сумма,
		|	ВременнаяТаблицаДанные.СуммаНСП КАК СуммаНСП,
		|	ВременнаяТаблицаДанные.Количество КАК Количество
		|ИЗ
		|	ВременнаяТаблицаДанные КАК ВременнаяТаблицаДанные";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРеестрЗакупок", РезультатЗапроса.Выгрузить());	
КонецПроцедуры 

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаПараметрыУчетаОС(ДокументСсылка, СтруктураДополнительныеСвойства)
	 	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	&Период КАК Период,
		|	ВременнаяТаблицаПараметрыУчетаОС.Организация КАК Организация,
		|	ВременнаяТаблицаПараметрыУчетаОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ВременнаяТаблицаПараметрыУчетаОС.ИнвентарныйНомер КАК ИнвентарныйНомер,
		|	ВременнаяТаблицаПараметрыУчетаОС.СчетУчета КАК СчетУчета,
		|	ВременнаяТаблицаПараметрыУчетаОС.ГруппаНУ КАК ГруппаНУ,
		|	ВременнаяТаблицаПараметрыУчетаОС.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
		|	ВременнаяТаблицаПараметрыУчетаОС.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
		|	ВременнаяТаблицаПараметрыУчетаОС.СрокСлужбы КАК СрокСлужбы,
		|	ВременнаяТаблицаПараметрыУчетаОС.СпособНачисленияАмортизации КАК СпособНачисленияАмортизации,
		|	ВременнаяТаблицаПараметрыУчетаОС.СпособОтраженияРасходовПоАмортизации КАК СпособОтраженияРасходовПоАмортизации,
		|	ВременнаяТаблицаПараметрыУчетаОС.ПараметрВыработки КАК ПараметрВыработки,
		|	ВременнаяТаблицаПараметрыУчетаОС.ОбъемПродукции КАК ОбъемПродукции,
		|	ВременнаяТаблицаПараметрыУчетаОС.КоэффициентУскорения КАК КоэффициентУскорения,
		|	ВременнаяТаблицаПараметрыУчетаОС.СпособПоступления КАК СпособПоступления,
		|	ВременнаяТаблицаПараметрыУчетаОС.ДатаВводаВЭксплуатацию КАК ДатаВводаВЭксплуатацию,
		|	ВременнаяТаблицаПараметрыУчетаОС.СтоимостьДляРасчетаАмортизации КАК СтоимостьДляРасчетаАмортизации,
		|	ВременнаяТаблицаПараметрыУчетаОС.ДатаНачисленияНалогаНаИмущество КАК ДатаНачисленияНалогаНаИмущество,
		|	ВременнаяТаблицаПараметрыУчетаОС.Консервация КАК Консервация
		|ИЗ
		|	ВременнаяТаблицаПараметрыУчетаОС КАК ВременнаяТаблицаПараметрыУчетаОС";
	Запрос.УстановитьПараметр("Период", СтруктураДополнительныеСвойства.ДляПроведения.Дата);
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПараметрыУчетаОС", Запрос.Выполнить().Выгрузить()); 		
КонецПроцедуры

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДокумента.Ссылка КАК Ссылка,
		|	ТаблицаДокумента.Дата КАК Дата,
		|	ТаблицаДокумента.Организация КАК Организация,
		|	ТаблицаДокумента.ВалютаДокумента КАК ВалютаДокумента,
		|	ТаблицаДокумента.Контрагент КАК Контрагент,
		|	ТаблицаДокумента.Контрагент.ПризнакСтраны КАК ПризнакСтраны,
		|	ТаблицаДокумента.Контрагент.СЭЗ КАК КонтрагентСЭЗ,
		|	ТаблицаДокумента.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	ТаблицаДокумента.СчетРасчетов КАК СчетРасчетов,
		|	ТаблицаДокумента.Курс КАК Курс,
		|	ТаблицаДокумента.Кратность КАК Кратность,
		|	ТаблицаДокумента.НоменклатураРасходов КАК НоменклатураРасходов,
		|	ТаблицаДокумента.ЭтоПоступлениеБезНДС КАК ЭтоПоступлениеБезНДС,
		|	ТаблицаДокумента.ЭтоПоступлениеПоЧеку КАК ЭтоПоступлениеПоЧеку,
		|	ТаблицаДокумента.СерияБланкаСФ КАК СерияБланкаСФ,
		|	ТаблицаДокумента.НомерБланкаСФ КАК НомерБланкаСФ,
		|	ТаблицаДокумента.ДатаСФ КАК ДатаСФ,
		|	ТаблицаДокумента.ЗначениеСтавкиНДС КАК ЗначениеСтавкиНДС,
		|	ТаблицаДокумента.ЗначениеСтавкиНСП КАК ЗначениеСтавкиНСП,
		|	ТаблицаДокумента.БезналичныйРасчет КАК БезналичныйРасчет,
		|	ТаблицаДокумента.НеВключатьВРеестрСФ КАК НеВключатьВРеестрСФ,
		|	ТаблицаДокумента.СуммаДопРасходов КАК СуммаДопРасходов,
		|	ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
		|	ТаблицаДокумента.СуммаНСП КАК СуммаНСП,
		|	&ВедетсяУчетПоПартиям КАК ВедетсяУчетПоПартиям,
		|	&ПлательщикНДС КАК ПлательщикНДС,
		|	&УказыватьПризнакЗачетаНДСПриПоступлении КАК УказыватьПризнакЗачетаНДСПриПоступлении
		|ПОМЕСТИТЬ ВременнаяТаблицаШапка
		|ИЗ
		|	Документ.КорректировкаСтоимостиДополнительныхРасходов КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &ДокументСсылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.ДокументПоступления КАК ДокументПоступления,
		|	ТаблицаДокумента.ДокументПоступления.Склад КАК Склад,
		|	ТаблицаДокумента.Номенклатура КАК Номенклатура,
		|	ТаблицаДокумента.СчетУчета КАК СчетУчета,
		|	ТаблицаДокумента.СуммаРасходов КАК СуммаРасходов,
		|	ТаблицаДокумента.СуммаРасходовНДС КАК СуммаРасходовНДС,
		|	ТаблицаДокумента.СуммаРасходовНСП КАК СуммаРасходовНСП,
		|	ТаблицаДокумента.ЗачетНДС КАК ЗачетНДС
		|ПОМЕСТИТЬ ВременнаяТаблицаТовары
		|ИЗ
		|	Документ.КорректировкаСтоимостиДополнительныхРасходов.Товары КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &ДокументСсылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.ДокументПоступления КАК ДокументПоступления,
		|	ТаблицаДокумента.ДокументПоступления.Склад КАК Склад,
		|	ТаблицаДокумента.ОсновноеСредство КАК ОсновноеСредство,
		|	ТаблицаДокумента.СчетУчета КАК СчетУчета,
		|	ТаблицаДокумента.СуммаРасходов КАК СуммаРасходов,
		|	ТаблицаДокумента.СуммаРасходовНДС КАК СуммаРасходовНДС,
		|	ТаблицаДокумента.СуммаРасходовНСП КАК СуммаРасходовНСП,
		|	ТаблицаДокумента.ЗачетНДС КАК ЗачетНДС
		|ПОМЕСТИТЬ ВременнаяТаблицаОС
		|ИЗ
		|	Документ.КорректировкаСтоимостиДополнительныхРасходов.ОС КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &ДокументСсылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПараметрыУчетаОССрезПоследних.Организация КАК Организация,
		|	ПараметрыУчетаОССрезПоследних.ОсновноеСредство КАК ОсновноеСредство,
		|	ПараметрыУчетаОССрезПоследних.ИнвентарныйНомер КАК ИнвентарныйНомер,
		|	ПараметрыУчетаОССрезПоследних.СчетУчета КАК СчетУчета,
		|	ПараметрыУчетаОССрезПоследних.ГруппаНУ КАК ГруппаНУ,
		|	ПараметрыУчетаОССрезПоследних.ПервоначальнаяСтоимость + ВременнаяТаблицаОС.СуммаРасходов + ВременнаяТаблицаОС.СуммаРасходовНСП КАК ПервоначальнаяСтоимость,
		|	ПараметрыУчетаОССрезПоследних.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
		|	ПараметрыУчетаОССрезПоследних.СрокСлужбы КАК СрокСлужбы,
		|	ПараметрыУчетаОССрезПоследних.СпособНачисленияАмортизации КАК СпособНачисленияАмортизации,
		|	ПараметрыУчетаОССрезПоследних.СпособОтраженияРасходовПоАмортизации КАК СпособОтраженияРасходовПоАмортизации,
		|	ПараметрыУчетаОССрезПоследних.ПараметрВыработки КАК ПараметрВыработки,
		|	ПараметрыУчетаОССрезПоследних.ОбъемПродукции КАК ОбъемПродукции,
		|	ПараметрыУчетаОССрезПоследних.КоэффициентУскорения КАК КоэффициентУскорения,
		|	ПараметрыУчетаОССрезПоследних.СпособПоступления КАК СпособПоступления,
		|	ПараметрыУчетаОССрезПоследних.ДатаВводаВЭксплуатацию КАК ДатаВводаВЭксплуатацию,
		|	ПараметрыУчетаОССрезПоследних.СтоимостьДляРасчетаАмортизации КАК СтоимостьДляРасчетаАмортизации,
		|	ПараметрыУчетаОССрезПоследних.ДатаНачисленияНалогаНаИмущество КАК ДатаНачисленияНалогаНаИмущество,
		|	ПараметрыУчетаОССрезПоследних.Консервация КАК Консервация
		|ПОМЕСТИТЬ ВременнаяТаблицаПараметрыУчетаОС
		|ИЗ
		|	РегистрСведений.ПараметрыУчетаОС.СрезПоследних(
		|			&Период,
		|			ОсновноеСредство В
		|				(ВЫБРАТЬ
		|					ВременнаяТаблицаОС.ОсновноеСредство
		|				ИЗ
		|					ВременнаяТаблицаОС)) КАК ПараметрыУчетаОССрезПоследних
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаОС КАК ВременнаяТаблицаОС
		|		ПО ПараметрыУчетаОССрезПоследних.ОсновноеСредство = ВременнаяТаблицаОС.ОсновноеСредство";	
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	УчетнаяПолитика = СтруктураДополнительныеСвойства.УчетнаяПолитика;
	Запрос.УстановитьПараметр("ПлательщикНДС", УчетнаяПолитика.ПлательщикНДС);
	Запрос.УстановитьПараметр("УказыватьПризнакЗачетаНДСПриПоступлении", УчетнаяПолитика.УказыватьПризнакЗачетаНДСПриПоступлении);	
	Запрос.УстановитьПараметр("ВедетсяУчетПоПартиям", УчетнаяПолитика.СпособОценкиТМЗ = Перечисления.СпособыОценки.ФИФО);
	Запрос.УстановитьПараметр("Период", СтруктураДополнительныеСвойства.ДляПроведения.Дата);
	Запрос.Выполнить();    		
	
	СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства);	
	СформироватьТаблицаПоступлениеТоваров(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаСведенияОПоступлении(СтруктураДополнительныеСвойства);
	СформироватьТаблицаРеестрЗакупок(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаРеестрПриобретенныхМатериальныхРесурсов(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаПараметрыУчетаОС(ДокументСсылка, СтруктураДополнительныеСвойства)
КонецПроцедуры // ИнициализироватьДанныеДокумента()

#КонецОбласти

#Область ВерсионированиеОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
//@skip-warning
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область ИнтерфейсПечати

Функция ПечатьДопРасходы(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();

	Запрос = Новый Запрос;
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	КорректировкаСтоимостиДополнительныхРасходов.Ссылка КАК Ссылка,
		|	КорректировкаСтоимостиДополнительныхРасходов.Дата КАК Дата,
		|	КорректировкаСтоимостиДополнительныхРасходов.Номер КАК Номер,
		|	КорректировкаСтоимостиДополнительныхРасходов.Контрагент.НаименованиеПолное КАК ПредставлениеПоставщика,
		|	КорректировкаСтоимостиДополнительныхРасходов.Организация.НаименованиеПолное КАК ПредставлениеПолучателя,
		|	КорректировкаСтоимостиДополнительныхРасходов.ДоговорКонтрагента.ВалютаРасчетов КАК ДоговорКонтрагентаВалютаРасчетов,
		|	КорректировкаСтоимостиДополнительныхРасходов.ВалютаДокумента КАК ВалютаДокументаДопрасхода,
		|	КорректировкаСтоимостиДополнительныхРасходов.СуммаДопРасходов - КорректировкаСтоимостиДополнительныхРасходов.СуммаНДС - КорректировкаСтоимостиДополнительныхРасходов.СуммаНСП КАК СуммаДопРасходов,
		|	КорректировкаСтоимостиДополнительныхРасходов.СуммаНДС КАК СуммаНДС,
		|	КорректировкаСтоимостиДополнительныхРасходов.СуммаНСП КАК СуммаНСП,
		|	КорректировкаСтоимостиДополнительныхРасходов.СуммаДопРасходов КАК Всего,
		|	(ВЫРАЗИТЬ(КорректировкаСтоимостиДополнительныхРасходов.СуммаДопРасходов * КорректировкаСтоимостиДополнительныхРасходов.Курс / КорректировкаСтоимостиДополнительныхРасходов.Кратность КАК ЧИСЛО(15, 2))) - (ВЫРАЗИТЬ(КорректировкаСтоимостиДополнительныхРасходов.СуммаНДС * КорректировкаСтоимостиДополнительныхРасходов.Курс / КорректировкаСтоимостиДополнительныхРасходов.Кратность КАК ЧИСЛО(15, 2))) - (ВЫРАЗИТЬ(КорректировкаСтоимостиДополнительныхРасходов.СуммаНСП * КорректировкаСтоимостиДополнительныхРасходов.Курс / КорректировкаСтоимостиДополнительныхРасходов.Кратность КАК ЧИСЛО(15, 2))) КАК СуммаДопРасходовВНацвалюте,
		|	ВЫРАЗИТЬ(КорректировкаСтоимостиДополнительныхРасходов.СуммаНДС * КорректировкаСтоимостиДополнительныхРасходов.Курс / КорректировкаСтоимостиДополнительныхРасходов.Кратность КАК ЧИСЛО(15, 2)) КАК СуммаНДСВНацвалюте,
		|	ВЫРАЗИТЬ(КорректировкаСтоимостиДополнительныхРасходов.СуммаНСП * КорректировкаСтоимостиДополнительныхРасходов.Курс / КорректировкаСтоимостиДополнительныхРасходов.Кратность КАК ЧИСЛО(15, 2)) КАК СуммаНСПВНацвалюте,
		|	ВЫРАЗИТЬ(КорректировкаСтоимостиДополнительныхРасходов.СуммаДопРасходов * КорректировкаСтоимостиДополнительныхРасходов.Курс / КорректировкаСтоимостиДополнительныхРасходов.Кратность КАК ЧИСЛО(15, 2)) КАК ВсегоВНацвалюте,
		|	ВЫБОР
		|		КОГДА (ВЫРАЗИТЬ(КорректировкаСтоимостиДополнительныхРасходов.Комментарий КАК СТРОКА(100))) = """"
		|			ТОГДА ""Дополнительные расходы""
		|		ИНАЧЕ КорректировкаСтоимостиДополнительныхРасходов.Комментарий
		|	КОНЕЦ КАК Содержание,
		|	КорректировкаСтоимостиДополнительныхРасходов.Товары.(
		|		Номенклатура.Наименование КАК Наименование,
		|		Количество КАК Количество,
		|		Сумма КАК Сумма,
		|		ВЫРАЗИТЬ(КорректировкаСтоимостиДополнительныхРасходов.Товары.Сумма * КорректировкаСтоимостиДополнительныхРасходов.Товары.ДокументПоступления.Курс / КорректировкаСтоимостиДополнительныхРасходов.Товары.ДокументПоступления.Кратность КАК ЧИСЛО(15, 2)) КАК СуммаВНацвалюте,
		|		СуммаРасходов КАК СуммаРасходов,
		|		ВЫРАЗИТЬ(КорректировкаСтоимостиДополнительныхРасходов.Товары.СуммаРасходов * КорректировкаСтоимостиДополнительныхРасходов.Курс / КорректировкаСтоимостиДополнительныхРасходов.Кратность КАК ЧИСЛО(15, 2)) КАК СуммаРасходовВНацвалюте,
		|		(ВЫРАЗИТЬ(КорректировкаСтоимостиДополнительныхРасходов.Товары.Сумма * КорректировкаСтоимостиДополнительныхРасходов.Товары.ДокументПоступления.Курс / КорректировкаСтоимостиДополнительныхРасходов.Товары.ДокументПоступления.Кратность КАК ЧИСЛО(15, 2))) + (ВЫРАЗИТЬ(КорректировкаСтоимостиДополнительныхРасходов.Товары.СуммаРасходов * КорректировкаСтоимостиДополнительныхРасходов.Курс / КорректировкаСтоимостиДополнительныхРасходов.Кратность КАК ЧИСЛО(15, 2))) КАК ВсегоВНацвалюте,
		|		ДокументПоступления.ВалютаДокумента КАК ВалютаДокументаПоступления
		|	) КАК Товары,
		|	КорректировкаСтоимостиДополнительныхРасходов.ОС.(
		|		ОсновноеСредство.Наименование КАК Наименование,
		|		1 КАК Количество,
		|		Сумма КАК Сумма,
		|		ВЫРАЗИТЬ(КорректировкаСтоимостиДополнительныхРасходов.ОС.Сумма * КорректировкаСтоимостиДополнительныхРасходов.ОС.ДокументПоступления.Курс / КорректировкаСтоимостиДополнительныхРасходов.ОС.ДокументПоступления.Кратность КАК ЧИСЛО(15, 2)) КАК СуммаВНацвалюте,
		|		СуммаРасходов КАК СуммаРасходов,
		|		ВЫРАЗИТЬ(КорректировкаСтоимостиДополнительныхРасходов.ОС.СуммаРасходов * КорректировкаСтоимостиДополнительныхРасходов.Курс / КорректировкаСтоимостиДополнительныхРасходов.Кратность КАК ЧИСЛО(15, 2)) КАК СуммаРасходовВНацвалюте,
		|		(ВЫРАЗИТЬ(КорректировкаСтоимостиДополнительныхРасходов.ОС.Сумма * КорректировкаСтоимостиДополнительныхРасходов.ОС.ДокументПоступления.Курс / КорректировкаСтоимостиДополнительныхРасходов.ОС.ДокументПоступления.Кратность КАК ЧИСЛО(15, 2))) + (ВЫРАЗИТЬ(КорректировкаСтоимостиДополнительныхРасходов.ОС.СуммаРасходов * КорректировкаСтоимостиДополнительныхРасходов.Курс / КорректировкаСтоимостиДополнительныхРасходов.Кратность КАК ЧИСЛО(15, 2))) КАК ВсегоВНацвалюте,
		|		ДокументПоступления.ВалютаДокумента КАК ВалютаДокументаПоступления
		|	) КАК ОС
		|ИЗ
		|	Документ.КорректировкаСтоимостиДополнительныхРасходов КАК КорректировкаСтоимостиДополнительныхРасходов
		|ГДЕ
		|	КорректировкаСтоимостиДополнительныхРасходов.Ссылка В(&МассивОбъектов)";
		
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "КорректировкаСтоимостиДополнительныхРасходов_ДопРасходы";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.КорректировкаСтоимостиДополнительныхРасходов.ПФ_MXL_ДопРасходы");
		
	Пока Выборка.Следующий() Цикл
		Если ТабличныйДокумент.ВысотаТаблицы > 0 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ДанныеПечати = Новый Структура;
		ДанныеПечати.Вставить("ВалютаДокументаДопрасхода", 	Выборка.ВалютаДокументаДопрасхода);
		ДанныеПечати.Вставить("СуммаДопРасходов", 			Выборка.СуммаДопРасходов);
		ДанныеПечати.Вставить("СуммаНДС", 					Выборка.СуммаНДС);
		ДанныеПечати.Вставить("СуммаНСП", 					Выборка.СуммаНСП);
		ДанныеПечати.Вставить("Всего", 						Выборка.Всего);
		ДанныеПечати.Вставить("СуммаДопРасходовВНацвалюте", Выборка.СуммаДопРасходовВНацвалюте);
		ДанныеПечати.Вставить("СуммаНДСВНацвалюте", 		Выборка.СуммаНДСВНацвалюте);
		ДанныеПечати.Вставить("СуммаНСПВНацвалюте", 		Выборка.СуммаНСПВНацвалюте);
		ДанныеПечати.Вставить("ВсегоВНацвалюте", 			Выборка.ВсегоВНацвалюте);
		ДанныеПечати.Вставить("Содержание", 				Выборка.Содержание);
		ЗаполнитьЗначенияСвойств(ДанныеПечати, Выборка);
		
		ВременнаяТаблицаТовары 		= Выборка.Товары.Выгрузить();                                      
		ВременнаяТаблицаОС 			= Выборка.ОС.Выгрузить();
		
		ВыборкаТовары 		= Выборка.Товары.Выбрать();
		ВыборкаОС 			= Выборка.ОС.Выбрать();
		
		ТекстЗаголовка = СтрШаблон(НСтр("ru = 'Доп. расходы № %1 от %2'"), 
										ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Выборка.Номер),
										Формат(Выборка.Дата,"ДЛФ=DD"));
		ДанныеПечати.Вставить("ТекстЗаголовка", ТекстЗаголовка);
		ДанныеПечати.Вставить("ПредставлениеПоставщика", Выборка.ПредставлениеПоставщика);
		ДанныеПечати.Вставить("ПредставлениеПолучателя", Выборка.ПредставлениеПолучателя);
		
		СуммаТоварыИтого 					= ВременнаяТаблицаТовары.Итог("Сумма");
		СуммаТоварыИтогоВНацвалюте			= ВременнаяТаблицаТовары.Итог("СуммаВНацвалюте");
		СуммаОСИтого 						= ВременнаяТаблицаОС.Итог("Сумма");
		СуммаОСИтогоВНацвалюте 				= ВременнаяТаблицаОС.Итог("СуммаВНацвалюте");
		СуммаРасходовТоварыИтого 			= ВременнаяТаблицаТовары.Итог("СуммаРасходов");
		СуммаРасходовТоварыИтогоВНацвалюте 	= ВременнаяТаблицаТовары.Итог("СуммаРасходовВНацвалюте");
		СуммаРасходовОСИтого 				= ВременнаяТаблицаОС.Итог("СуммаРасходов");		
		СуммаРасходовОСИтогоВНацвалюте 		= ВременнаяТаблицаОС.Итог("СуммаРасходовВНацвалюте");
		ВсегоИтогоВНацвалюте                = СуммаТоварыИтогоВНацвалюте + СуммаРасходовТоварыИтогоВНацвалюте
									+ СуммаОСИтогоВНацвалюте + СуммаРасходовОСИтогоВНацвалюте;
		
		ДанныеПечати.Вставить("СуммаИтого", СуммаТоварыИтого + СуммаОСИтого);
		ДанныеПечати.Вставить("СуммаИтогоВНацвалюте", СуммаТоварыИтогоВНацвалюте + СуммаОСИтогоВНацвалюте);
		ДанныеПечати.Вставить("СуммаРасходовИтого", СуммаРасходовТоварыИтого + СуммаРасходовОСИтого);
		ДанныеПечати.Вставить("СуммаРасходовИтогоВНацвалюте", СуммаРасходовТоварыИтогоВНацвалюте + СуммаРасходовОСИтогоВНацвалюте);
		ДанныеПечати.Вставить("ВсегоИтогоВНацвалюте", ВсегоИтогоВНацвалюте);													
        ДанныеПечати.Вставить("СуммаДокументаПрописью", БухгалтерскийУчетСервер.СформироватьСуммуПрописью(Выборка.Всего, Выборка.ВалютаДокументаДопрасхода));
		
		МассивОбластейМакета = Новый Массив;  
		МассивОбластейМакета.Добавить("Заголовок");
		МассивОбластейМакета.Добавить("Поставщик");
		МассивОбластейМакета.Добавить("Покупатель");
		МассивОбластейМакета.Добавить("ШапкаТаблицы");
		МассивОбластейМакета.Добавить("СтрокаДопРасходы");		
		МассивОбластейМакета.Добавить("СтрокаДопРасходыВНацвалюте");
		МассивОбластейМакета.Добавить("СуммаПрописью");
		
		Если ВременнаяТаблицаТовары.Количество() > 0 Тогда
			ВалютаДокументаПоступления = ВременнаяТаблицаТовары[0].ВалютаДокументаПоступления;	
		Иначе
			ВалютаДокументаПоступления = ВременнаяТаблицаОС[0].ВалютаДокументаПоступления;
		КонецЕсли;	
		
		ДанныеПечати.Вставить("ВалютаДокументаПоступления", ВалютаДокументаПоступления);
		
		Если Выборка.ВалютаДокументаДопрасхода = ВалютаРегламентированногоУчета И ВалютаДокументаПоступления = ВалютаРегламентированногоУчета Тогда
			МассивОбластейМакета.Добавить("ШапкаТаблицыСомовая");
			МассивОбластейМакета.Добавить("СтрокаСомовая");
			МассивОбластейМакета.Добавить("ИтогиСомовые");
							
		ИначеЕсли Выборка.ВалютаДокументаДопрасхода = ВалютаРегламентированногоУчета И ВалютаДокументаПоступления <> ВалютаРегламентированногоУчета Тогда
			МассивОбластейМакета.Добавить("ШапкаТаблицыПоступлениеВВалюте");
			МассивОбластейМакета.Добавить("СтрокаПоступлениеВВалюте");
			МассивОбластейМакета.Добавить("ИтогиПоступлениеВВалюте");
			
		ИначеЕсли Выборка.ВалютаДокументаДопрасхода <> ВалютаРегламентированногоУчета И ВалютаДокументаПоступления = ВалютаРегламентированногоУчета Тогда	
			МассивОбластейМакета.Добавить("ШапкаТаблицыДопрасходВВалюте");
			МассивОбластейМакета.Добавить("СтрокаДопрасходВВалюте");
			МассивОбластейМакета.Добавить("ИтогиДопрасходВВалюте");
			
		Иначе
			МассивОбластейМакета.Добавить("ШапкаТаблицыВалютная");
			МассивОбластейМакета.Добавить("СтрокаВалютная");
			МассивОбластейМакета.Добавить("ИтогиВалютные");	
		КонецЕсли;	
		
		МассивОбластейМакета.Добавить("Подписи");
		
		Для Каждого ИмяОбласти Из МассивОбластейМакета Цикл
			ОбластьМакета = Макет.ПолучитьОбласть(ИмяОбласти);
			
			Если ИмяОбласти = "СтрокаДопРасходыВНацвалюте" Тогда
				Если НЕ Выборка.ВалютаДокументаДопрасхода = ВалютаРегламентированногоУчета Тогда
					ОбластьМакета.Параметры.Заполнить(Выборка);
					ТабличныйДокумент.Вывести(ОбластьМакета);				
				КонецЕсли;									
			ИначеЕсли ИмяОбласти = "СтрокаСомовая" ИЛИ ИмяОбласти = "СтрокаПоступлениеВВалюте"
				ИЛИ ИмяОбласти = "СтрокаДопрасходВВалюте" ИЛИ ИмяОбласти = "СтрокаВалютная" Тогда
				НомерСтроки = 0;
				Пока ВыборкаТовары.Следующий() Цикл
					НомерСтроки = НомерСтроки + 1;
					ОбластьМакета.Параметры.Заполнить(ВыборкаТовары);
					ОбластьМакета.Параметры.НомерСтроки = НомерСтроки;
					ТабличныйДокумент.Вывести(ОбластьМакета);
				КонецЦикла;
				Пока ВыборкаОС.Следующий() Цикл
					НомерСтроки = НомерСтроки + 1;
					ОбластьМакета.Параметры.Заполнить(ВыборкаОС);
					ОбластьМакета.Параметры.НомерСтроки = НомерСтроки;
					ОбластьМакета.Параметры.Количество = 1;
					ТабличныйДокумент.Вывести(ОбластьМакета);
				КонецЦикла;
			Иначе
				ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, ДанныеПечати);
				ТабличныйДокумент.Вывести(ОбластьМакета);
			КонецЕсли;
		КонецЦикла;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Выборка.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
			
КонецФункции

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ДопРасходы") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
		"ДопРасходы", НСтр("ru = 'Доп. расходы'"), ПечатьДопРасходы(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),,
		"Документ.КорректировкаСтоимостиДополнительныхРасходов.ПФ_MXL_ДопРасходы");
	КонецЕсли;
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ДопРасходы";
	КомандаПечати.Представление = НСтр("ru = 'Доп. расходы'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Обработчик = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Порядок = 1;
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор  = "РеестрДополнительныеРасходы";
	КомандаПечати.Представление  = НСтр("ru = 'Реестр документов'");
	КомандаПечати.ЗаголовокФормы = НСтр("ru = 'Реестр документов ""Дополнительные расходы""'");
	КомандаПечати.Обработчик     = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм     = "ФормаСписка";
	КомандаПечати.Порядок        = 100;
КонецПроцедуры

#КонецОбласти

#КонецЕсли