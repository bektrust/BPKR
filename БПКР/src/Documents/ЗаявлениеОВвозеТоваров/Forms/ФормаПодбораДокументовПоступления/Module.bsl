#Область ОбработчикиСобытийФормы 

// Процедура - обработчик события ПриСозданииНаСервере.
// В процедуре осуществляется
// - инициализация реквизитов формы,
// - установка параметров функциональных опций формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВидПодбора = Параметры.ВидПодбора;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СведенияОВвозимыхТоварах.ДокументСсылка КАК ДокументПоступления
		|ПОМЕСТИТЬ ВременнаяТаблицаСписокПоступленийВРаннихЗаявлениях
		|ИЗ
		|	РегистрСведений.СведенияОВвозимыхТоварах КАК СведенияОВвозимыхТоварах
		|ГДЕ
		|	СведенияОВвозимыхТоварах.Организация = &Организация
		|	И СведенияОВвозимыхТоварах.Дата <= &ДатаОтбора
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДокументПоступления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПоступлениеТоваровУслуг.Ссылка КАК ДокументПоступления
		|ПОМЕСТИТЬ ВременнаяТаблицаСписокУжеВыбранных
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
		|ГДЕ
		|	ПоступлениеТоваровУслуг.Ссылка В(&СписокУжеВыбранных)
		|	И ПоступлениеТоваровУслуг.Проведен
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВозвратТоваровПоставщику.Ссылка
		|ИЗ
		|	Документ.ВозвратТоваровПоставщику КАК ВозвратТоваровПоставщику
		|ГДЕ
		|	ВозвратТоваровПоставщику.Ссылка В(&СписокУжеВыбранных)
		|	И ВозвратТоваровПоставщику.Проведен
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДокументПоступления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПоступлениеТоваровУслуг.Ссылка КАК ДокументПоступления,
		|	ПоступлениеТоваровУслуг.Дата КАК Дата,
		|	ПоступлениеТоваровУслуг.СуммаДокумента КАК СуммаДокумента
		|ПОМЕСТИТЬ ВременнаяТаблицаПоступления
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
		|ГДЕ
		|	ПоступлениеТоваровУслуг.Контрагент.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
		|	И ПоступлениеТоваровУслуг.Дата <= &ДатаОтбора
		|	И ПоступлениеТоваровУслуг.Контрагент = &Контрагент
		|	И ПоступлениеТоваровУслуг.Организация = &Организация
		|	И ПоступлениеТоваровУслуг.Проведен
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВозвратТоваровПоставщику.Ссылка,
		|	ВозвратТоваровПоставщику.Дата,
		|	-ВозвратТоваровПоставщику.СуммаДокумента
		|ИЗ
		|	Документ.ВозвратТоваровПоставщику КАК ВозвратТоваровПоставщику
		|ГДЕ
		|	ВозвратТоваровПоставщику.Контрагент.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.ЕАЭС)
		|	И ВозвратТоваровПоставщику.Дата <= &ДатаОтбора
		|	И ВозвратТоваровПоставщику.Контрагент = &Контрагент
		|	И ВозвратТоваровПоставщику.Организация = &Организация
		|	И ВозвратТоваровПоставщику.Проведен
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДокументПоступления,
		|	Дата
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаПоступления.ДокументПоступления КАК ДокументПоступления,
		|	ВременнаяТаблицаПоступления.СуммаДокумента КАК СуммаДокумента,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаСписокУжеВыбранных.ДокументПоступления ЕСТЬ NULL
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Доступно
		|ИЗ
		|	ВременнаяТаблицаПоступления КАК ВременнаяТаблицаПоступления
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСписокУжеВыбранных КАК ВременнаяТаблицаСписокУжеВыбранных
		|		ПО ВременнаяТаблицаПоступления.ДокументПоступления = ВременнаяТаблицаСписокУжеВыбранных.ДокументПоступления
		|ГДЕ
		|	НЕ ВременнаяТаблицаПоступления.ДокументПоступления В
		|				(ВЫБРАТЬ
		|					ВременнаяТаблицаСписокПоступленийВРаннихЗаявлениях.ДокументПоступления
		|				ИЗ
		|					ВременнаяТаблицаСписокПоступленийВРаннихЗаявлениях)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВременнаяТаблицаПоступления.Дата";
	Запрос.УстановитьПараметр("ДатаОтбора", Параметры.ДатаОтбора);
	Запрос.УстановитьПараметр("Контрагент", Параметры.Контрагент);
	Запрос.УстановитьПараметр("Организация", Параметры.Организация);
	Запрос.УстановитьПараметр("СписокУжеВыбранных", Параметры.СписокУжеВыбранных);	
	
	ТаблицаДокументов.Загрузить(Запрос.Выполнить().Выгрузить());
	
	УстановитьУсловноеОформление();

	// РаботаСФормами
	РаботаСФормамиСервер.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец РаботаСФормами
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиТабличныхЧастей 

&НаКлиенте
Процедура ТаблицаДокументовВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	МассивДокументов = Новый Массив;	
	Для каждого ИндексСтроки Из Значение Цикл
		Если ТаблицаДокументов[ИндексСтроки].Доступно Тогда
			МассивДокументов.Добавить(ТаблицаДокументов[ИндексСтроки].ДокументПоступления);
		КонецЕсли;
	КонецЦикла;
	
	ЗакрыватьПриВыборе = Истина;
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("ВидПодбора", 		ВидПодбора);
	СтруктураВозврата.Вставить("МассивДокументов", 	МассивДокументов);
	
	ОповеститьОВыборе(СтруктураВозврата);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура настройки условного оформления форм и динамических списков .
//
&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	// Таблица документов.
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ТаблицаДокументов");

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаДокументов.Доступно");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НедоступныеДанныеЦвет);
	
КонецПроцедуры

#КонецОбласти