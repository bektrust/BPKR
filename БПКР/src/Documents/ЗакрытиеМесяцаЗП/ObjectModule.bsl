#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события ОбработкаЗаполнения объекта.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеОбъектовБП.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения);
	
	Если НЕ Дата = КонецМесяца(ТекущаяДатаСеанса()) Тогда
		Дата = КонецМесяца(ТекущаяДатаСеанса());
	КонецЕсли;
		
КонецПроцедуры

// Процедура - обработчик события ОбработкаПроведения объекта.
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ИнициализироватьДанные(Отказ, РежимПроведения);
	
	ОтразитьДвижения(Отказ, РежимПроведения);
	
	// Запись наборов записей.
	БухгалтерскийУчетСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
КонецПроцедуры // ОбработкаПроведения()

// Процедура - обработчик события ОбработкаУдаленияПроведения объекта.
//
Процедура ОбработкаУдаленияПроведения(Отказ)
	
	// Инициализация дополнительных свойств для удаления проведения документа
	БухгалтерскийУчетСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	БухгалтерскийУчетСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Запись наборов записей
	БухгалтерскийУчетСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
КонецПроцедуры

// Процедура - обработчик события ОбработкаПроверкиЗаполнения объекта.
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	// Предварительный контроль
	ВыполнитьПредварительныйКонтроль(Отказ);	
КонецПроцедуры // ОбработкаПроверкиЗаполнения()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура выполняет контроль противоречий.
//
Процедура ВыполнитьПредварительныйКонтроль(Отказ)
	
	// Проверка повторного закрытия месяца
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЗакрытиеМесяцаЗП.Ссылка
		|ИЗ
		|	Документ.ЗакрытиеМесяцаЗП КАК ЗакрытиеМесяцаЗП
		|ГДЕ
		|	ЗакрытиеМесяцаЗП.Организация = &Организация
		|	И ЗакрытиеМесяцаЗП.Дата = &Дата
		|	И ЗакрытиеМесяцаЗП.Проведен
		|	И ЗакрытиеМесяцаЗП.Ссылка <> &Ссылка";
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда 
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Документ закрытия месяца по заработной плате за выбранный период уже существует: %1.'"), 
			ВыборкаДетальныеЗаписи.Ссылка);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЕсли;
	
	ДанныеУчетнойПолитики = БухгалтерскийУчетСервер.ПолучитьДанныеУчетнойПолитикиПоПерсоналу(Дата, Организация);
	
	// Проверка заполнения данных учетной политики.
	Если НЕ ЗначениеЗаполнено(ДанныеУчетнойПолитики.СчетУчетаРасходовПоДоплатеПН) Тогда 
		ТекстСообщения = НСтр("ru = 'В учетной политике по персоналу не заполнен счет учета расходов по доплате ПН.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЕсли;	
	
	Если НЕ ЗначениеЗаполнено(ДанныеУчетнойПолитики.СчетУчетаРасходовПоКорректировкеСФ) Тогда 
		ТекстСообщения = НСтр("ru = 'В учетной политике по персоналу не заполнен счет учета расходов по корректировке СФ.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЕсли;	

	Если НЕ ЗначениеЗаполнено(ДанныеУчетнойПолитики.СчетУчетаРасчетовПФР) Тогда 
		ТекстСообщения = НСтр("ru = 'В учетной политике по персоналу не заполнен счет учета расчетов ПФР.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЕсли;	
	
	Если НЕ ЗначениеЗаполнено(ДанныеУчетнойПолитики.СчетУчетаРасчетовГНПФР) Тогда 
		ТекстСообщения = НСтр("ru = 'В учетной политике по персоналу не заполнен счет учета расчетов ГНПФР.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЕсли;	

	Если НЕ ЗначениеЗаполнено(ДанныеУчетнойПолитики.СчетУчетаРасчетовМСФ) Тогда 
		ТекстСообщения = НСтр("ru = 'В учетной политике по персоналу не заполнен счет учета расчетов МСФ.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЕсли;	

	Если НЕ ЗначениеЗаполнено(ДанныеУчетнойПолитики.СчетУчетаРасчетовПФФ) Тогда 
		ТекстСообщения = НСтр("ru = 'В учетной политике по персоналу не заполнен счет учета расчетов ПФФ.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЕсли;	

	Если НЕ ЗначениеЗаполнено(ДанныеУчетнойПолитики.СчетУчетаРасчетовФОТФ) Тогда 
		ТекстСообщения = НСтр("ru = 'В учетной политике по персоналу не заполнен счет учета расчетов ФОТФ.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЕсли;	

	Если ДанныеУчетнойПолитики.СтавкаПрофВзнос > 0 
		И НЕ ЗначениеЗаполнено(ДанныеУчетнойПолитики.СчетУчетаРасчетовПрофВзнос) Тогда 
		ТекстСообщения = НСтр("ru = 'В учетной политике по персоналу не заполнен счет учета расчетов ПрофВзнос.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЕсли;	

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Начисления.ФизЛицо КАК ФизЛицо,
		|	Начисления.ВидРасчета КАК ВидРасчета,
		|	Начисления.СпособОтражения КАК СпособОтражения,
		|	Начисления.Подразделение КАК Подразделение,
		|	ПРЕДСТАВЛЕНИЕ(Начисления.Регистратор) КАК ДокументСсылка
		|ПОМЕСТИТЬ ВременнаяТаблицаНачисления
		|ИЗ
		|	РегистрРасчета.Начисления КАК Начисления
		|ГДЕ
		|	Начисления.ПериодРегистрации МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Начисления.Организация = &Организация
		|	И НЕ Начисления.Результат = 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПРЕДСТАВЛЕНИЕ(ВременнаяТаблицаНачисления.ФизЛицо) КАК ФизЛицо,
		|	ПРЕДСТАВЛЕНИЕ(ВременнаяТаблицаНачисления.ВидРасчета) КАК ВидРасчета,
		|	ВременнаяТаблицаНачисления.СпособОтражения КАК СпособОтражения,
		|	ВременнаяТаблицаНачисления.ДокументСсылка КАК ДокументСсылка
		|ИЗ
		|	ВременнаяТаблицаНачисления КАК ВременнаяТаблицаНачисления
		|ГДЕ
		|	ВременнаяТаблицаНачисления.СпособОтражения = ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗаработнойПлаты.ПустаяСсылка)";
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("Организация", Организация);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	// Пустой счет или статья затрат. 
	Если НЕ МассивРезультатов[1].Пустой() Тогда
		ВыборкаДетальныеЗаписи = МассивРезультатов[1].Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
			Если НЕ ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.СпособОтражения) Тогда 	
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Не заполнен способ отражения Сотрудник: %1, Вид расчета: %2, Документ: %3.'"), 
					ВыборкаДетальныеЗаписи.ФизЛицо, ВыборкаДетальныеЗаписи.ВидРасчета, ВыборкаДетальныеЗаписи.ДокументСсылка);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Процедура выполняет расчет налогов и удержаний
//
Процедура ВыполнитьЗакрытиеМесяца(ЕстьОшибки) Экспорт 
	
	// 1. Расчет налогов по заработной плате
	// 2. Расчет удержаний с учетом рассчитанных налогов
	// 3. Заполнение соответствующих табличных частей- итого удержано	
	// 4. Распределение сумм налогов с организации по счетам учета
	// 5. Распределение сумм - корректировок налогов с организации по счетам учета
	
	РассчитатьНалогиПоЗарплате();	
	РассчитатьПлановыеУдержания();
	ДозаполнитьНалогиПоЗарплате();
	РаспределитьНалогиСОрганизации();
	РаспределитьНалогиСОрганизацииКорректировки();
	ДозаполнитьВыплату();
	
КонецПроцедуры // ЗаполнитьТабличнуюЧасть()

// Процедура - Рассчитать налоги по зарплате
//
Процедура РассчитатьНалогиПоЗарплате()
	
	ТекстыЗапроса = Новый Массив;

	// Список сотрудников, принятых на работу в текущем месяце
	// Этот список будет исключающим для сотрудников, которые работали на начло месяца
	// а затем были уволены и снова приняты.
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	СотрудникиСрезПервых.ФизЛицо КАК ФизЛицо,
		|	СотрудникиСрезПервых.Подразделение КАК Подразделение,
		|	СотрудникиСрезПервых.ГрафикРаботы КАК ГрафикРаботы,
		|	СотрудникиСрезПервых.ЗанимаемыхСтавок КАК ЗанимаемыхСтавок
		|ПОМЕСТИТЬ ВременнаяТаблицаСотрудникиПринятыеНаРаботу
		|ИЗ
		|	РегистрСведений.Сотрудники.СрезПервых(
		|			&НачалоПериода,
		|			Организация = &Организация
		|				И ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Прием)
		|				И Период <= &КонецПериода) КАК СотрудникиСрезПервых
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		// Сотрудники, принятые на работу по трудовым соглашениям.
		|ВЫБРАТЬ
		|	СотрудникиПоТрудовымСоглашениямСрезПервых.ФизЛицо,
		|	СотрудникиПоТрудовымСоглашениямСрезПервых.Подразделение,
		|	СотрудникиПоТрудовымСоглашениямСрезПервых.ГрафикРаботы,
		|	1
		|ИЗ
		|	РегистрСведений.СотрудникиПоТрудовымСоглашениям.СрезПервых(
		|			&НачалоПериода,
		|			Организация = &Организация
		|				И ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Прием)
		|				И Период <= &КонецПериода) КАК СотрудникиПоТрудовымСоглашениямСрезПервых
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо";	
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// Список сотрудников, работающих на начало месяца и принятых в текущем месяце.
	// Этот список сотрудников будет основным.
	// Для этих сотрудников будем рассчитывать налоги, даже если не было начислений (для накопления вычетов).
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаСотрудникиПринятыеНаРаботу.ФизЛицо КАК ФизЛицо,
		|	ВременнаяТаблицаСотрудникиПринятыеНаРаботу.ФизЛицо.Наименование КАК ФизЛицоНаименование,
		|	ВременнаяТаблицаСотрудникиПринятыеНаРаботу.Подразделение КАК Подразделение,
		|	ВременнаяТаблицаСотрудникиПринятыеНаРаботу.ГрафикРаботы КАК ГрафикРаботы,
		|	ВременнаяТаблицаСотрудникиПринятыеНаРаботу.ЗанимаемыхСтавок КАК ЗанимаемыхСтавок
		|ПОМЕСТИТЬ ВременнаяТаблицаСотрудники
		|ИЗ
		|	ВременнаяТаблицаСотрудникиПринятыеНаРаботу КАК ВременнаяТаблицаСотрудникиПринятыеНаРаботу
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СотрудникиСрезПоследних.ФизЛицо,
		|	СотрудникиСрезПоследних.ФизЛицо.Наименование,
		|	СотрудникиСрезПоследних.Подразделение,
		|	СотрудникиСрезПоследних.ГрафикРаботы,
		|	СотрудникиСрезПоследних.ЗанимаемыхСтавок
		|ИЗ
		|	РегистрСведений.Сотрудники.СрезПоследних(
		|			&НачалоПериода,
		|			Организация = &Организация
		|				И НЕ ФизЛицо В
		|						(ВЫБРАТЬ
		|							ВременнаяТаблицаСотрудникиПринятыеНаРаботу.ФизЛицо
		|						ИЗ
		|							ВременнаяТаблицаСотрудникиПринятыеНаРаботу КАК ВременнаяТаблицаСотрудникиПринятыеНаРаботу)) КАК СотрудникиСрезПоследних
		|ГДЕ
		// Исключаем уволенных в пред.периодах
		|	ВЫБОР
		|			КОГДА СотрудникиСрезПоследних.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
		|					И СотрудникиСрезПоследних.Период < &НачалоПериода
		|				ТОГДА ЛОЖЬ
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|              
		// Сотрудники, принятые на работу по трудовым соглашениям.
		|ВЫБРАТЬ
		|	СотрудникиПоТрудовымСоглашениямСрезПоследних.ФизЛицо,
		|	СотрудникиПоТрудовымСоглашениямСрезПоследних.ФизЛицо.Наименование,
		|	СотрудникиПоТрудовымСоглашениямСрезПоследних.Подразделение,
		|	СотрудникиПоТрудовымСоглашениямСрезПоследних.ГрафикРаботы,
		|	1
		|ИЗ
		|	РегистрСведений.СотрудникиПоТрудовымСоглашениям.СрезПоследних(
		|			&НачалоПериода,
		|			Организация = &Организация
		|				И НЕ ФизЛицо В
		|						(ВЫБРАТЬ
		|							ВременнаяТаблицаСотрудникиПринятыеНаРаботу.ФизЛицо
		|						ИЗ
		|							ВременнаяТаблицаСотрудникиПринятыеНаРаботу КАК ВременнаяТаблицаСотрудникиПринятыеНаРаботу)) КАК СотрудникиПоТрудовымСоглашениямСрезПоследних
		|ГДЕ
		// Исключаем уволенных в пред.периодах
		|	ВЫБОР
		|			КОГДА СотрудникиПоТрудовымСоглашениямСрезПоследних.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
		|					И СотрудникиПоТрудовымСоглашениямСрезПоследних.Период < &НачалоПериода
		|				ТОГДА ЛОЖЬ
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо,
		|	Подразделение,
		|	ГрафикРаботы";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// Определение больше 15 дней (в месяце приема и увольнения).
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	СотрудникиСрезПоследних.ФизЛицо КАК ФизЛицо,
		|	ДЕНЬ(СотрудникиСрезПоследних.Период) КАК ДеньПриема,
		|	0 КАК ДеньУвольнения
		|ПОМЕСТИТЬ ВременнаяТаблицаСотрудникиПриемУвольнение
		|ИЗ
		|	РегистрСведений.Сотрудники.СрезПоследних(
		|			&КонецПериода,
		|			Период МЕЖДУ &НачалоПериода И &КонецПериода
		|				И Организация = &Организация
		|				И ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Прием)) КАК СотрудникиСрезПоследних
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СотрудникиСрезПоследних.ФизЛицо,
		|	0,
		|	ДЕНЬ(СотрудникиСрезПоследних.Период)
		|ИЗ
		|	РегистрСведений.Сотрудники.СрезПоследних(
		|			&КонецПериода,
		|			Период МЕЖДУ &НачалоПериода И &КонецПериода
		|				И Организация = &Организация
		|				И ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)) КАК СотрудникиСрезПоследних
		// Сотрудники, принятые на работу по трудовым соглашениям.
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СотрудникиПоТрудовымСоглашениямСрезПоследних.ФизЛицо КАК ФизЛицо,
		|	ДЕНЬ(СотрудникиПоТрудовымСоглашениямСрезПоследних.Период) КАК ДеньПриема,
		|	0 КАК ДеньУвольнения
		|ИЗ
		|	РегистрСведений.СотрудникиПоТрудовымСоглашениям.СрезПоследних(
		|			&КонецПериода,
		|			Период МЕЖДУ &НачалоПериода И &КонецПериода
		|				И Организация = &Организация
		|				И ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Прием)) КАК СотрудникиПоТрудовымСоглашениямСрезПоследних
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СотрудникиПоТрудовымСоглашениямСрезПоследних.ФизЛицо,
		|	0,
		|	ДЕНЬ(СотрудникиПоТрудовымСоглашениямСрезПоследних.Период)
		|ИЗ
		|	РегистрСведений.СотрудникиПоТрудовымСоглашениям.СрезПоследних(
		|			&КонецПериода,
		|			Период МЕЖДУ &НачалоПериода И &КонецПериода
		|				И Организация = &Организация
		|				И ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)) КАК СотрудникиПоТрудовымСоглашениямСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаСотрудникиПриемУвольнение.ФизЛицо КАК ФизЛицо,
		|	СУММА(ВременнаяТаблицаСотрудникиПриемУвольнение.ДеньПриема) КАК ДеньПриема,
		|	СУММА(ВременнаяТаблицаСотрудникиПриемУвольнение.ДеньУвольнения) КАК ДеньУвольнения,
		|	ВЫБОР
		|		КОГДА ВЫБОР
		|				КОГДА СУММА(ВременнаяТаблицаСотрудникиПриемУвольнение.ДеньУвольнения) = 0
		|					ТОГДА ДЕНЬ(&КонецПериода)
		|				ИНАЧЕ СУММА(ВременнаяТаблицаСотрудникиПриемУвольнение.ДеньУвольнения)
		|			КОНЕЦ - СУММА(ВременнаяТаблицаСотрудникиПриемУвольнение.ДеньПриема) >= 15
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Больше15дней
		|ПОМЕСТИТЬ ВременнаяТаблицаСотрудникиОтработаноДней
		|ИЗ
		|	ВременнаяТаблицаСотрудникиПриемУвольнение КАК ВременнаяТаблицаСотрудникиПриемУвольнение
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаСотрудникиПриемУвольнение.ФизЛицо
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВременнаяТаблицаСотрудникиПриемУвольнение";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// Получение начислений за месяц, определение расчетных баз.
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	1 КАК Порядок,
		|	Начисления.ФизЛицо КАК ФизЛицо,
		|	СУММА(ВЫБОР
		|			КОГДА Начисления.ВидРасчета.ЗачетОтработанногоВремени
		|				ТОГДА Начисления.ОтработаноДней
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОтработаноДней,
		|	СУММА(ВЫБОР
		|			КОГДА Начисления.ВидРасчета.ДополнительныйДоход
		|				ТОГДА 0
		|			ИНАЧЕ Начисления.Результат
		|		КОНЕЦ) КАК СуммаНачислено,
		|	СУММА(ВЫБОР
		|			КОГДА Начисления.ВидРасчета.ДополнительныйДоход
		|				ТОГДА Начисления.Результат
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СуммаДопДоход,
		|	СУММА(ВЫБОР
		|			КОГДА Начисления.ВидРасчета.ОблагаетсяСФ
		|				ТОГДА Начисления.Результат
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОДСФ,
		|	СУММА(ВЫБОР
		|			КОГДА Начисления.ВидРасчета.ОблагаетсяПН
		|				ТОГДА Начисления.Результат
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОДПН,
		|	СУММА(ВЫБОР
		|			КОГДА Начисления.ВидРасчета.ОблагаетсяСФ
		|					И Начисления.ВидРасчета.ОблагаетсяПН
		|				ТОГДА Начисления.Результат
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОДСФПН,
		|	СУММА(ВЫБОР
		|			КОГДА Начисления.ВидРасчета.ОблагаетсяПрофВзнос
		|				ТОГДА Начисления.Результат
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОДПрофВзнос
		|ПОМЕСТИТЬ ВременнаяТаблицаНачисленияЗаМесяц
		|ИЗ
		|	РегистрРасчета.Начисления КАК Начисления
		|ГДЕ
		|	Начисления.ПериодРегистрации МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Начисления.Организация = &Организация
		|	И НЕ Начисления.Результат = 0
		|
		|СГРУППИРОВАТЬ ПО
		|	Начисления.ФизЛицо
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// Статусы сотрудников и ставки налогов по этим статусам.
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	3 КАК Порядок,
		|	СтатусыСотрудниковСрезПоследних.ФизЛицо КАК ФизЛицо,
		|	СтатусыСотрудниковСрезПоследних.Статус КАК Статус,
		|	ЕСТЬNULL(ВременнаяТаблицаСотрудникиОтработаноДней.Больше15дней, ИСТИНА) КАК ПрименятьВычет,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ВременнаяТаблицаСотрудникиОтработаноДней.Больше15дней, ИСТИНА)
		|			ТОГДА ЕСТЬNULL(СтавкиНалоговЗаработнойПлатыСрезПоследних.Вычеты, 0) + ЕСТЬNULL(СтатусыСотрудниковСрезПоследних.ДопВычет, 0)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Вычеты,
		|	СтавкиНалоговЗаработнойПлатыСрезПоследних.СтавкаПФР КАК СтавкаПФР,
		|	СтавкиНалоговЗаработнойПлатыСрезПоследних.СтавкаГНПФР КАК СтавкаГНПФР,
		|	СтавкиНалоговЗаработнойПлатыСрезПоследних.СтавкаПН КАК СтавкаПН,
		|	СтавкиНалоговЗаработнойПлатыСрезПоследних.СтавкаПФФ КАК СтавкаПФФ,
		|	СтавкиНалоговЗаработнойПлатыСрезПоследних.СтавкаМСФ КАК СтавкаМСФ,
		|	СтавкиНалоговЗаработнойПлатыСрезПоследних.СтавкаФОТФ КАК СтавкаФОТФ,
		|	СтавкиНалоговЗаработнойПлатыСрезПоследних.НеВычитатьПФ КАК НеВычитатьПФ,
		|	СтавкиНалоговЗаработнойПлатыСрезПоследних.СтавкаМСР КАК СтавкаМСР,
		|	СтатусыСотрудниковСрезПоследних.Статус.МетодРасчетаПН КАК МетодРасчетаПН,
		|	СтатусыСотрудниковСрезПоследних.Статус.МетодРасчетаСФ КАК МетодРасчетаСФ
		|ПОМЕСТИТЬ ВременнаяТаблицаСтатусы
		|ИЗ
		|	РегистрСведений.СтатусыСотрудников.СрезПоследних(
		|			&КонецПериода,
		|			Организация = &Организация
		|				И ФизЛицо В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						ВременнаяТаблицаСотрудники.ФизЛицо
		|					ИЗ
		|						ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники)) КАК СтатусыСотрудниковСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНалоговЗаработнойПлаты.СрезПоследних(&КонецПериода, ) КАК СтавкиНалоговЗаработнойПлатыСрезПоследних
		|		ПО СтатусыСотрудниковСрезПоследних.Статус = СтавкиНалоговЗаработнойПлатыСрезПоследних.Статус
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСотрудникиОтработаноДней КАК ВременнаяТаблицаСотрудникиОтработаноДней
		|		ПО СтатусыСотрудниковСрезПоследних.ФизЛицо = ВременнаяТаблицаСотрудникиОтработаноДней.ФизЛицо
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// Пределы.
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	4 КАК Порядок,
		|	ВременнаяТаблицаСотрудники.ФизЛицо КАК ФизЛицо,
		|	ПределыПоПодразделениямСрезПоследних.МинимальныйРасчетныйДоход КАК МРД,
		|	ПределыПоПодразделениямСрезПоследних.СреднемесячнаяЗПСФ КАК СреднемесячнаяЗПСФ
		|ПОМЕСТИТЬ ВременнаяТаблицаПределы
		|ИЗ
		|	РегистрСведений.ПределыПоПодразделениям.СрезПоследних КАК ПределыПоПодразделениямСрезПоследних
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники
		|		ПО (ВременнаяТаблицаСотрудники.Подразделение = ПределыПоПодразделениямСрезПоследних.Подразделение)
		|ГДЕ
		|	&ПределыПоПодразделениям
		|	И ПределыПоПодразделениямСрезПоследних.Организация = &Организация
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	4,
		|	ВременнаяТаблицаСотрудники.ФизЛицо,
		|	&МРД,
		|	&СреднемесячнаяЗПСФ
		|ИЗ
		|	ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники
		|ГДЕ
		|	НЕ &ПределыПоПодразделениям
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// Графики сотрудников и нормы дней по этим графикам.
	// Расчет СЗП приведенного (для Арендодателя полная СЗП, для других 40% от СЗП)
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаСотрудники.ФизЛицо КАК ФизЛицо,
		|	ГрафикиРаботы.ГрафикРаботы КАК ГрафикРаботы,
		|	СУММА(ГрафикиРаботы.ЗначениеДней) КАК НормаДней
		|ПОМЕСТИТЬ ВременнаяТаблицаНормыДней
		|ИЗ
		|	ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КалендариГрафиковРабот КАК ГрафикиРаботы
		|		ПО ВременнаяТаблицаСотрудники.ГрафикРаботы = ГрафикиРаботы.ГрафикРаботы
		|ГДЕ
		|	ГрафикиРаботы.ГрафикРаботы В
		|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				ВременнаяТаблицаСотрудники.ГрафикРаботы
		|			ИЗ
		|				ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники)
		|	И ГрафикиРаботы.Год = ГОД(&КонецПериода)
		|	И ГрафикиРаботы.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаСотрудники.ФизЛицо,
		|	ГрафикиРаботы.ГрафикРаботы
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	5 КАК Порядок,
		|	ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо КАК ФизЛицо,
		|	ВременнаяТаблицаНачисленияЗаМесяц.ОтработаноДней КАК ОтработаноДней,
		|	ВременнаяТаблицаНачисленияЗаМесяц.ОДСФ КАК ОДСФ,
		|	ВременнаяТаблицаНормыДней.НормаДней КАК НормаДней,
		|	ЕСТЬNULL(ВременнаяТаблицаПределы.СреднемесячнаяЗПСФ, 0) КАК СреднемесячнаяЗПСФ,
		//|	ВЫБОР
		//|		КОГДА ВременнаяТаблицаСтатусы.Статус.Категория = ЗНАЧЕНИЕ(справочник.КатегорииСотрудников.Арендодатели)
		//|			ТОГДА ЕСТЬNULL(ВременнаяТаблицаПределы.СреднемесячнаяЗПСФ, 0)
		//|		ИНАЧЕ ЕСТЬNULL(ВременнаяТаблицаПределы.СреднемесячнаяЗПСФ, 0) / 100 * &ПроцентОтСЗПСФ
		//|	КОНЕЦ КАК СреднемесячнаяЗПСФ,		
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ВременнаяТаблицаНормыДней.НормаДней, 0) = 0
		|			ТОГДА ЕСТЬNULL(ВременнаяТаблицаПределы.СреднемесячнаяЗПСФ, 0) / 100 * &ПроцентОтСЗПСФ
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ВременнаяТаблицаНачисленияЗаМесяц.ОтработаноДней > ЕСТЬNULL(ВременнаяТаблицаНормыДней.НормаДней, 0)
		|					ТОГДА ЕСТЬNULL(ВременнаяТаблицаПределы.СреднемесячнаяЗПСФ, 0) / 100 * &ПроцентОтСЗПСФ
		|				ИНАЧЕ ВЫРАЗИТЬ(ЕСТЬNULL(ВременнаяТаблицаПределы.СреднемесячнаяЗПСФ, 0) / 100 * &ПроцентОтСЗПСФ * ВременнаяТаблицаНачисленияЗаМесяц.ОтработаноДней / ЕСТЬNULL(ВременнаяТаблицаНормыДней.НормаДней, 0) КАК ЧИСЛО(15, 2))
		|			КОНЕЦ
		|	КОНЕЦ * ВременнаяТаблицаСотрудники.ЗанимаемыхСтавок КАК СреднемесячнаяЗПСФПриведенная
		|ПОМЕСТИТЬ ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная
		|ИЗ
		|	ВременнаяТаблицаНачисленияЗаМесяц КАК ВременнаяТаблицаНачисленияЗаМесяц
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаСтатусы КАК ВременнаяТаблицаСтатусы
		|		ПО ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ВременнаяТаблицаСтатусы.ФизЛицо
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники
		|		ПО ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ВременнаяТаблицаСотрудники.ФизЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНормыДней КАК ВременнаяТаблицаНормыДней
		|		ПО ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ВременнаяТаблицаНормыДней.ФизЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаПределы КАК ВременнаяТаблицаПределы
		|		ПО ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ВременнаяТаблицаПределы.ФизЛицо
		|ГДЕ
		|	(ВременнаяТаблицаСтатусы.МетодРасчетаСФ = ЗНАЧЕНИЕ(Перечисление.МетодыРасчетаСФ.СЗП)
		|			ИЛИ ВременнаяТаблицаСтатусы.МетодРасчетаСФ = ЗНАЧЕНИЕ(Перечисление.МетодыРасчетаСФ.НачисленоБольшеСЗП)
		|			ИЛИ ВременнаяТаблицаСтатусы.МетодРасчетаСФ = ЗНАЧЕНИЕ(Перечисление.МетодыРасчетаСФ.ПроцентОтСЗП))
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
		
	// Расчет налогов СФ.
	// 1 пакет - расчет от начисленной суммы.
	// 2 пакет - расчет от СЗП без 40% (для арендодателя).
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	6 КАК Порядок,
		|	ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо КАК ФизЛицо,
		|	ВременнаяТаблицаНачисленияЗаМесяц.ОДСФ КАК ОДСФ,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ВременнаяТаблицаСтатусы.СтавкаПФР = 0
		|				ТОГДА 0
		|			ИНАЧЕ ВременнаяТаблицаНачисленияЗаМесяц.ОДСФ * ВременнаяТаблицаСтатусы.СтавкаПФР / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК ПФР,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ВременнаяТаблицаСтатусы.СтавкаГНПФР = 0
		|				ТОГДА 0
		|			ИНАЧЕ ВременнаяТаблицаНачисленияЗаМесяц.ОДСФ * ВременнаяТаблицаСтатусы.СтавкаГНПФР / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК ГНПФР,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ВременнаяТаблицаСтатусы.СтавкаПФР = 0
		|					ИЛИ ВременнаяТаблицаСтатусы.НеВычитатьПФ
		|					ИЛИ ВременнаяТаблицаСтатусы.СтавкаПН = 0
		|				ТОГДА 0
		|			ИНАЧЕ ВременнаяТаблицаНачисленияЗаМесяц.ОДСФПН * ВременнаяТаблицаСтатусы.СтавкаПФР / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК ПФРДляВычетаПН,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ВременнаяТаблицаСтатусы.СтавкаГНПФР = 0
		|					ИЛИ ВременнаяТаблицаСтатусы.НеВычитатьПФ
		|					ИЛИ ВременнаяТаблицаСтатусы.СтавкаПН = 0
		|				ТОГДА 0
		|			ИНАЧЕ ВременнаяТаблицаНачисленияЗаМесяц.ОДСФПН * ВременнаяТаблицаСтатусы.СтавкаГНПФР / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК ГНПФРДляВычетаПН,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ВременнаяТаблицаСтатусы.СтавкаМСР = 0
		|				ТОГДА 0
		|			ИНАЧЕ ВременнаяТаблицаНачисленияЗаМесяц.ОДСФ * ВременнаяТаблицаСтатусы.СтавкаМСР / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК МСР,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ВременнаяТаблицаСтатусы.СтавкаПФФ = 0
		|				ТОГДА 0
		|			ИНАЧЕ ВременнаяТаблицаНачисленияЗаМесяц.ОДСФ * ВременнаяТаблицаСтатусы.СтавкаПФФ / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК ПФФ,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ВременнаяТаблицаСтатусы.СтавкаМСФ = 0
		|				ТОГДА 0
		|			ИНАЧЕ ВременнаяТаблицаНачисленияЗаМесяц.ОДСФ * ВременнаяТаблицаСтатусы.СтавкаМСФ / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК МСФ,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ВременнаяТаблицаСтатусы.СтавкаФОТФ = 0
		|				ТОГДА 0
		|			ИНАЧЕ ВременнаяТаблицаНачисленияЗаМесяц.ОДСФ * ВременнаяТаблицаСтатусы.СтавкаФОТФ / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК ФОТФ
		|ПОМЕСТИТЬ ВременнаяТаблицаНалогиСФ
		|ИЗ
		|	ВременнаяТаблицаНачисленияЗаМесяц КАК ВременнаяТаблицаНачисленияЗаМесяц
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаСтатусы КАК ВременнаяТаблицаСтатусы
		|		ПО ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ВременнаяТаблицаСтатусы.ФизЛицо
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники
		|		ПО ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ВременнаяТаблицаСотрудники.ФизЛицо
		|ГДЕ
		|	ВременнаяТаблицаНачисленияЗаМесяц.ОДСФ > 0
		|	И (ВременнаяТаблицаСтатусы.МетодРасчетаСФ = ЗНАЧЕНИЕ(Перечисление.МетодыРасчетаСФ.Начислено)
		|			ИЛИ ВременнаяТаблицаСтатусы.МетодРасчетаСФ = ЗНАЧЕНИЕ(Перечисление.МетодыРасчетаСФ.НачисленоБольшеСЗП))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	6,
		|	ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.ФизЛицо,
		|	ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.СреднемесячнаяЗПСФ * ВременнаяТаблицаСотрудники.ЗанимаемыхСтавок,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ВременнаяТаблицаСтатусы.СтавкаПФР = 0
		|				ТОГДА 0
		|			ИНАЧЕ ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.СреднемесячнаяЗПСФ * ВременнаяТаблицаСотрудники.ЗанимаемыхСтавок * ВременнаяТаблицаСтатусы.СтавкаПФР / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)),
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ВременнаяТаблицаСтатусы.СтавкаГНПФР = 0
		|				ТОГДА 0
		|			ИНАЧЕ ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.СреднемесячнаяЗПСФ * ВременнаяТаблицаСотрудники.ЗанимаемыхСтавок * ВременнаяТаблицаСтатусы.СтавкаГНПФР / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)),
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ВременнаяТаблицаСтатусы.СтавкаПФР = 0
		|					ИЛИ ВременнаяТаблицаСтатусы.НеВычитатьПФ
		|				ТОГДА 0
		|			ИНАЧЕ ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.СреднемесячнаяЗПСФ * ВременнаяТаблицаСотрудники.ЗанимаемыхСтавок * ВременнаяТаблицаСтатусы.СтавкаПФР / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)),
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ВременнаяТаблицаСтатусы.СтавкаГНПФР = 0
		|					ИЛИ ВременнаяТаблицаСтатусы.НеВычитатьПФ
		|				ТОГДА 0
		|			ИНАЧЕ ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.СреднемесячнаяЗПСФ * ВременнаяТаблицаСотрудники.ЗанимаемыхСтавок * ВременнаяТаблицаСтатусы.СтавкаГНПФР / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)),
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ВременнаяТаблицаСтатусы.СтавкаМСР = 0
		|				ТОГДА 0
		|			ИНАЧЕ ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.СреднемесячнаяЗПСФ * ВременнаяТаблицаСотрудники.ЗанимаемыхСтавок * ВременнаяТаблицаСтатусы.СтавкаМСР / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)),
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ВременнаяТаблицаСтатусы.СтавкаПФФ = 0
		|				ТОГДА 0
		|			ИНАЧЕ ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.СреднемесячнаяЗПСФ * ВременнаяТаблицаСотрудники.ЗанимаемыхСтавок * ВременнаяТаблицаСтатусы.СтавкаПФФ / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)),
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ВременнаяТаблицаСтатусы.СтавкаМСФ = 0
		|				ТОГДА 0
		|			ИНАЧЕ ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.СреднемесячнаяЗПСФ * ВременнаяТаблицаСотрудники.ЗанимаемыхСтавок * ВременнаяТаблицаСтатусы.СтавкаМСФ / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)),
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ВременнаяТаблицаСтатусы.СтавкаФОТФ = 0
		|				ТОГДА 0
		|			ИНАЧЕ ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.СреднемесячнаяЗПСФ * ВременнаяТаблицаСотрудники.ЗанимаемыхСтавок * ВременнаяТаблицаСтатусы.СтавкаФОТФ / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2))
		|ИЗ
		|	ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная КАК ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаСтатусы КАК ВременнаяТаблицаСтатусы
		|		ПО ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.ФизЛицо = ВременнаяТаблицаСтатусы.ФизЛицо
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники
		|		ПО ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.ФизЛицо = ВременнаяТаблицаСотрудники.ФизЛицо
		|ГДЕ
		|	ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.ОДСФ <> 0
		|	И ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.СреднемесячнаяЗПСФ * ВременнаяТаблицаСотрудники.ЗанимаемыхСтавок > 0
		|	И ВременнаяТаблицаСтатусы.МетодРасчетаСФ = ЗНАЧЕНИЕ(Перечисление.МетодыРасчетаСФ.СЗП)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	6,
		|	ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.ФизЛицо,
		|	ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.СреднемесячнаяЗПСФПриведенная,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ВременнаяТаблицаСтатусы.СтавкаПФР = 0
		|				ТОГДА 0
		|			ИНАЧЕ ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.СреднемесячнаяЗПСФПриведенная * ВременнаяТаблицаСтатусы.СтавкаПФР / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)),
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ВременнаяТаблицаСтатусы.СтавкаГНПФР = 0
		|				ТОГДА 0
		|			ИНАЧЕ ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.СреднемесячнаяЗПСФПриведенная * ВременнаяТаблицаСтатусы.СтавкаГНПФР / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)),
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ВременнаяТаблицаСтатусы.СтавкаПФР = 0
		|					ИЛИ ВременнаяТаблицаСтатусы.НеВычитатьПФ
		|				ТОГДА 0
		|			ИНАЧЕ ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.СреднемесячнаяЗПСФПриведенная * ВременнаяТаблицаСтатусы.СтавкаПФР / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)),
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ВременнаяТаблицаСтатусы.СтавкаГНПФР = 0
		|					ИЛИ ВременнаяТаблицаСтатусы.НеВычитатьПФ
		|				ТОГДА 0
		|			ИНАЧЕ ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.СреднемесячнаяЗПСФПриведенная * ВременнаяТаблицаСтатусы.СтавкаГНПФР / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)),
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ВременнаяТаблицаСтатусы.СтавкаМСР = 0
		|				ТОГДА 0
		|			ИНАЧЕ ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.СреднемесячнаяЗПСФПриведенная * ВременнаяТаблицаСтатусы.СтавкаМСР / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)),
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ВременнаяТаблицаСтатусы.СтавкаПФФ = 0
		|				ТОГДА 0
		|			ИНАЧЕ ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.СреднемесячнаяЗПСФПриведенная * ВременнаяТаблицаСтатусы.СтавкаПФФ / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)),
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ВременнаяТаблицаСтатусы.СтавкаМСФ = 0
		|				ТОГДА 0
		|			ИНАЧЕ ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.СреднемесячнаяЗПСФПриведенная * ВременнаяТаблицаСтатусы.СтавкаМСФ / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)),
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ВременнаяТаблицаСтатусы.СтавкаФОТФ = 0
		|				ТОГДА 0
		|			ИНАЧЕ ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.СреднемесячнаяЗПСФПриведенная * ВременнаяТаблицаСтатусы.СтавкаФОТФ / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2))
		|ИЗ
		|	ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная КАК ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаСтатусы КАК ВременнаяТаблицаСтатусы
		|		ПО ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.ФизЛицо = ВременнаяТаблицаСтатусы.ФизЛицо
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники
		|		ПО ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.ФизЛицо = ВременнаяТаблицаСотрудники.ФизЛицо
		|ГДЕ
		|	ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.ОДСФ <> 0
		|	И ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.СреднемесячнаяЗПСФПриведенная > 0
		|	И ВременнаяТаблицаСтатусы.МетодРасчетаСФ = ЗНАЧЕНИЕ(Перечисление.МетодыРасчетаСФ.ПроцентОтСЗП)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
		
	// Расчет корректировок налогов СФ.
	// Только для метода Начислено больше СЗП.
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо КАК ФизЛицо,
		|	ВременнаяТаблицаНачисленияЗаМесяц.ОДСФ КАК ОДСФ,
		|	ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.СреднемесячнаяЗПСФПриведенная КАК СреднемесячнаяЗПСФПриведенная,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ЕСТЬNULL(ВременнаяТаблицаСтатусы.СтавкаПФФ, 0) = 0
		|				ТОГДА 0
		|			ИНАЧЕ ЕСТЬNULL(ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.СреднемесячнаяЗПСФПриведенная, 0) * ВременнаяТаблицаСтатусы.СтавкаПФФ / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК ПФФотСЗП,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ЕСТЬNULL(ВременнаяТаблицаСтатусы.СтавкаМСФ, 0) = 0
		|				ТОГДА 0
		|			ИНАЧЕ ЕСТЬNULL(ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.СреднемесячнаяЗПСФПриведенная, 0) * ВременнаяТаблицаСтатусы.СтавкаМСФ / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК МСФотСЗП,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ЕСТЬNULL(ВременнаяТаблицаСтатусы.СтавкаФОТФ, 0) = 0
		|				ТОГДА 0
		|			ИНАЧЕ ЕСТЬNULL(ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.СреднемесячнаяЗПСФПриведенная, 0) * ВременнаяТаблицаСтатусы.СтавкаФОТФ / 100
		|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК ФОТФотСЗП
		|ПОМЕСТИТЬ ВременнаяТаблицаНалогиСФКорректировкиПредварит
		|ИЗ
		|	ВременнаяТаблицаНачисленияЗаМесяц КАК ВременнаяТаблицаНачисленияЗаМесяц
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСтатусы КАК ВременнаяТаблицаСтатусы
		|		ПО ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ВременнаяТаблицаСтатусы.ФизЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная КАК ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная
		|		ПО ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ВременнаяТаблицаСреднемесячнаяЗПСФПриведенная.ФизЛицо
		|ГДЕ
		|	ВременнаяТаблицаНачисленияЗаМесяц.ОДСФ > 0
		|	И ВременнаяТаблицаСтатусы.МетодРасчетаСФ = ЗНАЧЕНИЕ(Перечисление.МетодыРасчетаСФ.НачисленоБольшеСЗП)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	7 КАК Порядок,
		|	ВременнаяТаблицаНалогиСФ.ФизЛицо КАК ФизЛицо,
		|	ВременнаяТаблицаНалогиСФКорректировкиПредварит.СреднемесячнаяЗПСФПриведенная КАК СреднемесячнаяЗПСФПриведенная,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаНалогиСФКорректировкиПредварит.ПФФотСЗП - ВременнаяТаблицаНалогиСФ.ПФФ > 0
		|			ТОГДА ВременнаяТаблицаНалогиСФКорректировкиПредварит.ПФФотСЗП - ВременнаяТаблицаНалогиСФ.ПФФ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ПФФКорректировка,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаНалогиСФКорректировкиПредварит.МСФотСЗП - ВременнаяТаблицаНалогиСФ.МСФ > 0
		|			ТОГДА ВременнаяТаблицаНалогиСФКорректировкиПредварит.МСФотСЗП - ВременнаяТаблицаНалогиСФ.МСФ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК МСФКорректировка,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаНалогиСФКорректировкиПредварит.ФОТФотСЗП - ВременнаяТаблицаНалогиСФ.ФОТФ > 0
		|			ТОГДА ВременнаяТаблицаНалогиСФКорректировкиПредварит.ФОТФотСЗП - ВременнаяТаблицаНалогиСФ.ФОТФ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ФОТФКорректировка
		|ПОМЕСТИТЬ ВременнаяТаблицаНалогиСФКорректировки
		|ИЗ
		|	ВременнаяТаблицаНалогиСФ КАК ВременнаяТаблицаНалогиСФ
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиСФКорректировкиПредварит КАК ВременнаяТаблицаНалогиСФКорректировкиПредварит
		|		ПО ВременнаяТаблицаНалогиСФ.ФизЛицо = ВременнаяТаблицаНалогиСФКорректировкиПредварит.ФизЛицо
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// Расчет налогов ПН.
	ДанныеУчетнойПолитикиПоПерсоналу = БухгалтерскийУчетСервер.ПолучитьДанныеУчетнойПолитикиПоПерсоналу(Дата, Организация);
	                        
	// ПН НЕ нарастающим итогом.
	Если ДанныеУчетнойПолитикиПоПерсоналу.РасчетПНБезНакопления Тогда
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	8 КАК Порядок,
			|	ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо КАК ФизЛицо,
			|	ВЫРАЗИТЬ(ВЫБОР
			|			КОГДА ЕСТЬNULL(ВременнаяТаблицаСтатусы.СтавкаПН, 0) = 0
			|				ТОГДА 0
			|			ИНАЧЕ (ВременнаяТаблицаНачисленияЗаМесяц.ОДПН - ЕСТЬNULL(ВременнаяТаблицаНалогиСФ.ПФРДляВычетаПН, 0) - ЕСТЬNULL(ВременнаяТаблицаНалогиСФ.ГНПФРДляВычетаПН, 0) - ВременнаяТаблицаСтатусы.Вычеты * 100) * ВременнаяТаблицаСтатусы.СтавкаПН / 100
			|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК ПН,
			|	ВЫРАЗИТЬ(ВЫБОР
			|			КОГДА ВременнаяТаблицаНачисленияЗаМесяц.ОтработаноДней = 0
			|						И ВременнаяТаблицаНачисленияЗаМесяц.ОДПН > 0
			|					ИЛИ ЕСТЬNULL(ВременнаяТаблицаНормыДней.НормаДней, 0) = 0
			|					ИЛИ ВременнаяТаблицаНачисленияЗаМесяц.ОтработаноДней > ЕСТЬNULL(ВременнаяТаблицаНормыДней.НормаДней, 0)
			|				ТОГДА ЕСТЬNULL(ВременнаяТаблицаПределы.МРД, 0) * ВременнаяТаблицаСтатусы.СтавкаПН / 100
			|			ИНАЧЕ ЕСТЬNULL(ВременнаяТаблицаПределы.МРД, 0) * ВременнаяТаблицаНачисленияЗаМесяц.ОтработаноДней / ЕСТЬNULL(ВременнаяТаблицаНормыДней.НормаДней, 0) * ВременнаяТаблицаСтатусы.СтавкаПН / 100
			|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК ПНотМРД,
			|	ВременнаяТаблицаСтатусы.Вычеты КАК Вычеты,
			|	ЕСТЬNULL(ВременнаяТаблицаПределы.МРД, 0) КАК МРД,
			|	&ДоплатаПНЗаСчетФирмы КАК ДоплатаПНЗаСчетФирмы
			|ПОМЕСТИТЬ ВременнаяТаблицаНалогиПН
			|ИЗ
			|	ВременнаяТаблицаНачисленияЗаМесяц КАК ВременнаяТаблицаНачисленияЗаМесяц
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСтатусы КАК ВременнаяТаблицаСтатусы
			|		ПО ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ВременнаяТаблицаСтатусы.ФизЛицо
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиСФ КАК ВременнаяТаблицаНалогиСФ
			|		ПО ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ВременнаяТаблицаНалогиСФ.ФизЛицо
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНормыДней КАК ВременнаяТаблицаНормыДней
			|		ПО ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ВременнаяТаблицаНормыДней.ФизЛицо
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаПределы КАК ВременнаяТаблицаПределы
			|		ПО ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ВременнаяТаблицаПределы.ФизЛицо
			|ГДЕ
			|	ВременнаяТаблицаНачисленияЗаМесяц.ОДПН > 0
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	ФизЛицо";
		ТекстыЗапроса.Добавить(ТекстЗапроса);
		
	// ПН нарастающим итогом.
	Иначе	
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	8 КАК Порядок,
			|	ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо КАК ФизЛицо,
			|	ВЫРАЗИТЬ(ВЫБОР
			|			КОГДА ЕСТЬNULL(ВременнаяТаблицаСтатусы.СтавкаПН, 0) = 0
			|				ТОГДА 0
			|			КОГДА &НачалоПериода = &НачалоГода
			|				ТОГДА (ВременнаяТаблицаНачисленияЗаМесяц.ОДПН - ЕСТЬNULL(ВременнаяТаблицаНалогиСФ.ПФРДляВычетаПН, 0) - ЕСТЬNULL(ВременнаяТаблицаНалогиСФ.ГНПФРДляВычетаПН, 0) - ВременнаяТаблицаСтатусы.Вычеты * 100) * ВременнаяТаблицаСтатусы.СтавкаПН / 100
			|			ИНАЧЕ (ВременнаяТаблицаНачисленияЗаМесяц.ОДПН + ЕСТЬNULL(ПодоходныйНалогОбороты.ОДПНОборот, 0) - ЕСТЬNULL(ВременнаяТаблицаНалогиСФ.ПФРДляВычетаПН, 0) - ЕСТЬNULL(ПодоходныйНалогОбороты.ПФРДляРасчетаПНОборот, 0) - ЕСТЬNULL(ВременнаяТаблицаНалогиСФ.ГНПФРДляВычетаПН, 0) - ЕСТЬNULL(ПодоходныйНалогОбороты.ГНПФРДляРасчетаПНОборот, 0) - (ВременнаяТаблицаСтатусы.Вычеты * 100 + ЕСТЬNULL(ПодоходныйНалогОбороты.ВычетыОборот, 0) * 100)) * ВременнаяТаблицаСтатусы.СтавкаПН / 100 - (ЕСТЬNULL(ПодоходныйНалогОбороты.ПНОборот, 0) - ЕСТЬNULL(ПодоходныйНалогОбороты.ДоплатаПНОборот, 0))
			|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК ПН,
			|	ВЫРАЗИТЬ(ВЫБОР
			|			КОГДА ВременнаяТаблицаНачисленияЗаМесяц.ОтработаноДней = 0
			|				ТОГДА 0
			|			КОГДА ЕСТЬNULL(ВременнаяТаблицаНормыДней.НормаДней, 0) = 0
			|					ИЛИ ВременнаяТаблицаНачисленияЗаМесяц.ОтработаноДней > ЕСТЬNULL(ВременнаяТаблицаНормыДней.НормаДней, 0)
			|				ТОГДА ЕСТЬNULL(ВременнаяТаблицаПределы.МРД, 0) * ВременнаяТаблицаСтатусы.СтавкаПН / 100
			|			ИНАЧЕ ЕСТЬNULL(ВременнаяТаблицаПределы.МРД, 0) * ВременнаяТаблицаНачисленияЗаМесяц.ОтработаноДней / ЕСТЬNULL(ВременнаяТаблицаНормыДней.НормаДней, 0) * ВременнаяТаблицаСтатусы.СтавкаПН / 100
			|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК ПНотМРД,
			|	ВременнаяТаблицаСтатусы.Вычеты КАК Вычеты,
			|	ЕСТЬNULL(ВременнаяТаблицаПределы.МРД, 0) КАК МРД,
			|	&ДоплатаПНЗаСчетФирмы КАК ДоплатаПНЗаСчетФирмы
			|ПОМЕСТИТЬ ВременнаяТаблицаНалогиПН
			|ИЗ
			|	ВременнаяТаблицаНачисленияЗаМесяц КАК ВременнаяТаблицаНачисленияЗаМесяц
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСтатусы КАК ВременнаяТаблицаСтатусы
			|		ПО ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ВременнаяТаблицаСтатусы.ФизЛицо
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиСФ КАК ВременнаяТаблицаНалогиСФ
			|		ПО ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ВременнаяТаблицаНалогиСФ.ФизЛицо
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПодоходныйНалог.Обороты(&НачалоГода, &КонецПрошлогоМесяца, , Организация = &Организация) КАК ПодоходныйНалогОбороты
			|		ПО ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ПодоходныйНалогОбороты.ФизЛицо
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНормыДней КАК ВременнаяТаблицаНормыДней
			|		ПО ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ВременнаяТаблицаНормыДней.ФизЛицо
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаПределы КАК ВременнаяТаблицаПределы
			|		ПО ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ВременнаяТаблицаПределы.ФизЛицо
			|ГДЕ
			|	ВременнаяТаблицаНачисленияЗаМесяц.ОДПН > 0
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	ФизЛицо";
		ТекстыЗапроса.Добавить(ТекстЗапроса);
	КонецЕсли;	
		
	// Расчет налогов Проф.взнос.
	// Если результат меньше 0- то обнуляется.
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	9 КАК Порядок,
		|	ПрофсоюзСрезПоследних.ФизЛицо КАК ФизЛицо,
		|	ВЫРАЗИТЬ((ВременнаяТаблицаНачисленияЗаМесяц.ОДПрофВзнос - ВЫБОР
		|			КОГДА &ПрофВзносЗаВычетомПФРиГНПФР
		|				ТОГДА ЕСТЬNULL(ВременнаяТаблицаНалогиСФ.ПФР, 0) + ЕСТЬNULL(ВременнаяТаблицаНалогиСФ.ГНПФР, 0)
		|			ИНАЧЕ 0
		|		КОНЕЦ - ВЫБОР
		|			КОГДА &ПрофВзносЗаВычетомПН
		|				ТОГДА ЕСТЬNULL(ВременнаяТаблицаНалогиПН.ПН, 0)
		|			ИНАЧЕ 0
		|		КОНЕЦ) * &СтавкаПрофВзнос / 100 КАК ЧИСЛО(15, 2)) КАК ПрофВзнос
		|ПОМЕСТИТЬ ВременнаяТаблицаНалогиПрофВзнос
		|ИЗ
		|	ВременнаяТаблицаНачисленияЗаМесяц КАК ВременнаяТаблицаНачисленияЗаМесяц
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Профсоюз.СрезПоследних(
		|				&КонецПериода,
		|				Организация = &Организация
		|					И ФизЛицо В
		|						(ВЫБРАТЬ
		|							ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо
		|						ИЗ
		|							ВременнаяТаблицаНачисленияЗаМесяц КАК ВременнаяТаблицаНачисленияЗаМесяц)) КАК ПрофсоюзСрезПоследних
		|		ПО ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ПрофсоюзСрезПоследних.ФизЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиСФ КАК ВременнаяТаблицаНалогиСФ
		|		ПО ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ВременнаяТаблицаНалогиСФ.ФизЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиПН КАК ВременнаяТаблицаНалогиПН
		|		ПО ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо = ВременнаяТаблицаНалогиПН.ФизЛицо
		|ГДЕ
		|	ПрофсоюзСрезПоследних.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПоПрофсоюзам.Вступление)
		|	И ВременнаяТаблицаНачисленияЗаМесяц.ОДПрофВзнос - ВЫБОР
		|			КОГДА &ПрофВзносЗаВычетомПФРиГНПФР
		|				ТОГДА ЕСТЬNULL(ВременнаяТаблицаНалогиСФ.ПФР, 0) + ЕСТЬNULL(ВременнаяТаблицаНалогиСФ.ГНПФР, 0)
		|			ИНАЧЕ 0
		|		КОНЕЦ - ВЫБОР
		|			КОГДА &ПрофВзносЗаВычетомПН
		|				ТОГДА ЕСТЬNULL(ВременнаяТаблицаНалогиПН.ПН, 0)
		|			ИНАЧЕ 0
		|		КОНЕЦ > 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
		
	// Результат.
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаСотрудники.ФизЛицо КАК ФизЛицо,
		|	ВременнаяТаблицаСотрудники.ФизЛицоНаименование КАК ФизЛицоНаименование,
		|	ВременнаяТаблицаСтатусы.Статус КАК Статус,
		|	ВременнаяТаблицаСтатусы.ПрименятьВычет КАК ПрименятьВычет,
		|	ВременнаяТаблицаСтатусы.Вычеты КАК Вычеты,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаНачисленияЗаМесяц.ОтработаноДней = 0
		|				И ВременнаяТаблицаНачисленияЗаМесяц.СуммаНачислено > 0
		|			ТОГДА 1
		|		ИНАЧЕ ВременнаяТаблицаНачисленияЗаМесяц.ОтработаноДней
		|	КОНЕЦ КАК ОтработаноДней,
		|	ВременнаяТаблицаНачисленияЗаМесяц.СуммаНачислено КАК СуммаНачислено,
		|	ВременнаяТаблицаНачисленияЗаМесяц.СуммаДопДоход КАК СуммаДопДоход,
		|	ВременнаяТаблицаНалогиСФ.ОДСФ КАК ОДСФ,
		//|	ВременнаяТаблицаНачисленияЗаМесяц.ОДПН КАК ОДПН,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ВременнаяТаблицаСтатусы.СтавкаПН, 0) = 0
		|			ТОГДА 0
		|		ИНАЧЕ ВременнаяТаблицаНачисленияЗаМесяц.ОДПН
		|	КОНЕЦ КАК ОДПН,	
		|	ВременнаяТаблицаНачисленияЗаМесяц.ОДПрофВзнос КАК ОДПрофВзнос,
		|	ВременнаяТаблицаНалогиСФ.ПФР КАК ПФР,
		|	ВременнаяТаблицаНалогиСФ.ГНПФР КАК ГНПФР,
		|	ВременнаяТаблицаНалогиСФ.МСР КАК МСР,
		|	ВременнаяТаблицаНалогиСФ.ПФФ КАК ПФФ,
		|	ВременнаяТаблицаНалогиСФ.МСФ КАК МСФ,
		|	ВременнаяТаблицаНалогиСФ.ФОТФ КАК ФОТФ,
		|	ВременнаяТаблицаНалогиСФ.ПФРДляВычетаПН КАК ПФРДляРасчетаПН,
		|	ВременнаяТаблицаНалогиСФ.ГНПФРДляВычетаПН КАК ГНПФРДляРасчетаПН,
		|	ВременнаяТаблицаНалогиПН.МРД КАК МРД,
		// в таб части сумма ПН всегда с доплатой в том числе
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаСтатусы.МетодРасчетаПН = ЗНАЧЕНИЕ(Перечисление.МетодыРасчетаПН.МРД)
		|				ИЛИ ВременнаяТаблицаСтатусы.МетодРасчетаПН = ЗНАЧЕНИЕ(Перечисление.МетодыРасчетаПН.НачисленоБольшеМРД)
		|					И ВременнаяТаблицаНалогиПН.ПН <= ВременнаяТаблицаНалогиПН.ПНотМРД
		|			ТОГДА ВременнаяТаблицаНалогиПН.ПНотМРД
		|		КОГДА ВременнаяТаблицаНалогиПН.ПН < 0
		|			ТОГДА 0
		|		КОГДА ВременнаяТаблицаСтатусы.МетодРасчетаПН = ЗНАЧЕНИЕ(Перечисление.МетодыРасчетаПН.Начислено)
		|				ИЛИ ВременнаяТаблицаСтатусы.МетодРасчетаПН = ЗНАЧЕНИЕ(Перечисление.МетодыРасчетаПН.НачисленоБольшеМРД)
		|					И ВременнаяТаблицаНалогиПН.ПН > ВременнаяТаблицаНалогиПН.ПНотМРД
		|			ТОГДА ВременнаяТаблицаНалогиПН.ПН
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ПН,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаСтатусы.МетодРасчетаПН = ЗНАЧЕНИЕ(Перечисление.МетодыРасчетаПН.НачисленоБольшеМРД)
		|				И ВременнаяТаблицаНалогиПН.ПН < ВременнаяТаблицаНалогиПН.ПНотМРД
		|			ТОГДА ВЫБОР
		|					КОГДА ВременнаяТаблицаНалогиПН.ПН < 0
		|						ТОГДА ВременнаяТаблицаНалогиПН.ПНотМРД * ВременнаяТаблицаСотрудники.ЗанимаемыхСтавок
		|					ИНАЧЕ ВременнаяТаблицаНалогиПН.ПНотМРД * ВременнаяТаблицаСотрудники.ЗанимаемыхСтавок - ВременнаяТаблицаНалогиПН.ПН
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ДоплатаПН,
		|	ВременнаяТаблицаНалогиПрофВзнос.ПрофВзнос КАК ПрофВзнос,
		|	ВременнаяТаблицаНалогиСФКорректировки.СреднемесячнаяЗПСФПриведенная КАК СреднемесячнаяЗПСФПриведенная,
		|	ВременнаяТаблицаНалогиСФКорректировки.ПФФКорректировка КАК ПФФКорректировка,
		|	ВременнаяТаблицаНалогиСФКорректировки.МСФКорректировка КАК МСФКорректировка,
		|	ВременнаяТаблицаНалогиСФКорректировки.ФОТФКорректировка КАК ФОТФКорректировка
		|ИЗ
		|	ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНачисленияЗаМесяц КАК ВременнаяТаблицаНачисленияЗаМесяц
		|		ПО ВременнаяТаблицаСотрудники.ФизЛицо = ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиСФ КАК ВременнаяТаблицаНалогиСФ
		|		ПО ВременнаяТаблицаСотрудники.ФизЛицо = ВременнаяТаблицаНалогиСФ.ФизЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиПН КАК ВременнаяТаблицаНалогиПН
		|		ПО ВременнаяТаблицаСотрудники.ФизЛицо = ВременнаяТаблицаНалогиПН.ФизЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиПрофВзнос КАК ВременнаяТаблицаНалогиПрофВзнос
		|		ПО ВременнаяТаблицаСотрудники.ФизЛицо = ВременнаяТаблицаНалогиПрофВзнос.ФизЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСтатусы КАК ВременнаяТаблицаСтатусы
		|		ПО ВременнаяТаблицаСотрудники.ФизЛицо = ВременнаяТаблицаСтатусы.ФизЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиСФКорректировки КАК ВременнаяТаблицаНалогиСФКорректировки
		|		ПО ВременнаяТаблицаСотрудники.ФизЛицо = ВременнаяТаблицаНалогиСФКорректировки.ФизЛицо
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВременнаяТаблицаСотрудники.ФизЛицоНаименование";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("НачалоГода", НачалоГода(Дата));
	Запрос.УстановитьПараметр("КонецПрошлогоМесяца", НачалоМесяца(Дата) - 1);
	Запрос.УстановитьПараметр("Организация", Организация);
		
	// По данным учетной политики
	ДанныеУчетнойПолитикиПоПерсоналу = БухгалтерскийУчетСервер.ПолучитьДанныеУчетнойПолитикиПоПерсоналу(Дата, Организация);
	
	Запрос.УстановитьПараметр("ПределыПоПодразделениям", ДанныеУчетнойПолитикиПоПерсоналу.ПределыПоПодразделениям);
	
	// СФ
	Запрос.УстановитьПараметр("СреднемесячнаяЗПСФ", ДанныеУчетнойПолитикиПоПерсоналу.СреднемесячнаяЗПСФ);
	Запрос.УстановитьПараметр("ПроцентОтСЗПСФ", ДанныеУчетнойПолитикиПоПерсоналу.ПроцентОтСЗПСФ);
	
	// ПН
	Запрос.УстановитьПараметр("МРД", ДанныеУчетнойПолитикиПоПерсоналу.МинимальныйРасчетныйДоход);	
	Запрос.УстановитьПараметр("ДоплатаПНЗаСчетФирмы", ДанныеУчетнойПолитикиПоПерсоналу.ДоплатаПНЗаСчетФирмы);
	Запрос.УстановитьПараметр("ПННарастающимИтогом", НЕ ДанныеУчетнойПолитикиПоПерсоналу.РасчетПНБезНакопления);
	
	// Корп.отчисления
	Запрос.УстановитьПараметр("СтавкаПрофВзнос", ДанныеУчетнойПолитикиПоПерсоналу.СтавкаПрофВзнос);
	Запрос.УстановитьПараметр("ПрофВзносЗаВычетомПН", ДанныеУчетнойПолитикиПоПерсоналу.ПрофВзносЗаВычетомПН);
	Запрос.УстановитьПараметр("ПрофВзносЗаВычетомПФРиГНПФР", ДанныеУчетнойПолитикиПоПерсоналу.ПрофВзносЗаВычетомПФРиГНПФР);
	
	НалогиПоЗарплате.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры // РассчитатьНалогиПоЗарплате()

// Процедура - Рассчитать удержания
//
Процедура РассчитатьПлановыеУдержания()
	
	// 1. Получение рассчитанных налогов для Расчета размера вычета
	// 2. Получение актуальных плановых удержаний
	// 3. Расчет удержаний
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВременнаяТаблицаНалоги.ФизЛицо КАК ФизЛицо,
		|	ВременнаяТаблицаНалоги.ПФР КАК ПФР,
		|	ВременнаяТаблицаНалоги.ГНПФР КАК ГНПФР,
		|	ВременнаяТаблицаНалоги.ПН КАК ПН,
		|	ВременнаяТаблицаНалоги.ПрофВзнос КАК ПрофВзнос
		|ПОМЕСТИТЬ ВременнаяТаблицаНалоги
		|ИЗ
		|	&ВременнаяТаблицаНалоги КАК ВременнаяТаблицаНалоги
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПлановыеУдержанияНачало.ФизЛицо КАК ФизЛицо,
		|	ПлановыеУдержанияНачало.ВидРасчета КАК ВидРасчета,
		|	ПлановыеУдержанияНачало.Размер КАК Размер,
		|	ПлановыеУдержанияНачало.Контрагент КАК Контрагент,
		|	ВЫБОР
		|		КОГДА ПлановыеУдержанияНачало.ВидРасчета.РасчетЗаМинусомСФ
		|			ТОГДА ЕСТЬNULL(ВременнаяТаблицаНалоги.ПФР, 0) + ЕСТЬNULL(ВременнаяТаблицаНалоги.ГНПФР, 0)
		|		ИНАЧЕ 0
		|	КОНЕЦ + ВЫБОР
		|		КОГДА ПлановыеУдержанияНачало.ВидРасчета.РасчетЗаМинусомПН
		|			ТОГДА ЕСТЬNULL(ВременнаяТаблицаНалоги.ПН, 0)
		|		ИНАЧЕ 0
		|	КОНЕЦ + ВЫБОР
		|		КОГДА ПлановыеУдержанияНачало.ВидРасчета.РасчетЗаМинусомПрофВзнос
		|			ТОГДА ЕСТЬNULL(ВременнаяТаблицаНалоги.ПрофВзнос, 0)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК РазмерВычета
		|ИЗ
		|	РегистрСведений.ПлановыеУдержанияНачало.СрезПоследних(&Период, Организация = &Организация) КАК ПлановыеУдержанияНачало
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлановыеУдержанияОкончание.СрезПоследних(&НачалоПериода, Организация = &Организация) КАК ПлановыеУдержанияОкончание
		|		ПО ПлановыеУдержанияНачало.Организация = ПлановыеУдержанияОкончание.Организация
		|			И ПлановыеУдержанияНачало.ФизЛицо = ПлановыеУдержанияОкончание.ФизЛицо
		|			И ПлановыеУдержанияНачало.ВидРасчета = ПлановыеУдержанияОкончание.ВидРасчета
		|			И ПлановыеУдержанияНачало.Регистратор = ПлановыеУдержанияОкончание.ДокументСсылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалоги КАК ВременнаяТаблицаНалоги
		|		ПО ПлановыеУдержанияНачало.ФизЛицо = ВременнаяТаблицаНалоги.ФизЛицо
		|ГДЕ
		|	ПлановыеУдержанияОкончание.Организация ЕСТЬ NULL";
	Запрос.УстановитьПараметр("ВременнаяТаблицаНалоги", НалогиПоЗарплате.Выгрузить());
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата)-1); // т.к. Все прекращения оформляются в месяце их завершения, т.е. Период = начало месяца прекращения.  
	Запрос.УстановитьПараметр("Организация", Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// Нет плановых удержаний для расчета
	Если РезультатЗапроса.Пустой() Тогда 
		Возврат;
	КонецЕсли;	
	
	Удержания.Загрузить(Запрос.Выполнить().Выгрузить());
	
	// Подготовка таблицы для движения в регистр
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаУдержания.ФизЛицо,
		|	ВременнаяТаблицаУдержания.ВидРасчета,
		|	ВременнаяТаблицаУдержания.Размер,
		|	ВременнаяТаблицаУдержания.РазмерВычета,
		|	ВременнаяТаблицаУдержания.Результат
		|ПОМЕСТИТЬ ВременнаяТаблицаУдержания
		|ИЗ
		|	&ВременнаяТаблицаУдержания КАК ВременнаяТаблицаУдержания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ) КАК ПериодРегистрации,
		|	НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ) КАК БазовыйПериодНачало,
		|	КОНЕЦПЕРИОДА(&Дата, ДЕНЬ) КАК БазовыйПериодКонец,
		|	ТаблицаДокумента.ВидРасчета,
		|	&Организация КАК Организация,
		|	ТаблицаДокумента.ФизЛицо,
		|	ТаблицаДокумента.Размер,
		|	ТаблицаДокумента.РазмерВычета,
		|	ТаблицаДокумента.Результат
		|ИЗ
		|	ВременнаяТаблицаУдержания КАК ТаблицаДокумента";
	Запрос.УстановитьПараметр("ВременнаяТаблицаУдержания", Удержания.Выгрузить());	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// Инициализация дополнительных свойств для проведения документа.
	БухгалтерскийУчетСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);

	ДополнительныеСвойства.Вставить("ТаблицыДляДвижений", Новый Структура);
	ДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаУдержания", РезультатЗапроса.Выгрузить());
	
	// Подготовка наборов записей.
	БухгалтерскийУчетСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета.
	БухгалтерскийУчетСервер.ОтразитьУдержания(ДополнительныеСвойства, Движения, Ложь);

	// Запись наборов записей.
	БухгалтерскийУчетСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);

	Ошибки = Неопределено;
	ПроведениеРасчетовПоЗарплатеСервер.РассчитатьЗаписиРегистраРасчета("Удержания", Движения.Удержания, Удержания, Ошибки);
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
	
	// Подготовка наборов записей
	БухгалтерскийУчетСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Запись наборов записей
	БухгалтерскийУчетСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
КонецПроцедуры // РассчитатьПлановыеУдержания()

// Процедура - Дозаполнить налоги по зарплате
//
Процедура ДозаполнитьНалогиПоЗарплате()
	
	// 1. Получение рассчитанных удержаний
	// 2. Получение разовых удержаний
	// 3. Заполнение суммы удержано
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВременнаяТаблицаУдержания.ФизЛицо,
		|	ВременнаяТаблицаУдержания.Результат
		|ПОМЕСТИТЬ ВременнаяТаблицаУдержания
		|ИЗ
		|	&ВременнаяТаблицаУдержания КАК ВременнаяТаблицаУдержания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Удержания.ФизЛицо,
		|	Удержания.Результат
		|ПОМЕСТИТЬ ВременнаяТаблицаУдержанияЗаМесяц
		|ИЗ
		|	РегистрРасчета.Удержания КАК Удержания
		|ГДЕ
		|	Удержания.ПериодРегистрации МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Удержания.Организация = &Организация
		|	И НЕ Удержания.Результат = 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.ФизЛицо,
		|	СУММА(ВложенныйЗапрос.Результат) КАК Результат
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВременнаяТаблицаУдержания.ФизЛицо КАК ФизЛицо,
		|		ВременнаяТаблицаУдержания.Результат КАК Результат
		|	ИЗ
		|		ВременнаяТаблицаУдержания КАК ВременнаяТаблицаУдержания
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ВременнаяТаблицаУдержанияЗаМесяц.ФизЛицо,
		|		ВременнаяТаблицаУдержанияЗаМесяц.Результат
		|	ИЗ
		|		ВременнаяТаблицаУдержанияЗаМесяц КАК ВременнаяТаблицаУдержанияЗаМесяц) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.ФизЛицо";
	Запрос.УстановитьПараметр("ВременнаяТаблицаУдержания", Удержания.Выгрузить());		
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("Организация", Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Для Каждого СтрокаТабличнойЧасти Из НалогиПоЗарплате Цикл 
		СуммаУдержано = 0;
		// Удержания
		Если ВыборкаДетальныеЗаписи.НайтиСледующий(Новый Структура("ФизЛицо", СтрокаТабличнойЧасти.ФизЛицо)) Тогда 
			СуммаУдержано = СуммаУдержано + ВыборкаДетальныеЗаписи.Результат;
			ВыборкаДетальныеЗаписи.Сбросить();
		КонецЕсли;	
		
		// Налоги
		СуммаУдержано = СуммаУдержано 
			+ СтрокаТабличнойЧасти.ПФР 
			+ СтрокаТабличнойЧасти.ГНПФР 
			+ СтрокаТабличнойЧасти.ПН 
			+ СтрокаТабличнойЧасти.ПрофВзнос;
			
		СтрокаТабличнойЧасти.СуммаУдержано = СуммаУдержано;	
	КонецЦикла;	
	
КонецПроцедуры // ДозаполнитьНалогиПоЗарплате()

// Процедура - Дозаполнить выплату
//
Процедура ДозаполнитьВыплату()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВыплаченнаяЗарплатаОбороты.ФизЛицо,
		|	ВыплаченнаяЗарплатаОбороты.СуммаОборот КАК Выплачено
		|ИЗ
		|	РегистрНакопления.ВыплаченнаяЗарплата.Обороты(&НачалоПериода, &КонецПериода, , Организация = &Организация) КАК ВыплаченнаяЗарплатаОбороты";
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("Организация", Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
		НайденнаяСтрокаТабличнойЧасти = НалогиПоЗарплате.Найти(ВыборкаДетальныеЗаписи.ФизЛицо, "ФизЛицо");
		
		Если НайденнаяСтрокаТабличнойЧасти = Неопределено Тогда 
			Продолжить;
		КонецЕсли;	
		
		ЗаполнитьЗначенияСвойств(НайденнаяСтрокаТабличнойЧасти, ВыборкаДетальныеЗаписи, "Выплачено");
	КонецЦикла;	
	
КонецПроцедуры // ДозаполнитьВыплату()

// Процедура - Распределить налоги с организации
//
Процедура РаспределитьНалогиСОрганизации()
	
	// Выборка всех начислений за месяц в разрезе Способа отражения.
	// Для каждого налога определяется база распределения, для исключения нулевых ставок.
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Начисления.ФизЛицо КАК ФизЛицо,
		|	Начисления.СпособОтражения КАК СпособОтражения,
		|	Начисления.СпособОтражения.СчетЗатратСФ КАК СчетЗатрат,
		|	Начисления.СпособОтражения.СубконтоЗатратСФ1 КАК СубконтоЗатрат1,
		|	Начисления.СпособОтражения.СубконтоЗатратСФ2 КАК СубконтоЗатрат2,
		|	Начисления.СпособОтражения.СубконтоЗатратСФ3 КАК СубконтоЗатрат3,
		|	СУММА(Начисления.Результат) КАК Результат
		|ИЗ
		|	РегистрРасчета.Начисления КАК Начисления
		|ГДЕ
		|	Начисления.ПериодРегистрации МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Начисления.Организация = &Организация
		|	И НЕ Начисления.Результат = 0
		|	И Начисления.ВидРасчета.ОблагаетсяСФ
		|
		|СГРУППИРОВАТЬ ПО
		|	Начисления.ФизЛицо,
		|	Начисления.СпособОтражения,
		|	Начисления.СпособОтражения.СчетЗатратСФ,
		|	Начисления.СпособОтражения.СубконтоЗатратСФ1,
		|	Начисления.СпособОтражения.СубконтоЗатратСФ2,
		|	Начисления.СпособОтражения.СубконтоЗатратСФ3
		|ИТОГИ ПО
		|	ФизЛицо";
	Запрос.УстановитьПараметр("НалогиПоЗарплате", НалогиПоЗарплате.Выгрузить());
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("Организация", Организация);

	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда 
		Возврат;	
	КонецЕсли;
	
	ВыборкаФизЛицо = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаФизЛицо.Следующий() Цикл 
		
		// Распределение сумм налогов.
		НайденныеСтроки = НалогиПоЗарплате.НайтиСтроки(Новый Структура("ФизЛицо", ВыборкаФизЛицо.ФизЛицо));
		Если НайденныеСтроки.Количество() = 0 Тогда 
			Продолжить;
		Иначе 
			ПФФ = НайденныеСтроки[0].ПФФ;
			МСФ = НайденныеСтроки[0].МСФ;
			ФОТФ = НайденныеСтроки[0].ФОТФ;
		КонецЕсли;
		
		ВыборкаДетальныеЗаписи = ВыборкаФизЛицо.Выбрать();
		
		МассивКоэффициентов = Новый Массив;
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
			МассивКоэффициентов.Добавить(ВыборкаДетальныеЗаписи.Результат);
		КонецЦикла;
		
		МассивСуммПФФ = ОбщегоНазначенияКлиентСервер.РаспределитьСуммуПропорциональноКоэффициентам(ПФФ, МассивКоэффициентов);
		МассивСуммМСФ = ОбщегоНазначенияКлиентСервер.РаспределитьСуммуПропорциональноКоэффициентам(МСФ, МассивКоэффициентов);
		МассивСуммФОТФ = ОбщегоНазначенияКлиентСервер.РаспределитьСуммуПропорциональноКоэффициентам(ФОТФ, МассивКоэффициентов);
		
		// Определение параметров отражения в учете.
		ВыборкаДетальныеЗаписи.Сбросить();
		
		ИндексВМассиве = 0;
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			Отбор = Новый Структура();
			Отбор.Вставить("СчетЗатрат", ВыборкаДетальныеЗаписи.СчетЗатрат);
			Отбор.Вставить("СубконтоЗатрат1", ВыборкаДетальныеЗаписи.СубконтоЗатрат1);
			Отбор.Вставить("СубконтоЗатрат2", ВыборкаДетальныеЗаписи.СубконтоЗатрат2);
			Отбор.Вставить("СубконтоЗатрат3", ВыборкаДетальныеЗаписи.СубконтоЗатрат3);
			
			НайденныеСтроки = НалогиСОрганизации.НайтиСтроки(Отбор);
				
			Если НайденныеСтроки.Количество() = 0 Тогда 
				СтрокаТабличнойЧасти = НалогиСОрганизации.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ВыборкаДетальныеЗаписи);
			Иначе 
				СтрокаТабличнойЧасти = НайденныеСтроки[0];
			КонецЕсли;			
			
			Если НЕ МассивСуммПФФ = Неопределено Тогда 
				СтрокаТабличнойЧасти.ПФФ = СтрокаТабличнойЧасти.ПФФ + МассивСуммПФФ[ИндексВМассиве];
			КонецЕсли;
			Если НЕ МассивСуммМСФ = Неопределено Тогда 
				СтрокаТабличнойЧасти.МСФ = СтрокаТабличнойЧасти.МСФ + МассивСуммМСФ[ИндексВМассиве];
			КонецЕсли;
			Если НЕ МассивСуммФОТФ = Неопределено Тогда 
				СтрокаТабличнойЧасти.ФОТФ = СтрокаТабличнойЧасти.ФОТФ + МассивСуммФОТФ[ИндексВМассиве];
			КонецЕсли;	
			
			ИндексВМассиве = ИндексВМассиве + 1;
		КонецЦикла;
	КонецЦикла;	
КонецПроцедуры // РаспределитьНалогиСОрганизации()

// Процедура - Распределить налоги с организации (Корректировки)
//
Процедура РаспределитьНалогиСОрганизацииКорректировки()
	
	// Выборка всех начислений за месяц в разрезе подразделения и вида начисления для Физлиц, у которых есть корректировки.
	// Для каждого налога определяется база распределения, для исключения нулевых ставок.
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗакрытиеМесяцаЗПНалогиПоЗарплате.ФизЛицо КАК ФизЛицо
		|ПОМЕСТИТЬ ВременнаяТаблицаНалогиКорректировкиФизлицаДляОтбора
		|ИЗ
		|	&НалогиПоЗарплате КАК ЗакрытиеМесяцаЗПНалогиПоЗарплате
		|ГДЕ
		|	(ЗакрытиеМесяцаЗПНалогиПоЗарплате.ПФФКорректировка <> 0
		|			ИЛИ ЗакрытиеМесяцаЗПНалогиПоЗарплате.МСФКорректировка <> 0
		|			ИЛИ ЗакрытиеМесяцаЗПНалогиПоЗарплате.ФОТФКорректировка <> 0)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Начисления.ФизЛицо КАК ФизЛицо,
		|	Начисления.СпособОтражения КАК СпособОтражения,
		|	Начисления.СпособОтражения.СчетЗатратСФ КАК СчетЗатрат,
		|	Начисления.СпособОтражения.СубконтоЗатратСФ1 КАК СубконтоЗатрат1,
		|	Начисления.СпособОтражения.СубконтоЗатратСФ2 КАК СубконтоЗатрат2,
		|	Начисления.СпособОтражения.СубконтоЗатратСФ3 КАК СубконтоЗатрат3,
		|	СУММА(Начисления.Результат) КАК Результат
		|ИЗ
		|	РегистрРасчета.Начисления КАК Начисления
		|ГДЕ
		|	Начисления.ПериодРегистрации МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Начисления.Организация = &Организация
		|	И НЕ Начисления.Результат = 0
		|	И Начисления.ВидРасчета.ОблагаетсяСФ
		|	И Начисления.ФизЛицо В
		|			(ВЫБРАТЬ
		|				ВременнаяТаблицаНалогиКорректировкиФизлицаДляОтбора.ФизЛицо КАК ФизЛицо
		|			ИЗ
		|				ВременнаяТаблицаНалогиКорректировкиФизлицаДляОтбора КАК ВременнаяТаблицаНалогиКорректировкиФизлицаДляОтбора)
		|
		|СГРУППИРОВАТЬ ПО
		|	Начисления.ФизЛицо,
		|	Начисления.СпособОтражения,
		|	Начисления.СпособОтражения.СчетЗатратСФ,
		|	Начисления.СпособОтражения.СубконтоЗатратСФ1,
		|	Начисления.СпособОтражения.СубконтоЗатратСФ2,
		|	Начисления.СпособОтражения.СубконтоЗатратСФ3
		|ИТОГИ ПО
		|	ФизЛицо";
	Запрос.УстановитьПараметр("НалогиПоЗарплате", НалогиПоЗарплате.Выгрузить());
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("Организация", Организация);

	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда 
		Возврат;	
	КонецЕсли;
			 
	ВыборкаФизЛицо = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаФизЛицо.Следующий() Цикл 
		
		// Распределение сумм налогов.
		НайденныеСтроки = НалогиПоЗарплате.НайтиСтроки(Новый Структура("ФизЛицо", ВыборкаФизЛицо.ФизЛицо));
		Если НайденныеСтроки.Количество() = 0 Тогда 
			Продолжить;
		Иначе 
			ПФФКорректировка = НайденныеСтроки[0].ПФФКорректировка;
			МСФКорректировка = НайденныеСтроки[0].МСФКорректировка;
			ФОТФКорректировка = НайденныеСтроки[0].ФОТФКорректировка;
		КонецЕсли;
		
		ВыборкаДетальныеЗаписи = ВыборкаФизЛицо.Выбрать();
		
		МассивКоэффициентов = Новый Массив;
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
			МассивКоэффициентов.Добавить(ВыборкаДетальныеЗаписи.Результат);
		КонецЦикла;
		
		МассивСуммПФФ = ОбщегоНазначенияКлиентСервер.РаспределитьСуммуПропорциональноКоэффициентам(ПФФКорректировка, МассивКоэффициентов);
		МассивСуммМСФ = ОбщегоНазначенияКлиентСервер.РаспределитьСуммуПропорциональноКоэффициентам(МСФКорректировка, МассивКоэффициентов);
		МассивСуммФОТФ = ОбщегоНазначенияКлиентСервер.РаспределитьСуммуПропорциональноКоэффициентам(ФОТФКорректировка, МассивКоэффициентов);
		
		// Определение параметров отражения в учете.
		ВыборкаДетальныеЗаписи.Сбросить();
		
		ИндексВМассиве = 0;
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
			
			Отбор = Новый Структура();
			Отбор.Вставить("СчетЗатрат", ВыборкаДетальныеЗаписи.СчетЗатрат);
			Отбор.Вставить("СубконтоЗатрат1", ВыборкаДетальныеЗаписи.СубконтоЗатрат1);
			Отбор.Вставить("СубконтоЗатрат2", ВыборкаДетальныеЗаписи.СубконтоЗатрат2);
			Отбор.Вставить("СубконтоЗатрат3", ВыборкаДетальныеЗаписи.СубконтоЗатрат3);
			
			НайденныеСтроки = НалогиСОрганизацииКорректировки.НайтиСтроки(Отбор);
				
			Если НайденныеСтроки.Количество() = 0 Тогда 
				СтрокаТабличнойЧасти = НалогиСОрганизацииКорректировки.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ВыборкаДетальныеЗаписи);
			Иначе 
				СтрокаТабличнойЧасти = НайденныеСтроки[0];
			КонецЕсли;			
			
			Если НЕ МассивСуммПФФ = Неопределено Тогда 
				СтрокаТабличнойЧасти.ПФФКорректировка = СтрокаТабличнойЧасти.ПФФКорректировка + МассивСуммПФФ[ИндексВМассиве];
			КонецЕсли;
			Если НЕ МассивСуммМСФ = Неопределено Тогда 
				СтрокаТабличнойЧасти.МСФКорректировка = СтрокаТабличнойЧасти.МСФКорректировка + МассивСуммМСФ[ИндексВМассиве];
			КонецЕсли;
			Если НЕ МассивСуммФОТФ = Неопределено Тогда 
				СтрокаТабличнойЧасти.ФОТФКорректировка = СтрокаТабличнойЧасти.ФОТФКорректировка + МассивСуммФОТФ[ИндексВМассиве];
			КонецЕсли;	
			
			ИндексВМассиве = ИндексВМассиве + 1;
		КонецЦикла;
	КонецЦикла;	
КонецПроцедуры // РаспределитьНалогиСОрганизацииКорректировки()

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииОбработкаПроведения

// Процедура инициализирует данные документа
// и подготавливает таблицы для движения в регистры
//
Процедура ИнициализироватьДанные(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа.
	БухгалтерскийУчетСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	ДополнительныеСвойства.ДляПроведения.Вставить("ДанныеУчетнойПолитикиПоПерсоналу", БухгалтерскийУчетСервер.ПолучитьДанныеУчетнойПолитикиПоПерсоналу(Дата, Организация));

	// Инициализация данных документа.
	Документы.ЗакрытиеМесяцаЗП.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства, ЭтотОбъект);
	
	// Подготовка наборов записей.
	БухгалтерскийУчетСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
КонецПроцедуры
	
// Процедура заполняет регистры данными
//
Процедура ОтразитьДвижения(Отказ, РежимПроведения)

	// Отражение в разделах учета.
	БухгалтерскийУчетСервер.ОтразитьХозрасчетный(ДополнительныеСвойства, Движения, Отказ);
	БухгалтерскийУчетСервер.ОтразитьУдержания(ДополнительныеСвойства, Движения, Отказ);
	БухгалтерскийУчетСервер.ОтразитьПодоходныйНалог(ДополнительныеСвойства, Движения, Отказ);
	БухгалтерскийУчетСервер.ОтразитьСоциальныйФонд(ДополнительныеСвойства, Движения, Отказ);
	БухгалтерскийУчетСервер.ОтразитьНалогиПоЗаработнойПлате(ДополнительныеСвойства, Движения, Отказ);
	БухгалтерскийУчетСервер.ОтразитьДанныеДляОтчетаПоСФЕжемесячному(ДополнительныеСвойства, Движения, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли