#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПроцедурыПроведенияДокумента

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	// 1. Начисления за месяц
	// 2. Удержания за месяц
	// 3. Удержания этого документа
	// 4. ПФР
	// 5. ГНПФР
	// 6. ПН
	// 7. ПрофВзнос
	// 8. ПФФ
	// 9. МСФ
	// 10. ФОТФ
	// 11. ПФФКорректировка
	// 12. МСФКорректировка
	// 13. ФОТФКорректировка
	ТекстыЗапроса = Новый Массив;

	ТекстЗапроса =
		// Начисления за месяц.
		"ВЫБРАТЬ
		|	1 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		// Счет Дт/Кт
		|	ВременнаяТаблицаНачисленияЗаМесяц.СчетЗатрат КАК СчетДт,
		|	&СчетУчета КАК СчетКт,
		// Субконто Дт
		|	ВременнаяТаблицаНачисленияЗаМесяц.СубконтоЗатрат1 КАК СубконтоДт1,
		|	ВременнаяТаблицаНачисленияЗаМесяц.СубконтоЗатрат2 КАК СубконтоДт2,
		|	ВременнаяТаблицаНачисленияЗаМесяц.СубконтоЗатрат3 КАК СубконтоДт3,
		// Субконто Кт
		|	ВременнаяТаблицаНачисленияЗаМесяц.ФизЛицо КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		// Суммы
		|	ВременнаяТаблицаНачисленияЗаМесяц.Результат КАК Сумма,
		|	&ВалютаРегламентированногоУчета КАК ВалютаДт,
		|	ВременнаяТаблицаНачисленияЗаМесяц.Результат КАК ВалютнаяСуммаДт,
		|	&ВалютаРегламентированногоУчета КАК ВалютаКт,
		|	ВременнаяТаблицаНачисленияЗаМесяц.Результат КАК ВалютнаяСуммаКт,
		|	&СодержаниеНачисление КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНачисленияЗаМесяц КАК ВременнаяТаблицаНачисленияЗаМесяц
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаНачисленияЗаМесяц.Результат = 0";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// Удержания за месяц.
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	2 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		// Счет Дт/Кт
		|	&СчетУчета КАК СчетДт,
		|	ВременнаяТаблицаУдержанияЗаМесяц.СчетУчета КАК СчетКт,
		// Субконто Дт
		|	ВременнаяТаблицаУдержанияЗаМесяц.ФизЛицо КАК СубконтоДт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		// Субконто Кт
		|	ВременнаяТаблицаУдержанияЗаМесяц.ФизЛицо КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		// Суммы
		|	ВременнаяТаблицаУдержанияЗаМесяц.Результат КАК Сумма,
		|	&ВалютаРегламентированногоУчета КАК ВалютаДт,
		|	ВременнаяТаблицаУдержанияЗаМесяц.Результат КАК ВалютнаяСуммаДт,
		|	&ВалютаРегламентированногоУчета КАК ВалютаКт,
		|	ВременнаяТаблицаУдержанияЗаМесяц.Результат КАК ВалютнаяСуммаКт,
		|	&СодержаниеУдержание КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаУдержанияЗаМесяц КАК ВременнаяТаблицаУдержанияЗаМесяц
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаУдержанияЗаМесяц.Результат = 0";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// Удержания этого документа.
	ТекстЗапроса =	
		"ВЫБРАТЬ
		|	3 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		// Счет Дт/Кт
		|	&СчетУчета КАК СчетДт,
		|	ВременнаяТаблицаУдержания.СчетУчета КАК СчетКт,
		// Субконто Дт
		|	ВременнаяТаблицаУдержания.ФизЛицо КАК СубконтоДт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		// Субконто Кт
		|	ВременнаяТаблицаУдержания.Контрагент КАК СубконтоКт1,
		|	ВременнаяТаблицаУдержания.ФизЛицо КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		// Суммы
		|	ВременнаяТаблицаУдержания.Результат КАК Сумма,
		|	&ВалютаРегламентированногоУчета КАК ВалютаДт,
		|	ВременнаяТаблицаУдержания.Результат КАК ВалютнаяСуммаДт,
		|	&ВалютаРегламентированногоУчета КАК ВалютаКт,
		|	ВременнаяТаблицаУдержания.Результат КАК ВалютнаяСуммаКт,
		|	&СодержаниеУдержание КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаУдержания КАК ВременнаяТаблицаУдержания
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаУдержания.Результат = 0";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// ПФР.
	ТекстЗапроса =	
		"ВЫБРАТЬ
		|	4 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		// Счет Дт/Кт
		|	&СчетУчета КАК СчетДт,
		|	&СчетУчетаРасчетовПФР КАК СчетКт,
		// Субконто Дт
		|	ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо КАК СубконтоДт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
		|	ЗНАЧЕНИЕ(ПланВидовРасчета.ВидыНалогов.ПФР) КАК СубконтоДт3,
		// Субконто Кт
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		// Суммы
		|	ВременнаяТаблицаНалогиПоЗарплате.ПФР КАК Сумма,
		|	&ВалютаРегламентированногоУчета КАК ВалютаДт,
		|	ВременнаяТаблицаНалогиПоЗарплате.ПФР КАК ВалютнаяСуммаДт,
		|	&ВалютаРегламентированногоУчета КАК ВалютаКт,
		|	ВременнаяТаблицаНалогиПоЗарплате.ПФР КАК ВалютнаяСуммаКт,
		|	&СодержаниеПФР КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиПоЗарплате КАК ВременнаяТаблицаНалогиПоЗарплате
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаНалогиПоЗарплате.ПФР = 0";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// ГНПФР.
	ТекстЗапроса =	
		"ВЫБРАТЬ
		|	5 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		// Счет Дт/Кт
		|	&СчетУчета КАК СчетДт,
		|	&СчетУчетаРасчетовГНПФР КАК СчетКт,
		// Субконто Дт
		|	ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо КАК СубконтоДт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
		|	ЗНАЧЕНИЕ(ПланВидовРасчета.ВидыНалогов.ГНПФР) КАК СубконтоДт3,
		// Субконто Кт
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		// Суммы
		|	ВременнаяТаблицаНалогиПоЗарплате.ГНПФР КАК Сумма,
		|	&ВалютаРегламентированногоУчета КАК ВалютаДт,
		|	ВременнаяТаблицаНалогиПоЗарплате.ГНПФР КАК ВалютнаяСуммаДт,
		|	&ВалютаРегламентированногоУчета КАК ВалютаКт,
		|	ВременнаяТаблицаНалогиПоЗарплате.ГНПФР КАК ВалютнаяСуммаКт,
		|	&СодержаниеГНПФР КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиПоЗарплате КАК ВременнаяТаблицаНалогиПоЗарплате
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаНалогиПоЗарплате.ГНПФР = 0";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// ПН.
	ТекстЗапроса =	
		"ВЫБРАТЬ
		|	6 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		// Счет Дт/Кт
		|	&СчетУчета КАК СчетДт,
		|	&СчетУчетаРасчетовПН КАК СчетКт,
		// Субконто Дт
		|	ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо КАК СубконтоДт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
		|	ЗНАЧЕНИЕ(ПланВидовРасчета.ВидыНалогов.ПН) КАК СубконтоДт3,
		// Субконто Кт
		|	ВременнаяТаблицаСотрудники.Подразделение КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		// Суммы
		|	ВЫБОР
		|		КОГДА &ДоплатаПНЗаСчетФирмы
		|				И ВременнаяТаблицаНалогиПоЗарплате.ДоплатаПН > 0
		|			ТОГДА ВременнаяТаблицаНалогиПоЗарплате.ПН - ВременнаяТаблицаНалогиПоЗарплате.ДоплатаПН
		|		ИНАЧЕ ВременнаяТаблицаНалогиПоЗарплате.ПН
		|	КОНЕЦ КАК Сумма,
		|	&ВалютаРегламентированногоУчета КАК ВалютаДт,
		|	ВременнаяТаблицаНалогиПоЗарплате.ПН КАК ВалютнаяСуммаДт,
		|	&ВалютаРегламентированногоУчета КАК ВалютаКт,
		|	ВременнаяТаблицаНалогиПоЗарплате.ПН КАК ВалютнаяСуммаКт,
		|	&СодержаниеПН КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиПоЗарплате КАК ВременнаяТаблицаНалогиПоЗарплате
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники
		|			ПО ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо = ВременнаяТаблицаСотрудники.ФизЛицо
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаНалогиПоЗарплате.ПН > 0";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// Доплата ПН за счет Фирмы.
	ТекстЗапроса =	
		"ВЫБРАТЬ
		|	6 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		// Счет Дт/Кт
		|	&СчетУчетаРасходовПоДоплатеПН КАК СчетДт,
		|	&СчетУчетаРасчетовПН КАК СчетКт,
		// Субконто Дт
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		// Субконто Кт
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		// Суммы
		|	ВременнаяТаблицаНалогиПоЗарплате.ДоплатаПН КАК Сумма,
		|	&ВалютаРегламентированногоУчета КАК ВалютаДт,
		|	ВременнаяТаблицаНалогиПоЗарплате.ПН КАК ВалютнаяСуммаДт,
		|	&ВалютаРегламентированногоУчета КАК ВалютаКт,
		|	ВременнаяТаблицаНалогиПоЗарплате.ПН КАК ВалютнаяСуммаКт,
		|	&СодержаниеДоплатаПН КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиПоЗарплате КАК ВременнаяТаблицаНалогиПоЗарплате
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники
		|			ПО ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо = ВременнаяТаблицаСотрудники.ФизЛицо
		|		ПО (ИСТИНА)
		|ГДЕ
		|	&ДоплатаПНЗаСчетФирмы И ВременнаяТаблицаНалогиПоЗарплате.ДоплатаПН > 0";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// ПрофВзнос.
	ТекстЗапроса =	
		"ВЫБРАТЬ
		|	7 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		// Счет Дт/Кт
		|	&СчетУчета КАК СчетДт,
		|	&СчетУчетаРасчетовПрофВзнос КАК СчетКт,
		// Субконто Дт
		|	ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо КАК СубконтоДт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
		|	ЗНАЧЕНИЕ(ПланВидовРасчета.ВидыНалогов.ПрофВзнос) КАК СубконтоДт3,
		// Субконто Кт
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		// Суммы
		|	ВременнаяТаблицаНалогиПоЗарплате.ПрофВзнос КАК Сумма,
		|	&ВалютаРегламентированногоУчета КАК ВалютаДт,
		|	ВременнаяТаблицаНалогиПоЗарплате.ПрофВзнос КАК ВалютнаяСуммаДт,
		|	&ВалютаРегламентированногоУчета КАК ВалютаКт,
		|	ВременнаяТаблицаНалогиПоЗарплате.ПрофВзнос КАК ВалютнаяСуммаКт,
		|	&СодержаниеПрофВзнос КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиПоЗарплате КАК ВременнаяТаблицаНалогиПоЗарплате
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаНалогиПоЗарплате.ПрофВзнос = 0";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// ПФФ.
	ТекстЗапроса =	
		"ВЫБРАТЬ
		|	8 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		// Счет Дт/Кт
		|	ВременнаяТаблицаНалогиСОрганизации.СчетЗатрат КАК СчетДт,
		|	&СчетУчетаРасчетовПФФ КАК СчетКт,
		// Субконто Дт
		|	ВременнаяТаблицаНалогиСОрганизации.СубконтоЗатрат1 КАК СубконтоДт1,
		|	ВременнаяТаблицаНалогиСОрганизации.СубконтоЗатрат2 КАК СубконтоДт2,
		|	ВременнаяТаблицаНалогиСОрганизации.СубконтоЗатрат3 КАК СубконтоДт3,
		// Субконто Кт
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		// Суммы
		|	ВременнаяТаблицаНалогиСОрганизации.ПФФ КАК Сумма,
		|	&ВалютаРегламентированногоУчета КАК ВалютаДт,
		|	ВременнаяТаблицаНалогиСОрганизации.ПФФ КАК ВалютнаяСуммаДт,
		|	&ВалютаРегламентированногоУчета КАК ВалютаКт,
		|	ВременнаяТаблицаНалогиСОрганизации.ПФФ КАК ВалютнаяСуммаКт,
		|	&СодержаниеПФФ КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиСОрганизации КАК ВременнаяТаблицаНалогиСОрганизации
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаНалогиСОрганизации.ПФФ = 0";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// МСФ.
	ТекстЗапроса =	
		"ВЫБРАТЬ
		|	9 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		// Счет Дт/Кт
		|	ВременнаяТаблицаНалогиСОрганизации.СчетЗатрат КАК СчетДт,
		|	&СчетУчетаРасчетовМСФ КАК СчетКт,
		// Субконто Дт
		|	ВременнаяТаблицаНалогиСОрганизации.СубконтоЗатрат1 КАК СубконтоДт1,
		|	ВременнаяТаблицаНалогиСОрганизации.СубконтоЗатрат2 КАК СубконтоДт2,
		|	ВременнаяТаблицаНалогиСОрганизации.СубконтоЗатрат3 КАК СубконтоДт3,
		// Субконто Кт
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		// Суммы
		|	ВременнаяТаблицаНалогиСОрганизации.МСФ КАК Сумма,
		|	&ВалютаРегламентированногоУчета КАК ВалютаДт,
		|	ВременнаяТаблицаНалогиСОрганизации.МСФ КАК ВалютнаяСуммаДт,
		|	&ВалютаРегламентированногоУчета КАК ВалютаКт,
		|	ВременнаяТаблицаНалогиСОрганизации.МСФ КАК ВалютнаяСуммаКт,
		|	&СодержаниеМСФ КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиСОрганизации КАК ВременнаяТаблицаНалогиСОрганизации
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаНалогиСОрганизации.МСФ = 0";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// ФОТФ.
	ТекстЗапроса =	
		"ВЫБРАТЬ
		|	10 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		// Счет Дт/Кт
		|	ВременнаяТаблицаНалогиСОрганизации.СчетЗатрат КАК СчетДт,
		|	&СчетУчетаРасчетовФОТФ КАК СчетКт,
		// Субконто Дт
		|	ВременнаяТаблицаНалогиСОрганизации.СубконтоЗатрат1 КАК СубконтоДт1,
		|	ВременнаяТаблицаНалогиСОрганизации.СубконтоЗатрат2 КАК СубконтоДт2,
		|	ВременнаяТаблицаНалогиСОрганизации.СубконтоЗатрат3 КАК СубконтоДт3,
		// Субконто Кт
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		// Суммы
		|	ВременнаяТаблицаНалогиСОрганизации.ФОТФ КАК Сумма,
		|	&ВалютаРегламентированногоУчета КАК ВалютаДт,
		|	ВременнаяТаблицаНалогиСОрганизации.ФОТФ КАК ВалютнаяСуммаДт,
		|	&ВалютаРегламентированногоУчета КАК ВалютаКт,
		|	ВременнаяТаблицаНалогиСОрганизации.ФОТФ КАК ВалютнаяСуммаКт,
		|	&СодержаниеФОТФ КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиСОрганизации КАК ВременнаяТаблицаНалогиСОрганизации
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаНалогиСОрганизации.ФОТФ = 0";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// ПФФ Корректировка.
	ТекстЗапроса =	
		"ВЫБРАТЬ
		|	11 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		// Счет Дт/Кт
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.СчетЗатрат КАК СчетДт,
		|	&СчетУчетаРасчетовПФФ КАК СчетКт,
		// Субконто Дт
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.СубконтоЗатрат1 КАК СубконтоДт1,
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.СубконтоЗатрат2 КАК СубконтоДт2,
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.СубконтоЗатрат3 КАК СубконтоДт3,
		// Субконто Кт
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		// Суммы
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.ПФФКорректировка КАК Сумма,
		|	&ВалютаРегламентированногоУчета КАК ВалютаДт,
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.ПФФКорректировка КАК ВалютнаяСуммаДт,
		|	&ВалютаРегламентированногоУчета КАК ВалютаКт,
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.ПФФКорректировка КАК ВалютнаяСуммаКт,
		|	&СодержаниеПФФКорр КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиСОрганизацииКорректировки КАК ВременнаяТаблицаНалогиСОрганизацииКорректировки
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаНалогиСОрганизацииКорректировки.ПФФКорректировка = 0";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// МСФ Корректировка.
	ТекстЗапроса =	
		"ВЫБРАТЬ
		|	12 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		// Счет Дт/Кт
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.СчетЗатрат КАК СчетДт,
		|	&СчетУчетаРасчетовМСФ КАК СчетКт,
		// Субконто Дт
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.СубконтоЗатрат1 КАК СубконтоДт1,
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.СубконтоЗатрат2 КАК СубконтоДт2,
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.СубконтоЗатрат3 КАК СубконтоДт3,
		// Субконто Кт
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		// Суммы
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.МСФКорректировка КАК Сумма,
		|	&ВалютаРегламентированногоУчета КАК ВалютаДт,
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.МСФКорректировка КАК ВалютнаяСуммаДт,
		|	&ВалютаРегламентированногоУчета КАК ВалютаКт,
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.МСФКорректировка КАК ВалютнаяСуммаКт,
		|	&СодержаниеМСФКорр КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиСОрганизацииКорректировки КАК ВременнаяТаблицаНалогиСОрганизацииКорректировки
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаНалогиСОрганизацииКорректировки.МСФКорректировка = 0";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// ФОТФ Корректировка.
	ТекстЗапроса =	
		"ВЫБРАТЬ
		|	13 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		// Счет Дт/Кт
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.СчетЗатрат КАК СчетДт,
		|	&СчетУчетаРасчетовФОТФ КАК СчетКт,
		// Субконто Дт
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.СубконтоЗатрат1 КАК СубконтоДт1,
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.СубконтоЗатрат2 КАК СубконтоДт2,
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.СубконтоЗатрат3 КАК СубконтоДт3,
		// Субконто Кт
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		// Суммы
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.ФОТФКорректировка КАК Сумма,
		|	&ВалютаРегламентированногоУчета КАК ВалютаДт,
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.ФОТФКорректировка КАК ВалютнаяСуммаДт,
		|	&ВалютаРегламентированногоУчета КАК ВалютаКт,
		|	ВременнаяТаблицаНалогиСОрганизацииКорректировки.ФОТФКорректировка КАК ВалютнаяСуммаКт,
		|	&СодержаниеФОТФКорр КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиСОрганизацииКорректировки КАК ВременнаяТаблицаНалогиСОрганизацииКорректировки
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаНалогиСОрганизацииКорректировки.ФОТФКорректировка = 0";
	ТекстыЗапроса.Добавить(ТекстЗапроса);

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст = СтрСоединить(ТекстыЗапроса, Символы.ПС + " ОБЪЕДИНИТЬ ВСЕ " + Символы.ПС);
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", СтруктураДополнительныеСвойства.ДляПроведения.ВалютаРегламентированногоУчета);	
	Запрос.УстановитьПараметр("СчетУчета", ПланыСчетов.Хозрасчетный.НачисленнаяЗаработнаяПлата);	
	Запрос.УстановитьПараметр("СчетУчетаРасчетовПФР", СтруктураДополнительныеСвойства.ДляПроведения.ДанныеУчетнойПолитикиПоПерсоналу.СчетУчетаРасчетовПФР);	
	Запрос.УстановитьПараметр("СчетУчетаРасчетовГНПФР", СтруктураДополнительныеСвойства.ДляПроведения.ДанныеУчетнойПолитикиПоПерсоналу.СчетУчетаРасчетовГНПФР);	
	Запрос.УстановитьПараметр("СчетУчетаРасчетовПрофВзнос", СтруктураДополнительныеСвойства.ДляПроведения.ДанныеУчетнойПолитикиПоПерсоналу.СчетУчетаРасчетовПрофВзнос);	
	Запрос.УстановитьПараметр("СчетУчетаРасчетовПФФ", СтруктураДополнительныеСвойства.ДляПроведения.ДанныеУчетнойПолитикиПоПерсоналу.СчетУчетаРасчетовПФФ);	
	Запрос.УстановитьПараметр("СчетУчетаРасчетовМСФ", СтруктураДополнительныеСвойства.ДляПроведения.ДанныеУчетнойПолитикиПоПерсоналу.СчетУчетаРасчетовМСФ);	
	Запрос.УстановитьПараметр("СчетУчетаРасчетовФОТФ", СтруктураДополнительныеСвойства.ДляПроведения.ДанныеУчетнойПолитикиПоПерсоналу.СчетУчетаРасчетовФОТФ);	
	Запрос.УстановитьПараметр("СчетУчетаРасходовПоДоплатеПН", СтруктураДополнительныеСвойства.ДляПроведения.ДанныеУчетнойПолитикиПоПерсоналу.СчетУчетаРасходовПоДоплатеПН);	
	Запрос.УстановитьПараметр("СчетУчетаРасчетовПН", ПланыСчетов.Хозрасчетный.ПодоходныйНалогНаДоходыСотрудников);	
	Запрос.УстановитьПараметр("ДоплатаПНЗаСчетФирмы", СтруктураДополнительныеСвойства.ДляПроведения.ДанныеУчетнойПолитикиПоПерсоналу.ДоплатаПНЗаСчетФирмы);
	
	Запрос.УстановитьПараметр("СодержаниеНачисление", НСтр("ru = 'Начисление заработной платы'"));	
	Запрос.УстановитьПараметр("СодержаниеУдержание", НСтр("ru = 'Удержание заработной платы'"));	
	Запрос.УстановитьПараметр("СодержаниеПФР", НСтр("ru = 'Начисление взносов по пенсионным отчислениям с сотрудника'"));	
	Запрос.УстановитьПараметр("СодержаниеГНПФР", НСтр("ru = 'Начисление взносов по отчислениям в гос.накопительный пенсионный фонд с сотрудника'"));	
	Запрос.УстановитьПараметр("СодержаниеПН", НСтр("ru = 'Начисление взносов по подоходному налогу на доходы сотрудника'"));	
	Запрос.УстановитьПараметр("СодержаниеДоплатаПН", НСтр("ru = 'Начисление взносов по подоходному налогу на доходы сотрудника за счет организации, '"));	
	Запрос.УстановитьПараметр("СодержаниеПрофВзнос", НСтр("ru = 'Профсоюзные отчисления с сотрудника'"));	
	Запрос.УстановитьПараметр("СодержаниеПФФ", НСтр("ru = 'Начисление взносов по пенсионным отчислениям с фирмы'"));	
	Запрос.УстановитьПараметр("СодержаниеМСФ", НСтр("ru = 'Начисление взносов по медицинскому страхованию с фирмы'"));	
	Запрос.УстановитьПараметр("СодержаниеФОТФ", НСтр("ru = 'Начисление взносов по фонду оздоровления трудящихся с фирмы'"));	
	Запрос.УстановитьПараметр("СодержаниеПФФКорр", НСтр("ru = 'Начисление взносов по пенсионным отчислениям с фирмы (корректировка)'"));	
	Запрос.УстановитьПараметр("СодержаниеМСФКорр", НСтр("ru = 'Начисление взносов по медицинскому страхованию с фирмы (корректировка)'"));	
	Запрос.УстановитьПараметр("СодержаниеФОТФКорр", НСтр("ru = 'Начисление взносов по фонду оздоровления трудящихся с фирмы (корректировка)'"));	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаХозрасчетный", РезультатЗапроса.Выгрузить());
КонецПроцедуры // СформироватьТаблицаХозрасчетный()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаУдержания(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(ВременнаяТаблицаШапка.Дата, МЕСЯЦ) КАК ПериодРегистрации,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаУдержания.ВидРасчета КАК ВидРасчета,
		|	ВременнаяТаблицаУдержания.ФизЛицо КАК ФизЛицо,
		|	ВременнаяТаблицаУдержания.Размер КАК Размер,
		|	ВременнаяТаблицаУдержания.Результат КАК Результат
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаУдержания КАК ВременнаяТаблицаУдержания
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаУдержания.Результат = 0";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаУдержания", РезультатЗапроса.Выгрузить());
КонецПроцедуры // СформироватьТаблицаУдержания()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаСоциальныйФонд(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо КАК ФизЛицо,
		|	ВременнаяТаблицаНалогиПоЗарплате.ПФФ КАК ПФФ,
		|	ВременнаяТаблицаНалогиПоЗарплате.ПФР КАК ПФР,
		|	ВременнаяТаблицаНалогиПоЗарплате.ГНПФР КАК ГНПФР,
		|	ВременнаяТаблицаНалогиПоЗарплате.МСФ КАК МСФ,
		|	ВременнаяТаблицаНалогиПоЗарплате.МСР КАК МСР,
		|	ВременнаяТаблицаНалогиПоЗарплате.ФОТФ КАК ФОТФ,
		|	ВременнаяТаблицаНалогиПоЗарплате.ФОТР КАК ФОТР,
		|	ВременнаяТаблицаНалогиПоЗарплате.ОДСФ КАК ОблагаемаяСуммаДохода,
		|	ВременнаяТаблицаНалогиПоЗарплате.ПФФКорректировка КАК ПФФКорректировка,
		|	ВременнаяТаблицаНалогиПоЗарплате.МСФКорректировка КАК МСФКорректировка,
		|	ВременнаяТаблицаНалогиПоЗарплате.ФОТФКорректировка КАК ФОТФКорректировка,
		|	ВременнаяТаблицаНалогиПоЗарплате.СреднемесячнаяЗПСФПриведенная КАК ПриведеннаяСЗП
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиПоЗарплате КАК ВременнаяТаблицаНалогиПоЗарплате
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаНалогиПоЗарплате.ОДСФ > 0";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСоциальныйФонд", РезультатЗапроса.Выгрузить());
КонецПроцедуры // СформироватьТаблицаСоциальныйФонд()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаПодоходныйНалог(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =		
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо КАК ФизЛицо,
		|	ВременнаяТаблицаНалогиПоЗарплате.ДоплатаПН КАК ДоплатаПН,
		|	ВременнаяТаблицаНалогиПоЗарплате.МРД КАК МРД,
		|	ВременнаяТаблицаНалогиПоЗарплате.ОДПН КАК ОДПН,
		|	ВременнаяТаблицаНалогиПоЗарплате.ОДСФ КАК ОДСФ,
		|	ВременнаяТаблицаНалогиПоЗарплате.Вычеты КАК Вычеты,
		// Из суммы ПН всегда исключанием сумму доплаты.
		|	ВременнаяТаблицаНалогиПоЗарплате.ПН - ВременнаяТаблицаНалогиПоЗарплате.ДоплатаПН КАК ПН,
		|	ВременнаяТаблицаНалогиПоЗарплате.ПФР КАК ПФР,
		|	ВременнаяТаблицаНалогиПоЗарплате.ГНПФР КАК ГНПФР,
		|	ВременнаяТаблицаНалогиПоЗарплате.ПФРДляРасчетаПН КАК ПФРДляРасчетаПН,
		|	ВременнаяТаблицаНалогиПоЗарплате.ГНПФРДляРасчетаПН КАК ГНПФРДляРасчетаПН,
		|	ВременнаяТаблицаНалогиПоЗарплате.МСР КАК МСР,
		|	ВременнаяТаблицаНалогиПоЗарплате.ФОТР КАК ФОТР,
		|	ВременнаяТаблицаНалогиПоЗарплате.СуммаНачислено КАК ВсегоНачислено,
		|	ВременнаяТаблицаНалогиПоЗарплате.ОтработаноДней КАК ОтработаноДней
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиПоЗарплате КАК ВременнаяТаблицаНалогиПоЗарплате
		|		ПО (ИСТИНА)
		|ГДЕ
		|	(ВременнаяТаблицаНалогиПоЗарплате.СуммаНачислено > 0
		|			ИЛИ ВременнаяТаблицаНалогиПоЗарплате.Вычеты > 0)";		
		РезультатЗапроса = Запрос.Выполнить();
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПодоходныйНалог", РезультатЗапроса.Выгрузить());
КонецПроцедуры // СформироватьТаблицаПодоходныйНалог()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаНалогиПоЗаработнойПлате(ДокументСсылка,СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо КАК ФизЛицо,
		|	ЗНАЧЕНИЕ(ПланВидовРасчета.ВидыНалогов.ПФР) КАК ВидНалога,
		|	ВременнаяТаблицаНалогиПоЗарплате.ПФР КАК Сумма
		|ПОМЕСТИТЬ ВременнаяТаблицаНалогиИСуммы
		|ИЗ
		|	ВременнаяТаблицаНалогиПоЗарплате КАК ВременнаяТаблицаНалогиПоЗарплате
		|ГДЕ
		|	НЕ ВременнаяТаблицаНалогиПоЗарплате.ПФР = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо,
		|	ЗНАЧЕНИЕ(ПланВидовРасчета.ВидыНалогов.ГНПФР),
		|	ВременнаяТаблицаНалогиПоЗарплате.ГНПФР
		|ИЗ
		|	ВременнаяТаблицаНалогиПоЗарплате КАК ВременнаяТаблицаНалогиПоЗарплате
		|ГДЕ
		|	НЕ ВременнаяТаблицаНалогиПоЗарплате.ГНПФР = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо,
		|	ЗНАЧЕНИЕ(ПланВидовРасчета.ВидыНалогов.ПН),
		|	ВременнаяТаблицаНалогиПоЗарплате.ПН
		|ИЗ
		|	ВременнаяТаблицаНалогиПоЗарплате КАК ВременнаяТаблицаНалогиПоЗарплате
		|ГДЕ
		|	НЕ ВременнаяТаблицаНалогиПоЗарплате.ПН = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо,
		|	ЗНАЧЕНИЕ(ПланВидовРасчета.ВидыНалогов.Профвзнос),
		|	ВременнаяТаблицаНалогиПоЗарплате.ПрофВзнос
		|ИЗ
		|	ВременнаяТаблицаНалогиПоЗарплате КАК ВременнаяТаблицаНалогиПоЗарплате
		|ГДЕ
		|	НЕ ВременнаяТаблицаНалогиПоЗарплате.ПрофВзнос = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо,
		|	ЗНАЧЕНИЕ(ПланВидовРасчета.ВидыНалогов.ПФФ),
		|	ВременнаяТаблицаНалогиПоЗарплате.ПФФ
		|ИЗ
		|	ВременнаяТаблицаНалогиПоЗарплате КАК ВременнаяТаблицаНалогиПоЗарплате
		|ГДЕ
		|	НЕ ВременнаяТаблицаНалогиПоЗарплате.ПФФ = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо,
		|	ЗНАЧЕНИЕ(ПланВидовРасчета.ВидыНалогов.МСФ),
		|	ВременнаяТаблицаНалогиПоЗарплате.МСФ
		|ИЗ
		|	ВременнаяТаблицаНалогиПоЗарплате КАК ВременнаяТаблицаНалогиПоЗарплате
		|ГДЕ
		|	НЕ ВременнаяТаблицаНалогиПоЗарплате.МСФ = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо,
		|	ЗНАЧЕНИЕ(ПланВидовРасчета.ВидыНалогов.ФОТФ),
		|	ВременнаяТаблицаНалогиПоЗарплате.ФОТФ
		|ИЗ
		|	ВременнаяТаблицаНалогиПоЗарплате КАК ВременнаяТаблицаНалогиПоЗарплате
		|ГДЕ
		|	НЕ ВременнаяТаблицаНалогиПоЗарплате.ФОТФ = 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаНалогиИСуммы.ФизЛицо КАК ФизЛицо,
		|	ВременнаяТаблицаНалогиИСуммы.ВидНалога КАК ВидНалога,
		|	ВременнаяТаблицаНалогиИСуммы.Сумма КАК Сумма
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиИСуммы КАК ВременнаяТаблицаНалогиИСуммы
		|		ПО (ИСТИНА)";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаНалогиПоЗаработнойПлате", РезультатЗапроса.Выгрузить());
КонецПроцедуры

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицуДляОтчетаПоЕжемесячномуОтчетуПоСФ(ДокументСсылка, СтруктураДополнительныеСвойства);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаСотрудники.Подразделение КАК Подразделение,
		|	ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо КАК ФизЛицо,
		|	ВременнаяТаблицаНалогиПоЗарплате.Статус.Категория КАК Категория,
		|	ВременнаяТаблицаНалогиПоЗарплате.Статус КАК Статус,
		|	ВременнаяТаблицаНалогиПоЗарплате.ОДСФ КАК ФОТ,
		|	ВременнаяТаблицаНалогиПоЗарплате.ГНПФР КАК ГНПФР,
		|	ВременнаяТаблицаНалогиПоЗарплате.ПФР + ВременнаяТаблицаНалогиПоЗарплате.ПФФ + ВременнаяТаблицаНалогиПоЗарплате.МСФ + ВременнаяТаблицаНалогиПоЗарплате.ФОТФ + ВременнаяТаблицаНалогиПоЗарплате.МСР + ВременнаяТаблицаНалогиПоЗарплате.ПФФКорректировка + ВременнаяТаблицаНалогиПоЗарплате.МСФКорректировка + ВременнаяТаблицаНалогиПоЗарплате.ФОТФКорректировка КАК НачисленныеВзносы,
		|	ВременнаяТаблицаНалогиПоЗарплате.ОтработаноДней КАК ФактДней,
		|	ТаблицаНормыДней.НормаДней,
		|	ВременнаяТаблицаНалогиПоЗарплате.СреднемесячнаяЗПСФПриведенная КАК ПриведеннаяСЗП,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаНалогиПоЗарплате.СреднемесячнаяЗПСФПриведенная *  ЕСТЬNULL(ВременнаяТаблицаСотрудники.ЗанимаемыхСтавок, 1) > ВременнаяТаблицаНалогиПоЗарплате.ОДСФ
		|			ТОГДА ВременнаяТаблицаНалогиПоЗарплате.СреднемесячнаяЗПСФПриведенная *  ЕСТЬNULL(ВременнаяТаблицаСотрудники.ЗанимаемыхСтавок, 1) - ВременнаяТаблицаНалогиПоЗарплате.ОДСФ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ДопФОТ	
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалогиПоЗарплате КАК ВременнаяТаблицаНалогиПоЗарплате
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники
		|				ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаНормыДней КАК ТаблицаНормыДней
		|				ПО ВременнаяТаблицаСотрудники.ГрафикРаботы = ТаблицаНормыДней.ГрафикРаботы
		|			ПО ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо = ВременнаяТаблицаСотрудники.ФизЛицо
		|		ПО (ИСТИНА)	
		|ГДЕ
		|	ВременнаяТаблицаНалогиПоЗарплате.ОДСФ > 0";
		
	Запрос.УстановитьПараметр("СреднемесячнаяЗПСФ", СтруктураДополнительныеСвойства.ДляПроведения.ДанныеУчетнойПолитикиПоПерсоналу.СреднемесячнаяЗПСФ);
	Запрос.УстановитьПараметр("ПроцентОтСЗПСФ", СтруктураДополнительныеСвойства.ДляПроведения.ДанныеУчетнойПолитикиПоПерсоналу.ПроцентОтСЗПСФ);

	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДанныеДляОтчетаПоЕжемесячномуСФ", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства, ДокументОбъект) Экспорт
	
	// 1. Получение данных этого документа
	// 2. Получение начислений за месяц
	// 3. Получение удержаний за месяц
	// 4. Формирование таблиц для движения по регистрам	
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДокумента.Дата КАК Дата,
		|	ТаблицаДокумента.Организация КАК Организация
		|ПОМЕСТИТЬ ВременнаяТаблицаШапка
		|ИЗ
		|	Документ.ЗакрытиеМесяцаЗП КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.ФизЛицо КАК ФизЛицо,
		|	ТаблицаДокумента.Статус КАК Статус,
		|	ТаблицаДокумента.ПрименятьВычет КАК ПрименятьВычет,
		|	ТаблицаДокумента.СуммаНачислено КАК СуммаНачислено,
		|	ТаблицаДокумента.ПФР КАК ПФР,
		|	ТаблицаДокумента.ГНПФР КАК ГНПФР,
		|	ТаблицаДокумента.ПФРДляРасчетаПН КАК ПФРДляРасчетаПН,
		|	ТаблицаДокумента.ГНПФРДляРасчетаПН КАК ГНПФРДляРасчетаПН,
		|	ТаблицаДокумента.ПН КАК ПН,
		|	ТаблицаДокумента.ПрофВзнос КАК ПрофВзнос,
		|	ТаблицаДокумента.ПФФ КАК ПФФ,
		|	ТаблицаДокумента.МСФ КАК МСФ,
		|	ТаблицаДокумента.ФОТФ КАК ФОТФ,
		|	ТаблицаДокумента.МСР КАК МСР,
		|	ТаблицаДокумента.ФОТР КАК ФОТР,
		|	ТаблицаДокумента.Вычеты КАК Вычеты,
		|	ТаблицаДокумента.ОДСФ КАК ОДСФ,
		|	ТаблицаДокумента.ОДПН КАК ОДПН,
		|	ТаблицаДокумента.ОДПрофВзнос КАК ОДПрофВзнос,
		|	ТаблицаДокумента.СуммаДопДоход КАК СуммаДопДоход,
		|	ТаблицаДокумента.ДоплатаПН КАК ДоплатаПН,
		|	ТаблицаДокумента.МРД КАК МРД,
		|	ТаблицаДокумента.ОтработаноДней КАК ОтработаноДней,
		|	ТаблицаДокумента.ПФФКорректировка КАК ПФФКорректировка,
		|	ТаблицаДокумента.МСФКорректировка КАК МСФКорректировка,
		|	ТаблицаДокумента.ФОТФКорректировка КАК ФОТФКорректировка,
		|	ТаблицаДокумента.СреднемесячнаяЗПСФПриведенная КАК СреднемесячнаяЗПСФПриведенная
		|ПОМЕСТИТЬ ВременнаяТаблицаНалогиПоЗарплате
		|ИЗ
		|	Документ.ЗакрытиеМесяцаЗП.НалогиПоЗарплате КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.СчетЗатрат КАК СчетЗатрат,
		|	ТаблицаДокумента.СубконтоЗатрат1 КАК СубконтоЗатрат1,
		|	ТаблицаДокумента.СубконтоЗатрат2 КАК СубконтоЗатрат2,
		|	ТаблицаДокумента.СубконтоЗатрат3 КАК СубконтоЗатрат3,
		|	ТаблицаДокумента.ПФФ КАК ПФФ,
		|	ТаблицаДокумента.МСФ КАК МСФ,
		|	ТаблицаДокумента.ФОТФ КАК ФОТФ
		|ПОМЕСТИТЬ ВременнаяТаблицаНалогиСОрганизации
		|ИЗ
		|	Документ.ЗакрытиеМесяцаЗП.НалогиСОрганизации КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.СчетЗатрат КАК СчетЗатрат,
		|	ТаблицаДокумента.СубконтоЗатрат1 КАК СубконтоЗатрат1,
		|	ТаблицаДокумента.СубконтоЗатрат2 КАК СубконтоЗатрат2,
		|	ТаблицаДокумента.СубконтоЗатрат3 КАК СубконтоЗатрат3,
		|	ТаблицаДокумента.ПФФКорректировка КАК ПФФКорректировка,
		|	ТаблицаДокумента.МСФКорректировка КАК МСФКорректировка,
		|	ТаблицаДокумента.ФОТФКорректировка КАК ФОТФКорректировка
		|ПОМЕСТИТЬ ВременнаяТаблицаНалогиСОрганизацииКорректировки
		|ИЗ
		|	Документ.ЗакрытиеМесяцаЗП.НалогиСОрганизацииКорректировки КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.ФизЛицо КАК ФизЛицо,
		|	ТаблицаДокумента.ВидРасчета КАК ВидРасчета,
		|	ТаблицаДокумента.ВидРасчета.СчетУчета КАК СчетУчета,
		|	ТаблицаДокумента.Контрагент КАК Контрагент,
		|	ТаблицаДокумента.Размер КАК Размер,
		|	ТаблицаДокумента.Результат КАК Результат
		|ПОМЕСТИТЬ ВременнаяТаблицаУдержания
		|ИЗ
		|	Документ.ЗакрытиеМесяцаЗП.Удержания КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Начисления.ФизЛицо КАК ФизЛицо,
		|	Начисления.СпособОтражения.СчетЗатрат КАК СчетЗатрат,
		|	Начисления.СпособОтражения.СубконтоЗатрат1 КАК СубконтоЗатрат1,
		|	Начисления.СпособОтражения.СубконтоЗатрат2 КАК СубконтоЗатрат2,
		|	Начисления.СпособОтражения.СубконтоЗатрат3 КАК СубконтоЗатрат3,
		|	СУММА(Начисления.Результат) КАК Результат
		|ПОМЕСТИТЬ ВременнаяТаблицаНачисленияЗаМесяц
		|ИЗ
		|	РегистрРасчета.Начисления КАК Начисления
		|ГДЕ
		|	Начисления.ПериодРегистрации МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Начисления.Организация = &Организация
		|	И НЕ Начисления.Результат = 0
		|	И НЕ Начисления.ВидРасчета.ДополнительныйДоход
		|	И НЕ Начисления.Регистратор = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	Начисления.ФизЛицо,
		|	Начисления.СпособОтражения,
		|	Начисления.СпособОтражения.СчетЗатрат,
		|	Начисления.СпособОтражения.СубконтоЗатрат1,
		|	Начисления.СпособОтражения.СубконтоЗатрат2,
		|	Начисления.СпособОтражения.СубконтоЗатрат3
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Удержания.ФизЛицо КАК ФизЛицо,
		|	Удержания.ВидРасчета.СчетУчета КАК СчетУчета,
		|	СУММА(Удержания.Результат) КАК Результат
		|ПОМЕСТИТЬ ВременнаяТаблицаУдержанияЗаМесяц
		|ИЗ
		|	РегистрРасчета.Удержания КАК Удержания
		|ГДЕ
		|	Удержания.ПериодРегистрации МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Удержания.Организация = &Организация
		|	И НЕ Удержания.Результат = 0
		|	И НЕ Удержания.Регистратор = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	Удержания.ФизЛицо,
		|	Удержания.ВидРасчета.СчетУчета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СотрудникиСрезПоследних.ФизЛицо КАК ФизЛицо,
		|	СотрудникиСрезПоследних.Подразделение КАК Подразделение,
		|	СотрудникиСрезПоследних.ГрафикРаботы КАК ГрафикРаботы,
		|	СотрудникиСрезПоследних.ЗанимаемыхСтавок КАК ЗанимаемыхСтавок
		|ПОМЕСТИТЬ ВременнаяТаблицаСрезСотрудники
		|ИЗ
		|	РегистрСведений.Сотрудники.СрезПоследних(
		|			&КонецПериода,
		|			Организация = &Организация
		|				И ФизЛицо В
		|					(ВЫБРАТЬ
		|						ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо
		|					ИЗ
		|						ВременнаяТаблицаНалогиПоЗарплате КАК ВременнаяТаблицаНалогиПоЗарплате)) КАК СотрудникиСрезПоследних
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СотрудникиПоТрудовымСоглашениямСрезПоследних.ФизЛицо,
		|	СотрудникиПоТрудовымСоглашениямСрезПоследних.Подразделение,
		|	СотрудникиПоТрудовымСоглашениямСрезПоследних.ГрафикРаботы,
		|	1
		|ИЗ
		|	РегистрСведений.СотрудникиПоТрудовымСоглашениям.СрезПоследних(
		|			&КонецПериода,
		|			Организация = &Организация
		|				И ФизЛицо В
		|					(ВЫБРАТЬ
		|						ВременнаяТаблицаНалогиПоЗарплате.ФизЛицо
		|					ИЗ
		|						ВременнаяТаблицаНалогиПоЗарплате КАК ВременнаяТаблицаНалогиПоЗарплате)) КАК СотрудникиПоТрудовымСоглашениямСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаСрезСотрудники.ФизЛицо КАК ФизЛицо,
		|	МАКСИМУМ(ВременнаяТаблицаСрезСотрудники.Подразделение) КАК Подразделение,
		|	МАКСИМУМ(ВременнаяТаблицаСрезСотрудники.ГрафикРаботы) КАК ГрафикРаботы,
		|	МАКСИМУМ(ВременнаяТаблицаСрезСотрудники.ЗанимаемыхСтавок) КАК ЗанимаемыхСтавок
		|ПОМЕСТИТЬ ВременнаяТаблицаСотрудники
		|ИЗ
		|	ВременнаяТаблицаСрезСотрудники КАК ВременнаяТаблицаСрезСотрудники
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаСрезСотрудники.ФизЛицо
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ГрафикиРаботы.ГрафикРаботы КАК ГрафикРаботы,
		|	СУММА(ГрафикиРаботы.ЗначениеДней) КАК НормаДней
		|ПОМЕСТИТЬ ТаблицаНормыДней
		|ИЗ
		|	РегистрСведений.КалендариГрафиковРабот КАК ГрафикиРаботы
		|ГДЕ
		|	ГрафикиРаботы.ГрафикРаботы В
		|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				ВременнаяТаблицаСотрудники.ГрафикРаботы
		|			ИЗ
		|				ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники)
		|	И ГрафикиРаботы.Год = ГОД(&КонецПериода)
		|	И ГрафикиРаботы.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|
		|СГРУППИРОВАТЬ ПО
		|	ГрафикиРаботы.ГрафикРаботы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВременнаяТаблицаСрезСотрудники";
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(СтруктураДополнительныеСвойства.ДляПроведения.Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(СтруктураДополнительныеСвойства.ДляПроведения.Дата));
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	
	Запрос.Выполнить();
	
	СформироватьТаблицаУдержания(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаСоциальныйФонд(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаПодоходныйНалог(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаНалогиПоЗаработнойПлате(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства);
	
	СформироватьТаблицуДляОтчетаПоЕжемесячномуОтчетуПоСФ(ДокументСсылка, СтруктураДополнительныеСвойства);
		
КонецПроцедуры // ИнициализироватьДанныеДокумента()

#КонецОбласти

#Область ВерсионированиеОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
//@skip-warning
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#КонецЕсли