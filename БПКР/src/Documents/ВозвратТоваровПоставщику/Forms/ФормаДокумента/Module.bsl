#Область ОбработчикиСобытийФормы

// Процедура - обработчик события ПриСозданииНаСервере.
// В процедуре осуществляется
// - инициализация реквизитов формы,
// - установка параметров функциональных опций формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Установка реквизитов формы.	
	ДатаДокумента = Объект.Дата;
	
	ПризнакСтраны = Объект.Контрагент.ПризнакСтраны;
	
	ДанныеУчетнойПолитики = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьДанныеУчетнойПолитикиОрганизаций(ДатаДокумента, Объект.Организация);
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();	
	
	Если ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		СтруктураКурсКратность 	= РаботаСКурсамиВалют.ПолучитьКурсВалюты(Объект.ДоговорКонтрагента.ВалютаРасчетов, ДатаДокумента);	
		КурсНБКР      			= ?(СтруктураКурсКратность.Курс = 0, 1, СтруктураКурсКратность.Курс);
		КратностьНБКР 			= ?(СтруктураКурсКратность.Кратность = 0, 1, СтруктураКурсКратность.Кратность);
	КонецЕсли;	
	
	УстановитьФункциональныеОпцииФормы();

	БухгалтерскийУчетКлиентСервер.УстановитьКартинкуДляКомментария(Элементы.СтраницаДополнительно, Объект.Комментарий);
	
	Если ПолучитьФункциональнуюОпцию("УказыватьТочностьЦены") Тогда 
		ЗаполнитьСписокВыбораТочностьЦены();	
	КонецЕсли;	

	Если БухгалтерскийУчетВызовСервераПовтИсп.ВедетсяУчетЭСФ() Тогда
		ПолучитьПредставлениеЭСФ();
	КонецЕсли;	

	// Установить видимость и доступность элементов формы
	УстановитьВидимостьДоступностьЭлементов();
	
	// КопированиеСтрокТабличныхЧастей
	КопированиеТабличнойЧастиСервер.ПриСозданииНаСервере(Элементы, "Товары");
	КопированиеТабличнойЧастиСервер.ПриСозданииНаСервере(Элементы, "Услуги");
	// Конец КопированиеСтрокТабличныхЧастей
	
	// РаботаСФормами
	РаботаСФормамиСервер.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец РаботаСФормами
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.ГруппаВажныеКоманды;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	   		
	// СтандартныеПодсистемы.РаботаСФайлами
	ПараметрыГиперссылки = РаботаСФайлами.ГиперссылкаФайлов();
	ПараметрыГиперссылки.Размещение = "КоманднаяПанель";
	РаботаСФайлами.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыГиперссылки);
	// Конец СтандартныеПодсистемы.РаботаСФайлами

	// ПодключаемоеОборудование
	ИспользоватьПодключаемоеОборудование = ПодключаемоеОборудованиеБППовтИсп.ИспользоватьПодключаемоеОборудование();
	// Конец ПодключаемоеОборудование
КонецПроцедуры

// Процедура - обработчик события ПриЧтенииНаСервере.
//
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	УстановитьНачальныеСвойстваСубконтоТаблицы();

	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом 
	
КонецПроцедуры

// Процедура - обработчик события ПриОткрытии.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьТекущуюСтраницу();
	ОбновитьПодвалФормы();

	// Проверка заполнения возврата на основании поступления при создании на основании.
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) И ЗначениеЗаполнено(Объект.ДокументОснование)
		И Объект.Товары.Количество() = 0 И Объект.Услуги.Количество() = 0 И Объект.ОС.Количество() = 0 Тогда
		
		ТекстСообщения = СтрШаблон(НСтр("ru = 'По поступлению ""%1"" возвращены все позиции.'"), Объект.ДокументОснование);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;	
	
	УстановитьФорматЦены();

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// ПодключаемоеОборудование
	Если ИспользоватьПодключаемоеОборудование Тогда 
		МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(Неопределено, ЭтотОбъект, "СканерШтрихкода");
	КонецЕсли;	
	// Конец ПодключаемоеОборудование
	
	// СтандартныеПодсистемы.РаботаСФайлами
	РаботаСФайламиКлиент.ПриОткрытии(ЭтотОбъект, Отказ);
	// Конец СтандартныеПодсистемы.РаботаСФайлами
КонецПроцедуры

// Процедура - обработчик события ПриЗакрытии.
//
&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	// ПодключаемоеОборудование
	Если ИспользоватьПодключаемоеОборудование Тогда 
		МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(Неопределено, ЭтотОбъект);
	КонецЕсли;	
	// Конец ПодключаемоеОборудование
КонецПроцедуры

// Процедура - обработчик события ОбработкаОповещения.
//
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "МодифицированДоговораКонтрагента"
		И Параметр = Объект.ДоговорКонтрагента Тогда
		ОбработатьИзменениеДоговора();
		
	ИначеЕсли ИмяСобытия = "ПодборНоменклатурыПроизведен" 
		И ТипЗнч(Параметр) = Тип("Структура")
		// Проверка на владельца формы
		И Источник <> Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000")
		И Источник = УникальныйИдентификатор Тогда
		
		АдресЗапасовВХранилище = Параметр.АдресЗапасовВХранилище;
		ПолучитьТоварыИзХранилища(АдресЗапасовВХранилище);
		
		ОбновитьПодвалФормы();
		
	ИначеЕсли ИмяСобытия = "СчетФактураПолученныйПослеЗаписи"
		И НЕ Параметр.Найти(Объект.Ссылка) = Неопределено Тогда  
		
		ЗаполнитьСчетФактураПолученный();
		УстановитьВидимостьДоступностьЭлементов();	
		
	ИначеЕсли ИмяСобытия = "ЭлектронныйСчетФактураПолученныйПослеЗаписи"
		И НЕ Параметр.Найти(Объект.Ссылка) = Неопределено Тогда  
		
		Если БухгалтерскийУчетВызовСервераПовтИсп.ВедетсяУчетЭСФ() Тогда
			ПолучитьПредставлениеЭСФ();
		КонецЕсли;	

	Иначе
		ОбщегоНазначенияБПКлиент.ОбработкаОповещенияФормыДокумента(ЭтаФорма, Объект.Ссылка, ИмяСобытия, Параметр, Источник);
	КонецЕсли;
	
	// КопированиеСтрокТабличныхЧастей
	Если ИмяСобытия = "БуферОбменаТабличнаяЧастьКопированиеСтрок" Тогда
		КопированиеТабличнойЧастиКлиент.ОбработкаОповещения(Элементы, "Товары");
		КопированиеТабличнойЧастиКлиент.ОбработкаОповещения(Элементы, "Услуги");
	КонецЕсли;
	// Конец КопированиеСтрокТабличныхЧастей
	    	  	
	// СтандартныеПодсистемы.РаботаСФайлами
	РаботаСФайламиКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия);
	// Конец СтандартныеПодсистемы.РаботаСФайлами

	// ПодключаемоеОборудование
	Если Источник = "ПодключаемоеОборудование"
		И ВводДоступен() Тогда
		Если ИмяСобытия = "ScanData" Тогда
			// Преобразуем предварительно к ожидаемому формату.
			Если Параметр[1] = Неопределено Тогда
				ТекШтрихкод = Параметр[0]; // Достаем штрихкод из основных данных
			Иначе
				ТекШтрихкод = Параметр[1][1]; // Достаем штрихкод из дополнительных данных
			КонецЕсли;
			
			Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаТовары Тогда 
				ПоискПоШтрихкодуЗавершение(ТекШтрихкод, Новый Структура("ТекШтрихкод, ИмяТабличнойЧасти", ТекШтрихкод, "Товары"));
			ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаУслуги Тогда
				ПоискПоШтрихкодуЗавершение(ТекШтрихкод, Новый Структура("ТекШтрихкод, ИмяТабличнойЧасти", ТекШтрихкод, "Услуги"));
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;
	// Конец ПодключаемоеОборудование
КонецПроцедуры

// Процедура - обработчик события ПослеЗаписиНаСервере.
//
&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)	
	
	// РаботаСФормами
	РаботаСФормамиСервер.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец РаботаСФормами
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.КонтрольВеденияУчета
	КонтрольВеденияУчета.ПослеЗаписиНаСервере(ТекущийОбъект);
	// Конец СтандартныеПодсистемы.КонтрольВеденияУчета
КонецПроцедуры

// Процедура - обработчик события ПослеЗаписи.
//
&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Процедура - обработчик события ПриИзменении поля ввода Дата.
// В процедуре определяется ситуация, когда при изменении своей даты документ 
// оказывается в другом периоде нумерации документов, и в этом случае
// присваивает документу новый уникальный номер.
// Переопределяет соответствующий параметр формы.
//
&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	// Обработка события изменения даты.
	ДатаПередИзменением = ДатаДокумента;
	ДатаДокумента = Объект.Дата;
	Если Объект.Дата <> ДатаПередИзменением Тогда
		СтруктураДанные = ПолучитьДанныеДатаПриИзменении(Объект.Ссылка, Объект.Дата, ДатаПередИзменением, Объект.ВалютаДокумента);
		Если СтруктураДанные.РазностьДат <> 0 Тогда
			Объект.Номер = "";
		КонецЕсли;
	КонецЕсли;
	
	БухгалтерскийУчетВызовСервера.УчетнаяПолитикаСуществует(Объект.Организация, ДатаДокумента, Истина, Объект.Ссылка);				
	УстановитьФункциональныеОпцииФормы();
	ОбработатьИзменениеУчетнойПолитики();
	ОбработатьИзменениеДоговора();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Организация.
//
&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	Объект.Номер = "";
	
	БухгалтерскийУчетВызовСервера.УчетнаяПолитикаСуществует(Объект.Организация, ДатаДокумента, Истина, Объект.Ссылка);
	УстановитьФункциональныеОпцииФормы();	
	ОбработатьИзменениеУчетнойПолитики();	
	
	// Договор автоматически очистился- заполнение по умолчанию.
	СтруктураДанные = ПолучитьДанныеКонтрагентПриИзменении(Объект.Контрагент, Объект.Организация);
	Объект.ДоговорКонтрагента = СтруктураДанные.ДоговорКонтрагента;
	ОбработатьИзменениеДоговора();
	
	Если БухгалтерскийУчетВызовСервераПовтИсп.ВедетсяУчетЭСФ() Тогда
		ПолучитьПредставлениеЭСФ();
	КонецЕсли;
	
	УстановитьВидимостьДоступностьЭлементов();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Вид операции.
//
&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент)
	УстановитьВидимостьДоступностьЭлементов();
КонецПроцедуры

&НаКлиенте
Процедура БезналичныйРасчетПриИзменении(Элемент)
	
	ПараметрыРасчета = ПодготовитьПараметрыРасчета();
	
	Если Объект.БезналичныйРасчет Тогда
		УстановитьЗначениеСтавкиНСП("Товары");
		УстановитьЗначениеСтавкиНСП("Услуги");
		УстановитьЗначениеСтавкиНСП("ОС");
	КонецЕсли;	
	
	// Расчет налогов
	ОбработкаТабличныхЧастейКлиентСервер.ПересчитатьНалогиТабличнойЧастиПоступление(
		Объект, "Товары", ПараметрыРасчета);
		
	ОбработкаТабличныхЧастейКлиентСервер.ПересчитатьНалогиТабличнойЧастиПоступление(
		Объект, "Услуги", ПараметрыРасчета);
		
	// Для ТЧ "ОС" не расчитывается "База НДС" и не расчитывается НДС от базы.
	ПараметрыРасчета = ПодготовитьПараметрыРасчета(Истина);	
		
	ОбработкаТабличныхЧастейКлиентСервер.ПересчитатьНалогиТабличнойЧастиПоступление(
		Объект, "ОС", ПараметрыРасчета);
		
	Для Каждого СтрокаТабличнойЧасти Из Объект.Товары Цикл	 
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьЦенуСтрокиТабличнойЧасти(
			СтрокаТабличнойЧасти,ПараметрыРасчета);
	КонецЦикла;
	
	Для Каждого СтрокаТабличнойЧасти Из Объект.Услуги Цикл	
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьЦенуСтрокиТабличнойЧасти(
			СтрокаТабличнойЧасти,ПараметрыРасчета);	
	КонецЦикла;	
		
	// Установить видимость и доступность элементов формы
	УстановитьВидимостьДоступностьЭлементов();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Контрагент.
//
&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)

	СтруктураДанные = ПолучитьДанныеКонтрагентПриИзменении(Объект.Контрагент, Объект.Организация);
	
	Объект.ДоговорКонтрагента = СтруктураДанные.ДоговорКонтрагента;
	ПризнакСтраны = СтруктураДанные.ПризнакСтраны;
	
	Если ПризнакСтраны <> ПредопределенноеЗначение("Перечисление.ПризнакиСтраны.ЕАЭС") Тогда
		Для Каждого СтрокаТабличнойЧасти Из Объект.Товары Цикл
			СтрокаТабличнойЧасти.СуммаАкциза = 0;
			СтрокаТабличнойЧасти.СуммаАкцизаПоПоступлению = 0;
		КонецЦикла;
		
		Для Каждого СтрокаТабличнойЧасти Из Объект.Услуги Цикл
			СтрокаТабличнойЧасти.СуммаАкциза = 0;
		КонецЦикла;
	КонецЕсли;	
	
	Если СтруктураДанные.СЭЗ Тогда
		Объект.ЗначениеСтавкиНДС = 0;
		Объект.ЭтоПоступлениеПоЧеку = Ложь;
	КонецЕсли;	
	
	// Изменение признака страны
	Если ПризнакСтраны = ПредопределенноеЗначение("Перечисление.ПризнакиСтраны.ЕАЭС") Тогда
		Объект.БезналичныйРасчет = Истина;
		Объект.ЭтоПоступлениеПоЧеку = Ложь;
		// Ставки
		УстановитьЗначениеСтавкиНСП("Товары");
		УстановитьЗначениеСтавкиНСП("Услуги");
		УстановитьЗначениеСтавкиНСП("ОС");
	ИначеЕсли ПризнакСтраны = ПредопределенноеЗначение("Перечисление.ПризнакиСтраны.ИмпортЭкспорт") Тогда	
		Объект.БезналичныйРасчет = Истина;
		Объект.ЭтоПоступлениеПоЧеку = Ложь;
		// Ставки
		Объект.ЗначениеСтавкиНДС = 0;
		УстановитьЗначениеСтавкиНСП("Товары");
		УстановитьЗначениеСтавкиНСП("Услуги");
		УстановитьЗначениеСтавкиНСП("ОС");
	Иначе
		Объект.ПоступлениеОтНерезидента = Ложь;
	КонецЕсли;	

	ОбработатьИзменениеДоговора();  
	УстановитьВидимостьДоступностьЭлементов();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Договор контрагента.
//
&НаКлиенте
Процедура ДоговорКонтрагентаПриИзменении(Элемент)
	ОбработатьИзменениеДоговора();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении флажка Это поступление без НДС.
//
&НаКлиенте
Процедура ЭтоПоступлениеБезНДСПриИзменении(Элемент)
	
	// Сброс признака ЭтоПоступлениеПоЧеку.
	Если Объект.ЭтоПоступлениеПоЧеку Тогда 
		Объект.ЭтоПоступлениеПоЧеку = Ложь;	
	КонецЕсли;	

	Объект.НомерБланкаСФ = "";
	Объект.ЗначениеСтавкиНДС = 0;
	
	ПараметрыРасчета = ПодготовитьПараметрыРасчета();
	
	// Расчет налогов
	ОбработкаТабличныхЧастейКлиентСервер.ПересчитатьНалогиТабличнойЧастиПоступление(
		Объект, "Товары", ПараметрыРасчета);
		
	ОбработкаТабличныхЧастейКлиентСервер.ПересчитатьНалогиТабличнойЧастиПоступление(
		Объект, "Услуги", ПараметрыРасчета);
		
	ПараметрыРасчета = ПодготовитьПараметрыРасчета(Истина);
		
	ОбработкаТабличныхЧастейКлиентСервер.ПересчитатьНалогиТабличнойЧастиПоступление(
		Объект, "ОС", ПараметрыРасчета);
		
	Для Каждого СтрокаТабличнойЧасти Из Объект.Товары Цикл	 
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьЦенуСтрокиТабличнойЧасти(СтрокаТабличнойЧасти, ПараметрыРасчета);
	КонецЦикла;
	
	Для Каждого СтрокаТабличнойЧасти Из Объект.Услуги Цикл	
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьЦенуСтрокиТабличнойЧасти(СтрокаТабличнойЧасти, ПараметрыРасчета);	
	КонецЦикла;
	
	// Установить видимость и доступность элементов формы
	УстановитьВидимостьДоступностьЭлементов();
	ОбновитьПодвалФормы();	
КонецПроцедуры

&НаКлиенте
Процедура ЭтоПоступлениеПоЧекуПриИзменении(Элемент)
	
	Объект.НомерБланкаСФ = "";
	
	// Сброс признака ЭтоПоступлениеБезНДС.
	Если Объект.ЭтоПоступлениеБезНДС Тогда 
		
		Объект.ЭтоПоступлениеБезНДС = Ложь;
		
		Объект.ЗначениеСтавкиНДС = 0;
		
		ПараметрыРасчета = ПодготовитьПараметрыРасчета();
		
		// Расчет налогов
		ОбработкаТабличныхЧастейКлиентСервер.ПересчитатьНалогиТабличнойЧастиПоступление(
			Объект, "Товары", ПараметрыРасчета);
			
		ОбработкаТабличныхЧастейКлиентСервер.ПересчитатьНалогиТабличнойЧастиПоступление(
			Объект, "Услуги", ПараметрыРасчета);
			
		ПараметрыРасчета = ПодготовитьПараметрыРасчета(Истина);
			
		ОбработкаТабличныхЧастейКлиентСервер.ПересчитатьНалогиТабличнойЧастиПоступление(
			Объект, "ОС", ПараметрыРасчета);
			
		Для Каждого СтрокаТабличнойЧасти Из Объект.Товары Цикл	 
			ОбработкаТабличныхЧастейКлиентСервер.РассчитатьЦенуСтрокиТабличнойЧасти(СтрокаТабличнойЧасти, ПараметрыРасчета);
		КонецЦикла;
		
		Для Каждого СтрокаТабличнойЧасти Из Объект.Услуги Цикл	
			ОбработкаТабличныхЧастейКлиентСервер.РассчитатьЦенуСтрокиТабличнойЧасти(СтрокаТабличнойЧасти, ПараметрыРасчета);	
		КонецЦикла;
	КонецЕсли;
	
	// Установить видимость и доступность элементов формы
	УстановитьВидимостьДоступностьЭлементов();
	ОбновитьПодвалФормы();	
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Ставка НДС.
//
&НаКлиенте
Процедура ЗначениеСтавкиНДСПриИзменении(Элемент)
	
	ПараметрыРасчета = ПодготовитьПараметрыРасчета();
	
	// Расчет налогов
	ОбработкаТабличныхЧастейКлиентСервер.ПересчитатьНалогиТабличнойЧастиПоступление(
		Объект, "Товары", ПараметрыРасчета);
		
	ОбработкаТабличныхЧастейКлиентСервер.ПересчитатьНалогиТабличнойЧастиПоступление(
		Объект, "Услуги", ПараметрыРасчета);
		
	// Для ТЧ "ОС" не расчитывается "База НДС" и не расчитывается НДС от базы.
	ПараметрыРасчета = ПодготовитьПараметрыРасчета(Истина);	
		
	ОбработкаТабличныхЧастейКлиентСервер.ПересчитатьНалогиТабличнойЧастиПоступление(
		Объект, "ОС", ПараметрыРасчета);
		
	Для Каждого СтрокаТабличнойЧасти Из Объект.Товары Цикл	 
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьЦенуСтрокиТабличнойЧасти(
			СтрокаТабличнойЧасти, ПараметрыРасчета);
	КонецЦикла;
	
	Для Каждого СтрокаТабличнойЧасти Из Объект.Услуги Цикл	
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьЦенуСтрокиТабличнойЧасти(
			СтрокаТабличнойЧасти, ПараметрыРасчета);	
	КонецЦикла;	
		
	УстановитьВидимостьДоступностьЭлементов();
	ОбновитьПодвалФормы();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Ставка НСП.
//
&НаКлиенте
Процедура ЗначениеСтавкиНСППриИзменении(Элемент)
	
	МассивТабличныхЧастей = Новый Массив();
	МассивТабличныхЧастей.Добавить("Товары");
	МассивТабличныхЧастей.Добавить("Услуги");
	
	Для Каждого ТабличнаяЧасть Из МассивТабличныхЧастей Цикл
		Для Каждого СтрокаТабличнойЧасти Из Объект[ТабличнаяЧасть] Цикл
			СтрокаТабличнойЧасти.ЗначениеСтавкиНСП = Объект.ЗначениеСтавкиНСП;
		КонецЦикла;
	КонецЦикла;	
	
	ПараметрыРасчета = ПодготовитьПараметрыРасчета();
	
	// Расчет налогов
	ОбработкаТабличныхЧастейКлиентСервер.ПересчитатьНалогиТабличнойЧастиПоступление(
		Объект, "Товары", ПараметрыРасчета);
		
	ОбработкаТабличныхЧастейКлиентСервер.ПересчитатьНалогиТабличнойЧастиПоступление(
		Объект, "Услуги", ПараметрыРасчета);
		
	Для Каждого СтрокаТабличнойЧасти Из Объект.Товары Цикл	 
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьЦенуСтрокиТабличнойЧасти(
			СтрокаТабличнойЧасти, ПараметрыРасчета);
	КонецЦикла;
	
	Для Каждого СтрокаТабличнойЧасти Из Объект.Услуги Цикл	
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьЦенуСтрокиТабличнойЧасти(
			СтрокаТабличнойЧасти, ПараметрыРасчета);	
	КонецЦикла;	
		
	УстановитьВидимостьДоступностьЭлементов();
	ОбновитьПодвалФормы();
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Ставка НСП для ОС.
//
&НаКлиенте
Процедура ЗначениеСтавкиНСПДляОСПриИзменении(Элемент)
	
	ПараметрыРасчета = ПодготовитьПараметрыРасчета(Истина);	
	
	Для Каждого СтрокаТабличнойЧасти Из Объект.ОС Цикл
		СтрокаТабличнойЧасти.ЗначениеСтавкиНСП = Объект.ЗначениеСтавкиНСПДляОС;
	КонецЦикла;
	
	ОбработкаТабличныхЧастейКлиентСервер.ПересчитатьНалогиТабличнойЧастиПоступление(
		Объект, "ОС", ПараметрыРасчета);	
		
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода ДокументОснование.
//
&НаКлиенте
Процедура ДокументОснованиеПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда  
		ЗаполнитьПоДокументуОснования();
	КонецЕсли;
	
	УстановитьВидимостьДоступностьЭлементов();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении флага Расчет по емкости.
//
&НаКлиенте
Процедура ИспользоватьДопЕдиницыПриИзменении(Элемент)
	УстановитьВидимостьДоступностьЭлементов();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Серия бланка счет-фактуры.
//
&НаКлиенте
Процедура СерияБланкаСФПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Объект.СерияБланкаСФ) И (НЕ ЗначениеЗаполнено(Объект.ДатаСФ)) Тогда
		Объект.ДатаСФ = ДатаДокумента;
	КонецЕсли;
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Комментарий.
//
&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	ПодключитьОбработчикОжидания("УстановитьПиктограммуКомментария", 0.1, Истина);
КонецПроцедуры

// Процедура - обработчик события НачалоВыбора поля ввода Комментарий.
//
&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");
КонецПроцедуры

&НаКлиенте
Процедура КурсПриИзменении(Элемент)
	
	ПараметрыРасчета = ПодготовитьПараметрыРасчета();
	
	Для Каждого СтрокаТабличнойЧасти Из Объект.Товары Цикл
		СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.Цена;
		
		// Расчет суммы
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуИБазуНДССтрокиТабличнойЧастиПоступление(
			СтрокаТабличнойЧасти, ПараметрыРасчета);
			
		// Расчет налогов	
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммыНалоговСтрокиТабличнойЧастиПоступление(
			СтрокаТабличнойЧасти, ПараметрыРасчета);
	КонецЦикла;
		
	Для Каждого СтрокаТабличнойЧасти Из Объект.Услуги Цикл
		СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.Цена;
		
		// Расчет суммы
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуИБазуНДССтрокиТабличнойЧастиПоступление(
			СтрокаТабличнойЧасти, ПараметрыРасчета);
			
		// Расчет налогов	
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммыНалоговСтрокиТабличнойЧастиПоступление(
			СтрокаТабличнойЧасти, ПараметрыРасчета);
	КонецЦикла;
	
	// Для ТЧ "ОС" не расчитывается "База НДС" и не расчитывается НДС от базы.
	ПараметрыРасчета = ПодготовитьПараметрыРасчета(Истина);
	
	Для Каждого СтрокаТабличнойЧасти Из Объект.ОС Цикл	
		// Расчет суммы
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуИБазуНДССтрокиТабличнойЧастиПоступление(
			СтрокаТабличнойЧасти, ПараметрыРасчета);
			
		// Расчет налогов	
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммыНалоговСтрокиТабличнойЧастиПоступление(
			СтрокаТабличнойЧасти, ПараметрыРасчета);
	КонецЦикла;	
КонецПроцедуры

&НаКлиенте
Процедура КратностьПриИзменении(Элемент)
	
	ПараметрыРасчета = ПодготовитьПараметрыРасчета();
	
	Для Каждого СтрокаТабличнойЧасти Из Объект.Товары Цикл
		СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.Цена;
		
		// Расчет суммы
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуИБазуНДССтрокиТабличнойЧастиПоступление(
			СтрокаТабличнойЧасти, ПараметрыРасчета);
			
		// Расчет налогов	
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммыНалоговСтрокиТабличнойЧастиПоступление(
			СтрокаТабличнойЧасти, ПараметрыРасчета);
	КонецЦикла;
		
	Для Каждого СтрокаТабличнойЧасти Из Объект.Услуги Цикл
		СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.Цена;
		
		// Расчет суммы
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуИБазуНДССтрокиТабличнойЧастиПоступление(
			СтрокаТабличнойЧасти, ПараметрыРасчета);
			
		// Расчет налогов	
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммыНалоговСтрокиТабличнойЧастиПоступление(
			СтрокаТабличнойЧасти, ПараметрыРасчета);
	КонецЦикла;
	
	// Для ТЧ "ОС" не расчитывается "База НДС" и не расчитывается НДС от базы.
	ПараметрыРасчета = ПодготовитьПараметрыРасчета(Истина);
	
	Для Каждого СтрокаТабличнойЧасти Из Объект.ОС Цикл	
		// Расчет суммы
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуИБазуНДССтрокиТабличнойЧастиПоступление(
			СтрокаТабличнойЧасти, ПараметрыРасчета);
			
		// Расчет налогов	
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммыНалоговСтрокиТабличнойЧастиПоступление(
			СтрокаТабличнойЧасти, ПараметрыРасчета);
	КонецЦикла;
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля выбора Точность цены.
//
&НаКлиенте
Процедура ТочностьЦеныПриИзменении(Элемент)
	
	УстановитьФорматЦены();
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеЭСФНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		ТекстСообщения = НСтр("ru = 'Документ еще не записан.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;

	ЭСФКлиент.ПредставлениеЭСФНажатие(ПредставлениеЭСФ, ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ПоступлениеОтНерезидентаПриИзменении(Элемент)
	
	УстановитьВидимостьДоступностьЭлементов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

// Процедура - обработчик события ПередНачаломДобавления таблицы Товары.
//
&НаКлиенте
Процедура ТоварыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Если Копирование Тогда 
		ИтогВсего = Объект.Товары.Итог("Всего") + Объект.Услуги.Итог("Всего") + Объект.ОС.Итог("Всего") + Элемент.ТекущиеДанные.Всего;
		ИтогСуммаНДС = Объект.Товары.Итог("СуммаНДС") + Объект.Услуги.Итог("СуммаНДС") + Объект.ОС.Итог("СуммаНДС") + Элемент.ТекущиеДанные.СуммаНДС;
		ИтогСуммаНСП = Объект.Товары.Итог("СуммаНСП") + Объект.Услуги.Итог("СуммаНСП") + Объект.ОС.Итог("СуммаНСП") + Элемент.ТекущиеДанные.СуммаНСП;	
	КонецЕсли;	
КонецПроцедуры

// Процедура - обработчик события ПриНачалеРедактирования таблицы Товары.
//
&НаКлиенте
Процедура ТоварыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если Копирование Тогда 
		ОбновитьПодвалФормы();
	КонецЕсли;	
КонецПроцедуры

// Процедура - обработчик события ПослеУдаления таблицы Товары.
//
&НаКлиенте
Процедура ТоварыПослеУдаления(Элемент)
	ОбновитьПодвалФормы();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Номенклатура.
//
&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
	ОбработатьИзменениеНоменклатуры(СтрокаТабличнойЧасти, "Товары");	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Количество.
//
&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)	
	
	ПараметрыРасчета = ПодготовитьПараметрыРасчета();
	
	СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
	
	//Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
	//	Если СтрокаТабличнойЧасти.Количество = 0 Тогда
	//		СтрокаТабличнойЧасти.Всего = 0;
	//		СтрокаТабличнойЧасти.Сумма = 0;
	//		СтрокаТабличнойЧасти.СуммаНДС = 0;
	//		СтрокаТабличнойЧасти.СуммаНСП = 0;
	//		СтрокаТабличнойЧасти.СуммаАкциза = 0;
	//	Иначе
	//		СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Количество 
	//						/ ?(СтрокаТабличнойЧасти.КоличествоПоПоступлению = 0, 1, СтрокаТабличнойЧасти.КоличествоПоПоступлению) 
	//						* СтрокаТабличнойЧасти.ВсегоПоПоступлению;
	//		СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Количество 
	//						/ ?(СтрокаТабличнойЧасти.КоличествоПоПоступлению = 0, 1, СтрокаТабличнойЧасти.КоличествоПоПоступлению) 
	//						* СтрокаТабличнойЧасти.СуммаПоПоступлению;
	//		СтрокаТабличнойЧасти.СуммаНДС = СтрокаТабличнойЧасти.Количество 
	//						/ ?(СтрокаТабличнойЧасти.КоличествоПоПоступлению = 0, 1, СтрокаТабличнойЧасти.КоличествоПоПоступлению) 
	//						* СтрокаТабличнойЧасти.СуммаНДСПоПоступлению;
	//		СтрокаТабличнойЧасти.СуммаНСП = СтрокаТабличнойЧасти.Количество 
	//						/ ?(СтрокаТабличнойЧасти.КоличествоПоПоступлению = 0, 1, СтрокаТабличнойЧасти.КоличествоПоПоступлению) 
	//						* СтрокаТабличнойЧасти.СуммаНСППоПоступлению;
	//		СтрокаТабличнойЧасти.СуммаАкциза = СтрокаТабличнойЧасти.Количество 
	//						/ ?(СтрокаТабличнойЧасти.КоличествоПоПоступлению = 0, 1, СтрокаТабличнойЧасти.КоличествоПоПоступлению) 
	//						* СтрокаТабличнойЧасти.СуммаАкцизаПоПоступлению;
	//	КонецЕсли;	
	//	
	//Иначе
	
	
		Если ЗначениеЗаполнено(Объект.ДокументОснование) 
			И НЕ СтрокаТабличнойЧасти.КоличествоПоПоступлению = 0 Тогда		
				СтрокаТабличнойЧасти.Всего = Окр(СтрокаТабличнойЧасти.ВсегоПоПоступлению 
					/ СтрокаТабличнойЧасти.КоличествоПоПоступлению * СтрокаТабличнойЧасти.Количество, 2);					
		КонецЕсли;
	
	
		СтрокаТабличнойЧасти.Количество = ?(СтрокаТабличнойЧасти.Количество = 0, 1, СтрокаТабличнойЧасти.Количество);
		
		// Расчет суммы
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуИБазуНДССтрокиТабличнойЧастиПоступление(
			СтрокаТабличнойЧасти, ПараметрыРасчета);
			
		// Расчет налогов	
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммыНалоговСтрокиТабличнойЧастиПоступление(
			СтрокаТабличнойЧасти, ПараметрыРасчета);
			
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьЦенуСтрокиТабличнойЧасти(
			СтрокаТабличнойЧасти,ПараметрыРасчета);	
			
		//Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда	
		//	СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.Цена;
		//КонецЕсли;
	//КонецЕсли;
		
	Если Объект.ИспользоватьДопЕдиницы Тогда
		РассчитатьКоличествоДопЕдиницСтроки(СтрокаТабличнойЧасти);
	КонецЕсли;
	
	ОбновитьПодвалФормы();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Всего.
//
&НаКлиенте
Процедура ТоварыВсегоПриИзменении(Элемент)
	СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;

	ПараметрыРасчета = ПодготовитьПараметрыРасчета();
	
	// Расчет суммы
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуИБазуНДССтрокиТабличнойЧастиПоступление(
		СтрокаТабличнойЧасти, ПараметрыРасчета);
		
	// Расчет налогов	
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммыНалоговСтрокиТабличнойЧастиПоступление(
		СтрокаТабличнойЧасти, ПараметрыРасчета);

	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьЦенуСтрокиТабличнойЧасти(
		СтрокаТабличнойЧасти,ПараметрыРасчета);
	
	ОбновитьПодвалФормы();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода СуммаНДС.
//
&НаКлиенте
Процедура ТоварыСуммаНДСПриИзменении(Элемент)
	
	Если ПризнакСтраны = ПредопределенноеЗначение("Перечисление.ПризнакиСтраны.ЕАЭС") Тогда
		СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
		
		ПараметрыРасчета = ПодготовитьПараметрыРасчета();
		
		// Расчет суммы
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНСПСтрокиТабличнойЧастиПоступление(
			СтрокаТабличнойЧасти, ПараметрыРасчета); 
	КонецЕсли;
	
	ОбновитьПодвалФормы();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода ЗначениеСтавкиНСП.
//
&НаКлиенте
Процедура ТоварыЗначениеСтавкиНСППриИзменении(Элемент)
	ПараметрыРасчета = ПодготовитьПараметрыРасчета();
	
	СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
	// Расчет суммы
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуИБазуНДССтрокиТабличнойЧастиПоступление(СтрокаТабличнойЧасти, ПараметрыРасчета);
	// Расчет налогов
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммыНалоговСтрокиТабличнойЧастиПоступление(СтрокаТабличнойЧасти, ПараметрыРасчета);
	// Расчет цены.
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьЦенуСтрокиТабличнойЧасти(СтрокаТабличнойЧасти, ПараметрыРасчета);
		
	ОбновитьПодвалФормы();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода ИмпортОсвобожденныйОтНДС.
//
&НаКлиенте
Процедура ТоварыИмпортОсвобожденныйОтНДСПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.ИмпортОсвобожденныйОтНДС) Тогда
		СтрокаТабличнойЧасти.СуммаНДС = 0;
		
	ИначеЕсли СтрокаТабличнойЧасти.СуммаНДС = 0 Тогда
		ПараметрыРасчета = ПодготовитьПараметрыРасчета();
	
		// Расчет налогов	
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммыНалоговСтрокиТабличнойЧастиПоступление(
			СтрокаТабличнойЧасти, ПараметрыРасчета);
	КонецЕсли;
		
	ОбновитьПодвалФормы();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода КоэффициентДопЕдиниц.
//
&НаКлиенте
Процедура ТоварыКоэффициентДопЕдиницыПриИзменении(Элемент)
	Если Объект.ИспользоватьДопЕдиницы Тогда
		СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
		РассчитатьКоличествоДопЕдиницСтроки(СтрокаТабличнойЧасти);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

// Процедура - обработчик события ПередНачаломДобавления таблицы Услуги.
//
&НаКлиенте
Процедура УслугиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Если Копирование Тогда 
		ИтогВсего = Объект.Товары.Итог("Всего") + Объект.Услуги.Итог("Всего") + Объект.ОС.Итог("Всего") + Элемент.ТекущиеДанные.Всего;
		ИтогСуммаНДС = Объект.Товары.Итог("СуммаНДС") + Объект.Услуги.Итог("СуммаНДС") + Объект.ОС.Итог("СуммаНДС") + Элемент.ТекущиеДанные.СуммаНДС;
		ИтогСуммаНСП = Объект.Товары.Итог("СуммаНСП") + Объект.Услуги.Итог("СуммаНСП") + Объект.ОС.Итог("СуммаНСП") + Элемент.ТекущиеДанные.СуммаНСП;	
	КонецЕсли;	
КонецПроцедуры

// Процедура - обработчик события ПередНачаломИзменения таблицы Товары.
//
&НаКлиенте
Процедура УслугиПередНачаломИзменения(Элемент, Отказ)
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	БухгалтерскийУчетКлиентСервер.УстановитьНачальныеСвойстваСубконтоСтроки(
		ЭтотОбъект, ТекущиеДанные, ПараметрыУстановкиСвойствСубконто(ЭтотОбъект));
КонецПроцедуры

// Процедура - обработчик события ПриНачалеРедактирования таблицы Услуги.
//
&НаКлиенте
Процедура УслугиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если Копирование Тогда 
		ОбновитьПодвалФормы();
	КонецЕсли;	
КонецПроцедуры

// Процедура - обработчик события ПослеУдаления таблицы Услуги.
//
&НаКлиенте
Процедура УслугиПослеУдаления(Элемент)
	ОбновитьПодвалФормы();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Номенклатура.
//
&НаКлиенте
Процедура УслугиНоменклатураПриИзменении(Элемент)
	СтрокаТабличнойЧасти = Элементы.Услуги.ТекущиеДанные;
	ОбработатьИзменениеНоменклатуры(СтрокаТабличнойЧасти, "Услуги");	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Количество.
//
&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Услуги.ТекущиеДанные;
	
	ПараметрыРасчета = ПодготовитьПараметрыРасчета();
	
	//Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
	//	Если СтрокаТабличнойЧасти.Количество = 0 Тогда
	//		СтрокаТабличнойЧасти.Всего = 0;
	//		СтрокаТабличнойЧасти.Сумма = 0;
	//		СтрокаТабличнойЧасти.СуммаНДС = 0;
	//		СтрокаТабличнойЧасти.СуммаНСП = 0;
	//		СтрокаТабличнойЧасти.СуммаАкциза = 0;
	//	Иначе
	//		СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Количество 
	//						/ ?(СтрокаТабличнойЧасти.КоличествоПоПоступлению = 0, 1, СтрокаТабличнойЧасти.КоличествоПоПоступлению) 
	//						* СтрокаТабличнойЧасти.ВсегоПоПоступлению;
	//		СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Количество 
	//						/ ?(СтрокаТабличнойЧасти.КоличествоПоПоступлению = 0, 1, СтрокаТабличнойЧасти.КоличествоПоПоступлению) 
	//						* СтрокаТабличнойЧасти.СуммаПоПоступлению;
	//		СтрокаТабличнойЧасти.СуммаНДС = СтрокаТабличнойЧасти.Количество 
	//						/ ?(СтрокаТабличнойЧасти.КоличествоПоПоступлению = 0, 1, СтрокаТабличнойЧасти.КоличествоПоПоступлению) 
	//						* СтрокаТабличнойЧасти.СуммаНДСПоПоступлению;
	//		СтрокаТабличнойЧасти.СуммаНСП = СтрокаТабличнойЧасти.Количество 
	//						/ ?(СтрокаТабличнойЧасти.КоличествоПоПоступлению = 0, 1, СтрокаТабличнойЧасти.КоличествоПоПоступлению) 
	//						* СтрокаТабличнойЧасти.СуммаНСППоПоступлению;
	//		СтрокаТабличнойЧасти.СуммаАкциза = СтрокаТабличнойЧасти.Количество 
	//						/ ?(СтрокаТабличнойЧасти.КоличествоПоПоступлению = 0, 1, СтрокаТабличнойЧасти.КоличествоПоПоступлению) 
	//						* СтрокаТабличнойЧасти.СуммаАкцизаПоПоступлению;
	//	КонецЕсли;	
	//	
	//Иначе
	
	
		Если ЗначениеЗаполнено(Объект.ДокументОснование) 
			И НЕ СтрокаТабличнойЧасти.КоличествоПоПоступлению = 0 Тогда		
				СтрокаТабличнойЧасти.Всего = Окр(СтрокаТабличнойЧасти.ВсегоПоПоступлению 
					/ СтрокаТабличнойЧасти.КоличествоПоПоступлению * СтрокаТабличнойЧасти.Количество, 2);					
		КонецЕсли;
	
	
		СтрокаТабличнойЧасти.Количество = ?(СтрокаТабличнойЧасти.Количество = 0, 1, СтрокаТабличнойЧасти.Количество);
		
		// Расчет суммы
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуИБазуНДССтрокиТабличнойЧастиПоступление(
			СтрокаТабличнойЧасти, ПараметрыРасчета);
			
		// Расчет налогов	
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммыНалоговСтрокиТабличнойЧастиПоступление(
			СтрокаТабличнойЧасти, ПараметрыРасчета);
			
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьЦенуСтрокиТабличнойЧасти(
			СтрокаТабличнойЧасти,ПараметрыРасчета);	
			
		//Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда	
		//	СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.Цена;
		//КонецЕсли;
	//КонецЕсли;
		
	ОбновитьПодвалФормы();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Всего.
//
&НаКлиенте
Процедура УслугиВсегоПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Услуги.ТекущиеДанные;
	
	ПараметрыРасчета = ПодготовитьПараметрыРасчета();
	
	// Расчет суммы
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуИБазуНДССтрокиТабличнойЧастиПоступление(
		СтрокаТабличнойЧасти, ПараметрыРасчета);
		
	// Расчет налогов	
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммыНалоговСтрокиТабличнойЧастиПоступление(
		СтрокаТабличнойЧасти, ПараметрыРасчета);

	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьЦенуСтрокиТабличнойЧасти(
		СтрокаТабличнойЧасти, ПараметрыРасчета);	

	ОбновитьПодвалФормы();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода СуммаНДС.
//
&НаКлиенте
Процедура УслугиСуммаНДСПриИзменении(Элемент)
	
	Если ПризнакСтраны = ПредопределенноеЗначение("Перечисление.ПризнакиСтраны.ЕАЭС") Тогда
		СтрокаТабличнойЧасти = Элементы.Услуги.ТекущиеДанные;
		
		ПараметрыРасчета = ПодготовитьПараметрыРасчета();
		
		// Расчет суммы
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНСПСтрокиТабличнойЧастиПоступление(
			СтрокаТабличнойЧасти, ПараметрыРасчета); 
	КонецЕсли;
	
	ОбновитьПодвалФормы();	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода ЗначениеСтавкиНСП.
//
&НаКлиенте
Процедура УслугиЗначениеСтавкиНСППриИзменении(Элемент)
	ПараметрыРасчета = ПодготовитьПараметрыРасчета();
	
	СтрокаТабличнойЧасти = Элементы.Услуги.ТекущиеДанные;
	// Расчет суммы
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуИБазуНДССтрокиТабличнойЧастиПоступление(СтрокаТабличнойЧасти, ПараметрыРасчета);
	// Расчет налогов
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммыНалоговСтрокиТабличнойЧастиПоступление(СтрокаТабличнойЧасти, ПараметрыРасчета);
	// Расчет цены.
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьЦенуСтрокиТабличнойЧасти(СтрокаТабличнойЧасти, ПараметрыРасчета);
		
	ОбновитьПодвалФормы();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода СчетРасходов.
//
&НаКлиенте
Процедура УслугиСчетРасходовПриИзменении(Элемент)
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоСтрокиПриИзмененииСчета(
		ЭтотОбъект, ТекущиеДанные, ПараметрыУстановкиСвойствСубконто(ЭтотОбъект));
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода УслугиСубконто1.
//
&НаКлиенте
Процедура УслугиСубконто1ПриИзменении(Элемент)
	ПриИзмененииСубконто(1);
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода УслугиСубконто2.
//
&НаКлиенте
Процедура УслугиСубконто2ПриИзменении(Элемент)
	ПриИзмененииСубконто(2);
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода УслугиСубконто3.
//
&НаКлиенте
Процедура УслугиСубконто3ПриИзменении(Элемент)
	ПриИзмененииСубконто(3);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОС

// Процедура - обработчик события ПередНачаломДобавления таблицы ОС.
//
&НаКлиенте
Процедура ОСПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Если Копирование Тогда 
		ИтогВсего = Объект.Товары.Итог("Всего") + Объект.Услуги.Итог("Всего") + Объект.ОС.Итог("Всего") + Элемент.ТекущиеДанные.Всего;
		ИтогСуммаНДС = Объект.Товары.Итог("СуммаНДС") + Объект.Услуги.Итог("СуммаНДС") + Объект.ОС.Итог("СуммаНДС") + Элемент.ТекущиеДанные.СуммаНДС;
		ИтогСуммаНСП = Объект.Товары.Итог("СуммаНСП") + Объект.Услуги.Итог("СуммаНСП") + Объект.ОС.Итог("СуммаНСП") + Элемент.ТекущиеДанные.СуммаНСП;	
	КонецЕсли;	
КонецПроцедуры

// Процедура - обработчик события ПриНачалеРедактирования таблицы ОС.
//
&НаКлиенте
Процедура ОСПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если Копирование Тогда 
		ОбновитьПодвалФормы();
	КонецЕсли;	
КонецПроцедуры

// Процедура - обработчик события ПослеУдаления таблицы ОС.
//
&НаКлиенте
Процедура ОСПослеУдаления(Элемент)
	ОбновитьПодвалФормы();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Всего.
//
&НаКлиенте
Процедура ОСВсегоПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.ОС.ТекущиеДанные;
	
	// Для ТЧ "ОС" не расчитывается "База НДС" и не расчитывается НДС от базы.
	ПараметрыРасчета = ПодготовитьПараметрыРасчета(Истина);
	
	// Расчет суммы
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуИБазуНДССтрокиТабличнойЧастиПоступление(
		СтрокаТабличнойЧасти, ПараметрыРасчета);	
		
	// Расчет налогов	
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммыНалоговСтрокиТабличнойЧастиПоступление(
		СтрокаТабличнойЧасти, ПараметрыРасчета);

	ОбновитьПодвалФормы();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Сумма.
//
&НаКлиенте
Процедура ОССуммаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.ОС.ТекущиеДанные;
	
	// Для ТЧ "ОС" не расчитывается "База НДС" и не расчитывается НДС от базы.
	ПараметрыРасчета = ПодготовитьПараметрыРасчета(Истина);
	
	// Расчет налогов
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммыНалоговСтрокиТабличнойЧастиПоступление(
		СтрокаТабличнойЧасти, ПараметрыРасчета); 
	
	ОбновитьПодвалФормы();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода СуммаНДС.
//
&НаКлиенте
Процедура ОССуммаНДСПриИзменении(Элемент)
	
	Если ПризнакСтраны = ПредопределенноеЗначение("Перечисление.ПризнакиСтраны.ЕАЭС") Тогда
		СтрокаТабличнойЧасти = Элементы.ОС.ТекущиеДанные;
		
		// Для ТЧ "ОС" не расчитывается "База НДС" и не расчитывается НДС от базы.
		ПараметрыРасчета = ПодготовитьПараметрыРасчета(Истина);
		
		// Расчет суммы
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНСПСтрокиТабличнойЧастиПоступление(
			СтрокаТабличнойЧасти, ПараметрыРасчета); 
	КонецЕсли;
	
	ОбновитьПодвалФормы();	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода ЗначениеСтавкиНСП.
//
&НаКлиенте
Процедура ОСЗначениеСтавкиНСППриИзменении(Элемент)
	ПараметрыРасчета = ПодготовитьПараметрыРасчета();
	
	СтрокаТабличнойЧасти = Элементы.ОС.ТекущиеДанные;
	// Расчет суммы
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуИБазуНДССтрокиТабличнойЧастиПоступление(СтрокаТабличнойЧасти, ПараметрыРасчета);
	// Расчет налогов
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммыНалоговСтрокиТабличнойЧастиПоступление(СтрокаТабличнойЧасти, ПараметрыРасчета);
		
	ОбновитьПодвалФормы();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода ОС.
//
&НаКлиенте
Процедура ОСОсновноеСредствоПриИзменении(Элемент)
	СтрокаТабличнойЧасти = Элементы.ОС.ТекущиеДанные;
	СтрокаТабличнойЧасти.ЗначениеСтавкиНСП = Объект.ЗначениеСтавкиНСПДляОС;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьПоПоступлению(Команда)
	
	ЗаполнитьТабличныеЧастиПоПоступлению("Заполнить");
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИзПоступления(Команда)

	ЗаполнитьТабличныеЧастиПоПоступлению("Добавить");
	
КонецПроцедуры

// Процедура - обработчик события действия команды Подбор в табличную часть Товары.
// Открывает форму подбора.
//
&НаКлиенте
Процедура ПодборНоменклатуры(Команда)
	РаботаСПодборомНоменклатурыКлиент.ОткрытьПодбор(ЭтаФорма, "Товары", "Списание");
КонецПроцедуры

// ПодключаемоеОборудование
&НаКлиенте
Процедура ПоискПоШтрихкодуТовары(Команда)
	
	ТекШтрихкод = "";
	ПоказатьВводЗначения(Новый ОписаниеОповещения("ПоискПоШтрихкодуЗавершение", 
		ЭтотОбъект, Новый Структура("ТекШтрихкод, ИмяТабличнойЧасти", ТекШтрихкод, "Товары")), ТекШтрихкод, НСтр("ru = 'Введите штрихкод'"));
		
КонецПроцедуры

&НаКлиенте
Процедура ПоискПоШтрихкодуУслуги(Команда)
	
	ТекШтрихкод = "";
	ПоказатьВводЗначения(Новый ОписаниеОповещения("ПоискПоШтрихкодуЗавершение", 
		ЭтотОбъект, Новый Структура("ТекШтрихкод, ИмяТабличнойЧасти", ТекШтрихкод, "Услуги")), ТекШтрихкод, НСтр("ru = 'Введите штрихкод'"));

КонецПроцедуры

&НаКлиенте
Процедура ПоискПоШтрихкодуЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    ТекШтрихкод = ?(Результат = Неопределено, ДополнительныеПараметры.ТекШтрихкод, Результат);
    
	Если НЕ ПустаяСтрока(ТекШтрихкод) Тогда
        ПолученыШтрихкоды(Новый Структура("Штрихкод, Количество", ТекШтрихкод, 1), ДополнительныеПараметры.ИмяТабличнойЧасти);
	КонецЕсли;	

КонецПроцедуры 
// Конец ПодключаемоеОборудование

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура устанавливает видимость и доступность элементов.
//
&НаСервере
Процедура УстановитьВидимостьДоступностьЭлементов()
	
	ПризнакЗачетаНДСП = ДанныеУчетнойПолитики.УказыватьПризнакЗачетаНДСПриПоступлении;
	ЭтоКонтрагентСЭЗ = Объект.Контрагент.СЭЗ;
	
	Элементы.СтраницаСчетФактура.Видимость = НЕ ЭтоКонтрагентСЭЗ;
	
	Если ПризнакСтраны = Перечисления.ПризнакиСтраны.ЕАЭС Тогда
		Элементы.БезналичныйРасчет.ТолькоПросмотр = НЕ ДанныеУчетнойПолитики.ПлательщикНСП ИЛИ НЕ Объект.ПоступлениеОтНерезидента;
		
		Элементы.ЭтоПоступлениеПоЧеку.Видимость = Ложь;

		// Ставки
		Элементы.ЗначениеСтавкиНДС.Видимость = НЕ ЭтоКонтрагентСЭЗ;
		// Счет-фактура
		Элементы.СерияБланкаСФ.Видимость = Ложь;
		Элементы.НомерБланкаСФ.Видимость = Ложь;
		Элементы.ДатаСФ.Видимость = Ложь;
		Элементы.СчетФактураПолученный.Видимость = Ложь;
		// Курс прихода
		Если Объект.ВалютаДокумента = ВалютаРегламентированногоУчета Тогда 
			Элементы.Курс.Видимость = Ложь;
			Элементы.Кратность.Видимость = Ложь;
		Иначе
			Элементы.Курс.Видимость = Истина;
			Если Объект.Кратность <> 0 ИЛИ Объект.Кратность <> 1 Тогда
				Элементы.Кратность.Видимость = Истина;
			Иначе
				Элементы.Кратность.Видимость = Ложь;
			КонецЕсли;
		КонецЕсли;
		// Товары
		// Зачет НДС
		Элементы.ТоварыЗачетНДС.Видимость = ПризнакЗачетаНДСП И ДанныеУчетнойПолитики.ПлательщикНДС;
		// Импорт
		Элементы.ТоварыИмпортОсвобожденныйОтНДС.Видимость = Истина;
		// Доп единицы
		Элементы.ТоварыКоличествоДопЕдиницы.Видимость = Объект.ИспользоватьДопЕдиницы;
		Элементы.ТоварыКоэффициентДопЕдиницы.Видимость = Объект.ИспользоватьДопЕдиницы;
		
		// Услуги
		// Зачет НДС
		Элементы.УслугиЗачетНДС.Видимость = ПризнакЗачетаНДСП И ДанныеУчетнойПолитики.ПлательщикНДС;
		
		// ОС
		// Зачет НДС
		Элементы.ОСЗачетНДС.Видимость = ПризнакЗачетаНДСП И ДанныеУчетнойПолитики.ПлательщикНДС;
		
		// База НДС
		Элементы.ТоварыБазаНДС.Видимость = Истина;
		Элементы.УслугиБазаНДС.Видимость = Истина;
		
		Элементы.ПоступлениеОтНерезидента.Видимость = Истина;
		
	ИначеЕсли ПризнакСтраны = Перечисления.ПризнакиСтраны.ИмпортЭкспорт Тогда
		Элементы.БезналичныйРасчет.ТолькоПросмотр = НЕ ДанныеУчетнойПолитики.ПлательщикНСП ИЛИ НЕ Объект.ПоступлениеОтНерезидента;
		
		Элементы.ЭтоПоступлениеПоЧеку.Видимость = Ложь;

		// Ставки
		Элементы.ЗначениеСтавкиНДС.Видимость = Ложь;
		// Счет-фактура
		Элементы.СерияБланкаСФ.Видимость = Ложь;
		Элементы.НомерБланкаСФ.Видимость = Ложь;
		Элементы.ДатаСФ.Видимость = Ложь;
		Элементы.СчетФактураПолученный.Видимость = Ложь;
		// Курс прихода
		Если Объект.ВалютаДокумента = ВалютаРегламентированногоУчета Тогда 
			Элементы.Курс.Видимость = Ложь;
			Элементы.Кратность.Видимость = Ложь;
		Иначе
			Элементы.Курс.Видимость = Истина;
			Если Объект.Кратность <> 0 ИЛИ Объект.Кратность <> 1 Тогда
				Элементы.Кратность.Видимость = Истина;
			Иначе
				Элементы.Кратность.Видимость = Ложь;
			КонецЕсли;
		КонецЕсли;
		// Товары
		// Акциз
		Элементы.ТоварыВес.Видимость = Ложь;
		Элементы.ТоварыИмпортОсвобожденныйОтНДС.Видимость = Ложь;
		Элементы.ТоварыСуммаАкциза.Видимость = Ложь;
		// Зачет НДС
		Элементы.ТоварыЗачетНДС.Видимость = Ложь;
		// Доп единицы
		Элементы.ТоварыКоличествоДопЕдиницы.Видимость = Объект.ИспользоватьДопЕдиницы;
		Элементы.ТоварыКоэффициентДопЕдиницы.Видимость = Объект.ИспользоватьДопЕдиницы;
		
		// Услуги
		// Зачет НДС
		Элементы.УслугиЗачетНДС.Видимость = Ложь;
		
		// ОС
		// Зачет НДС
		Элементы.ОСЗачетНДС.Видимость = Ложь;
		
		// База НДС
		Элементы.ТоварыБазаНДС.Видимость = Ложь;
		Элементы.УслугиБазаНДС.Видимость = Ложь;
		
		Элементы.ПоступлениеОтНерезидента.Видимость = Истина;
		
	Иначе // КР или не заполнен Контрагент	
		Элементы.БезналичныйРасчет.ТолькоПросмотр = НЕ ДанныеУчетнойПолитики.ПлательщикНСП;
		
		Элементы.ЭтоПоступлениеПоЧеку.Видимость = ДанныеУчетнойПолитики.ПлательщикНДС И НЕ ЭтоКонтрагентСЭЗ;

		// Ставки	
		Элементы.ЗначениеСтавкиНДС.Видимость = ДанныеУчетнойПолитики.ПлательщикНДС И НЕ ЭтоКонтрагентСЭЗ;
		// Счет-фактура
		Элементы.СерияБланкаСФ.Видимость = ДанныеУчетнойПолитики.ПлательщикНДС И НЕ ЗначениеЗаполнено(СчетФактураПолученный);
		Элементы.НомерБланкаСФ.Видимость = ДанныеУчетнойПолитики.ПлательщикНДС И НЕ ЗначениеЗаполнено(СчетФактураПолученный);
		Элементы.ДатаСФ.Видимость = ДанныеУчетнойПолитики.ПлательщикНДС И НЕ ЗначениеЗаполнено(СчетФактураПолученный);
		Элементы.СчетФактураПолученный.Видимость = ЗначениеЗаполнено(СчетФактураПолученный);
		// Курс прихода
		Элементы.Курс.Видимость = Ложь;
		Элементы.Кратность.Видимость = Ложь;
		// Акциз
		Элементы.ТоварыСуммаАкциза.Видимость = Ложь;
		// Товары
		Элементы.ТоварыКоличествоДопЕдиницы.Видимость = Объект.ИспользоватьДопЕдиницы;
		Элементы.ТоварыКоэффициентДопЕдиницы.Видимость = Объект.ИспользоватьДопЕдиницы;
		Элементы.ТоварыВес.Видимость = Ложь;
		Элементы.ТоварыИмпортОсвобожденныйОтНДС.Видимость = Ложь;

		// Зачет НДС
		Элементы.ТоварыЗачетНДС.Видимость = ПризнакЗачетаНДСП И ДанныеУчетнойПолитики.ПлательщикНДС;
		Элементы.УслугиЗачетНДС.Видимость = ПризнакЗачетаНДСП И ДанныеУчетнойПолитики.ПлательщикНДС;
		Элементы.ОСЗачетНДС.Видимость = ПризнакЗачетаНДСП И ДанныеУчетнойПолитики.ПлательщикНДС;
		
		// База НДС
		Элементы.ТоварыБазаНДС.Видимость = Ложь;
		Элементы.УслугиБазаНДС.Видимость = Ложь;
		
		Элементы.ПоступлениеОтНерезидента.Видимость = Ложь;
	КонецЕсли;
	
	// ИСКЛЮЧЕНИЕ - если значение ставки НДС или НСП заполнено,
	// но видимость отлкючена, то видимость возвращается,
	// чтобы пользователь понял что это ошибка и решил проблему на свое усмотрение.
	Если НЕ Элементы.ЗначениеСтавкиНДС.Видимость
		И Объект.ЗначениеСтавкиНДС <> 0 Тогда
		Элементы.ЗначениеСтавкиНДС.Видимость = Истина;
	КонецЕсли;
	
	// Видимость сумм НДС зависит от значения НДС.
	Если Объект.ЗначениеСтавкиНДС = 0 Тогда 
		Элементы.ТоварыСуммаНДС.Видимость = Ложь;
		Элементы.УслугиСуммаНДС.Видимость = Ложь;
		Элементы.ОССуммаНДС.Видимость = Ложь;
		Элементы.ИтогСуммаНДС.Видимость = Ложь;
	Иначе 
		Элементы.ТоварыСуммаНДС.Видимость = Истина;
		Элементы.УслугиСуммаНДС.Видимость = Истина;
		Элементы.ОССуммаНДС.Видимость = Истина;
		Элементы.ИтогСуммаНДС.Видимость = Истина;
	КонецЕсли;	
	
 	// Видимость НСП
	Если Объект.БезналичныйРасчет
		Или НЕ ДанныеУчетнойПолитики.ПлательщикНСП Тогда
		
		Элементы.ТоварыЗначениеСтавкиНСП.Видимость 	= Ложь;
		Элементы.ТоварыСуммаНСП.Видимость 			= Ложь;
		Элементы.УслугиЗначениеСтавкиНСП.Видимость 	= Ложь;
		Элементы.УслугиСуммаНСП.Видимость 			= Ложь;
		Элементы.ОСЗначениеСтавкиНСП.Видимость 		= Ложь;
		Элементы.ОССуммаНСП.Видимость 				= Ложь;
		Элементы.ИтогСуммаНСП.Видимость 			= Ложь;
		Элементы.ЗначениеСтавкиНСП.Видимость 		= Ложь;
		Элементы.ЗначениеСтавкиНСПДляОС.Видимость 	= Ложь;
		
	Иначе 
		ВидимостьНСП = ПризнакСтраны = Перечисления.ПризнакиСтраны.КР ИЛИ Объект.ПоступлениеОтНерезидента;
		
		Элементы.ТоварыЗначениеСтавкиНСП.Видимость 	= ВидимостьНСП;
		Элементы.ТоварыСуммаНСП.Видимость 			= ВидимостьНСП;
		Элементы.УслугиЗначениеСтавкиНСП.Видимость 	= ВидимостьНСП;
		Элементы.УслугиСуммаНСП.Видимость 			= ВидимостьНСП;
		Элементы.ОСЗначениеСтавкиНСП.Видимость 		= ВидимостьНСП;
		Элементы.ОССуммаНСП.Видимость 				= ВидимостьНСП;
		Элементы.ИтогСуммаНСП.Видимость 			= ВидимостьНСП;
		Элементы.ЗначениеСтавкиНСП.Видимость 		= ВидимостьНСП;
		Элементы.ЗначениеСтавкиНСПДляОС.Видимость 	= ВидимостьНСП;
	КонецЕсли;		
	
	Если Объект.ЭтоПоступлениеБезНДС
		Или Объект.ЭтоПоступлениеПоЧеку
		Или ПризнакСтраны = Перечисления.ПризнакиСтраны.ЕАЭС Тогда 
		Элементы.НомерБланкаСФРучнойВвод.Видимость = Истина;
		Элементы.ПредставлениеЭСФ.Видимость = Ложь;
	Иначе 
		Элементы.НомерБланкаСФРучнойВвод.Видимость = Ложь;
		Элементы.ПредставлениеЭСФ.Видимость = Истина;
	КонецЕсли;	

	Если Объект.ВидОперации = Перечисления.ВидыОперацийВозвратТоваровПоставщику.ВозвратПоставщику Тогда
		Элементы.Контрагент.Заголовок = "Поставщик";
	ИначеЕсли Объект.ВидОперации = Перечисления.ВидыОперацийВозвратТоваровПоставщику.ИзПереработки Тогда
		Элементы.Контрагент.Заголовок = "Заказчик";
	КонецЕсли;
	
	// Переход на ЭСФ.
	Если ДатаДокумента >= Дата(2020, 07, 01) Тогда 
		Элементы.СерияБланкаСФ.Видимость = Ложь;
		Элементы.НомерБланкаСФ.Видимость = Ложь;
		Элементы.ДатаСФ.Видимость = Ложь;
		Элементы.СтраницаСчетФактура.Видимость = Ложь;
	КонецЕсли;	
	
КонецПроцедуры 

// Процедура устанавливает функциональные опции формы документа.
//
&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()
	ОбщегоНазначенияБПКлиентСервер.УстановитьПараметрыФункциональныхОпцийФормыДокумента(ЭтаФорма, Объект.Организация, ДатаДокумента);
КонецПроцедуры

// Процедура устанавливает текущую страницу при открытии формы.
//
&НаКлиенте
Процедура УстановитьТекущуюСтраницу()	
	Если Объект.Товары.Количество() > 0 Тогда 
		Возврат;
	ИначеЕсли Объект.Услуги.Количество() > 0 Тогда 
		Элементы.Страницы.ТекущаяСтраница = Элементы.Страницы.ПодчиненныеЭлементы.СтраницаУслуги;
	ИначеЕсли Объект.ОС.Количество() > 0 Тогда 
		Элементы.Страницы.ТекущаяСтраница = Элементы.Страницы.ПодчиненныеЭлементы.СтраницаОС;
	КонецЕсли;	
КонецПроцедуры

// Получает набор данных с сервера для процедуры ДатаПриИзменении.
//
&НаСервере
Функция ПолучитьДанныеДатаПриИзменении(ДокументСсылка, ДатаНовая, ДатаПередИзменением, ВалютаДокумента)
	РазностьДат = БухгалтерскийУчетСервер.ПроверитьНомерДокумента(ДокументСсылка, ДатаНовая, ДатаПередИзменением);
	ВалютаКурсКратность = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаДокумента, ДатаНовая);
	
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить(
		"РазностьДат",
		РазностьДат);
	СтруктураДанные.Вставить(
		"ВалютаКурсКратность",
		ВалютаКурсКратность);
	
	Возврат СтруктураДанные;
КонецФункции // ПолучитьДанныеДатаПриИзменении()

// Процедура - Установить пиктограмму комментария.
//
&НаКлиенте
Процедура УстановитьПиктограммуКомментария()
	БухгалтерскийУчетКлиентСервер.УстановитьКартинкуДляКомментария(Элементы.СтраницаДополнительно, Объект.Комментарий);
КонецПроцедуры

// Процедура устанавлнивает значение ставки НСП в табличной части
//
// Параметры:
//  ИмяТабличнойЧасти	 - Строка	- Имя табличной части, в которой нужно изменить значение ставки НСП
//  ЗначениеСтавкиНСП	 - Число	- Новое значение ставки НСП
//
&НаКлиенте
Процедура УстановитьЗначениеСтавкиНСП(ИмяТабличнойЧасти, ЗначениеСтавкиНСП = 0)
	Для Каждого СтрокаТабличнойЧасти Из Объект[ИмяТабличнойЧасти] Цикл 
		СтрокаТабличнойЧасти.ЗначениеСтавкиНСП = ЗначениеСтавкиНСП;	
	КонецЦикла;	
КонецПроцедуры // УстановитьЗначениеСтавкиНСП()

// Получает набор данных с сервера для процедуры КонтрагентПриИзменении.
//
&НаСервере
Функция ПолучитьДанныеКонтрагентПриИзменении(Контрагент, Организация)
	
	ДоговорПоУмолчанию = ПолучитьДоговорПоУмолчанию(Объект.Ссылка, Контрагент, Организация, Объект.ВидОперации);
	
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить(
		"ДоговорКонтрагента",
		ДоговорПоУмолчанию);
		
	СтруктураДанные.Вставить(
		"ПризнакСтраны",
		Контрагент.ПризнакСтраны);
		
	СтруктураДанные.Вставить(
		"СЭЗ",
		Контрагент.СЭЗ);
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеКонтрагентПриИзменении()

&НаСервереБезКонтекста
Функция ПолучитьДоговорПоУмолчанию(Документ, Контрагент, Организация, ВидОперации)
	
	МенеджерСправочника = Справочники.ДоговорыКонтрагентов;
	
	СписокВидовДоговоров = МенеджерСправочника.ВидыДоговораДляДокумента(Документ, ВидОперации);
	ДоговорПоУмолчанию = МенеджерСправочника.ПолучитьДоговорПоУмолчаниюПоОрганизацииВидуДоговора(Контрагент, Организация, СписокВидовДоговоров);
	
	Возврат ДоговорПоУмолчанию;
	
КонецФункции

// Получает набор данных с сервера для процедуры ДоговорПриИзменении.
//
&НаСервереБезКонтекста
Функция ПолучитьДанныеДоговорПриИзменении(Период, ВалютаДокумента, ДоговорКонтрагента, ВидОперации)
	
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить(
		"ВалютаРасчетов",
		ДоговорКонтрагента.ВалютаРасчетов);
	
	СтруктураДанные.Вставить(
		"ВалютаРасчетовКурсКратность",
		РаботаСКурсамиВалют.ПолучитьКурсВалюты(ДоговорКонтрагента.ВалютаРасчетов, Период));

	СтруктураДанные.Вставить(
		"ТипЦен",
		ДоговорКонтрагента.ТипЦен);
	
	СтруктураДанные.Вставить(
		"СуммаВключаетНалоги",
		ДоговорКонтрагента.СуммаВключаетНалоги);
	
	СчетаУчета = БухгалтерскийУчетСервер.ПолучитьСчетаРасчетовСКонтрагентом(ДоговорКонтрагента.Организация, ДоговорКонтрагента.Владелец, ДоговорКонтрагента);
	
	Если ВидОперации = Перечисления.ВидыОперацийВозвратТоваровПоставщику.ВозвратПоставщику Тогда 
		СчетРасчетов = СчетаУчета.СчетРасчетовПоставщика;		
	Иначе 
		СчетРасчетов = СчетаУчета.СчетРасчетовПокупателя;		
	КонецЕсли;	

	СтруктураДанные.Вставить(
		"СчетРасчетов",
		СчетРасчетов);

	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеДоговорПриИзменении()

&НаСервере
Процедура ЗаполнитьСчетФактураПолученный()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СчетаФактурыПолученные.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрСведений.СчетаФактурыПолученные КАК СчетаФактурыПолученные
		|ГДЕ
		|	СчетаФактурыПолученные.Организация = &Организация
		|	И СчетаФактурыПолученные.Документ = &Документ";
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("Документ", Объект.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда 
		СчетФактураПолученный = Выборка.Регистратор;
		Если ЗначениеЗаполнено(Объект.СерияБланкаСФ)
			Или ЗначениеЗаполнено(Объект.НомерБланкаСФ) Тогда 
			Объект.СерияБланкаСФ = "";
			Объект.НомерБланкаСФ = "";
		КонецЕсли;	
	КонецЕсли;	
КонецПроцедуры // ЗаполнитьСчетФактураПолученный()

&НаСервере
Процедура ЗаполнитьПоДокументуОснования()

	Документ = РеквизитФормыВЗначение("Объект");
	Документ.ЗаполнитьПоПоступлениюТоваровУслуг(Объект.ДокументОснование);
	ЗначениеВРеквизитФормы(Документ, "Объект");

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеДоговора()
	
	СтруктураДанные = ПолучитьДанныеДоговорПриИзменении(ДатаДокумента, Объект.ВалютаДокумента, Объект.ДоговорКонтрагента, Объект.ВидОперации);

	// Обработка изменения валюты
	СтруктураКурсыПред = Новый Структура("Валюта, Курс, Кратность", Объект.ВалютаДокумента, Объект.Курс, Объект.Кратность);
	
	Объект.ВалютаДокумента = СтруктураДанные.ВалютаРасчетов;
	Объект.Курс      = ?(СтруктураДанные.ВалютаРасчетовКурсКратность.Курс = 0, 1, СтруктураДанные.ВалютаРасчетовКурсКратность.Курс);
	Объект.Кратность = ?(СтруктураДанные.ВалютаРасчетовКурсКратность.Кратность = 0, 1, СтруктураДанные.ВалютаРасчетовКурсКратность.Кратность);
	
	КурсНБКР      = ?(СтруктураДанные.ВалютаРасчетовКурсКратность.Курс = 0, 1, СтруктураДанные.ВалютаРасчетовКурсКратность.Курс);
	КратностьНБКР = ?(СтруктураДанные.ВалютаРасчетовКурсКратность.Кратность = 0, 1, СтруктураДанные.ВалютаРасчетовКурсКратность.Кратность);
	
	СтруктураКурсы = Новый Структура("Валюта, Курс, Кратность", Объект.ВалютаДокумента, Объект.Курс, Объект.Кратность);
	
	// Обработка изменения налогообложения
	Объект.СуммаВключаетНалоги = СтруктураДанные.СуммаВключаетНалоги;
	
	// Обработка изменения отражения в учете
	Объект.СчетРасчетов = СтруктураДанные.СчетРасчетов;
	
	Если НЕ ДанныеУчетнойПолитики.ПлательщикНДС Тогда
		Объект.ЗначениеСтавкиНДС = 0;		
	КонецЕсли;
	
	// Пересчет табличной части
	// Цена, Сумма
	ОбработкаТабличныхЧастейКлиентСервер.ПересчитатьЦеныТабличнойЧастиПоВалюте(Объект, ДатаДокумента, СтруктураКурсыПред, СтруктураКурсы, "Товары");
	ОбработкаТабличныхЧастейКлиентСервер.ПересчитатьЦеныТабличнойЧастиПоВалюте(Объект, ДатаДокумента, СтруктураКурсыПред, СтруктураКурсы, "Услуги");
	
	// Налоги
	ПараметрыРасчета = ПодготовитьПараметрыРасчета();
	
	ОбработкаТабличныхЧастейКлиентСервер.ПересчитатьНалогиТабличнойЧастиПоступление(
		Объект, "Товары", ПараметрыРасчета);
		
	ОбработкаТабличныхЧастейКлиентСервер.ПересчитатьНалогиТабличнойЧастиПоступление(
		Объект, "Услуги", ПараметрыРасчета);
		
	// Для ТЧ "ОС" не расчитывается "База НДС" и не расчитывается НДС от базы.
	ПараметрыРасчета = ПодготовитьПараметрыРасчета(Истина);
	
	ОбработкаТабличныхЧастейКлиентСервер.ПересчитатьНалогиТабличнойЧастиПоступление(
		Объект, "ОС", ПараметрыРасчета);
	
	// Расчет цены.
	Для Каждого СтрокаТабличнойЧасти Из Объект.Товары Цикл	
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьЦенуСтрокиТабличнойЧасти(
			СтрокаТабличнойЧасти, ПараметрыРасчета);
	КонецЦикла;
	
	Для Каждого СтрокаТабличнойЧасти Из Объект.Услуги Цикл	
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьЦенуСтрокиТабличнойЧасти(
			СтрокаТабличнойЧасти, ПараметрыРасчета);
	КонецЦикла;
	
	ОбновитьПодвалФормы();
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеНоменклатуры(СтрокаТабличнойЧасти, ИмяТабличнойЧасти)
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("Организация", Объект.Организация);
	СтруктураДанные.Вставить("ВалютаДокумента", Объект.ВалютаДокумента);
	СтруктураДанные.Вставить("ЗначениеСтавкиНДС", Объект.ЗначениеСтавкиНДС);
	СтруктураДанные.Вставить("ЗначениеСтавкиНСП", Объект.ЗначениеСтавкиНСП);
	СтруктураДанные.Вставить("СуммаВключаетНалоги", Объект.СуммаВключаетНалоги);
	СтруктураДанные.Вставить("БезналичныйРасчет", Объект.БезналичныйРасчет);
	СтруктураДанные.Вставить("Номенклатура", СтрокаТабличнойЧасти.Номенклатура);
	
	СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные, ДанныеУчетнойПолитики.ПлательщикНСП);

	// Заполнение по данным номенклатуры
	Если ИмяТабличнойЧасти = "Товары" Тогда 
		СтрокаТабличнойЧасти.СчетУчета = СтруктураДанные.СчетУчета;
	ИначеЕсли ИмяТабличнойЧасти = "Услуги" Тогда 
		СтрокаТабличнойЧасти.СчетРасходов = СтруктураДанные.СчетРасходов;
		
		БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоСтрокиПриИзмененииСчета(
			ЭтотОбъект, СтрокаТабличнойЧасти, ПараметрыУстановкиСвойствСубконто(ЭтотОбъект));
	КонецЕсли;
	
	СтрокаТабличнойЧасти.ЗначениеСтавкиНСП = СтруктураДанные.ЗначениеСтавкиНСП;
	
	СтрокаТабличнойЧасти.Цена = СтруктураДанные.Цена;

	Если СтрокаТабличнойЧасти.Количество = 0 Тогда  
		СтрокаТабличнойЧасти.Количество = 1;
	КонецЕсли;	
	
	ПараметрыРасчета = ПодготовитьПараметрыРасчета();
	
	// Расчет налогов.
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммыНалоговСтрокиТабличнойЧастиПоступление(
		СтрокаТабличнойЧасти, ПараметрыРасчета);
		
	ОбновитьПодвалФормы();
	
КонецПроцедуры	

// Получает набор данных с сервера для процедуры НоменклатураПриИзменении.
//
&НаСервереБезКонтекста
Функция ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные, ПлательщикНСП)
	
	// Счета учета
	СчетаУчетаНоменклатуры = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСчетаУчетаНоменклатуры(СтруктураДанные.Организация, СтруктураДанные.Номенклатура);
	СтруктураДанные.Вставить("СчетУчета", СчетаУчетаНоменклатуры.СчетУчета);
	СтруктураДанные.Вставить("СчетРасходов", СчетаУчетаНоменклатуры.СчетРасходов);
	
	// Значение Ставки НСП.
	//СтруктураДанные.Вставить("ЗначениеСтавкиНСП", ?(ПлательщикНСП И НЕ СтруктураДанные.БезналичныйРасчет, 
	//	СтруктураДанные.Номенклатура.ЗначениеСтавкиНСП, 0));
	
	// Цены
	СтруктураДанные.Вставить("СпособЗаполненияЦены", Перечисления.СпособыЗаполненияЦен.ПоЗакупочнымЦенам);
	Цена = Ценообразование.ПолучитьЦенуПоДокументам(СтруктураДанные);
	СтруктураДанные.Вставить("Цена", Цена);
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеНоменклатураПриИзменении()

// Процедура - обработчик подбора товаров.
//
&НаСервере
Процедура ПолучитьТоварыИзХранилища(АдресЗапасовВХранилище)
	
	ТаблицаДляЗагрузки = ПолучитьИзВременногоХранилища(АдресЗапасовВХранилище);
	
	ПараметрыРасчета = ПодготовитьПараметрыРасчета();
	
	Для Каждого СтрокаЗагрузки Из ТаблицаДляЗагрузки Цикл
		НайденныеСтроки = Объект.Товары.НайтиСтроки(Новый Структура("Номенклатура, СчетУчета", СтрокаЗагрузки.Номенклатура, СтрокаЗагрузки.СчетУчета));
		
		Если НайденныеСтроки.Количество() > 0 Тогда 
			Продолжить;
		КонецЕсли;	
		
		СтрокаТабличнойЧасти = Объект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, СтрокаЗагрузки);
		
		// Расчет суммы
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуИБазуНДССтрокиТабличнойЧастиПоступление(
			СтрокаТабличнойЧасти, ПараметрыРасчета);
			
		// Расчет налогов	
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммыНалоговСтрокиТабличнойЧастиПоступление(
			СтрокаТабличнойЧасти, ПараметрыРасчета);
	КонецЦикла;
		
КонецПроцедуры // ПолучитьЗапасыИзХранилища()

&НаКлиенте
Процедура РассчитатьКоличествоДопЕдиницСтроки(СтрокаТабличнойЧасти)

	СтрокаТабличнойЧасти.КоличествоДопЕдиницы = СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.КоэффициентДопЕдиницы;	

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеУчетнойПолитики()
	ДанныеУчетнойПолитики = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьДанныеУчетнойПолитикиОрганизаций(ДатаДокумента, Объект.Организация);	
	
	Если НЕ ДанныеУчетнойПолитики.ПлательщикНДС Тогда
		Объект.ЗначениеСтавкиНДС = 0;
		Объект.СерияБланкаСФ = "";
		Объект.НомерБланкаСФ = "";
		Объект.ДатаСФ = Дата(0001,01,01);
		Объект.ЗначениеСтавкиНДС = 0;
	КонецЕсли;
	
	Если НЕ ДанныеУчетнойПолитики.ПлательщикНСП Тогда
		УстановитьЗначениеСтавкиНСП("Товары");
		УстановитьЗначениеСтавкиНСП("Услуги");
		УстановитьЗначениеСтавкиНСП("ОС");
	КонецЕсли;
	
	УстановитьВидимостьДоступностьЭлементов();
КонецПроцедуры

// Процедура рассчитывает итоги для подвала формы.
//
&НаКлиенте
Процедура ОбновитьПодвалФормы()
	
	ИтогВсего = Объект.Товары.Итог("Всего") + Объект.Услуги.Итог("Всего") + Объект.ОС.Итог("Всего");
	ИтогСуммаНДС = Объект.Товары.Итог("СуммаНДС") + Объект.Услуги.Итог("СуммаНДС") + Объект.ОС.Итог("СуммаНДС");
	ИтогСуммаНСП = Объект.Товары.Итог("СуммаНСП") + Объект.Услуги.Итог("СуммаНСП") + Объект.ОС.Итог("СуммаНСП");
	
КонецПроцедуры // ОбновитьПодвалФормы()

// Подготовка параметров для расчетов
//
// Параметры:
//	ДляОС - Булево - признак того, что параметры формируются для расчета ОС
//
// Возвращаемое значение:
//	Структура - разделенные данные URI
//		*ПризнакСтраныЕАЭС - Булево - признак страны ЕАЭС или нет
//		*ПризнакСтраныИмпортЭкспорт - Булево - признак страны ИмпортЭкспорт или нет
//		*СуммаВключаетНалоги - Булево - сумма включает налоги или нет
//		*ЗначениеСтавкиНДС - Число - значение ставки НДС
//		*БезналичныйРасчет - Булево - признак безначличного расчета
//		*КурсДокумента - Число - курс валюты
//		*КратностьДокумента - Число - кратность валюты
//		*КурсНБКР - Число - курс валюты по нац банку
//		*КратностьНБКР - Число - кратность валюты по нац банку
//		*РассчитатьБазуНДС - Булево - признак необходимости рассчитать базу НДС
//		*РассчитатьОтБазыНДС - Булево - признак необходимости рассчитывать от базы НДС
//		*СчитатьОтДохода - Булево - признак необходимости рассчитывать от дохода (нужен для реализации)
//		*Точность - Число - точность цены (знаки после запятой, всегда 2)
//
&НаСервере
Функция ПодготовитьПараметрыРасчета(ДляОС = Ложь)
	Документ = РеквизитФормыВЗначение("Объект");
	ПараметрыРасчета = Документ.ПодготовитьПараметрыРасчета();
	
	ПараметрыРасчета.КурсНБКР = КурсНБКР;
	ПараметрыРасчета.КратностьНБКР = КратностьНБКР;
	
	Возврат ПараметрыРасчета;
КонецФункции

#Область РаботаССубконто

&НаСервере
Процедура УстановитьНачальныеСвойстваСубконтоТаблицы()
	БухгалтерскийУчетКлиентСервер.УстановитьНачальныеСвойстваСубконтоТаблицы(
		Объект.Услуги,
		ПараметрыУстановкиСвойствСубконто(ЭтотОбъект));
КонецПроцедуры 

&НаКлиентеНаСервереБезКонтекста
Функция ПараметрыУстановкиСвойствСубконто(Форма)

	Результат = БухгалтерскийУчетКлиентСервер.ПараметрыУстановкиСвойствСубконтоПоШаблону(
		"УслугиСубконто", "Субконто", "СчетРасходов");
	
	Результат.ДопРеквизиты.Вставить("Организация", Форма.Объект.Организация);
	Результат.СкрыватьСубконто = Ложь;
	
	Возврат Результат;

КонецФункции

&НаКлиенте
Процедура ПриИзмененииСубконто(НомерСубконто)
	
	СтрокаТаблицы = Элементы.Услуги.ТекущиеДанные;
	
	БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоСтрокиПриИзмененииСубконто(
		ЭтотОбъект, 
		СтрокаТаблицы,
		НомерСубконто, 
		ПараметрыУстановкиСвойствСубконто(ЭтотОбъект));
	
КонецПроцедуры

#КонецОбласти

// Процедура заполняет список выбора Точность цены
//
&НаСервере
Процедура ЗаполнитьСписокВыбораТочностьЦены()

	Элементы.ТочностьЦены.СписокВыбора.Очистить();

	ЗначенияТочностиЦены = Ценообразование.ЗначенияТочностиЦены();
	Для Каждого ЗначениеТочностиЦены Из ЗначенияТочностиЦены Цикл 
		Элементы.ТочностьЦены.СписокВыбора.Добавить(ЗначениеТочностиЦены.Значение, ЗначениеТочностиЦены.Представление);
	КонецЦикла;	

КонецПроцедуры // ЗаполнитьСписокВыбораТочностьЦены()

// Процедура устанавливает формат и формат редактирования элементов "Цена" в ТЧ "Товары" и "Услуги"
//
&НаКлиенте
Процедура УстановитьФорматЦены()

	ФорматЦены = ЦенообразованиеКлиент.ФорматЦены(Объект.ТочностьЦены);
	
	Элементы.ТоварыЦена.ФорматРедактирования = ФорматЦены;
	Элементы.ТоварыЦена.Формат = ФорматЦены;

	Элементы.УслугиЦена.ФорматРедактирования = ФорматЦены;
	Элементы.УслугиЦена.Формат = ФорматЦены;

	Элементы.Товары.Обновить();
	Элементы.Услуги.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьПредставлениеЭСФ()
	СведенияОбЭСФ = ЭСФСервер.СведенияОбЭСФПоДокументуОснованию(Объект.Организация, Объект.Ссылка, НСтр("ru = 'Создать новый ЭСФ (полученный)'"));
	ПредставлениеЭСФ = СведенияОбЭСФ.ПредставлениеЭСФ;
КонецПроцедуры

// ПодключаемоеОборудование

// Процедура - Получены штрихкоды
//
// Параметры:
//  ДанныеШтрихкодов	 - Структура/Массив - В зависимости от точки вызова передается структура (обработка сканера) или массив (обработка ТСД)
//  ИмяТабличнойЧасти	 - Строка	 - Имя табличной части для загрузки
//
&НаКлиенте
Процедура ПолученыШтрихкоды(ДанныеШтрихкодов, ИмяТабличнойЧасти) Экспорт
	
	Модифицированность = Истина;
	
	НеДобавленныеШтрихкоды = ЗаполнитьПоДаннымШтрихкодов(ДанныеШтрихкодов, ИмяТабличнойЧасти);
	
	// Неизвестные штрихкоды.
	Если НеДобавленныеШтрихкоды.НеизвестныеШтрихкоды.Количество() > 0 Тогда
		Для Каждого СтруктураДанные Из НеДобавленныеШтрихкоды.НеизвестныеШтрихкоды Цикл 
			СтрокаСообщения = СтрШаблон(НСтр("ru = 'Данные по штрихкоду не найдены: %1'"), СтруктураДанные.Штрихкод);
			ОбщегоНазначенияКлиент.СообщитьПользователю(СтрокаСообщения);
		КонецЦикла;	
	// Штрихкоды некорректного типа.
	ИначеЕсли НеДобавленныеШтрихкоды.ШтрихкодыНекорректногоТипа.Количество() > 0 Тогда 
		Для Каждого СтруктураДанные Из НеДобавленныеШтрихкоды.ШтрихкодыНекорректногоТипа Цикл 
			СтрокаСообщения = СтрШаблон(НСтр("ru = 'Найденная по штрихкоду %1 номенклатура: ""%2"", не подходит для этой табличной части'"),
				СтруктураДанные.ТекШтрихкод, СтруктураДанные.Номенклатура);
			ОбщегоНазначенияКлиент.СообщитьПользователю(СтрокаСообщения);
		КонецЦикла;	
	КонецЕсли;	
	
КонецПроцедуры // ПолученыШтрихкоды()

&НаСервереБезКонтекста
Функция ПолучитьДанныеПоШтрихкоду(ТекШтрихкод)
	
	Номенклатура = РегистрыСведений.ШтрихкодыНоменклатуры.ПолучитьНоменклатуруПоШтрихкоду(ТекШтрихкод);
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("Номенклатура", Номенклатура);
	СтруктураДанные.Вставить("ТипНоменклатурыУслуга", ?(ТипЗнч(Номенклатура) = Тип("СправочникСсылка.Номенклатура"), Номенклатура.Услуга, Ложь));
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеПоШтрихкоду()

// Функция - Заполнить по данным штрихкодов
//
// Параметры:
//  ДанныеШтрихкодов		 - 	 - Структура/Массив	 - В зависимости от точки вызова передается структура (обработка сканера) или массив (обработка ТСД)
//  ИмяТабличнойЧасти	 - Строка	 - Имя табличной части для загрузки
// 
// Возвращаемое значение:
//  Структура - Массивы неизвестных штрих кодов
//
&НаКлиенте
Функция ЗаполнитьПоДаннымШтрихкодов(ДанныеШтрихкодов, ИмяТабличнойЧасти) 
	
	НеизвестныеШтрихкоды = Новый Массив;
	ШтрихкодыНекорректногоТипа = Новый Массив;
	
	Если ТипЗнч(ДанныеШтрихкодов) = Тип("Массив") Тогда
		МассивШтрихкодов = ДанныеШтрихкодов;
	Иначе
		МассивШтрихкодов = Новый Массив;
		МассивШтрихкодов.Добавить(ДанныеШтрихкодов);
	КонецЕсли;
	
	Для каждого ТекШтрихкод Из МассивШтрихкодов Цикл
		СтруктураДанные = ПолучитьДанныеПоШтрихкоду(ТекШтрихкод);
		
		Если НЕ ЗначениеЗаполнено(СтруктураДанные.Номенклатура) Тогда 
			НеизвестныеШтрихкоды.Добавить(ТекШтрихкод);
		ИначеЕсли ИмяТабличнойЧасти = "Товары"
			И СтруктураДанные.ТипНоменклатурыУслуга Тогда
			ШтрихкодыНекорректногоТипа.Добавить(Новый Структура("ТекШтрихкод, Номенклатура", ТекШтрихкод, СтруктураДанные.Номенклатура));
		ИначеЕсли ИмяТабличнойЧасти = "Услуги"
			И НЕ СтруктураДанные.ТипНоменклатурыУслуга Тогда
			ШтрихкодыНекорректногоТипа.Добавить(Новый Структура("ТекШтрихкод, Номенклатура", ТекШтрихкод, СтруктураДанные.Номенклатура));
		Иначе 
			СтрокаТабличнойЧасти = Объект[ИмяТабличнойЧасти].Добавить();
			СтрокаТабличнойЧасти.Номенклатура = СтруктураДанные.Номенклатура;
			СтрокаТабличнойЧасти.Количество = ТекШтрихкод.Количество;
			
			ОбработатьИзменениеНоменклатуры(СтрокаТабличнойЧасти, ИмяТабличнойЧасти);
		КонецЕсли;
	КонецЦикла;	
	
	Возврат Новый Структура("НеизвестныеШтрихкоды, ШтрихкодыНекорректногоТипа",
		НеизвестныеШтрихкоды, ШтрихкодыНекорректногоТипа);

КонецФункции // ЗаполнитьПоДаннымШтрихкодов()
// Конец ПодключаемоеОборудование

#КонецОбласти

#Область ОбработчикиЗаполнения

&НаКлиенте
Асинх Процедура ЗаполнитьТабличныеЧастиПоПоступлению(СпособЗаполнения)
	Отказ = Ложь;

	// Перед заполнением договор должен быть выбран.
	Если Не ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		ТекстСообщения = НСтр("ru = 'Не выбран договор контрагента.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, "Объект.ДоговорКонтрагента",, Отказ);
	КонецЕсли;

	Если Отказ Тогда 
		Возврат
	КонецЕсли;	
	
	// Если заполняем, то почистим ТЧ
	Если СпособЗаполнения = "Заполнить"
		И (Объект.Товары.Количество() > 0 ИЛИ Объект.Услуги.Количество() > 0) Тогда 
		
		ТекстВопроса = НСтр("ru = 'Перед заполнением табличные части будут очищены. Продолжить выполнение операции?'");
		Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60);
		
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
		
		Объект.Товары.Очистить();
		Объект.Услуги.Очистить();
		Объект.ОС.Очистить();
	КонецЕсли;
	
	ДополнитьТабличныеЧастиПоПоступлению(СпособЗаполнения);
КонецПроцедуры

&НаКлиенте
Процедура ДополнитьТабличныеЧастиПоПоступлению(СпособЗаполнения)
		
	ПараметрыФормы = Новый Структура;

	Отбор = Новый Структура();
	
	Если СпособЗаполнения = "Заполнить" Тогда
		Отбор.Вставить("Организация", Объект.Организация);
		Отбор.Вставить("Контрагент", Объект.Контрагент);
		Отбор.Вставить("ДоговорКонтрагента", Объект.ДоговорКонтрагента);
		Отбор.Вставить("Проведен", Истина);			
	Иначе 
		Отбор.Вставить("Организация", Объект.Организация);
		Отбор.Вставить("Контрагент", Объект.Контрагент);
		Отбор.Вставить("ДоговорКонтрагента", Объект.ДоговорКонтрагента);
		Отбор.Вставить("Проведен", Истина);
		Отбор.Вставить("ЗначениеСтавкиНДС", Объект.ЗначениеСтавкиНДС);
	КонецЕсли;

	ПараметрыФормы.Вставить("Отбор", Отбор);
	ПараметрыФормы.Вставить("ДокументВозвратаТоваров", Истина);

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("СпособЗаполнения", СпособЗаполнения);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ЗаполнитьТабличныеЧастиПоПоступлениюЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ОткрытьФорму("Документ.ПоступлениеТоваровУслуг.ФормаВыбора", ПараметрыФормы,ЭтаФорма,,,,ОповещениеОЗакрытии);	

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТабличныеЧастиПоПоступлениюЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	ДокументПоступления = РезультатЗакрытия;
	
	Если Не ЗначениеЗаполнено(ДокументПоступления) Тогда
		Возврат; // ничего не выбрали.
	КонецЕсли;

	СпособЗаполнения = ДополнительныеПараметры.СпособЗаполнения;
	
	Если СпособЗаполнения = "Заполнить" Тогда
		Объект.ДокументОснование = ДокументПоступления;
	Иначе
		Объект.ДокументОснование = Неопределено;
	КонецЕсли;
	
	ЗаполнитьТабличныеЧастиПоПоступлениюПослеПолученияДокумента(ДокументПоступления, СпособЗаполнения);

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТабличныеЧастиПоПоступлениюПослеПолученияДокумента(ДокументПоступления, СпособЗаполнения)
	
	// Вызываем процедуру заполнения.
	Если СпособЗаполнения = "Заполнить" Тогда
		
		ЗаполнитьТоварыПоОснованию(ДокументПоступления);
		УстановитьВидимостьДоступностьЭлементов();
		
	Иначе // При добавлении надо сворачивать дубли строк, аналогично подбору

		ДобавитьСтрокиТабличныхЧастейИзДокументаПоступления(ДокументПоступления);

	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТоварыПоОснованию(ДокументОснование)

	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.ЗаполнитьПоПоступлениюТоваровУслуг(ДокументОснование);
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьСтрокиТабличныхЧастейИзДокументаПоступления(ДокументПоступления)

	МассивРезультатовЗапроса = ПолучитьДанныеДляКопирования(ДокументПоступления);
		
	ДобавитьСтрокиТабличнойЧасти("Товары", ДокументПоступления, МассивРезультатовЗапроса[0].Выбрать());
	ДобавитьСтрокиТабличнойЧасти("Услуги", ДокументПоступления, МассивРезультатовЗапроса[1].Выбрать());
	ДобавитьСтрокиТабличнойЧасти("ОС", ДокументПоступления, МассивРезультатовЗапроса[2].Выбрать());
КонецПроцедуры

&НаСервере
Процедура ДобавитьСтрокиТабличнойЧасти(ИмяТабличнойЧасти, ДокументПоступления, Выборка)
	
	ВалютаДокументаПоступления = ДокументПоступления.ВалютаДокумента;
	КурсДокументаПоступления = ДокументПоступления.Курс;
	КратностьДокументаПоступления = ДокументПоступления.Кратность;
	
	Если ИмяТабличнойЧасти = "ОС" Тогда
		// Для ТЧ "ОС" не расчитывается "База НДС" и не расчитывается НДС от базы.
		ПараметрыРасчета = ПодготовитьПараметрыРасчета(Истина);
	Иначе
		ПараметрыРасчета = ПодготовитьПараметрыРасчета();
	КонецЕсли;		
	
	// Обход строк Поступления
	СтруктураОтбора = Новый Структура();
	Пока Выборка.Следующий() Цикл

		Если ИмяТабличнойЧасти = "ОС" Тогда
			СтрокаТабличнойЧасти = Объект.ОС.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, Выборка);
			
		Иначе	
			// Поиск текущей позиции основания в табличной части документа возврата.
			// Если найден - увеличение количества; не найден - добавление новой строки.
			СтруктураОтбора.Вставить("Номенклатура", Выборка.Номенклатура);

			СтрокаТабличнойЧасти = ОбработкаТабличныхЧастейКлиентСервер.НайтиСтрокуТабличнойЧасти(Объект, ИмяТабличнойЧасти, СтруктураОтбора);
			Если СтрокаТабличнойЧасти <> неопределено Тогда			
				// Найдена, увеличение количества в первой найденной строке.
				СтрокаТабличнойЧасти.Количество = СтрокаТабличнойЧасти.Количество + Выборка.Количество;
				
				// Пересчет цены в валюту документа.
				Цена = РаботаСКурсамиВалютКлиентСервер.ПересчитатьПоКурсу(Выборка.Цена, 
					Новый Структура("Валюта, Курс, Кратность", ВалютаДокументаПоступления, КурсДокументаПоступления, КратностьДокументаПоступления),
					Новый Структура("Валюта, Курс, Кратность", 
						Объект.ВалютаДокумента, 
						ЗаполнениеОбъектовБП.Курс(Объект, ВалютаРегламентированногоУчета),  
						ЗаполнениеОбъектовБП.Кратность(Объект, ВалютаРегламентированногоУчета)));

				СтрокаТабличнойЧасти.Цена = (СтрокаТабличнойЧасти.Цена + Цена) / 2;
				СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Всего + Выборка.Всего;
				
			Иначе
				// Не Найдена - добавление новой строки.
				СтрокаТабличнойЧасти = Объект[ИмяТабличнойЧасти].Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, Выборка,, "Цена, Сумма, СуммаНДС, СуммаНСП");			

				СтрокаТабличнойЧасти.Номенклатура = Выборка.Номенклатура;
				СтрокаТабличнойЧасти.Количество = Выборка.Количество;									

				// Пересчет цены в валюту документа.
				Цена = РаботаСКурсамиВалютКлиентСервер.ПересчитатьПоКурсу(Выборка.Цена, 
					Новый Структура("Валюта, Курс, Кратность", ВалютаДокументаПоступления, КурсДокументаПоступления, КратностьДокументаПоступления),
					Новый Структура("Валюта, Курс, Кратность", 
						Объект.ВалютаДокумента, 
						ЗаполнениеОбъектовБП.Курс(Объект, ВалютаРегламентированногоУчета),  
						ЗаполнениеОбъектовБП.Кратность(Объект, ВалютаРегламентированногоУчета)));

				СтрокаТабличнойЧасти.Цена = Цена;	
			КонецЕсли;
		КонецЕсли;

		// Расчет суммы
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуИБазуНДССтрокиТабличнойЧастиПоступление(
			СтрокаТабличнойЧасти, ПараметрыРасчета);
			
		// Расчет налогов	
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммыНалоговСтрокиТабличнойЧастиПоступление(
			СтрокаТабличнойЧасти, ПараметрыРасчета);
	КонецЦикла;
	
КонецПроцедуры // ДобавитьСтрокиТабличнойЧасти()

&НаСервере
Функция ПолучитьДанныеДляКопирования(ДокументСсылка)

	// Получение нужной информаций по документу Поступления
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
		|	ТаблицаДокумента.Номенклатура КАК Номенклатура,
		|	ТаблицаДокумента.Количество КАК Количество,
		|	ТаблицаДокумента.СчетУчета КАК СчетУчета,
		|	ТаблицаДокумента.Цена КАК Цена,
		|	ТаблицаДокумента.Сумма КАК Сумма,
		|	ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
		|	ТаблицаДокумента.СуммаНСП КАК СуммаНСП,
		|	ТаблицаДокумента.Всего КАК Всего,
		|	ТаблицаДокумента.СуммаАкциза КАК СуммаАкциза,
		|	ТаблицаДокумента.ЗачетНДС КАК ЗачетНДС,
		|	ТаблицаДокумента.ИмпортОсвобожденныйОтНДС КАК ИмпортОсвобожденныйОтНДС
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.Товары КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &ДокументОснование
		|
		|УПОРЯДОЧИТЬ ПО
		|	ТаблицаДокумента.НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПоступлениеТоваровУслугУслуги.НомерСтроки КАК НомерСтроки,
		|	ПоступлениеТоваровУслугУслуги.Номенклатура КАК Номенклатура,
		|	ПоступлениеТоваровУслугУслуги.Количество КАК Количество,
		|	ПоступлениеТоваровУслугУслуги.Цена КАК Цена,
		|	ПоступлениеТоваровУслугУслуги.Сумма КАК Сумма,
		|	ПоступлениеТоваровУслугУслуги.СуммаНДС КАК СуммаНДС,
		|	ПоступлениеТоваровУслугУслуги.СуммаНСП КАК СуммаНСП,
		|	ПоступлениеТоваровУслугУслуги.Всего КАК Всего,
		|	ПоступлениеТоваровУслугУслуги.СуммаАкциза КАК СуммаАкциза,
		|	ПоступлениеТоваровУслугУслуги.СчетРасходов КАК СчетРасходов,
		|	ПоступлениеТоваровУслугУслуги.Субконто1 КАК Субконто1,
		|	ПоступлениеТоваровУслугУслуги.Субконто2 КАК Субконто2,
		|	ПоступлениеТоваровУслугУслуги.Субконто3 КАК Субконто3,
		|	ПоступлениеТоваровУслугУслуги.ЗачетНДС КАК ЗачетНДС
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.Услуги КАК ПоступлениеТоваровУслугУслуги
		|ГДЕ
		|	ПоступлениеТоваровУслугУслуги.Ссылка = &ДокументОснование
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПоступлениеТоваровУслугУслуги.НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПоступлениеТоваровУслугОС.НомерСтроки КАК НомерСтроки,
		|	ПоступлениеТоваровУслугОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ПоступлениеТоваровУслугОС.Сумма КАК Сумма,
		|	ПоступлениеТоваровУслугОС.СуммаНДС КАК СуммаНДС,
		|	ПоступлениеТоваровУслугОС.СуммаНСП КАК СуммаНСП,
		|	ПоступлениеТоваровУслугОС.Всего КАК Всего,
		|	ПоступлениеТоваровУслугОС.СуммаАкциза КАК СуммаАкциза,
		|	ПоступлениеТоваровУслугОС.СчетУчета КАК СчетУчета,
		|	ПоступлениеТоваровУслугОС.ЗачетНДС КАК ЗачетНДС
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.ОС КАК ПоступлениеТоваровУслугОС
		|ГДЕ
		|	ПоступлениеТоваровУслугОС.Ссылка = &ДокументОснование
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПоступлениеТоваровУслугОС.НомерСтроки";
	Запрос.УстановитьПараметр("ДокументОснование", ДокументСсылка);	
	МассивРезультатовЗапроса = Запрос.ВыполнитьПакет();

	Возврат МассивРезультатовЗапроса;

КонецФункции

#КонецОбласти

#Область КопированиеСтрокТабличныхЧастей

&НаКлиенте
Процедура ТоварыКопироватьСтроки(Команда)
	
	КопироватьСтроки("Товары");
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыВставитьСтроки(Команда)
	
	ВставитьСтроки("Товары");
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиКопироватьСтроки(Команда)
	
	КопироватьСтроки("Услуги");
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиВставитьСтроки(Команда)
	
	ВставитьСтроки("Услуги");
	
КонецПроцедуры

&НаКлиенте
Процедура КопироватьСтроки(ИмяТЧ)
	
	Если КопированиеТабличнойЧастиКлиент.МожноКопироватьСтроки(Объект[ИмяТЧ], Элементы[ИмяТЧ].ТекущиеДанные) Тогда
		КоличествоСкопированных = 0;
		КопироватьСтрокиНаСервере(ИмяТЧ, КоличествоСкопированных);
		КопированиеТабличнойЧастиКлиент.ОповеститьПользователяОКопированииСтрок(КоличествоСкопированных);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьСтроки(ИмяТЧ)
	
	КоличествоСкопированных = 0;
	КоличествоВставленных = 0;
	ВставитьСтрокиНаСервере(ИмяТЧ, КоличествоСкопированных, КоличествоВставленных);
	КопированиеТабличнойЧастиКлиент.ОповеститьПользователяОВставкеСтрок(КоличествоСкопированных, КоличествоВставленных);
	
КонецПроцедуры

&НаСервере
Процедура КопироватьСтрокиНаСервере(ИмяТЧ, КоличествоСкопированных)
	
	КопированиеТабличнойЧастиСервер.Копировать(Объект[ИмяТЧ], Элементы[ИмяТЧ].ВыделенныеСтроки, КоличествоСкопированных);
	
КонецПроцедуры

&НаСервере
Процедура ВставитьСтрокиНаСервере(ИмяТЧ, КоличествоСкопированных, КоличествоВставленных)
	
	КопированиеТабличнойЧастиСервер.Вставить(Объект, ИмяТЧ, Элементы, КоличествоСкопированных, КоличествоВставленных);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ПодключаемыеКоманды

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
     ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
     ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
     ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры
 
&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
     ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.РаботаСФайлами

//@skip-warning
&НаКлиенте
Процедура Подключаемый_КомандаПанелиПрисоединенныхФайлов(Команда)

	РаботаСФайламиКлиент.КомандаУправленияПрисоединеннымиФайлами(ЭтотОбъект, Команда);

КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраНажатие(Элемент, СтандартнаяОбработка)
    РаботаСФайламиКлиент.ПолеПредпросмотраНажатие(ЭтотОбъект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
    РаботаСФайламиКлиент.ПолеПредпросмотраПеретаскивание(ЭтотОбъект, Элемент,
		ПараметрыПеретаскивания, СтандартнаяОбработка);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
    РаботаСФайламиКлиент.ПолеПредпросмотраПроверкаПеретаскивания(ЭтотОбъект, Элемент,
		ПараметрыПеретаскивания, СтандартнаяОбработка);
КонецПроцедуры

// Конец СтандартныеПодсистемы.РаботаСФайлами

#КонецОбласти
