#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события ОбработкаЗаполнения объекта.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеОбъектовБП.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПодоходныйНалог(ДанныеУчетнойПолитики) Экспорт
	
	Если ВидСубъекта = 2 Тогда
		// Крупный
		НачалоПериода = НачалоМесяца(Дата);
		КонецПериода = КонецМесяца(Дата);
		КоличествоМесяцев = 1;
	Иначе 
		// Малый, Средний
		НачалоПериода = НачалоКвартала(Дата);
		КонецПериода = КонецКвартала(Дата);
		КоличествоМесяцев = 3;
	КонецЕсли;
	
	ТекстыЗапроса = Новый Массив;
	
	// Определение больше 15 дней (в месяце приема и увольнения) для сотрудников.	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	СотрудникиСрезПоследних.ФизЛицо КАК ФизЛицо,
		|	ДЕНЬ(СотрудникиСрезПоследних.Период) КАК ДеньПриема,
		|	0 КАК ДеньУвольнения
		|ПОМЕСТИТЬ ВременнаяТаблицаСотрудникиПриемУвольнение
		|ИЗ
		|	РегистрСведений.Сотрудники.СрезПоследних(
		|			&КонецПериода,
		|			Период МЕЖДУ &НачалоПериода И &КонецПериода
		|				И Организация = &Организация
		|				И ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Прием)) КАК СотрудникиСрезПоследних
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СотрудникиСрезПоследних.ФизЛицо,
		|	0,
		|	ДЕНЬ(СотрудникиСрезПоследних.Период)
		|ИЗ
		|	РегистрСведений.Сотрудники.СрезПоследних(
		|			&КонецПериода,
		|			Период МЕЖДУ &НачалоПериода И &КонецПериода
		|				И Организация = &Организация
		|				И ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)) КАК СотрудникиСрезПоследних
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СотрудникиПоТрудовымСоглашениямСрезПоследних.ФизЛицо,
		|	ДЕНЬ(СотрудникиПоТрудовымСоглашениямСрезПоследних.Период),
		|	0
		|ИЗ
		|	РегистрСведений.СотрудникиПоТрудовымСоглашениям.СрезПоследних(
		|			&КонецПериода,
		|			Период МЕЖДУ &НачалоПериода И &КонецПериода
		|				И Организация = &Организация
		|				И ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Прием)) КАК СотрудникиПоТрудовымСоглашениямСрезПоследних
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СотрудникиПоТрудовымСоглашениямСрезПоследних.ФизЛицо,
		|	0,
		|	ДЕНЬ(СотрудникиПоТрудовымСоглашениямСрезПоследних.Период)
		|ИЗ
		|	РегистрСведений.СотрудникиПоТрудовымСоглашениям.СрезПоследних(
		|			&КонецПериода,
		|			Период МЕЖДУ &НачалоПериода И &КонецПериода
		|				И Организация = &Организация
		|				И ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)) КАК СотрудникиПоТрудовымСоглашениямСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаСотрудникиПриемУвольнение.ФизЛицо КАК ФизЛицо,
		|	СУММА(ВременнаяТаблицаСотрудникиПриемУвольнение.ДеньПриема) КАК ДеньПриема,
		|	СУММА(ВременнаяТаблицаСотрудникиПриемУвольнение.ДеньУвольнения) КАК ДеньУвольнения,
		|	ВЫБОР
		|		КОГДА ВЫБОР
		|				КОГДА СУММА(ВременнаяТаблицаСотрудникиПриемУвольнение.ДеньУвольнения) = 0
		|					ТОГДА ДЕНЬ(&КонецПериода)
		|				ИНАЧЕ СУММА(ВременнаяТаблицаСотрудникиПриемУвольнение.ДеньУвольнения)
		|			КОНЕЦ - СУММА(ВременнаяТаблицаСотрудникиПриемУвольнение.ДеньПриема) >= 15
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Больше15дней
		|ПОМЕСТИТЬ ВременнаяТаблицаСотрудникиОтработаноДней
		|ИЗ
		|	ВременнаяТаблицаСотрудникиПриемУвольнение КАК ВременнаяТаблицаСотрудникиПриемУвольнение
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаСотрудникиПриемУвольнение.ФизЛицо
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВременнаяТаблицаСотрудникиПриемУвольнение";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// Получение данных по сострудникам и статусам
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	СотрудникиСрезПоследних.ФизЛицо КАК ФизЛицо,
		|	СотрудникиСрезПоследних.Подразделение КАК Подразделение,
		| 	ЛОЖЬ КАК ТрудовоеСоглашение
		|ПОМЕСТИТЬ ВременнаяТаблицаСрезСотрудники
		|ИЗ
		|	РегистрСведений.Сотрудники.СрезПоследних(&КонецПериода, Организация = &Организация) КАК СотрудникиСрезПоследних
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СотрудникиПоТрудовымСоглашениямСрезПоследних.ФизЛицо,
		|	СотрудникиПоТрудовымСоглашениямСрезПоследних.Подразделение,
		|	ИСТИНА
		|ИЗ
		|	РегистрСведений.СотрудникиПоТрудовымСоглашениям.СрезПоследних(&КонецПериода, Организация = &Организация) КАК СотрудникиПоТрудовымСоглашениямСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаСрезСотрудники.ФизЛицо КАК ФизЛицо,
		|	МАКСИМУМ(ВременнаяТаблицаСрезСотрудники.Подразделение) КАК Подразделение,
		|	СтатусыСотрудниковСрезПоследних.Статус КАК Статус,
		|	ЕСТЬNULL(ВременнаяТаблицаСотрудникиОтработаноДней.Больше15дней, ИСТИНА) КАК ПрименятьВычет
		|ПОМЕСТИТЬ ВременнаяТаблицаСотрудники
		|ИЗ
		|	ВременнаяТаблицаСрезСотрудники КАК ВременнаяТаблицаСрезСотрудники
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыСотрудников.СрезПоследних(
		|				&КонецПериода,
		|				Организация = &Организация
		|					И ФизЛицо В
		|						(ВЫБРАТЬ
		|							ВременнаяТаблицаСрезСотрудники.ФизЛицо КАК ФизЛицо
		|						ИЗ
		|							ВременнаяТаблицаСрезСотрудники КАК ВременнаяТаблицаСрезСотрудники)) КАК СтатусыСотрудниковСрезПоследних
		|		ПО ВременнаяТаблицаСрезСотрудники.ФизЛицо = СтатусыСотрудниковСрезПоследних.ФизЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСотрудникиОтработаноДней КАК ВременнаяТаблицаСотрудникиОтработаноДней
		|		ПО ВременнаяТаблицаСрезСотрудники.ФизЛицо = ВременнаяТаблицаСотрудникиОтработаноДней.ФизЛицо
		|ГДЕ
		|	ВременнаяТаблицаСрезСотрудники.Подразделение В(&МассивПодразделений)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаСрезСотрудники.ФизЛицо,
		|	СтатусыСотрудниковСрезПоследних.Статус,
		|	ЕСТЬNULL(ВременнаяТаблицаСотрудникиОтработаноДней.Больше15дней, ИСТИНА)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо";
	
	Если НЕ ДанныеУчетнойПолитики.ПределыПоПодразделениям Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВременнаяТаблицаСрезСотрудники.Подразделение В(&МассивПодразделений)", "ИСТИНА");
	КонецЕсли;	

	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// Получение данных по подоходному налогу
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаДокумента.ПериодМесяц КАК ПериодМесяц,
		|	ТаблицаДокумента.Организация КАК Организация,
		|	ТаблицаДокумента.ФизЛицо КАК ФизЛицо,		
		|	ВЫБОР
		|		КОГДА НЕ ВременнаяТаблицаСрезСотрудники.ФизЛицо ЕСТЬ NULL
		|			И ВременнаяТаблицаСрезСотрудники.ТрудовоеСоглашение 
		|			ТОГДА &КоличествоМесяцев		
		|		ИНАЧЕ 1 
		|	КОНЕЦ КАК Количество,
		|	ТаблицаДокумента.ВсегоНачисленоОборот КАК ВсегоНачислено,
		|	ТаблицаДокумента.ВсегоНачисленоОборот - ТаблицаДокумента.ПФРДляРасчетаПНОборот - ТаблицаДокумента.ГНПФРДляРасчетаПНОборот - ТаблицаДокумента.ПНОборот КАК КВыплате,
		|	ТаблицаДокумента.ВсегоНачисленоОборот - ТаблицаДокумента.ОДПНОборот КАК СуммаНеоблагаемая,
		|	ВЫБОР
		|		КОГДА ТаблицаДокумента.ОДПНОборот > 0
		|			ТОГДА ТаблицаДокумента.ВычетыОборот * 100 + ТаблицаДокумента.ПФРДляРасчетаПНОборот + ТаблицаДокумента.ГНПФРДляРасчетаПНОборот
		|		ИНАЧЕ ТаблицаДокумента.ПФРДляРасчетаПНОборот + ТаблицаДокумента.ГНПФРДляРасчетаПНОборот
		|	КОНЕЦ КАК Вычеты,
		|	ВЫБОР
		|		КОГДА ТаблицаДокумента.ОДПНОборот > 0
		|			ТОГДА ТаблицаДокумента.ОДПНОборот - ТаблицаДокумента.ВычетыОборот * 100 - ТаблицаДокумента.ПФРДляРасчетаПНОборот - ТаблицаДокумента.ГНПФРДляРасчетаПНОборот
		|		ИНАЧЕ ТаблицаДокумента.ОДПНОборот - ТаблицаДокумента.ПФРДляРасчетаПНОборот - ТаблицаДокумента.ГНПФРДляРасчетаПНОборот
		|	КОНЕЦ КАК СуммаОблагаемая,
		|	ТаблицаДокумента.ПНОборот КАК СуммаПНсСотрудника,
		|	ТаблицаДокумента.ДоплатаПНОборот КАК ДоплатаПН,
		|	ТаблицаДокумента.ПНОборот + ТаблицаДокумента.ДоплатаПНОборот КАК ВсегоПН
		|ПОМЕСТИТЬ ВременнаяТаблицаДанныеПоПН
		|ИЗ
		|	РегистрНакопления.ПодоходныйНалог.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Авто,
		|			Организация = &Организация
		|				И ФизЛицо В
		|					(ВЫБРАТЬ
		|						ВременнаяТаблицаСотрудники.ФизЛицо КАК ФизЛицо
		|					ИЗ
		|						ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники)) КАК ТаблицаДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСрезСотрудники КАК ВременнаяТаблицаСрезСотрудники
		|		ПО ТаблицаДокумента.ФизЛицо = ВременнаяТаблицаСрезСотрудники.ФизЛицо
		|ГДЕ
		|	ТаблицаДокумента.ВсегоНачисленоОборот > 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// Выплаченная заработная плата.
	// Выборка по счету 3520 с корр.счетов Банк/Касса или 1520 (Под.отчет).
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ХозрасчетныйОбороты.Субконто1 КАК ФизЛицо,
		|	ХозрасчетныйОбороты.СуммаОборот КАК СуммаВыплачено,
		|	ХозрасчетныйОбороты.ПериодМесяц КАК ПериодМесяц
		|ПОМЕСТИТЬ ВременнаяТаблицаВыплата
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Авто,
		|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НачисленнаяЗаработнаяПлата),
		|			&СубконтоФизЛица,
		|			Организация = &Организация
		|				И Субконто1 В
		|					(ВЫБРАТЬ
		|						ВременнаяТаблицаСотрудники.ФизЛицо КАК ФизЛицо
		|					ИЗ
		|						ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники),
		|			КорСчет В (&СчетаУчетаДенежныхСредств),
		|			) КАК ХозрасчетныйОбороты
		|ГДЕ
		|	НЕ &НеЗаполнятьВыплатыПоЗП
		|	И НЕ &ЗаполнятьВыплатуПНПоНачислениям
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо,
		|	ПериодМесяц";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// По данным Закрытия месяца по ЗП.
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА НЕ ВременнаяТаблицаСотрудники.Статус.НомерСтрокиВОтчетеПН = """"
		|				И НЕ ВременнаяТаблицаСотрудники.Статус.НомерСтрокиВОтчетеПН = ""0""
		|			ТОГДА ВременнаяТаблицаСотрудники.Статус.НомерСтрокиВОтчетеПН
		|		КОГДА ВременнаяТаблицаСотрудники.Статус.Категория = ЗНАЧЕНИЕ(Справочник.КатегорииСотрудников.Арендодатели)
		|			ТОГДА ""4""
		|		КОГДА ВременнаяТаблицаСотрудники.Статус.Категория = ЗНАЧЕНИЕ(Справочник.КатегорииСотрудников.ИностранныеГраждане)
		|				ИЛИ ВременнаяТаблицаСотрудники.Статус.Категория = ЗНАЧЕНИЕ(Справочник.КатегорииСотрудников.ТрудовыеМигранты)
		|			ТОГДА ""3""
		|		КОГДА ЕСТЬNULL(ВременнаяТаблицаСотрудники.ПрименятьВычет, ИСТИНА)
		|			ТОГДА ""1""
		|		ИНАЧЕ ""2""
		|	КОНЕЦ КАК НомерСтрокиОтчета,
		|	ВременнаяТаблицаДанныеПоПН.ФизЛицо КАК ФизЛицо,
		|	ВременнаяТаблицаДанныеПоПН.Количество КАК Количество,
		|	ВременнаяТаблицаДанныеПоПН.ВсегоНачислено КАК ВсегоНачислено,
		|	ВременнаяТаблицаДанныеПоПН.СуммаНеоблагаемая КАК СуммаНеоблагаемая,
		|	ВременнаяТаблицаДанныеПоПН.Вычеты КАК ПиИВычеты,
		|	ВременнаяТаблицаДанныеПоПН.СуммаОблагаемая КАК СуммаОблагаемая,
		|	ВременнаяТаблицаДанныеПоПН.СуммаПНсСотрудника КАК СуммаПНсСотрудника,
		|	ВременнаяТаблицаДанныеПоПН.ДоплатаПН КАК ДоплатаПН,
		|	ВременнаяТаблицаДанныеПоПН.ВсегоПН КАК ВсегоПН,
		|	ВЫБОР
		|		КОГДА &ЗаполнятьВыплатуПНПоНачислениям
		|			ТОГДА ВременнаяТаблицаДанныеПоПН.КВыплате
		|		ИНАЧЕ ЕСТЬNULL(ВременнаяТаблицаВыплата.СуммаВыплачено, 0)
		|	КОНЕЦ КАК СуммаВыплачено,
		|	НЕОПРЕДЕЛЕНО КАК Регистратор,
		|	НЕОПРЕДЕЛЕНО КАК НомерСтроки
		|ПОМЕСТИТЬ ВременнаяТаблицаВсеСтроки
		|ИЗ
		|	ВременнаяТаблицаДанныеПоПН КАК ВременнаяТаблицаДанныеПоПН
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаВыплата КАК ВременнаяТаблицаВыплата
		|		ПО ВременнаяТаблицаДанныеПоПН.ФизЛицо = ВременнаяТаблицаВыплата.ФизЛицо
		|			И ВременнаяТаблицаДанныеПоПН.ПериодМесяц = ВременнаяТаблицаВыплата.ПериодМесяц
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники
		|		ПО ВременнаяТаблицаДанныеПоПН.ФизЛицо = ВременнаяТаблицаСотрудники.ФизЛицо
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА НЕ ВременнаяТаблицаСотрудники.Статус.НомерСтрокиВОтчетеПН = """"
		|				И НЕ ВременнаяТаблицаСотрудники.Статус.НомерСтрокиВОтчетеПН = ""0""
		|			ТОГДА ВременнаяТаблицаСотрудники.Статус.НомерСтрокиВОтчетеПН
		|		КОГДА ВременнаяТаблицаСотрудники.Статус.Категория = ЗНАЧЕНИЕ(Справочник.КатегорииСотрудников.Арендодатели)
		|			ТОГДА ""4""
		|		КОГДА ВременнаяТаблицаСотрудники.Статус.Категория = ЗНАЧЕНИЕ(Справочник.КатегорииСотрудников.ИностранныеГраждане)
		|				ИЛИ ВременнаяТаблицаСотрудники.Статус.Категория = ЗНАЧЕНИЕ(Справочник.КатегорииСотрудников.ТрудовыеМигранты)
		|			ТОГДА ""3""
		|		КОГДА ЕСТЬNULL(ВременнаяТаблицаСотрудники.ПрименятьВычет, ИСТИНА)
		|			ТОГДА ""1""
		|		ИНАЧЕ ""2""
		|	КОНЕЦ,
		|	ВременнаяТаблицаВыплата.ФизЛицо,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	ВременнаяТаблицаВыплата.СуммаВыплачено,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО
		|ИЗ
		|	ВременнаяТаблицаВыплата КАК ВременнаяТаблицаВыплата
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаДанныеПоПН КАК ВременнаяТаблицаДанныеПоПН
		|		ПО ВременнаяТаблицаВыплата.ФизЛицо = ВременнаяТаблицаДанныеПоПН.ФизЛицо
		|			И ВременнаяТаблицаВыплата.ПериодМесяц = ВременнаяТаблицаДанныеПоПН.ПериодМесяц
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники
		|		ПО ВременнаяТаблицаВыплата.ФизЛицо = ВременнаяТаблицаСотрудники.ФизЛицо
		|ГДЕ
		|	ВременнаяТаблицаДанныеПоПН.ФизЛицо ЕСТЬ NULL";
	ТекстЗапроса = ТекстЗапроса + Символы.ПС + " ОБЪЕДИНИТЬ ВСЕ " + Символы.ПС;
	
	// По данным ручных операций
	// Заполнение 4-ой строки
	// Количество равно количеству записей в документе.
	ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	4 КАК НомерСтрокиОтчета,
		|	ХозрасчетныйОбороты.Субконто1 КАК ФизЛицо,
		|	&КоличествоМесяцев КАК Количество,
		|	ХозрасчетныйОбороты.СуммаОборотКт * 10 КАК ВсегоНачислено,
		|	0 КАК СуммаНеоблагаемая,
		|	0 КАК ПиИВычеты,
		|	ХозрасчетныйОбороты.СуммаОборотКт * 10 КАК СуммаОблагаемая,
		|	ХозрасчетныйОбороты.СуммаОборотКт КАК СуммаПНсСотрудника,
		|	0 КАК ДоплатаПН,
		|	ХозрасчетныйОбороты.СуммаОборотКт КАК ВсегоПН,
		|	ХозрасчетныйОбороты.СуммаОборотКт * 10 - ХозрасчетныйОбороты.СуммаОборотКт КАК СуммаВыплачено,
		|	ХозрасчетныйОбороты.Регистратор КАК Регистратор,
		|	ХозрасчетныйОбороты.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Авто, Счет = &СчетУчетаПоПНДляПрочихФизическихЛиц, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СотрудникиОрганизаций), Организация = &Организация, , ) КАК ХозрасчетныйОбороты
		|ГДЕ
		|	НЕ ЕСТЬNULL(ХозрасчетныйОбороты.СуммаОборотКт, 0) = 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// Итоги объединения.
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаВсеСтроки.НомерСтрокиОтчета КАК НомерСтрокиОтчета,
	|	ВременнаяТаблицаВсеСтроки.ФизЛицо КАК ФизЛицо,
	|	СУММА(ВЫРАЗИТЬ(ВременнаяТаблицаВсеСтроки.Количество / &КоличествоМесяцев КАК ЧИСЛО(7, 2))) КАК Количество,
	|	СУММА(ВременнаяТаблицаВсеСтроки.ВсегоНачислено) КАК СуммаНачислено,
	|	СУММА(ВременнаяТаблицаВсеСтроки.СуммаНеоблагаемая) КАК СуммаНеоблагаемая,
	|	СУММА(ВременнаяТаблицаВсеСтроки.ПиИВычеты) КАК Вычеты,
	|	СУММА(ВременнаяТаблицаВсеСтроки.СуммаОблагаемая) КАК СуммаОблагаемая,
	|	СУММА(ВременнаяТаблицаВсеСтроки.СуммаПНсСотрудника) КАК СуммаПНсСотрудника,
	|	СУММА(ВременнаяТаблицаВсеСтроки.ДоплатаПН) КАК СуммаПНсМРД,
	|	СУММА(ВременнаяТаблицаВсеСтроки.ВсегоПН) КАК СуммаПН,
	|	СУММА(ВременнаяТаблицаВсеСтроки.ВсегоПН) КАК СальдоК,
	|	СУММА(ВременнаяТаблицаВсеСтроки.СуммаВыплачено) КАК СуммаВыплачено
	|ИЗ
	|	ВременнаяТаблицаВсеСтроки КАК ВременнаяТаблицаВсеСтроки
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаВсеСтроки.НомерСтрокиОтчета,
	|	ВременнаяТаблицаВсеСтроки.ФизЛицо
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтрокиОтчета";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);     
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);     
	Запрос.УстановитьПараметр("КоличествоМесяцев", КоличествоМесяцев);     
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("НеЗаполнятьВыплатыПоЗП", НеЗаполнятьВыплатыПоЗП);
	Запрос.УстановитьПараметр("СубконтоФизЛица", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СотрудникиОрганизаций);
	Запрос.УстановитьПараметр("ЗаполнятьВыплатуПНПоНачислениям", Константы.ЗаполнятьВыплатуПНПоНачислениям.Получить());
	Запрос.УстановитьПараметр("СчетУчетаПоПНДляПрочихФизическихЛиц", ДанныеУчетнойПолитики.СчетУчетаПоПНДляПрочихФизическихЛиц);
	
	// Определение отбора по подразделениям.
	Если ДанныеУчетнойПолитики.ПределыПоПодразделениям Тогда
		МассивПодразделений = Новый Массив;
		
		ЗапросПодразделения = Новый Запрос;
		ЗапросПодразделения.Текст = 
		"ВЫБРАТЬ
		|	ПределыПоПодразделениямСрезПоследних.Подразделение КАК Подразделение
		|ИЗ
		|	РегистрСведений.ПределыПоПодразделениям.СрезПоследних(
		|			&Период,
		|			Организация = &Организация) КАК ПределыПоПодразделениямСрезПоследних
		|ГДЕ
		|	ПределыПоПодразделениямСрезПоследних.УГНС = &Контрагент";
		ЗапросПодразделения.УстановитьПараметр("Период", КонецПериода);
		ЗапросПодразделения.УстановитьПараметр("Организация", Организация);
		ЗапросПодразделения.УстановитьПараметр("Контрагент", Контрагент);
		
		РезультатЗапроса = ЗапросПодразделения.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			МассивПодразделений.Добавить(ВыборкаДетальныеЗаписи.Подразделение);		
		КонецЦикла;
		
		Запрос.УстановитьПараметр("МассивПодразделений", МассивПодразделений);
	КонецЕсли;
	
	СчетаУчетаДенежныхСредств = БухгалтерскийУчетСервер.СчетаУчетаДенежныхСредств();
	СчетаУчетаДенежныхСредств.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицами); // 1520
	Запрос.УстановитьПараметр("СчетаУчетаДенежныхСредств", СчетаУчетаДенежныхСредств);
	
	ПодоходныйНалог.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

Процедура ЗаполнитьПодоходныйНалогПриложение(ДанныеУчетнойПолитики) Экспорт
	
	// Данные выбираются за 3 месяца квартала.	
	ТекстыЗапроса = Новый Массив;
	
	// Определение больше 15 дней (в месяце приема и увольнения) для сотрудников.	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	СотрудникиСрезПоследних.ФизЛицо КАК ФизЛицо,
		|	ДЕНЬ(СотрудникиСрезПоследних.Период) КАК ДеньПриема,
		|	0 КАК ДеньУвольнения
		|ПОМЕСТИТЬ ВременнаяТаблицаСотрудникиПриемУвольнение
		|ИЗ
		|	РегистрСведений.Сотрудники.СрезПоследних(
		|			&КонецПериода,
		|			Период МЕЖДУ &НачалоПериода И &КонецПериода
		|				И Организация = &Организация
		|				И ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Прием)) КАК СотрудникиСрезПоследних
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СотрудникиСрезПоследних.ФизЛицо,
		|	0,
		|	ДЕНЬ(СотрудникиСрезПоследних.Период)
		|ИЗ
		|	РегистрСведений.Сотрудники.СрезПоследних(
		|			&КонецПериода,
		|			Период МЕЖДУ &НачалоПериода И &КонецПериода
		|				И Организация = &Организация
		|				И ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)) КАК СотрудникиСрезПоследних
		// Сотрудники, принятые на работу по трудовым соглашениям.
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СотрудникиПоТрудовымСоглашениямСрезПоследних.ФизЛицо КАК ФизЛицо,
		|	ДЕНЬ(СотрудникиПоТрудовымСоглашениямСрезПоследних.Период) КАК ДеньПриема,
		|	0 КАК ДеньУвольнения
		|ИЗ
		|	РегистрСведений.СотрудникиПоТрудовымСоглашениям.СрезПоследних(
		|			&КонецПериода,
		|			Период МЕЖДУ &НачалоПериода И &КонецПериода
		|				И Организация = &Организация
		|				И ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Прием)) КАК СотрудникиПоТрудовымСоглашениямСрезПоследних
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СотрудникиПоТрудовымСоглашениямСрезПоследних.ФизЛицо,
		|	0,
		|	ДЕНЬ(СотрудникиПоТрудовымСоглашениямСрезПоследних.Период)
		|ИЗ
		|	РегистрСведений.СотрудникиПоТрудовымСоглашениям.СрезПоследних(
		|			&КонецПериода,
		|			Период МЕЖДУ &НачалоПериода И &КонецПериода
		|				И Организация = &Организация
		|				И ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)) КАК СотрудникиПоТрудовымСоглашениямСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаСотрудникиПриемУвольнение.ФизЛицо КАК ФизЛицо,
		|	СУММА(ВременнаяТаблицаСотрудникиПриемУвольнение.ДеньПриема) КАК ДеньПриема,
		|	СУММА(ВременнаяТаблицаСотрудникиПриемУвольнение.ДеньУвольнения) КАК ДеньУвольнения,
		|	ВЫБОР
		|		КОГДА ВЫБОР
		|				КОГДА СУММА(ВременнаяТаблицаСотрудникиПриемУвольнение.ДеньУвольнения) = 0
		|					ТОГДА ДЕНЬ(&КонецПериода)
		|				ИНАЧЕ СУММА(ВременнаяТаблицаСотрудникиПриемУвольнение.ДеньУвольнения)
		|			КОНЕЦ - СУММА(ВременнаяТаблицаСотрудникиПриемУвольнение.ДеньПриема) >= 15
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Больше15дней
		|ПОМЕСТИТЬ ВременнаяТаблицаСотрудникиОтработаноДней
		|ИЗ
		|	ВременнаяТаблицаСотрудникиПриемУвольнение КАК ВременнаяТаблицаСотрудникиПриемУвольнение
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаСотрудникиПриемУвольнение.ФизЛицо
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВременнаяТаблицаСотрудникиПриемУвольнение";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса);

	// Получение данных по сострудникам и статусам
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	СотрудникиСрезПоследних.ФизЛицо КАК ФизЛицо,
		|	СотрудникиСрезПоследних.Подразделение КАК Подразделение
		|ПОМЕСТИТЬ ВременнаяТаблицаСрезСотрудники
		|ИЗ
		|	РегистрСведений.Сотрудники.СрезПоследних(&КонецПериода, Организация = &Организация) КАК СотрудникиСрезПоследних
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СотрудникиПоТрудовымСоглашениямСрезПоследних.ФизЛицо,
		|	СотрудникиПоТрудовымСоглашениямСрезПоследних.Подразделение
		|ИЗ
		|	РегистрСведений.СотрудникиПоТрудовымСоглашениям.СрезПоследних(&КонецПериода, Организация = &Организация) КАК СотрудникиПоТрудовымСоглашениямСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаСрезСотрудники.ФизЛицо КАК ФизЛицо,
		|	МАКСИМУМ(ВременнаяТаблицаСрезСотрудники.Подразделение) КАК Подразделение,
		|	СтатусыСотрудниковСрезПоследних.Статус КАК Статус,
		|	ЕСТЬNULL(ВременнаяТаблицаСотрудникиОтработаноДней.Больше15дней, ИСТИНА) КАК ПрименятьВычет
		|ПОМЕСТИТЬ ВременнаяТаблицаСотрудники
		|ИЗ
		|	ВременнаяТаблицаСрезСотрудники КАК ВременнаяТаблицаСрезСотрудники
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыСотрудников.СрезПоследних(
		|				&КонецПериода,
		|				ФизЛицо В
		|					(ВЫБРАТЬ
		|						ВременнаяТаблицаСрезСотрудники.ФизЛицо КАК ФизЛицо
		|					ИЗ
		|						ВременнаяТаблицаСрезСотрудники КАК ВременнаяТаблицаСрезСотрудники)) КАК СтатусыСотрудниковСрезПоследних
		|		ПО ВременнаяТаблицаСрезСотрудники.ФизЛицо = СтатусыСотрудниковСрезПоследних.ФизЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСотрудникиОтработаноДней КАК ВременнаяТаблицаСотрудникиОтработаноДней
		|		ПО ВременнаяТаблицаСрезСотрудники.ФизЛицо = ВременнаяТаблицаСотрудникиОтработаноДней.ФизЛицо
		|ГДЕ
		|	ВременнаяТаблицаСрезСотрудники.Подразделение В(&МассивПодразделений)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаСрезСотрудники.ФизЛицо,
		|	СтатусыСотрудниковСрезПоследних.Статус,
		|	ЕСТЬNULL(ВременнаяТаблицаСотрудникиОтработаноДней.Больше15дней, ИСТИНА)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо";
	Если НЕ ДанныеУчетнойПолитики.ПределыПоПодразделениям Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВременнаяТаблицаСрезСотрудники.Подразделение В(&МассивПодразделений)", "ИСТИНА");
	КонецЕсли;	

	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// Получение данных по подоходному налогу
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаДокумента.ПериодМесяц КАК ПериодМесяц,
		|	ТаблицаДокумента.Организация КАК Организация,
		|	ТаблицаДокумента.ФизЛицо КАК ФизЛицо,
		|	1 КАК Количество,
		|	ТаблицаДокумента.ВсегоНачисленоОборот КАК ВсегоНачислено,
		|	ТаблицаДокумента.ВсегоНачисленоОборот - ТаблицаДокумента.ПФРДляРасчетаПНОборот - ТаблицаДокумента.ГНПФРДляРасчетаПНОборот - ТаблицаДокумента.ПНОборот КАК КВыплате,
		|	ТаблицаДокумента.ВсегоНачисленоОборот - ТаблицаДокумента.ОДПНОборот КАК СуммаНеоблагаемая,
		|	ВЫБОР
		|		КОГДА ТаблицаДокумента.ОДПНОборот > 0
		|			ТОГДА ТаблицаДокумента.ВычетыОборот * 100 + ТаблицаДокумента.ПФРДляРасчетаПНОборот + ТаблицаДокумента.ГНПФРДляРасчетаПНОборот
		|		ИНАЧЕ ТаблицаДокумента.ПФРДляРасчетаПНОборот + ТаблицаДокумента.ГНПФРДляРасчетаПНОборот
		|	КОНЕЦ КАК Вычеты,
		|	ВЫБОР
		|		КОГДА ТаблицаДокумента.ОДПНОборот > 0
		|			ТОГДА ТаблицаДокумента.ОДПНОборот - ТаблицаДокумента.ВычетыОборот * 100 - ТаблицаДокумента.ПФРДляРасчетаПНОборот - ТаблицаДокумента.ГНПФРДляРасчетаПНОборот
		|		ИНАЧЕ ТаблицаДокумента.ОДПНОборот - ТаблицаДокумента.ПФРДляРасчетаПНОборот - ТаблицаДокумента.ГНПФРДляРасчетаПНОборот
		|	КОНЕЦ КАК СуммаОблагаемая,
		|	ТаблицаДокумента.ПНОборот КАК СуммаПНсСотрудника,
		|	ТаблицаДокумента.ДоплатаПНОборот КАК ДоплатаПН,
		|	ТаблицаДокумента.ПНОборот + ТаблицаДокумента.ДоплатаПНОборот КАК ВсегоПН
		|ПОМЕСТИТЬ ВременнаяТаблицаДанныеПоПН
		|ИЗ
		|	РегистрНакопления.ПодоходныйНалог.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Авто,
		|			Организация = &Организация
		|				И ФизЛицо В
		|					(ВЫБРАТЬ
		|						ВременнаяТаблицаСотрудники.ФизЛицо КАК ФизЛицо
		|					ИЗ
		|						ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники)) КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.ВсегоНачисленоОборот > 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса);

	// Выплаченная заработная плата.
	// Выборка по счету 3520 с корр.счетов Банк/Касса. 
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ХозрасчетныйОбороты.Субконто1 КАК ФизЛицо,
		|	ХозрасчетныйОбороты.СуммаОборот КАК СуммаВыплачено,
		|	ХозрасчетныйОбороты.ПериодМесяц КАК ПериодМесяц
		|ПОМЕСТИТЬ ВременнаяТаблицаВыплата
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Авто,
		|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НачисленнаяЗаработнаяПлата),
		|			&СубконтоФизЛица,
		|			Организация = &Организация
		|				И Субконто1 В
		|					(ВЫБРАТЬ
		|						ВременнаяТаблицаСотрудники.ФизЛицо КАК ФизЛицо
		|					ИЗ
		|						ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники),
		|			КорСчет В (&СчетаУчетаДенежныхСредств),
		|			) КАК ХозрасчетныйОбороты
		|ГДЕ
		|	НЕ &НеЗаполнятьВыплатыПоЗП
		|	И НЕ &ЗаполнятьВыплатуПНПоНачислениям
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо,
		|	ПериодМесяц";
	ТекстыЗапроса.Добавить(ТекстЗапроса);

	// По данным Закрытия месяца по ЗП.
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаДанныеПоПН.ПериодМесяц КАК ПериодМесяц,
		|	ВЫБОР
		|		КОГДА НЕ ВременнаяТаблицаСотрудники.Статус.НомерСтрокиВОтчетеПН = """"
		|				И НЕ ВременнаяТаблицаСотрудники.Статус.НомерСтрокиВОтчетеПН = ""0""
		|			ТОГДА ВременнаяТаблицаСотрудники.Статус.НомерСтрокиВОтчетеПН
		|		КОГДА ВременнаяТаблицаСотрудники.Статус.Категория = ЗНАЧЕНИЕ(Справочник.КатегорииСотрудников.Арендодатели)
		|			ТОГДА ""4""
		|		КОГДА ВременнаяТаблицаСотрудники.Статус.Категория = ЗНАЧЕНИЕ(Справочник.КатегорииСотрудников.ИностранныеГраждане)
		|				ИЛИ ВременнаяТаблицаСотрудники.Статус.Категория = ЗНАЧЕНИЕ(Справочник.КатегорииСотрудников.ТрудовыеМигранты)
		|			ТОГДА ""3""
		|		КОГДА ЕСТЬNULL(ВременнаяТаблицаСотрудники.ПрименятьВычет, ИСТИНА)
		|			ТОГДА ""1""
		|		ИНАЧЕ ""2""
		|	КОНЕЦ КАК НомерСтрокиОтчета,
		|	ВременнаяТаблицаДанныеПоПН.ФизЛицо КАК ФизЛицо,
		|	ВременнаяТаблицаДанныеПоПН.Количество КАК Количество,
		|	ВременнаяТаблицаДанныеПоПН.ВсегоНачислено КАК ВсегоНачислено,
		|	ВременнаяТаблицаДанныеПоПН.СуммаНеоблагаемая КАК СуммаНеоблагаемая,
		|	ВременнаяТаблицаДанныеПоПН.Вычеты КАК ПиИВычеты,
		|	ВременнаяТаблицаДанныеПоПН.СуммаОблагаемая КАК СуммаОблагаемая,
		|	ВременнаяТаблицаДанныеПоПН.СуммаПНсСотрудника КАК СуммаПНсСотрудника,
		|	ВременнаяТаблицаДанныеПоПН.ДоплатаПН КАК ДоплатаПН,
		|	ВременнаяТаблицаДанныеПоПН.ВсегоПН КАК ВсегоПН,
		|	ВЫБОР
		|		КОГДА &ЗаполнятьВыплатуПНПоНачислениям
		|			ТОГДА ВременнаяТаблицаДанныеПоПН.КВыплате
		|		ИНАЧЕ ЕСТЬNULL(ВременнаяТаблицаВыплата.СуммаВыплачено, 0)
		|	КОНЕЦ КАК СуммаВыплачено
		|ПОМЕСТИТЬ ВременнаяТаблицаВсеСтроки
		|ИЗ
		|	ВременнаяТаблицаДанныеПоПН КАК ВременнаяТаблицаДанныеПоПН
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаВыплата КАК ВременнаяТаблицаВыплата
		|		ПО ВременнаяТаблицаДанныеПоПН.ФизЛицо = ВременнаяТаблицаВыплата.ФизЛицо
		|			И ВременнаяТаблицаДанныеПоПН.ПериодМесяц = ВременнаяТаблицаВыплата.ПериодМесяц
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники
		|		ПО ВременнаяТаблицаДанныеПоПН.ФизЛицо = ВременнаяТаблицаСотрудники.ФизЛицо
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаВыплата.ПериодМесяц,
		|	ВЫБОР
		|		КОГДА НЕ ВременнаяТаблицаСотрудники.Статус.НомерСтрокиВОтчетеПН = """"
		|				И НЕ ВременнаяТаблицаСотрудники.Статус.НомерСтрокиВОтчетеПН = ""0""
		|			ТОГДА ВременнаяТаблицаСотрудники.Статус.НомерСтрокиВОтчетеПН
		|		КОГДА ВременнаяТаблицаСотрудники.Статус.Категория = ЗНАЧЕНИЕ(Справочник.КатегорииСотрудников.Арендодатели)
		|			ТОГДА ""4""
		|		КОГДА ВременнаяТаблицаСотрудники.Статус.Категория = ЗНАЧЕНИЕ(Справочник.КатегорииСотрудников.ИностранныеГраждане)
		|				ИЛИ ВременнаяТаблицаСотрудники.Статус.Категория = ЗНАЧЕНИЕ(Справочник.КатегорииСотрудников.ТрудовыеМигранты)
		|			ТОГДА ""3""
		|		КОГДА ЕСТЬNULL(ВременнаяТаблицаСотрудники.ПрименятьВычет, ИСТИНА)
		|			ТОГДА ""1""
		|		ИНАЧЕ ""2""
		|	КОНЕЦ,
		|	ВременнаяТаблицаВыплата.ФизЛицо,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	ВременнаяТаблицаВыплата.СуммаВыплачено
		|ИЗ
		|	ВременнаяТаблицаВыплата КАК ВременнаяТаблицаВыплата
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаДанныеПоПН КАК ВременнаяТаблицаДанныеПоПН
		|		ПО ВременнаяТаблицаВыплата.ФизЛицо = ВременнаяТаблицаДанныеПоПН.ФизЛицо
		|			И ВременнаяТаблицаВыплата.ПериодМесяц = ВременнаяТаблицаДанныеПоПН.ПериодМесяц
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники
		|		ПО ВременнаяТаблицаВыплата.ФизЛицо = ВременнаяТаблицаСотрудники.ФизЛицо
		|ГДЕ
		|	ВременнаяТаблицаДанныеПоПН.ФизЛицо ЕСТЬ NULL";
	ТекстЗапроса = ТекстЗапроса + Символы.ПС + " ОБЪЕДИНИТЬ ВСЕ " + Символы.ПС;
	
	// По данным ручных операций
	// Заполнение 4-ой строки
	// Количество равно количеству записей в документе.
	ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ХозрасчетныйОбороты.ПериодМесяц КАК ПериодМесяц,
		|	4 КАК НомерСтрокиОтчета,
		|	ХозрасчетныйОбороты.Субконто1 КАК ФизЛицо,
		|	1 КАК Количество,
		|	ХозрасчетныйОбороты.СуммаОборотКт * 10 КАК ВсегоНачислено,
		|	0 КАК СуммаНеоблагаемая,
		|	0 КАК ПиИВычеты,
		|	ХозрасчетныйОбороты.СуммаОборотКт * 10 КАК СуммаОблагаемая,
		|	ХозрасчетныйОбороты.СуммаОборотКт КАК СуммаПНсСотрудника,
		|	0 КАК ДоплатаПН,
		|	ХозрасчетныйОбороты.СуммаОборотКт КАК ВсегоПН,
		|	ХозрасчетныйОбороты.СуммаОборотКт * 10 - ХозрасчетныйОбороты.СуммаОборотКт КАК СуммаВыплачено
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Авто, Счет = &СчетУчетаПоПНДляПрочихФизическихЛиц, , Организация = &Организация, , ) КАК ХозрасчетныйОбороты
		|ГДЕ
		|	НЕ ЕСТЬNULL(ХозрасчетныйОбороты.СуммаОборотКт, 0) = 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса);

	// Итоги объединения.
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаВсеСтроки.ПериодМесяц КАК ПериодМесяц,
		|	ВременнаяТаблицаВсеСтроки.НомерСтрокиОтчета КАК НомерСтрокиОтчета,
		|	СУММА(ВременнаяТаблицаВсеСтроки.Количество) КАК Количество,
		|	СУММА(ВременнаяТаблицаВсеСтроки.ВсегоНачислено) КАК СуммаНачислено,
		|	СУММА(ВременнаяТаблицаВсеСтроки.СуммаНеоблагаемая) КАК СуммаНеоблагаемая,
		|	СУММА(ВременнаяТаблицаВсеСтроки.ПиИВычеты) КАК Вычеты,
		|	СУММА(ВременнаяТаблицаВсеСтроки.СуммаОблагаемая) КАК СуммаОблагаемая,
		|	СУММА(ВременнаяТаблицаВсеСтроки.СуммаПНсСотрудника) КАК СуммаПНсСотрудника,
		|	СУММА(ВременнаяТаблицаВсеСтроки.ДоплатаПН) КАК СуммаПНсМРД,
		|	СУММА(ВременнаяТаблицаВсеСтроки.ВсегоПН) КАК СуммаПН,
		|	СУММА(ВременнаяТаблицаВсеСтроки.ВсегоПН) КАК СальдоК,
		|	СУММА(ВременнаяТаблицаВсеСтроки.СуммаВыплачено) КАК СуммаВыплачено
		|ИЗ
		|	ВременнаяТаблицаВсеСтроки КАК ВременнаяТаблицаВсеСтроки
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаВсеСтроки.ПериодМесяц,
		|	ВременнаяТаблицаВсеСтроки.НомерСтрокиОтчета
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПериодМесяц,
		|	НомерСтрокиОтчета
		|ИТОГИ ПО
		|	ПериодМесяц";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	Запрос.УстановитьПараметр("НачалоПериода", НачалоКвартала(Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецКвартала(Дата));
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СчетУчетаПоПНДляПрочихФизическихЛиц", ДанныеУчетнойПолитики.СчетУчетаПоПНДляПрочихФизическихЛиц);
	Запрос.УстановитьПараметр("НеЗаполнятьВыплатыПоЗП", НеЗаполнятьВыплатыПоЗП);
	Запрос.УстановитьПараметр("СубконтоФизЛица", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СотрудникиОрганизаций);
	Запрос.УстановитьПараметр("ЗаполнятьВыплатуПНПоНачислениям", Константы.ЗаполнятьВыплатуПНПоНачислениям.Получить());
	
	// Определение отбора по подразделениям.
	Если ДанныеУчетнойПолитики.ПределыПоПодразделениям Тогда
		МассивПодразделений = Новый Массив;
		
		ЗапросПодразделения = Новый Запрос;
		ЗапросПодразделения.Текст = 
			"ВЫБРАТЬ
			|	ПределыПоПодразделениямСрезПоследних.Подразделение КАК Подразделение
			|ИЗ
			|	РегистрСведений.ПределыПоПодразделениям.СрезПоследних(
			|			&Период,
			|			Организация = &Организация) КАК ПределыПоПодразделениямСрезПоследних
			|ГДЕ
			|	ПределыПоПодразделениямСрезПоследних.УГНС = &Контрагент";
		ЗапросПодразделения.УстановитьПараметр("Период", КонецКвартала(Дата));
		ЗапросПодразделения.УстановитьПараметр("Организация", Организация);
		ЗапросПодразделения.УстановитьПараметр("Контрагент", Контрагент);
		
		РезультатЗапроса = ЗапросПодразделения.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			МассивПодразделений.Добавить(ВыборкаДетальныеЗаписи.Подразделение);		
		КонецЦикла;

		Запрос.УстановитьПараметр("МассивПодразделений", МассивПодразделений);
	КонецЕсли;
	
	СчетаУчетаДенежныхСредств = БухгалтерскийУчетСервер.СчетаУчетаДенежныхСредств();
	Запрос.УстановитьПараметр("СчетаУчетаДенежныхСредств", СчетаУчетаДенежныхСредств);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// Обход результата по месяцам.
	ВыборкаПериодМесяц = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПериодМесяц.Следующий() Цикл
		
		// Определения имения табличной части для заполнения.
		// Это первый месяц квартала.
		Если ВыборкаПериодМесяц.ПериодМесяц = НачалоКвартала(Дата) Тогда  
			ИмяТабличнойЧасти = "ПодоходныйНалогПервыйМесяцКвартала";	
		// Это последний месяц квартала.	
		ИначеЕсли КонецМесяца(ВыборкаПериодМесяц.ПериодМесяц) = КонецКвартала(Дата) Тогда 
			ИмяТабличнойЧасти = "ПодоходныйНалогТретийМесяцКвартала";	
		// Это средний месяц квартала.	
		Иначе 
			ИмяТабличнойЧасти = "ПодоходныйНалогВторойМесяцКвартала";	
		КонецЕсли;
		
		ВыборкаДетальныеЗаписи = ВыборкаПериодМесяц.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
			СтрокаТабличнойЧасти = ЭтотОбъект[ИмяТабличнойЧасти].Добавить();	
			ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ВыборкаДетальныеЗаписи);
		КонецЦикла;	
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли