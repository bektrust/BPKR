#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПроцедурыПроведенияДокумента

Процедура СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	
		// 2. Проводки НДС по ТЧ "Товары" 	- если не Плательщик НДС и "ЗачетНДС" не равен "Себестоимость"
		//									  ИЛИ если Плательщик НДС и "ЗачетНДС" равен "Себестоимость".
		// 3. Проводки НДС по ТЧ "ОС" 		- если не Плательщик НДС и "ЗачетНДС" не равен "Себестоимость"
		//									  ИЛИ если Плательщик НДС и "ЗачетНДС" равен "Себестоимость".
		// 4. Проводки таможенного сопровождения по ТЧ "Товары".
		// 5. Проводки таможенного сопровождения по ТЧ "ОС".
		// 6. Проводки таможенной пошлины по ТЧ "Товары".
		// 7. Проводки таможенной пошлины по ТЧ "ОС".
		// 8. Проводки таможенного сбора по ТЧ "Товары".
		// 9. Проводки таможенного сбора по ТЧ "ОС".
		// 10. Проводки акциза по ТЧ "Товары".
		// 11. Проводки акциза по ТЧ "ОС".
		"ВЫБРАТЬ
		|	2 КАК Порядок,
		|	ВременнаяТаблицаШапка.Период КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаРазделы.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Себестоимость)
		|					И &УказыватьПризнакЗачетаНДС
		|				ИЛИ НЕ &ПлательщикНДС
		|			ТОГДА ВременнаяТаблицаТовары.СчетУчета
		|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДС_ПодлежащийВозмещению)
		|	КОНЕЦ КАК СчетДт,
		|	ВременнаяТаблицаШапка.СчетРасчетовСКонтрагентом КАК СчетКт,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаРазделы.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Себестоимость)
		|				И &УказыватьПризнакЗачетаНДС
		|				ИЛИ НЕ &ПлательщикНДС
		|			ТОГДА ВременнаяТаблицаТовары.Номенклатура
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК СубконтоДт1,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаРазделы.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Себестоимость)
		|				И &УказыватьПризнакЗачетаНДС
		|				ИЛИ НЕ &ПлательщикНДС
		|			ТОГДА ВременнаяТаблицаТовары.Склад
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК СубконтоДт2,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаРазделы.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Себестоимость)
		|				И &УказыватьПризнакЗачетаНДС
		|				ИЛИ НЕ &ПлательщикНДС
		|				И ВременнаяТаблицаШапка.ВедетсяУчетПоПартиям
		|			ТОГДА ВременнаяТаблицаТовары.ДокументПоступления
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК СубконтоДт3,
		|	ВременнаяТаблицаШапка.Контрагент КАК СубконтоКт1,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	ВременнаяТаблицаТовары.СуммаНДС КАК Сумма,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаДт,
		|	ВременнаяТаблицаТовары.СуммаНДС КАК ВалютнаяСуммаДт,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаКт,
		|	ВременнаяТаблицаТовары.СуммаНДС КАК ВалютнаяСуммаКт,
		|	""НДС по импорту"" КАК Содержание,
		|	0 КАК КоличествоДт,
		|	0 КАК КоличествоКт
		|ПОМЕСТИТЬ ВременнаяТаблицаПроводкиПоНДС
		|ИЗ
		|	ВременнаяТаблицаРазделы КАК ВременнаяТаблицаРазделы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО ВременнаяТаблицаРазделы.Ссылка = ВременнаяТаблицаШапка.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|		ПО ВременнаяТаблицаРазделы.КлючСвязи = ВременнаяТаблицаТовары.КлючСвязи
		|ГДЕ
		|	ВременнаяТаблицаТовары.СуммаНДС <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	3,
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаРазделы.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Себестоимость)
		|				И &УказыватьПризнакЗачетаНДС
		|				ИЛИ НЕ &ПлательщикНДС
		|			ТОГДА ВременнаяТаблицаОС.СчетУчета
		|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДС_ПодлежащийВозмещению)
		|	КОНЕЦ,
		|	ВременнаяТаблицаШапка.СчетРасчетовСКонтрагентом,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаРазделы.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Себестоимость)
		|				И &УказыватьПризнакЗачетаНДС
		|				ИЛИ НЕ &ПлательщикНДС
		|			ТОГДА ВременнаяТаблицаОС.ОсновноеСредство
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаШапка.Контрагент,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаОС.СуммаНДС,
		|	ВременнаяТаблицаШапка.ВалютаДокумента,
		|	ВременнаяТаблицаОС.СуммаНДС,
		|	ВременнаяТаблицаШапка.ВалютаДокумента,
		|	ВременнаяТаблицаОС.СуммаНДС,
		|	""НДС по импорту"",
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаРазделы КАК ВременнаяТаблицаРазделы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО ВременнаяТаблицаРазделы.Ссылка = ВременнаяТаблицаШапка.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаОС КАК ВременнаяТаблицаОС
		|		ПО ВременнаяТаблицаРазделы.КлючСвязи = ВременнаяТаблицаОС.КлючСвязи
		|ГДЕ
		|	ВременнаяТаблицаОС.СуммаНДС <> 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(ВременнаяТаблицаПроводкиПоНДС.Порядок) КАК Порядок,
		|	ВременнаяТаблицаПроводкиПоНДС.Период КАК Период,
		|	ВременнаяТаблицаПроводкиПоНДС.Организация КАК Организация,
		|	ВременнаяТаблицаПроводкиПоНДС.СчетДт КАК СчетДт,
		|	ВременнаяТаблицаПроводкиПоНДС.СчетКт КАК СчетКт,
		|	ВременнаяТаблицаПроводкиПоНДС.СубконтоДт1 КАК СубконтоДт1,
		|	ВременнаяТаблицаПроводкиПоНДС.СубконтоДт2 КАК СубконтоДт2,
		|	ВременнаяТаблицаПроводкиПоНДС.СубконтоДт3 КАК СубконтоДт3,
		|	ВременнаяТаблицаПроводкиПоНДС.СубконтоКт1 КАК СубконтоКт1,
		|	ВременнаяТаблицаПроводкиПоНДС.СубконтоКт2 КАК СубконтоКт2,
		|	ВременнаяТаблицаПроводкиПоНДС.СубконтоКт3 КАК СубконтоКт3,
		|	СУММА(ВременнаяТаблицаПроводкиПоНДС.Сумма) КАК Сумма,
		|	ВременнаяТаблицаПроводкиПоНДС.ВалютаДт КАК ВалютаДт,
		|	СУММА(ВременнаяТаблицаПроводкиПоНДС.ВалютнаяСуммаДт) КАК ВалютнаяСуммаДт,
		|	ВременнаяТаблицаПроводкиПоНДС.ВалютаКт КАК ВалютаКт,
		|	СУММА(ВременнаяТаблицаПроводкиПоНДС.ВалютнаяСуммаКт) КАК ВалютнаяСуммаКт,
		|	ВременнаяТаблицаПроводкиПоНДС.Содержание КАК Содержание,
		|	СУММА(ВременнаяТаблицаПроводкиПоНДС.КоличествоДт) КАК КоличествоДт,
		|	СУММА(ВременнаяТаблицаПроводкиПоНДС.КоличествоКт) КАК КоличествоКт
		|ИЗ
		|	ВременнаяТаблицаПроводкиПоНДС КАК ВременнаяТаблицаПроводкиПоНДС
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаПроводкиПоНДС.Период,
		|	ВременнаяТаблицаПроводкиПоНДС.Организация,
		|	ВременнаяТаблицаПроводкиПоНДС.СчетДт,
		|	ВременнаяТаблицаПроводкиПоНДС.СубконтоДт1,
		|	ВременнаяТаблицаПроводкиПоНДС.Содержание,
		|	ВременнаяТаблицаПроводкиПоНДС.СубконтоДт3,
		|	ВременнаяТаблицаПроводкиПоНДС.ВалютаДт,
		|	ВременнаяТаблицаПроводкиПоНДС.СубконтоКт1,
		|	ВременнаяТаблицаПроводкиПоНДС.ВалютаКт,
		|	ВременнаяТаблицаПроводкиПоНДС.СубконтоДт2,
		|	ВременнаяТаблицаПроводкиПоНДС.СубконтоКт3,
		|	ВременнаяТаблицаПроводкиПоНДС.СчетКт,
		|	ВременнаяТаблицаПроводкиПоНДС.СубконтоКт2
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	4,
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаТовары.СчетУчета,
		|	ВременнаяТаблицаШапка.СчетРасчетовСКонтрагентом,
		|	ВременнаяТаблицаТовары.Номенклатура,
		|	ВременнаяТаблицаТовары.Склад,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.ВедетсяУчетПоПартиям
		|			ТОГДА ВременнаяТаблицаТовары.ДокументПоступления
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ,
		|	ВременнаяТаблицаШапка.Контрагент,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаТовары.Сопровождение,
		|	ВременнаяТаблицаШапка.ВалютаДокумента,
		|	ВременнаяТаблицаТовары.Сопровождение,
		|	ВременнаяТаблицаШапка.ВалютаДокумента,
		|	ВременнаяТаблицаТовары.Сопровождение,
		|	""Таможенное сопровождение"",
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО ВременнаяТаблицаТовары.Ссылка = ВременнаяТаблицаШапка.Ссылка
		|ГДЕ
		|	ВременнаяТаблицаТовары.Сопровождение <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	5,
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаОС.СчетУчета,
		|	ВременнаяТаблицаШапка.СчетРасчетовСКонтрагентом,
		|	ВременнаяТаблицаОС.ОсновноеСредство,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаШапка.Контрагент,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаОС.Сопровождение,
		|	ВременнаяТаблицаШапка.ВалютаДокумента,
		|	ВременнаяТаблицаОС.Сопровождение,
		|	ВременнаяТаблицаШапка.ВалютаДокумента,
		|	ВременнаяТаблицаОС.Сопровождение,
		|	""Таможенное сопровождение"",
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаОС КАК ВременнаяТаблицаОС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО ВременнаяТаблицаОС.Ссылка = ВременнаяТаблицаШапка.Ссылка
		|ГДЕ
		|	ВременнаяТаблицаОС.Сопровождение <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	6,
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаТовары.СчетУчета,
		|	ВременнаяТаблицаШапка.СчетРасчетовСКонтрагентом,
		|	ВременнаяТаблицаТовары.Номенклатура,
		|	ВременнаяТаблицаТовары.Склад,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.ВедетсяУчетПоПартиям
		|			ТОГДА ВременнаяТаблицаТовары.ДокументПоступления
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ,
		|	ВременнаяТаблицаШапка.Контрагент,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаТовары.Пошлина,
		|	ВременнаяТаблицаШапка.ВалютаДокумента,
		|	ВременнаяТаблицаТовары.Пошлина,
		|	ВременнаяТаблицаШапка.ВалютаДокумента,
		|	ВременнаяТаблицаТовары.Пошлина,
		|	""Таможенная пошлина"",
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО ВременнаяТаблицаТовары.Ссылка = ВременнаяТаблицаШапка.Ссылка
		|ГДЕ
		|	ВременнаяТаблицаТовары.Пошлина <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	7,
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаОС.СчетУчета,
		|	ВременнаяТаблицаШапка.СчетРасчетовСКонтрагентом,
		|	ВременнаяТаблицаОС.ОсновноеСредство,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаШапка.Контрагент,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаОС.Пошлина,
		|	ВременнаяТаблицаШапка.ВалютаДокумента,
		|	ВременнаяТаблицаОС.Пошлина,
		|	ВременнаяТаблицаШапка.ВалютаДокумента,
		|	ВременнаяТаблицаОС.Пошлина,
		|	""Таможенная пошлина"",
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаОС КАК ВременнаяТаблицаОС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО ВременнаяТаблицаОС.Ссылка = ВременнаяТаблицаШапка.Ссылка
		|ГДЕ
		|	ВременнаяТаблицаОС.Пошлина <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	8,
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаТовары.СчетУчета,
		|	ВременнаяТаблицаШапка.СчетРасчетовСКонтрагентом,
		|	ВременнаяТаблицаТовары.Номенклатура,
		|	ВременнаяТаблицаТовары.Склад,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.ВедетсяУчетПоПартиям
		|			ТОГДА ВременнаяТаблицаТовары.ДокументПоступления
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ,
		|	ВременнаяТаблицаШапка.Контрагент,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаТовары.ТаможенныйСбор,
		|	ВременнаяТаблицаШапка.ВалютаДокумента,
		|	ВременнаяТаблицаТовары.ТаможенныйСбор,
		|	ВременнаяТаблицаШапка.ВалютаДокумента,
		|	ВременнаяТаблицаТовары.ТаможенныйСбор,
		|	""Таможенный сбор"",
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО ВременнаяТаблицаТовары.Ссылка = ВременнаяТаблицаШапка.Ссылка
		|ГДЕ
		|	ВременнаяТаблицаТовары.ТаможенныйСбор <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	9,
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаОС.СчетУчета,
		|	ВременнаяТаблицаШапка.СчетРасчетовСКонтрагентом,
		|	ВременнаяТаблицаОС.ОсновноеСредство,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаШапка.Контрагент,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаОС.ТаможенныйСбор,
		|	ВременнаяТаблицаШапка.ВалютаДокумента,
		|	ВременнаяТаблицаОС.ТаможенныйСбор,
		|	ВременнаяТаблицаШапка.ВалютаДокумента,
		|	ВременнаяТаблицаОС.ТаможенныйСбор,
		|	""Таможенный сбор"",
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаОС КАК ВременнаяТаблицаОС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО ВременнаяТаблицаОС.Ссылка = ВременнаяТаблицаШапка.Ссылка
		|ГДЕ
		|	ВременнаяТаблицаОС.ТаможенныйСбор <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	10,
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаТовары.СчетУчета,
		|	ВременнаяТаблицаШапка.СчетРасчетовСКонтрагентом,
		|	ВременнаяТаблицаТовары.Номенклатура,
		|	ВременнаяТаблицаТовары.Склад,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.ВедетсяУчетПоПартиям
		|			ТОГДА ВременнаяТаблицаТовары.ДокументПоступления
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ,
		|	ВременнаяТаблицаШапка.Контрагент,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаТовары.Акциз,
		|	ВременнаяТаблицаШапка.ВалютаДокумента,
		|	ВременнаяТаблицаТовары.Акциз,
		|	ВременнаяТаблицаШапка.ВалютаДокумента,
		|	ВременнаяТаблицаТовары.Акциз,
		|	""Акциз"",
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО ВременнаяТаблицаТовары.Ссылка = ВременнаяТаблицаШапка.Ссылка
		|ГДЕ
		|	ВременнаяТаблицаТовары.Акциз <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	11,
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаОС.СчетУчета,
		|	ВременнаяТаблицаШапка.СчетРасчетовСКонтрагентом,
		|	ВременнаяТаблицаОС.ОсновноеСредство,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаШапка.Контрагент,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаОС.Акциз,
		|	ВременнаяТаблицаШапка.ВалютаДокумента,
		|	ВременнаяТаблицаОС.Акциз,
		|	ВременнаяТаблицаШапка.ВалютаДокумента,
		|	ВременнаяТаблицаОС.Акциз,
		|	""Акциз"",
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаОС КАК ВременнаяТаблицаОС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО ВременнаяТаблицаОС.Ссылка = ВременнаяТаблицаШапка.Ссылка
		|ГДЕ
		|	ВременнаяТаблицаОС.Акциз <> 0";
	Запрос.УстановитьПараметр("УказыватьПризнакЗачетаНДС", СтруктураДополнительныеСвойства.УчетнаяПолитика.УказыватьПризнакЗачетаНДСПриПоступлении);
	Запрос.УстановитьПараметр("ПлательщикНДС", СтруктураДополнительныеСвойства.УчетнаяПолитика.ПлательщикНДС);
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаХозрасчетный", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаСведенияОНДСНаИмпорт(СтруктураДополнительныеСвойства) 
	                   
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.Период КАК Дата,
		|	ВременнаяТаблицаРазделы.СтавкаНДС КАК СтавкаНДС,
		|	ВЫБОР
		|		КОГДА НЕ &УказыватьПризнакЗачетаНДС
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Зачет)
		|		КОГДА ВременнаяТаблицаРазделы.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.ПустаяСсылка)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Зачет)
		|		ИНАЧЕ ВременнаяТаблицаРазделы.ЗачетНДС
		|	КОНЕЦ КАК ЗачетНДС,
		|	ВременнаяТаблицаРазделы.КлючСвязи КАК КлючСвязи
		|ПОМЕСТИТЬ ВременнаяТаблицаСтроки
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаРазделы КАК ВременнаяТаблицаРазделы
		|		ПО (ИСТИНА)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаСтроки.Организация КАК Организация,
		|	ВременнаяТаблицаСтроки.Дата КАК Дата,
		|	ВременнаяТаблицаСтроки.СтавкаНДС КАК СтавкаНДС,
		|	ВременнаяТаблицаСтроки.ЗачетНДС КАК ЗачетНДС,
		|	ВременнаяТаблицаТовары.ДокументПоступления КАК Документ,
		|	ВременнаяТаблицаТовары.Номенклатура КАК Номенклатура,
		|	СУММА(ВременнаяТаблицаТовары.СуммаБазыНДС) КАК Сумма,
		|	СУММА(ВременнаяТаблицаТовары.СуммаНДС) КАК СуммаНДС,
		|	СУММА(ВременнаяТаблицаТовары.Количество) КАК Количество
		|ИЗ
		|	ВременнаяТаблицаСтроки КАК ВременнаяТаблицаСтроки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|		ПО ВременнаяТаблицаСтроки.КлючСвязи = ВременнаяТаблицаТовары.КлючСвязи
		|ГДЕ
		|	(ВременнаяТаблицаТовары.СуммаБазыНДС <> 0
		|			ИЛИ ВременнаяТаблицаТовары.СуммаНДС <> 0)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаТовары.Номенклатура,
		|	ВременнаяТаблицаСтроки.Дата,
		|	ВременнаяТаблицаСтроки.СтавкаНДС,
		|	ВременнаяТаблицаСтроки.ЗачетНДС,
		|	ВременнаяТаблицаТовары.ДокументПоступления,
		|	ВременнаяТаблицаСтроки.Организация
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаСтроки.Организация,
		|	ВременнаяТаблицаСтроки.Дата,
		|	ВременнаяТаблицаСтроки.СтавкаНДС,
		|	ВременнаяТаблицаСтроки.ЗачетНДС,
		|	ВременнаяТаблицаОС.ДокументПоступления,
		|	ВременнаяТаблицаОС.ОсновноеСредство,
		|	СУММА(ВременнаяТаблицаОС.СуммаБазыНДС),
		|	СУММА(ВременнаяТаблицаОС.СуммаНДС),
		|	СУММА(1)
		|ИЗ
		|	ВременнаяТаблицаСтроки КАК ВременнаяТаблицаСтроки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаОС КАК ВременнаяТаблицаОС
		|		ПО ВременнаяТаблицаСтроки.КлючСвязи = ВременнаяТаблицаОС.КлючСвязи
		|ГДЕ
		|	(ВременнаяТаблицаОС.СуммаБазыНДС <> 0
		|			ИЛИ ВременнаяТаблицаОС.СуммаНДС <> 0)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаСтроки.Организация,
		|	ВременнаяТаблицаСтроки.ЗачетНДС,
		|	ВременнаяТаблицаОС.ДокументПоступления,
		|	ВременнаяТаблицаСтроки.Дата,
		|	ВременнаяТаблицаСтроки.СтавкаНДС,
		|	ВременнаяТаблицаОС.ОсновноеСредство";
	Запрос.УстановитьПараметр("УказыватьПризнакЗачетаНДС", СтруктураДополнительныеСвойства.УчетнаяПолитика.УказыватьПризнакЗачетаНДСПриПоступлении);
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСведенияОНДСНаИмпорт", Запрос.Выполнить().Выгрузить());		
КонецПроцедуры 

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаОборотыГТД(СтруктураДополнительныеСвойства) 
	                   
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.Период КАК Период,
		|	ВременнаяТаблицаШапка.Курс КАК Курс,
		|	ВременнаяТаблицаШапка.Кратность КАК Кратность,
		|	ВременнаяТаблицаРазделы.Валюта КАК Валюта,
		|	ВременнаяТаблицаРазделы.СтавкаПошлины КАК СтавкаПошлины,
		|	ВременнаяТаблицаРазделы.ЗачетНДС КАК ЗачетНДС,
		|	ВременнаяТаблицаРазделы.КлючСвязи КАК КлючСвязи
		|ПОМЕСТИТЬ ВременнаяТаблицаШапкаОборотыГТД
		|ИЗ
		|	ВременнаяТаблицаРазделы КАК ВременнаяТаблицаРазделы 
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаШапкаОборотыГТД.Организация КАК Организация,
		|	ВременнаяТаблицаШапкаОборотыГТД.Период КАК Период,
		|	ВременнаяТаблицаШапкаОборотыГТД.Валюта КАК Валюта,
		|	ВременнаяТаблицаШапкаОборотыГТД.СтавкаПошлины КАК СтавкаПошлины,
		|	ВременнаяТаблицаТовары.Номенклатура КАК Номенклатура,
		|	ВременнаяТаблицаТовары.ДокументПоступления КАК ДокументПоступления,
		|	ВременнаяТаблицаТовары.Количество КАК Количество,
		|	ВЫРАЗИТЬ(ВременнаяТаблицаТовары.Сумма / ВременнаяТаблицаТовары.Количество КАК ЧИСЛО(15,2)) КАК ЦенаВВалюте,
		|	ВЫРАЗИТЬ((ВременнаяТаблицаТовары.Сумма / ВременнаяТаблицаТовары.Количество) * ВременнаяТаблицаШапкаОборотыГТД.Курс / ВременнаяТаблицаШапкаОборотыГТД.Кратность КАК ЧИСЛО(15,2)) КАК Цена,
		|	ВЫРАЗИТЬ(ВременнаяТаблицаТовары.Сумма * ВременнаяТаблицаШапкаОборотыГТД.Курс / ВременнаяТаблицаШапкаОборотыГТД.Кратность КАК ЧИСЛО(15,2)) КАК Сумма,
		|	ВременнаяТаблицаТовары.СуммаНДС КАК НДС,
		|	ВременнаяТаблицаТовары.Пошлина КАК Пошлина,
		|	ВременнаяТаблицаТовары.ТаможенныйСбор КАК ТаможенныйСбор,
		|	ВременнаяТаблицаТовары.Сопровождение КАК Сопровождение,
		|	ВременнаяТаблицаТовары.Акциз КАК Акциз,
		|	ВременнаяТаблицаТовары.ДопРасходы КАК ДопРасходы,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапкаОборотыГТД.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Себестоимость)
		|			ИЛИ НЕ &ПлательщикНДС
		|			ТОГДА ВЫРАЗИТЬ((ВременнаяТаблицаТовары.Сумма * ВременнаяТаблицаШапкаОборотыГТД.Курс / ВременнаяТаблицаШапкаОборотыГТД.Кратность + ВременнаяТаблицаТовары.Пошлина + ВременнаяТаблицаТовары.ТаможенныйСбор + ВременнаяТаблицаТовары.Сопровождение + ВременнаяТаблицаТовары.Акциз + ВременнаяТаблицаТовары.ДопРасходы + ВременнаяТаблицаТовары.СуммаНДС) / ВременнаяТаблицаТовары.Количество КАК ЧИСЛО(15,2))
		|		ИНАЧЕ ВЫРАЗИТЬ((ВременнаяТаблицаТовары.Сумма * ВременнаяТаблицаШапкаОборотыГТД.Курс / ВременнаяТаблицаШапкаОборотыГТД.Кратность + ВременнаяТаблицаТовары.Пошлина + ВременнаяТаблицаТовары.ТаможенныйСбор + ВременнаяТаблицаТовары.Сопровождение + ВременнаяТаблицаТовары.Акциз + ВременнаяТаблицаТовары.ДопРасходы) / ВременнаяТаблицаТовары.Количество КАК ЧИСЛО(15,2))
		|	КОНЕЦ КАК ОбщаяЦена,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапкаОборотыГТД.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Себестоимость)
		|			ИЛИ НЕ &ПлательщикНДС
		|			ТОГДА ВЫРАЗИТЬ(ВременнаяТаблицаТовары.Сумма * ВременнаяТаблицаШапкаОборотыГТД.Курс / ВременнаяТаблицаШапкаОборотыГТД.Кратность + ВременнаяТаблицаТовары.Пошлина + ВременнаяТаблицаТовары.ТаможенныйСбор + ВременнаяТаблицаТовары.Сопровождение + ВременнаяТаблицаТовары.Акциз + ВременнаяТаблицаТовары.ДопРасходы + ВременнаяТаблицаТовары.СуммаНДС КАК ЧИСЛО(15,2))
		|		ИНАЧЕ ВЫРАЗИТЬ(ВременнаяТаблицаТовары.Сумма * ВременнаяТаблицаШапкаОборотыГТД.Курс / ВременнаяТаблицаШапкаОборотыГТД.Кратность + ВременнаяТаблицаТовары.Пошлина + ВременнаяТаблицаТовары.ТаможенныйСбор + ВременнаяТаблицаТовары.Сопровождение + ВременнаяТаблицаТовары.Акциз + ВременнаяТаблицаТовары.ДопРасходы КАК ЧИСЛО(15,2))
		|	КОНЕЦ КАК ОбщаяСумма 
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапкаОборотыГТД КАК ВременнаяТаблицаШапкаОборотыГТД
		|		ПО ВременнаяТаблицаТовары.КлючСвязи = ВременнаяТаблицаШапкаОборотыГТД.КлючСвязи
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаШапкаОборотыГТД.Организация,
		|	ВременнаяТаблицаШапкаОборотыГТД.Период,
		|	ВременнаяТаблицаШапкаОборотыГТД.Валюта,
		|	ВременнаяТаблицаШапкаОборотыГТД.СтавкаПошлины,
		|	ВременнаяТаблицаОС.ОсновноеСредство,
		|	ВременнаяТаблицаОС.ДокументПоступления КАК ДокументПоступления,
		|	1,
		|	ВременнаяТаблицаОС.Сумма,
		|	ВЫРАЗИТЬ(ВременнаяТаблицаОС.Сумма * ВременнаяТаблицаШапкаОборотыГТД.Курс / ВременнаяТаблицаШапкаОборотыГТД.Кратность КАК ЧИСЛО(15,2)),
		|	ВЫРАЗИТЬ(ВременнаяТаблицаОС.Сумма * ВременнаяТаблицаШапкаОборотыГТД.Курс / ВременнаяТаблицаШапкаОборотыГТД.Кратность КАК ЧИСЛО(15,2)),
		|	ВременнаяТаблицаОС.СуммаНДС,
		|	ВременнаяТаблицаОС.Пошлина,
		|	ВременнаяТаблицаОС.ТаможенныйСбор,
		|	ВременнаяТаблицаОС.Сопровождение,
		|	ВременнаяТаблицаОС.Акциз,
		|	ВременнаяТаблицаОС.ДопРасходы,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапкаОборотыГТД.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Себестоимость)
		|			ИЛИ НЕ &ПлательщикНДС
		|			ТОГДА ВЫРАЗИТЬ(ВременнаяТаблицаОС.Сумма * ВременнаяТаблицаШапкаОборотыГТД.Курс / ВременнаяТаблицаШапкаОборотыГТД.Кратность + ВременнаяТаблицаОС.Пошлина + ВременнаяТаблицаОС.ТаможенныйСбор + ВременнаяТаблицаОС.Сопровождение + ВременнаяТаблицаОС.Акциз + ВременнаяТаблицаОС.ДопРасходы + ВременнаяТаблицаОС.СуммаНДС КАК ЧИСЛО(15,2))
		|		ИНАЧЕ ВЫРАЗИТЬ(ВременнаяТаблицаОС.Сумма * ВременнаяТаблицаШапкаОборотыГТД.Курс / ВременнаяТаблицаШапкаОборотыГТД.Кратность + ВременнаяТаблицаОС.Пошлина + ВременнаяТаблицаОС.ТаможенныйСбор + ВременнаяТаблицаОС.Сопровождение + ВременнаяТаблицаОС.Акциз + ВременнаяТаблицаОС.ДопРасходы КАК ЧИСЛО(15,2))
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапкаОборотыГТД.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Себестоимость)
		|			ИЛИ НЕ &ПлательщикНДС
		|			ТОГДА ВЫРАЗИТЬ(ВременнаяТаблицаОС.Сумма * ВременнаяТаблицаШапкаОборотыГТД.Курс / ВременнаяТаблицаШапкаОборотыГТД.Кратность + ВременнаяТаблицаОС.Пошлина + ВременнаяТаблицаОС.ТаможенныйСбор + ВременнаяТаблицаОС.Сопровождение + ВременнаяТаблицаОС.Акциз + ВременнаяТаблицаОС.ДопРасходы + ВременнаяТаблицаОС.СуммаНДС КАК ЧИСЛО(15,2))
		|		ИНАЧЕ ВЫРАЗИТЬ(ВременнаяТаблицаОС.Сумма * ВременнаяТаблицаШапкаОборотыГТД.Курс / ВременнаяТаблицаШапкаОборотыГТД.Кратность + ВременнаяТаблицаОС.Пошлина + ВременнаяТаблицаОС.ТаможенныйСбор + ВременнаяТаблицаОС.Сопровождение + ВременнаяТаблицаОС.Акциз + ВременнаяТаблицаОС.ДопРасходы КАК ЧИСЛО(15,2))
		|	КОНЕЦ
		|ИЗ
		|	ВременнаяТаблицаОС КАК ВременнаяТаблицаОС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапкаОборотыГТД КАК ВременнаяТаблицаШапкаОборотыГТД
		|		ПО ВременнаяТаблицаОС.КлючСвязи = ВременнаяТаблицаШапкаОборотыГТД.КлючСвязи";
	Запрос.УстановитьПараметр("ПлательщикНДС", СтруктураДополнительныеСвойства.УчетнаяПолитика.ПлательщикНДС);
		
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОборотыГТД", Запрос.Выполнить().Выгрузить());		
КонецПроцедуры 

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаРеестрВвезенных(СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Период КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.НомерГТД КАК Номер,
		|	ВременнаяТаблицаШапка.Период КАК ДатаСФ,
		|	ВременнаяТаблицаШапка.КонтрагентПоПоступлению КАК Контрагент,
		|	ВЫБОР
		|		КОГДА НЕ &УказыватьПризнакЗачетаНДС
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Распределение)
		|		КОГДА ВременнаяТаблицаРазделы.ЗачетНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.ПустаяСсылка)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЗачетаНДС.Зачет)
		|		ИНАЧЕ ВременнаяТаблицаРазделы.ЗачетНДС
		|	КОНЕЦ КАК ЗачетНДС,
		|	СУММА(ВременнаяТаблицаРазделы.БазаНДС) - СУММА(ВременнаяТаблицаРазделы.Акциз) КАК Сумма,
		|	СУММА(ВременнаяТаблицаРазделы.НДС) КАК СуммаНДС,
		|	СУММА(ВременнаяТаблицаРазделы.Акциз) КАК СуммаАкциза
		|ИЗ
		|	ВременнаяТаблицаРазделы КАК ВременнаяТаблицаРазделы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	&ПлательщикНДС
		|	И ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.КонтрагентПоПоступлению.ПризнакСтраны = ЗНАЧЕНИЕ(Перечисление.ПризнакиСтраны.КР)
		|			ТОГДА ВременнаяТаблицаШапка.КонтрагентПоПоступлению.СЭЗ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаШапка.КонтрагентПоПоступлению,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.НомерГТД,
		|	ВременнаяТаблицаРазделы.ЗачетНДС";
	Запрос.УстановитьПараметр("ПлательщикНДС", СтруктураДополнительныеСвойства.УчетнаяПолитика.ПлательщикНДС);
	Запрос.УстановитьПараметр("УказыватьПризнакЗачетаНДС", СтруктураДополнительныеСвойства.УчетнаяПолитика.УказыватьПризнакЗачетаНДСПриПоступлении);
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРеестрВвезенных", Запрос.Выполнить().Выгрузить());	
КонецПроцедуры

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт
	
	ВалютаРегламентированногоУчета = СтруктураДополнительныеСвойства.ДляПроведения.ВалютаРегламентированногоУчета;	
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц 	= СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ГТДПоИмпорту.Ссылка,
		|	ГТДПоИмпорту.Дата КАК Период,
		|	ГТДПоИмпорту.Номер,
		|	ГТДПоИмпорту.НомерГТД,
		|	ГТДПоИмпорту.Организация,
		|	ГТДПоИмпорту.Контрагент,
		|	ГТДПоИмпорту.КонтрагентПоПоступлению,
		|	ГТДПоИмпорту.Курс,
		|	ГТДПоИмпорту.Кратность,
		|	ГТДПоИмпорту.ДоговорКонтрагента,
		|	ГТДПоИмпорту.СчетРасчетовСКонтрагентом,
		|	ГТДПоИмпорту.ВводСуммГТДВручную,
		|	&ВедетсяУчетПоПартиям КАК ВедетсяУчетПоПартиям,
		|	&ВалютаДокумента	
		|ПОМЕСТИТЬ ВременнаяТаблицаШапка
		|ИЗ
		|	Документ.ГТДПоИмпорту КАК ГТДПоИмпорту
		|ГДЕ
		|	ГТДПоИмпорту.Ссылка = &ДокументСсылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ГТДПоИмпортуРазделы.Ссылка,
		|	ГТДПоИмпортуРазделы.БазаНДС,
		|	ГТДПоИмпортуРазделы.НДС,
		|	ГТДПоИмпортуРазделы.Акциз,
		|	ГТДПоИмпортуРазделы.ЗачетНДС,
		|	ГТДПоИмпортуРазделы.Валюта,
		|	ГТДПоИмпортуРазделы.СтавкаПошлины,
		|	ГТДПоИмпортуРазделы.СтавкаНДС,
		|	ГТДПоИмпортуРазделы.КлючСвязи
		|ПОМЕСТИТЬ ВременнаяТаблицаРазделы
		|ИЗ
		|	Документ.ГТДПоИмпорту.Разделы КАК ГТДПоИмпортуРазделы
		|ГДЕ
		|	ГТДПоИмпортуРазделы.Ссылка = &ДокументСсылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ГТДПоИмпортуТовары.Ссылка,
		|	ГТДПоИмпортуТовары.Номенклатура,
		|	ГТДПоИмпортуТовары.СчетУчета,
		|	ГТДПоИмпортуТовары.Количество,
		|	ГТДПоИмпортуТовары.Акциз,
		|	ГТДПоИмпортуТовары.Сумма,
		|	ГТДПоИмпортуТовары.Пошлина,
		|	ГТДПоИмпортуТовары.Сопровождение,
		|	ГТДПоИмпортуТовары.ТаможенныйСбор,
		|	ГТДПоИмпортуТовары.СуммаБазыНДС,
		|	ГТДПоИмпортуТовары.СуммаНДС,
		|	ГТДПоИмпортуТовары.ДопРасходы,
		|	ГТДПоИмпортуТовары.ДокументПоступления,
		|	ГТДПоИмпортуТовары.ДокументПоступления.Склад КАК Склад,
		|	ГТДПоИмпортуТовары.КлючСвязи
		|ПОМЕСТИТЬ ВременнаяТаблицаТовары
		|ИЗ
		|	Документ.ГТДПоИмпорту.Товары КАК ГТДПоИмпортуТовары
		|ГДЕ
		|	ГТДПоИмпортуТовары.Ссылка = &ДокументСсылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ГТДПоИмпортуОС.Ссылка,
		|	ГТДПоИмпортуОС.ОсновноеСредство,
		|	ГТДПоИмпортуОС.СчетУчета,
		|	ГТДПоИмпортуОС.Сумма,
		|	ГТДПоИмпортуОС.Акциз,
		|	ГТДПоИмпортуОС.Пошлина,
		|	ГТДПоИмпортуОС.Сопровождение,
		|	ГТДПоИмпортуОС.ТаможенныйСбор,
		|	ГТДПоИмпортуОС.ДопРасходы,
		|	ГТДПоИмпортуОС.СуммаБазыНДС,
		|	ГТДПоИмпортуОС.СуммаНДС,
		|	ГТДПоИмпортуОС.ДокументПоступления,
		|	ГТДПоИмпортуОС.КлючСвязи
		|ПОМЕСТИТЬ ВременнаяТаблицаОС
		|ИЗ
		|	Документ.ГТДПоИмпорту.ОС КАК ГТДПоИмпортуОС
		|ГДЕ
		|	ГТДПоИмпортуОС.Ссылка = &ДокументСсылка";		
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	Запрос.УстановитьПараметр("ВалютаДокумента", ВалютаРегламентированногоУчета);
	Запрос.УстановитьПараметр("ВедетсяУчетПоПартиям", СтруктураДополнительныеСвойства.УчетнаяПолитика.СпособОценкиТМЗ = Перечисления.СпособыОценки.ФИФО);
	Запрос.Выполнить();    		
		
	СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаСведенияОНДСНаИмпорт(СтруктураДополнительныеСвойства);
	СформироватьТаблицаОборотыГТД(СтруктураДополнительныеСвойства);
	СформироватьТаблицаРеестрВвезенных(СтруктураДополнительныеСвойства);
КонецПроцедуры // ИнициализироватьДанныеДокумента()

#КонецОбласти

#Область ВерсионированиеОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
//@skip-warning
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область ИнтерфейсПечати

Функция ПечатьГТДПоИмпорту(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ГТДПоИмпорту.Ссылка КАК Ссылка,
		|	ГТДПоИмпорту.Номер КАК Номер,
		|	ГТДПоИмпорту.Дата КАК Дата,
		|	ГТДПоИмпорту.Организация КАК Организация,
		|	ГТДПоИмпорту.Организация.НаименованиеПолное КАК ОрганизацияНаименованиеПолное,
		|	ГТДПоИмпорту.Контрагент.НаименованиеПолное КАК КонтрагентНаименованиеПолное,
		|	ГТДПоИмпорту.НомерГТД КАК НомерГТД,
		|	ГТДПоИмпорту.Товары.(
		|		НомерСтроки КАК НомерСтроки,
		|		Номенклатура КАК Номенклатура,
		|		Номенклатура.НаименованиеПолное КАК НоменклатураПредставление,
		|		ПРЕДСТАВЛЕНИЕ(ГТДПоИмпорту.Товары.Номенклатура.ЕдиницаИзмерения) КАК ЕИ,
		|		Количество КАК Количество,
		|		СуммаНДС КАК СуммаНДС,
		|		ФактурнаяСтоимость КАК ФактурнаяСтоимость,
		|		ВЫБОР
		|			КОГДА ГТДПоИмпорту.Товары.Количество = 0
		|				ТОГДА 0
		|			ИНАЧЕ ВЫРАЗИТЬ(ГТДПоИмпорту.Товары.Сумма / ГТДПоИмпорту.Товары.Количество КАК ЧИСЛО(15, 2))
		|		КОНЕЦ КАК Цена,
		|		Пошлина КАК Пошлина,
		|		ТаможенныйСбор КАК ТаможенныйСбор,
		|		Акциз КАК Акциз,
		|		Сопровождение КАК Сопровождение,
		|		ДопРасходы КАК ДопРасходы,
		|		ФактурнаяСтоимость КАК ТаможеннаяСтоимость,
		|		ГТДПоИмпорту.Товары.ФактурнаяСтоимость + ГТДПоИмпорту.Товары.ДопРасходы + ГТДПоИмпорту.Товары.Пошлина + ГТДПоИмпорту.Товары.ТаможенныйСбор + ГТДПоИмпорту.Товары.Акциз + ГТДПоИмпорту.Товары.Сопровождение КАК СтоимостьИтого,
		|		ВЫБОР
		|			КОГДА ГТДПоИмпорту.Товары.Количество = 0
		|				ТОГДА 0
		|			ИНАЧЕ ВЫРАЗИТЬ((ГТДПоИмпорту.Товары.ФактурнаяСтоимость + ГТДПоИмпорту.Товары.ДопРасходы + ГТДПоИмпорту.Товары.Пошлина + ГТДПоИмпорту.Товары.ТаможенныйСбор + ГТДПоИмпорту.Товары.Акциз + ГТДПоИмпорту.Товары.Сопровождение) / ГТДПоИмпорту.Товары.Количество КАК ЧИСЛО(15, 2))
		|		КОНЕЦ КАК ЦенаИтого,
		|		КлючСвязи КАК КлючСвязи
		|	) КАК Товары,
		|	ГТДПоИмпорту.ОС.(
		|		НомерСтроки КАК НомерСтроки,
		|		ОсновноеСредство КАК ОсновноеСредство,
		|		ОсновноеСредство.НаименованиеПолное КАК НоменклатураПредставление,
		|		1 КАК Количество,
		|		СуммаНДС КАК СуммаНДС,
		|		ФактурнаяСтоимость КАК Цена,
		|		Пошлина КАК Пошлина,
		|		ТаможенныйСбор КАК ТаможенныйСбор,
		|		Акциз КАК Акциз,
		|		Сопровождение КАК Сопровождение,
		|		ДопРасходы КАК ДопРасходы,
		|		ФактурнаяСтоимость КАК ФактурнаяСтоимость,
		|		ФактурнаяСтоимость КАК ТаможеннаяСтоимость,
		|		ГТДПоИмпорту.ОС.ФактурнаяСтоимость + ГТДПоИмпорту.ОС.ДопРасходы + ГТДПоИмпорту.ОС.Пошлина + ГТДПоИмпорту.ОС.ТаможенныйСбор + ГТДПоИмпорту.ОС.Акциз + ГТДПоИмпорту.ОС.Сопровождение КАК СтоимостьИтого,
		|		ГТДПоИмпорту.ОС.ФактурнаяСтоимость + ГТДПоИмпорту.ОС.ДопРасходы + ГТДПоИмпорту.ОС.Пошлина + ГТДПоИмпорту.ОС.ТаможенныйСбор + ГТДПоИмпорту.ОС.Акциз + ГТДПоИмпорту.ОС.Сопровождение КАК ЦенаИтого,
		|		КлючСвязи КАК КлючСвязи
		|	) КАК ОС,
		|	ГТДПоИмпорту.Разделы.(
		|		КлючСвязи КАК КлючСвязи,
		|		СтавкаПошлины КАК СтавкаПошлины,
		|		СтавкаСбора КАК СтавкаСбора,
		|		СтавкаНДС КАК СтавкаНДС,
		|		ФактурнаяСтоимость КАК ФактурнаяСтоимость,
		|		ДопРасходы КАК ДопРасходы,
		|		ТаможеннаяСтоимость КАК ТаможеннаяСтоимость,
		|		Пошлина КАК Пошлина,
		|		ТаможенныйСбор КАК ТаможенныйСбор,
		|		Акциз КАК Акциз,
		|		Сопровождение КАК Сопровождение,
		|		НДС КАК СуммаНДС,
		|		ГТДПоИмпорту.Разделы.ФактурнаяСтоимость + ГТДПоИмпорту.Разделы.ДопРасходы + ГТДПоИмпорту.Разделы.Пошлина + ГТДПоИмпорту.Разделы.ТаможенныйСбор + ГТДПоИмпорту.Разделы.Акциз + ГТДПоИмпорту.Разделы.Сопровождение КАК СтоимостьИтого
		|	) КАК Разделы
		|ИЗ
		|	Документ.ГТДПоИмпорту КАК ГТДПоИмпорту
		|ГДЕ
		|	ГТДПоИмпорту.Ссылка В(&СписокДокументов)";		
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("СписокДокументов", МассивОбъектов);
	
	Шапка = Запрос.Выполнить().Выбрать();
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ГТДПоИмпорту_ГТДПоИмпорту";

	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ГТДПоИмпорту.ПФ_MXL_ГТДПоИмпорту");
	
	Пока Шапка.Следующий() Цикл
		Если ТабличныйДокумент.ВысотаТаблицы > 0 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Подготовка данных
		ДанныеПечати = Новый Структура;
		
		ТекстЗаголовка = СформироватьЗаголовокДокумента(Шапка, НСтр("ru = 'ГТД по импорту'"));
		ДанныеПечати.Вставить("ТекстЗаголовка", ТекстЗаголовка);
		ДанныеПечати.Вставить("ОрганизацияНаименованиеПолное", Шапка.ОрганизацияНаименованиеПолное);
		ДанныеПечати.Вставить("НомерГТД", Шапка.НомерГТД);
		ДанныеПечати.Вставить("КонтрагентНаименованиеПолное", Шапка.КонтрагентНаименованиеПолное);

		ВыборкаРазделы = Шапка.Разделы.Выбрать();
		ТаблицаТовары = Шапка.Товары.Выгрузить();
		ТаблицаОС = Шапка.ОС.Выгрузить();
		
		ТаблицаТовары.Индексы.Добавить("КлючСвязи");
		ТаблицаОС.Индексы.Добавить("КлючСвязи");
		
		ФактурнаяСтоимость 	= ТаблицаТовары.Итог("ФактурнаяСтоимость") + ТаблицаОС.Итог("ФактурнаяСтоимость");
		ТаможеннаяСтоимость = ТаблицаТовары.Итог("ТаможеннаяСтоимость") + ТаблицаОС.Итог("ТаможеннаяСтоимость");
		ДопРасходы 			= ТаблицаТовары.Итог("ДопРасходы") + ТаблицаОС.Итог("ДопРасходы");
		Пошлина 			= ТаблицаТовары.Итог("Пошлина") + ТаблицаОС.Итог("Пошлина");
		ТаможенныйСбор 		= ТаблицаТовары.Итог("ТаможенныйСбор") + ТаблицаОС.Итог("ТаможенныйСбор");
		Акциз 				= ТаблицаТовары.Итог("Акциз") + ТаблицаОС.Итог("Акциз");
		Сопровождение 		= ТаблицаТовары.Итог("Сопровождение") + ТаблицаОС.Итог("Сопровождение");
		СуммаНДС 			= ТаблицаТовары.Итог("СуммаНДС") + ТаблицаОС.Итог("СуммаНДС");
		СтоимостьИтого 		= ТаблицаТовары.Итог("СтоимостьИтого") + ТаблицаОС.Итог("СтоимостьИтого");
				
		ДанныеПечати.Вставить("ФактурнаяСтоимость", ФактурнаяСтоимость);
		ДанныеПечати.Вставить("ТаможеннаяСтоимость", ТаможеннаяСтоимость);
		ДанныеПечати.Вставить("ДопРасходы", ДопРасходы);
		ДанныеПечати.Вставить("Пошлина", Пошлина);
		ДанныеПечати.Вставить("ТаможенныйСбор", ТаможенныйСбор);
		ДанныеПечати.Вставить("Акциз", Акциз);
		ДанныеПечати.Вставить("Сопровождение", Сопровождение);
		ДанныеПечати.Вставить("СуммаНДС", СуммаНДС);
		ДанныеПечати.Вставить("СтоимостьИтого", СтоимостьИтого);

		// Области
		МассивОбластейМакета = Новый Массив;
		МассивОбластейМакета.Добавить("Заголовок");
		МассивОбластейМакета.Добавить("ШапкаТаблицы");
		МассивОбластейМакета.Добавить("Раздел");
		МассивОбластейМакета.Добавить("Подвал");
		МассивОбластейМакета.Добавить("Итоги");
	
		Для Каждого ИмяОбласти Из МассивОбластейМакета Цикл
			ОбластьМакета = Макет.ПолучитьОбласть(ИмяОбласти);
			Если ИмяОбласти <> "Раздел" Тогда
				ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, ДанныеПечати);
				ТабличныйДокумент.Вывести(ОбластьМакета);
				
			ИначеЕсли ИмяОбласти = "Раздел" Тогда
				ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
				Пока ВыборкаРазделы.Следующий() Цикл
					ОбластьМакета.Параметры.Заполнить(ВыборкаРазделы);
					ОбластьМакета.Параметры.Раздел = ОписаниеРаздела(ВыборкаРазделы);
					
					ТабличныйДокумент.Вывести(ОбластьМакета);				
					Отбор = Новый Структура("КлючСвязи", ВыборкаРазделы.КлючСвязи);
					МассивСтрок = ТаблицаТовары.НайтиСтроки(Отбор);
					НомерСтроки = 1;
					Для Каждого СтрокаТаблицы Из МассивСтрок Цикл
						ОбластьСтрока.Параметры.Заполнить(СтрокаТаблицы);
						ОбластьСтрока.Параметры.НомерСтроки = НомерСтроки;
						
						ТабличныйДокумент.Вывести(ОбластьСтрока);
						НомерСтроки = НомерСтроки + 1;
					КонецЦикла;
					
					МассивСтрок = ТаблицаОС.НайтиСтроки(Отбор);
					НомерСтроки = 1;
					Для Каждого СтрокаТаблицы Из МассивСтрок Цикл
						ОбластьСтрока.Параметры.Заполнить(СтрокаТаблицы);
						ОбластьСтрока.Параметры.НомерСтроки = НомерСтроки;
						
						ТабличныйДокумент.Вывести(ОбластьСтрока);
						НомерСтроки = НомерСтроки + 1;
					КонецЦикла;													
				КонецЦикла; 
				
			КонецЕсли;	
		КонецЦикла;
		
		БухгалтерскиеОтчеты.ВыводПодписиРуководителей(ТабличныйДокумент, Шапка);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
	КонецЦикла;
	
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Возврат ТабличныйДокумент;
КонецФункции

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
		
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ГТДПоИмпорту") Тогда
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
				"ГТДПоИмпорту", НСтр("ru = 'ГТД по импорту'"), ПечатьГТДПоИмпорту(МассивОбъектов, ОбъектыПечати,"ГТДПоИмпорту"),,
				"Документ.ГТДПоИмпорту.ГТДПоИмпорту");
	КонецЕсли;
		
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ГТДПоИмпорту";
	КомандаПечати.Представление = НСтр("ru = 'ГТД по импорту'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Обработчик = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Порядок = 1;
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор  = "РеестрГТДПоИмпорту";
	КомандаПечати.Представление  = НСтр("ru = 'Реестр документов'");
	КомандаПечати.ЗаголовокФормы = НСтр("ru = 'Реестр документов ""ГТД по импорту""'");
	КомандаПечати.Обработчик     = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм     = "ФормаСписка";
	КомандаПечати.Порядок        = 100;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает заголовок документа для печатной формы.
//
// Параметры:
//  Шапка - любая структура с полями:
//           Номер         - Строка или Число - номер документа;
//           Дата          - Дата - дата документа;
//           Представление - Строка - (необязательный) платформенное представление ссылки на документ.
//                                    Если параметр НазваниеДокумента не задан, то название документа будет вычисляться
//                                    из этого параметра.
//  НазваниеДокумента - Строка - название документа (например, "Счет на оплату").
//
// Возвращаемое значение:
//  Строка - заголовок документа.
//
Функция СформироватьЗаголовокДокумента(Шапка, Знач НазваниеДокумента = "")
	
	ДанныеДокумента = Новый Структура("Номер,Дата,Представление");
	ЗаполнитьЗначенияСвойств(ДанныеДокумента, Шапка);
	
	// Если название документа не передано, получим название по представлению документа.
	Если ПустаяСтрока(НазваниеДокумента) И ЗначениеЗаполнено(ДанныеДокумента.Представление) Тогда
		ПоложениеНомера = СтрНайти(ДанныеДокумента.Представление, ДанныеДокумента.Номер);
		Если ПоложениеНомера > 0 Тогда
			НазваниеДокумента = СокрЛП(Лев(ДанныеДокумента.Представление, ПоложениеНомера - 1));
		КонецЕсли;
	КонецЕсли;

	НомерНаПечать = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокумента.Номер);
	Возврат СтрШаблон(НСтр("ru = '%1 № %2 от %3'"),
		НазваниеДокумента, НомерНаПечать, Формат(ДанныеДокумента.Дата, "ДЛФ=DD"));
	
КонецФункции

Функция ОписаниеРаздела(Раздел)
	Описание = СтрШаблон(НСтр("ru = 'Раздел №%1.Пошлина %2%%,таможенный сбор %3%%,НДС %4%%'"),
		Раздел.КлючСвязи,
		Раздел.СтавкаПошлины,
		Раздел.СтавкаСбора,
		Раздел.СтавкаНДС);
		
	Возврат Описание;

КонецФункции // 

#КонецОбласти

#КонецЕсли
