#Область ОбработчикиСобытийФормы

// Процедура - обработчик события ПриСозданииНаСервере.
// В процедуре осуществляется
// - инициализация реквизитов формы,
// - установка параметров функциональных опций формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УникальныйИдентификаторФормыВладельца = Параметры.УникальныйИдентификаторФормыВладельца;
	
	Организация = Параметры.Организация;
	ЭтоВозврат = Параметры.ЭтоВозврат;
	ЭтоУслуги = Параметры.ЭтоУслуги;
	Контрагент = Параметры.Контрагент;
	ДоговорКонтрагента = Параметры.ДоговорКонтрагента;
	Курс = Параметры.Курс;
	ФормаОплаты = Параметры.ФормаОплаты;
	КодПричиныКорректировки = Параметры.КодПричиныКорректировки;
	
	НачалоПериода = Параметры.НачалоПериода;
	КонецПериода = Параметры.КонецПериода;
	
	ЗаполнитьТаблицуДокументов();
	
	// РаботаСФормами
	РаботаСФормамиСервер.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец РаботаСФормами	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьПериод(Команда)
	
	ПараметрыВыбора = Новый Структура("НачалоПериода,КонецПериода", НачалоПериода, КонецПериода);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериода", ПараметрыВыбора, Элементы.ВыбратьПериод,,,, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(ЭтаФорма, РезультатВыбора, "НачалоПериода,КонецПериода");

	ЗаполнитьТаблицуДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	МассивСсылок = Новый Массив();
	
	Для Каждого СтрокаТаблицы Из ТаблицаДокументов Цикл		
		Если СтрокаТаблицы.Подобрать Тогда
			МассивСсылок.Добавить(СтрокаТаблицы.Документ);	
		КонецЕсли;	
	КонецЦикла;	
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("МассивСсылок", МассивСсылок);  
	Оповестить("ПодборДокументовЭСФ", СтруктураВозврата, УникальныйИдентификаторФормыВладельца);
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура НачалоПериодаПриИзменении(Элемент)
	ЗаполнитьТаблицуДокументов();
КонецПроцедуры

&НаКлиенте
Процедура КонецПериодаПриИзменении(Элемент)
	ЗаполнитьТаблицуДокументов();
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДокументовВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	МассивСсылок = Новый Массив();
	
	Для Каждого СтрокаМассива Из Значение Цикл
		МассивСсылок.Добавить(ТаблицаДокументов.Получить(СтрокаМассива).Документ);	
	КонецЦикла;	
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("МассивСсылок", МассивСсылок);  
	Оповестить("ПодборДокументовЭСФ", СтруктураВозврата, УникальныйИдентификаторФормыВладельца);
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТаблицуДокументов()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	
		// 1. Данные ТЧ "Товары" документа "Реализация товаров и услуг".
		// 2. Данные ТЧ "ОС" документа "Реализация товаров и услуг".
		// 3. Данные ТЧ "Услуги" документа "Реализация товаров и услуг".
		// 4. Данные ТЧ "Товары" документа "Возврат товаров от покупателя".
		// 5. Данные ТЧ "ОС" документа "Возврат товаров от покупателя".
		// 6. Данные ТЧ "Услуги" документа "Возврат товаров от покупателя".
		"ВЫБРАТЬ
		|	ПоступлениеТоваровУслуг.Ссылка КАК Документ
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
		|		ПО ПоступлениеТоваровУслугТовары.Ссылка = ПоступлениеТоваровУслуг.Ссылка
		|ГДЕ
		|	ПоступлениеТоваровУслуг.Проведен
		|	И НЕ ПоступлениеТоваровУслугТовары.НомерСтроки ЕСТЬ NULL
		|	И НЕ &ЭтоВозврат
		|	И НЕ &ЭтоУслуги
		|	И ПоступлениеТоваровУслуг.Организация = &Организация
		|	И ПоступлениеТоваровУслуг.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ПоступлениеТоваровУслуг.Контрагент = &Контрагент
		|	И ПоступлениеТоваровУслуг.ДоговорКонтрагента = &ДоговорКонтрагента
		|	И ПоступлениеТоваровУслуг.Курс = &Курс
		|	И ВЫБОР
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная)
		|				ТОГДА ПоступлениеТоваровУслуг.БезналичныйРасчет
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличная)
		|				ТОГДА НЕ ПоступлениеТоваровУслуг.БезналичныйРасчет
		|		КОНЕЦ
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоступлениеТоваровУслуг.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПоступлениеТоваровУслуг.Ссылка
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.ОС КАК ПоступлениеТоваровУслугОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
		|		ПО ПоступлениеТоваровУслугОС.Ссылка = ПоступлениеТоваровУслуг.Ссылка
		|ГДЕ
		|	ПоступлениеТоваровУслуг.Проведен
		|	И НЕ ПоступлениеТоваровУслугОС.НомерСтроки ЕСТЬ NULL
		|	И НЕ &ЭтоВозврат
		|	И НЕ &ЭтоУслуги
		|	И ПоступлениеТоваровУслуг.Организация = &Организация
		|	И ПоступлениеТоваровУслуг.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ПоступлениеТоваровУслуг.Контрагент = &Контрагент
		|	И ПоступлениеТоваровУслуг.ДоговорКонтрагента = &ДоговорКонтрагента
		|	И ПоступлениеТоваровУслуг.Курс = &Курс
		|	И ВЫБОР
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная)
		|				ТОГДА ПоступлениеТоваровУслуг.БезналичныйРасчет
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличная)
		|				ТОГДА НЕ ПоступлениеТоваровУслуг.БезналичныйРасчет
		|		КОНЕЦ
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоступлениеТоваровУслуг.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПоступлениеТоваровУслуг.Ссылка
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.Услуги КАК ПоступлениеТоваровУслугУслуги
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
		|		ПО ПоступлениеТоваровУслугУслуги.Ссылка = ПоступлениеТоваровУслуг.Ссылка
		|ГДЕ
		|	ПоступлениеТоваровУслуг.Проведен
		|	И НЕ ПоступлениеТоваровУслугУслуги.НомерСтроки ЕСТЬ NULL
		|	И НЕ &ЭтоВозврат
		|	И &ЭтоУслуги
		|	И ПоступлениеТоваровУслуг.Организация = &Организация
		|	И ПоступлениеТоваровУслуг.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ПоступлениеТоваровУслуг.Контрагент = &Контрагент
		|	И ПоступлениеТоваровУслуг.ДоговорКонтрагента = &ДоговорКонтрагента
		|	И ПоступлениеТоваровУслуг.Курс = &Курс
		|	И ВЫБОР
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная)
		|				ТОГДА ПоступлениеТоваровУслуг.БезналичныйРасчет
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличная)
		|				ТОГДА НЕ ПоступлениеТоваровУслуг.БезналичныйРасчет
		|		КОНЕЦ
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоступлениеТоваровУслуг.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КорректировкаСтоимостиПоступления.Ссылка КАК Документ
		|ИЗ
		|	Документ.КорректировкаСтоимостиПоступления.Товары КАК КорректировкаСтоимостиПоступленияТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаСтоимостиПоступления КАК КорректировкаСтоимостиПоступления
		|		ПО КорректировкаСтоимостиПоступленияТовары.Ссылка = КорректировкаСтоимостиПоступления.Ссылка
		|ГДЕ
		|	КорректировкаСтоимостиПоступления.Проведен
		|	И НЕ КорректировкаСтоимостиПоступленияТовары.НомерСтроки ЕСТЬ NULL
		|	И &ЭтоВозврат
		|	И НЕ &ЭтоУслуги
		|	И КорректировкаСтоимостиПоступления.Организация = &Организация
		|	И КорректировкаСтоимостиПоступления.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И КорректировкаСтоимостиПоступления.Контрагент = &Контрагент
		|	И КорректировкаСтоимостиПоступления.ДоговорКонтрагента = &ДоговорКонтрагента
		|	И КорректировкаСтоимостиПоступления.Курс = &Курс
		|	И ВЫБОР
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная)
		|				ТОГДА КорректировкаСтоимостиПоступления.БезналичныйРасчет
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличная)
		|				ТОГДА НЕ КорректировкаСтоимостиПоступления.БезналичныйРасчет
		|		КОНЕЦ
		|	И &КодПричиныКорректировки = 30
		|
		|СГРУППИРОВАТЬ ПО
		|	КорректировкаСтоимостиПоступления.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КорректировкаСтоимостиПоступления.Ссылка
		|ИЗ
		|	Документ.КорректировкаСтоимостиПоступления.ОС КАК КорректировкаСтоимостиПоступленияОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаСтоимостиПоступления КАК КорректировкаСтоимостиПоступления
		|		ПО КорректировкаСтоимостиПоступленияОС.Ссылка = КорректировкаСтоимостиПоступления.Ссылка
		|ГДЕ
		|	КорректировкаСтоимостиПоступления.Проведен
		|	И НЕ КорректировкаСтоимостиПоступленияОС.НомерСтроки ЕСТЬ NULL
		|	И &ЭтоВозврат
		|	И НЕ &ЭтоУслуги
		|	И КорректировкаСтоимостиПоступления.Организация = &Организация
		|	И КорректировкаСтоимостиПоступления.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И КорректировкаСтоимостиПоступления.Контрагент = &Контрагент
		|	И КорректировкаСтоимостиПоступления.ДоговорКонтрагента = &ДоговорКонтрагента
		|	И КорректировкаСтоимостиПоступления.Курс = &Курс
		|	И ВЫБОР
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная)
		|				ТОГДА КорректировкаСтоимостиПоступления.БезналичныйРасчет
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличная)
		|				ТОГДА НЕ КорректировкаСтоимостиПоступления.БезналичныйРасчет
		|		КОНЕЦ
		|	И &КодПричиныКорректировки = 30
		|
		|СГРУППИРОВАТЬ ПО
		|	КорректировкаСтоимостиПоступления.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КорректировкаСтоимостиПоступления.Ссылка
		|ИЗ
		|	Документ.КорректировкаСтоимостиПоступления.Услуги КАК КорректировкаСтоимостиПоступленияУслуги
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаСтоимостиПоступления КАК КорректировкаСтоимостиПоступления
		|		ПО КорректировкаСтоимостиПоступленияУслуги.Ссылка = КорректировкаСтоимостиПоступления.Ссылка
		|ГДЕ
		|	КорректировкаСтоимостиПоступления.Проведен
		|	И НЕ КорректировкаСтоимостиПоступленияУслуги.НомерСтроки ЕСТЬ NULL
		|	И &ЭтоВозврат
		|	И &ЭтоУслуги
		|	И КорректировкаСтоимостиПоступления.Организация = &Организация
		|	И КорректировкаСтоимостиПоступления.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И КорректировкаСтоимостиПоступления.Контрагент = &Контрагент
		|	И КорректировкаСтоимостиПоступления.ДоговорКонтрагента = &ДоговорКонтрагента
		|	И КорректировкаСтоимостиПоступления.Курс = &Курс
		|	И ВЫБОР
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная)
		|				ТОГДА КорректировкаСтоимостиПоступления.БезналичныйРасчет
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличная)
		|				ТОГДА НЕ КорректировкаСтоимостиПоступления.БезналичныйРасчет
		|		КОНЕЦ
		|	И &КодПричиныКорректировки = 30
		|
		|СГРУППИРОВАТЬ ПО
		|	КорректировкаСтоимостиПоступления.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВозвратТоваровПоставщику.Ссылка
		|ИЗ
		|	Документ.ВозвратТоваровПоставщику.Товары КАК ВозвратТоваровПоставщикуТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровПоставщику КАК ВозвратТоваровПоставщику
		|		ПО ВозвратТоваровПоставщикуТовары.Ссылка = ВозвратТоваровПоставщику.Ссылка
		|ГДЕ
		|	ВозвратТоваровПоставщику.Проведен
		|	И НЕ ВозвратТоваровПоставщикуТовары.НомерСтроки ЕСТЬ NULL
		|	И &ЭтоВозврат
		|	И НЕ &ЭтоУслуги
		|	И ВозвратТоваровПоставщику.Организация = &Организация
		|	И ВозвратТоваровПоставщику.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ВозвратТоваровПоставщику.Контрагент = &Контрагент
		|	И ВозвратТоваровПоставщику.ДоговорКонтрагента = &ДоговорКонтрагента
		|	И ВозвратТоваровПоставщику.Курс = &Курс
		|	И ВЫБОР
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная)
		|				ТОГДА ВозвратТоваровПоставщику.БезналичныйРасчет
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличная)
		|				ТОГДА НЕ ВозвратТоваровПоставщику.БезналичныйРасчет
		|		КОНЕЦ
		|	И НЕ &КодПричиныКорректировки = 30
		|
		|СГРУППИРОВАТЬ ПО
		|	ВозвратТоваровПоставщику.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВозвратТоваровПоставщику.Ссылка
		|ИЗ
		|	Документ.ВозвратТоваровПоставщику.ОС КАК ВозвратТоваровПоставщикуОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровПоставщику КАК ВозвратТоваровПоставщику
		|		ПО ВозвратТоваровПоставщикуОС.Ссылка = ВозвратТоваровПоставщику.Ссылка
		|ГДЕ
		|	ВозвратТоваровПоставщику.Проведен
		|	И НЕ ВозвратТоваровПоставщикуОС.НомерСтроки ЕСТЬ NULL
		|	И &ЭтоВозврат
		|	И НЕ &ЭтоУслуги
		|	И ВозвратТоваровПоставщику.Организация = &Организация
		|	И ВозвратТоваровПоставщику.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ВозвратТоваровПоставщику.Контрагент = &Контрагент
		|	И ВозвратТоваровПоставщику.ДоговорКонтрагента = &ДоговорКонтрагента
		|	И ВозвратТоваровПоставщику.Курс = &Курс
		|	И ВЫБОР
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная)
		|				ТОГДА ВозвратТоваровПоставщику.БезналичныйРасчет
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличная)
		|				ТОГДА НЕ ВозвратТоваровПоставщику.БезналичныйРасчет
		|		КОНЕЦ
		|	И НЕ &КодПричиныКорректировки = 30
		|
		|СГРУППИРОВАТЬ ПО
		|	ВозвратТоваровПоставщику.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВозвратТоваровПоставщику.Ссылка
		|ИЗ
		|	Документ.ВозвратТоваровПоставщику.Услуги КАК ВозвратТоваровПоставщикуУслуги
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровПоставщику КАК ВозвратТоваровПоставщику
		|		ПО ВозвратТоваровПоставщикуУслуги.Ссылка = ВозвратТоваровПоставщику.Ссылка
		|ГДЕ
		|	ВозвратТоваровПоставщику.Проведен
		|	И НЕ ВозвратТоваровПоставщикуУслуги.НомерСтроки ЕСТЬ NULL
		|	И &ЭтоВозврат
		|	И &ЭтоУслуги
		|	И ВозвратТоваровПоставщику.Организация = &Организация
		|	И ВозвратТоваровПоставщику.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ВозвратТоваровПоставщику.Контрагент = &Контрагент
		|	И ВозвратТоваровПоставщику.ДоговорКонтрагента = &ДоговорКонтрагента
		|	И ВозвратТоваровПоставщику.Курс = &Курс
		|	И ВЫБОР
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная)
		|				ТОГДА ВозвратТоваровПоставщику.БезналичныйРасчет
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличная)
		|				ТОГДА НЕ ВозвратТоваровПоставщику.БезналичныйРасчет
		|		КОНЕЦ
		|	И НЕ &КодПричиныКорректировки = 30
		|
		|СГРУППИРОВАТЬ ПО
		|	ВозвратТоваровПоставщику.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДополнительныеРасходы.Ссылка
		|ИЗ
		|	Документ.ДополнительныеРасходы КАК ДополнительныеРасходы
		|ГДЕ
		|	ДополнительныеРасходы.Проведен
		|	И НЕ &ЭтоВозврат
		|	И &ЭтоУслуги
		|	И ДополнительныеРасходы.Организация = &Организация
		|	И ДополнительныеРасходы.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ДополнительныеРасходы.Контрагент = &Контрагент
		|	И ДополнительныеРасходы.ДоговорКонтрагента = &ДоговорКонтрагента
		|	И ДополнительныеРасходы.Курс = &Курс
		|	И ВЫБОР
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная)
		|				ТОГДА ДополнительныеРасходы.БезналичныйРасчет
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличная)
		|				ТОГДА НЕ ДополнительныеРасходы.БезналичныйРасчет
		|		КОНЕЦ
		|
		|СГРУППИРОВАТЬ ПО
		|	ДополнительныеРасходы.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КорректировкаСтоимостиДополнительныхРасходов.Ссылка
		|ИЗ
		|	Документ.КорректировкаСтоимостиДополнительныхРасходов КАК КорректировкаСтоимостиДополнительныхРасходов
		|ГДЕ
		|	КорректировкаСтоимостиДополнительныхРасходов.Проведен
		|	И &ЭтоВозврат
		|	И &ЭтоУслуги
		|	И КорректировкаСтоимостиДополнительныхРасходов.Организация = &Организация
		|	И КорректировкаСтоимостиДополнительныхРасходов.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И КорректировкаСтоимостиДополнительныхРасходов.Контрагент = &Контрагент
		|	И КорректировкаСтоимостиДополнительныхРасходов.ДоговорКонтрагента = &ДоговорКонтрагента
		|	И КорректировкаСтоимостиДополнительныхРасходов.Курс = &Курс
		|	И ВЫБОР
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Безналичная)
		|				ТОГДА КорректировкаСтоимостиДополнительныхРасходов.БезналичныйРасчет
		|			КОГДА &ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Наличная)
		|				ТОГДА НЕ КорректировкаСтоимостиДополнительныхРасходов.БезналичныйРасчет
		|		КОНЕЦ
		|	И &КодПричиныКорректировки = 30
		|
		|СГРУППИРОВАТЬ ПО
		|	КорректировкаСтоимостиДополнительныхРасходов.Ссылка";
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ЭтоВозврат", ЭтоВозврат);
	Запрос.УстановитьПараметр("ЭтоУслуги", ЭтоУслуги);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(КонецПериода));
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("ДоговорКонтрагента", ДоговорКонтрагента);
	Запрос.УстановитьПараметр("Курс", Курс);
	Запрос.УстановитьПараметр("ФормаОплаты", ФормаОплаты);
	Запрос.УстановитьПараметр("КодПричиныКорректировки", КодПричиныКорректировки);
	
	ТаблицаДокументов.Загрузить(Запрос.Выполнить().Выгрузить());	
КонецПроцедуры

#КонецОбласти
