#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УникальныйИдентификаторФормыВладельца = Параметры.УникальныйИдентификаторФормыВладельца;
	
	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПоступлениеТоваровУслугТовары.Ссылка.Дата КАК Дата,
		|	ПоступлениеТоваровУслугТовары.Ссылка КАК ДокументПоступления,
		|	ПоступлениеТоваровУслугТовары.Номенклатура КАК Номенклатура
		|ПОМЕСТИТЬ ВременнаяТаблицаПоступление
		|ИЗ
		|	Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДокументПоступления,
		|	Номенклатура,
		|	Дата
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ГТДПоИмпортуТовары.ДокументПоступления КАК ДокументПоступления,
		|	ГТДПоИмпортуТовары.Номенклатура КАК Номенклатура
		|ПОМЕСТИТЬ ВременнаяТаблицаГТДПоИмпорту
		|ИЗ
		|	Документ.ГТДПоИмпорту.Товары КАК ГТДПоИмпортуТовары
		|ГДЕ
		|	ГТДПоИмпортуТовары.ДокументПоступления В
		|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				ВременнаяТаблицаПоступление.ДокументПоступления
		|			ИЗ
		|				ВременнаяТаблицаПоступление)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДополнительныеРасходыТовары.ДокументПоступления КАК ДокументПоступления,
		|	ДополнительныеРасходыТовары.Номенклатура КАК Номенклатура
		|ПОМЕСТИТЬ ВременнаяТаблицаДопраходы
		|ИЗ
		|	Документ.ДополнительныеРасходы.Товары КАК ДополнительныеРасходыТовары
		|ГДЕ
		|	ДополнительныеРасходыТовары.ДокументПоступления В
		|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				ВременнаяТаблицаПоступление.ДокументПоступления
		|			ИЗ
		|				ВременнаяТаблицаПоступление)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаПоступление.Дата КАК Дата,
		|	ВременнаяТаблицаПоступление.ДокументПоступления КАК ДокументПоступления
		|ПОМЕСТИТЬ ВременнаяТаблицаПоступленияСДопРасходамиИГТД
		|ИЗ
		|	ВременнаяТаблицаПоступление КАК ВременнаяТаблицаПоступление
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаДопраходы КАК ВременнаяТаблицаДопраходы
		|		ПО ВременнаяТаблицаПоступление.ДокументПоступления = ВременнаяТаблицаДопраходы.ДокументПоступления
		|			И ВременнаяТаблицаПоступление.Номенклатура = ВременнаяТаблицаДопраходы.Номенклатура
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаПоступление.Дата,
		|	ВременнаяТаблицаПоступление.ДокументПоступления
		|ИЗ
		|	ВременнаяТаблицаПоступление КАК ВременнаяТаблицаПоступление
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаГТДПоИмпорту КАК ВременнаяТаблицаГТДПоИмпорту
		|		ПО ВременнаяТаблицаПоступление.ДокументПоступления = ВременнаяТаблицаГТДПоИмпорту.ДокументПоступления
		|			И ВременнаяТаблицаПоступление.Номенклатура = ВременнаяТаблицаГТДПоИмпорту.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВременнаяТаблицаПоступленияСДопРасходамиИГТД.Дата КАК Дата,
		|	ВременнаяТаблицаПоступленияСДопРасходамиИГТД.ДокументПоступления КАК Документ
		|ИЗ
		|	ВременнаяТаблицаПоступленияСДопРасходамиИГТД КАК ВременнаяТаблицаПоступленияСДопРасходамиИГТД
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата";
	
	Список.Загрузить(Запрос.Выполнить().Выгрузить());
	
	// РаботаСФормами
	РаботаСФормамиСервер.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец РаботаСФормами
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	МассивДокументов = Новый Массив();
	
	Для Каждого НомерВыделеннойСтроки Из Элементы.Список.ВыделенныеСтроки Цикл
		СтрокаТаблицы = Список.НайтиПоИдентификатору(НомерВыделеннойСтроки);
		МассивДокументов.Добавить(СтрокаТаблицы.Документ);
	КонецЦикла;
	
	Оповестить("ПодборДокументовПоступленияСДопРасходамиИГТД", МассивДокументов, УникальныйИдентификаторФормыВладельца);
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти
