#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ВерсионированиеОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
//@skip-warning
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область ИнтерфейсПечати

// Функция формирует табличный документ с печатной формой ТТН
//
// Возвращаемое значение:
//  Табличный документ - печатная форма
//
Функция ПечатьТТН(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ТТНИсходящая_ТТН";
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.АвтоМасштаб = Истина;

	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ЭТТНИсходящая.ПФ_MXL_ТТНВВалюте");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТаблицаДокумента.Ссылка КАК Ссылка,
		|	ТаблицаДокумента.Номер КАК Номер,
		|	ТаблицаДокумента.Дата КАК Дата,
		|	ТаблицаДокумента.Организация КАК Организация,
		|	ТаблицаДокумента.Контрагент КАК Контрагент,
		|	ТаблицаДокумента.Грузополучатель КАК Грузополучатель,
		|	ТаблицаДокумента.ДокументОснование.ВалютаДокумента КАК ВалютаДокумента,
		|	ТаблицаДокумента.ДокументОснование.Курс КАК Курс,
		|	ТаблицаДокумента.Автомобиль КАК МаркаАвтомобиля,
		|	ТаблицаДокумента.Водитель КАК ПредставлениеВодителя,
		|	ТаблицаДокумента.НомерПутевогоЛиста КАК НомерПутевогоЛиста,
		|	ТаблицаДокумента.ДоверенностьНомер КАК ДоверенностьНомер,
		|	ТаблицаДокумента.ДоверенностьДата КАК ДоверенностьДата,
		|	ТаблицаДокумента.ДоверенностьВыдана КАК ДоверенностьВыдана,
		|	ТаблицаДокумента.ДоверенностьЧерезКого КАК ДоверенностьЧерезКого,
		|	ТаблицаДокумента.Товары.(
		|		НомерСтроки КАК НомерСтроки,
		|		Номенклатура КАК Номенклатура,
		|		Номенклатура.НаименованиеПолное КАК НоменклатураПредставление,
		|		Номенклатура.Код КАК НоменклатураКод,
		|		Номенклатура.Артикул КАК НоменклатураАртикул,
		|		ПРЕДСТАВЛЕНИЕ(ТаблицаДокумента.Товары.Номенклатура.ЕдиницаИзмерения) КАК ЕдиницаИзмерения,
		|		НомерПрейскуранта КАК НомерПрейскуранта,
		|		Количество КАК Количество,
		|		Цена КАК Цена,
		|		Сумма КАК Сумма,
		|		ВидУпаковки КАК ВидУпаковки,
		|		ВесУпаковки КАК ВесУпаковки,
		|		КоличествоМест КАК КоличествоМест,
		|		МассаГрузаНетто КАК МассаГрузаНетто,
		|		МассаГрузаБрутто КАК МассаГрузаБрутто
		|	) КАК Товары
		|ИЗ
		|	Документ.ЭТТНИсходящая КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка В(&СписокДокументов)";		

	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("СписокДокументов", МассивОбъектов);
	
	Шапка = Запрос.Выполнить().Выбрать();
	Пока Шапка.Следующий() Цикл
		Если ТабличныйДокумент.ВысотаТаблицы > 0 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Подготовка данных.
		ДанныеПечати = Новый Структура;
		
		ДанныеПечати.Вставить("ДатаДокумента", Формат(Шапка.Дата, "ДЛФ=D"));
		НомерНаПечать = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Шапка.Номер);
		ДанныеПечати.Вставить("НомерДокумента", НомерНаПечать);
		ДанныеПечати.Вставить("Валюта", Шапка.ВалютаДокумента);
		ДанныеПечати.Вставить("НомерСтраницы", ""); 
		
		// Сведения об организации.
		СведенияОбОрганизации = БухгалтерскийУчетСервер.ПолучитьСведенияОбОрганизации(Шапка.Организация);
		АдресОрганизации = ?(СведенияОбОрганизации.Улица    = "","",СведенияОбОрганизации.Улица + ",")
		+ ?(СведенияОбОрганизации.Дом      = "",""," " + СведенияОбОрганизации.Дом + ",")
		+ ?(СведенияОбОрганизации.Корпус   = "",""," " + СведенияОбОрганизации.Корпус + ",")
		+ ?(СведенияОбОрганизации.Квартира = "",""," " + СведенияОбОрганизации.Квартира);
		Телефон = СведенияОбОрганизации.Тел;
		
		ДанныеПечати.Вставить("ОрганизацияПредставление", "" + СведенияОбОрганизации.НаименованиеПолное 
		+ ?(ЗначениеЗаполнено(СведенияОбОрганизации.ИНН),", " + СведенияОбОрганизации.ИНН, "") 
		+ ?(ЗначениеЗаполнено(АдресОрганизации),", " + АдресОрганизации, "")
		+ ", " + Телефон);
		
		// Сведения о контрагенте.
		ДанныеПечати.Вставить("КонтрагентПредставление", Шапка.Грузополучатель);
		
		// Заполнение шапки.
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		ОбластьМакета.Параметры.Заполнить(Шапка);
		ОбластьМакета.Параметры.Заполнить(ДанныеПечати);			
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		// Создаем массив для проверки вывода.
		МассивВыводимыхОбластей = Новый Массив;
		
		// Заполнение таблицы.
		ОбластьЗаголовокТаблицы      = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
		ОбластьМакета                = Макет.ПолучитьОбласть("Строка");
		ОбластьИтоговПоСтранице      = Макет.ПолучитьОбласть("ИтогоПоСтранице");
		ОбластьВсего                 = Макет.ПолучитьОбласть("Всего");
		ОбластьПодвала               = Макет.ПолучитьОбласть("Подвал");
		ОбластьТранспортногоРаздела  = Макет.ПолучитьОбласть("ТранспортныйРаздел");
		ОбластьСведенийОГрузе        = Макет.ПолучитьОбласть("СведенияОГрузе");
		ОбластьПодвалаСведенийОГрузе = Макет.ПолучитьОбласть("ПодвалСведенийОГрузе");
		ОбластьПрочихСведений        = Макет.ПолучитьОбласть("ПрочиеСведения");
		ОбластьПогрузочныеОперации	 = Макет.ПолучитьОбласть("ПогрузочныеОперации");
		
		СтруктураОбластейМакета = Новый Структура();
		СтруктураОбластейМакета.Вставить("ОбластьМакета", ОбластьМакета);
		СтруктураОбластейМакета.Вставить("ОбластьЗаголовокТаблицы", ОбластьЗаголовокТаблицы);
		СтруктураОбластейМакета.Вставить("ОбластьИтоговПоСтранице", ОбластьИтоговПоСтранице);
		СтруктураОбластейМакета.Вставить("ОбластьВсего", ОбластьВсего);
		СтруктураОбластейМакета.Вставить("ОбластьПодвала", ОбластьПодвала);
		
		ТаблицаТовары = Шапка.Товары.Выгрузить();
		
		ДанныеПечати.Вставить("КоличествоМест", ТаблицаТовары.Итог("КоличествоМест"));

		СтруктураПеременных = Новый Структура();
		СтруктураПеременных.Вставить("КоличествоСтрок", ТаблицаТовары.Количество());
		СтруктураПеременных.Вставить("ИтоговыеСуммы", СтруктураИтоговыеСуммыТТН());
		СтруктураПеременных.Вставить("НомерСтроки", 0);
		СтруктураПеременных.Вставить("Курс", Шапка.Курс);
		
		ЗаполнитьИВывестиСтрокиМакетаТТН(СтруктураОбластейМакета, ТаблицаТовары, ТабличныйДокумент, ДанныеПечати, СтруктураПеременных);
				
		// Выводим итоги по последней странице
		ОбластьИтоговПоСтранице.Параметры.Заполнить(СтруктураПеременных.ИтоговыеСуммы);
		ТабличныйДокумент.Вывести(ОбластьИтоговПоСтранице);
		
		// Выводим итоги по документу в целом
		ОбластьВсего.Параметры.Заполнить(СтруктураПеременных.ИтоговыеСуммы);		
		ТабличныйДокумент.Вывести(ОбластьВсего);
		
		// Выводим подвал документа				
		ПолнаяДатаДокумента = Формат(Шапка.Дата, "ДЛФ=DD");
		ДлинаСтроки = СтрДлина(ПолнаяДатаДокумента);
		ПервыйРазделитель = Найти(ПолнаяДатаДокумента, " ");
		ВторойРазделитель = Найти(Прав(ПолнаяДатаДокумента, ДлинаСтроки - ПервыйРазделитель), " ") + ПервыйРазделитель;
		
		ДанныеПечати.Вставить("КоличествоПорядковыхНомеровЗаписейПрописью", ЧислоПрописью(СтруктураПеременных.НомерСтроки, ,",,,,,,,,0"));
		ДанныеПечати.Вставить("СуммаПрописью", БухгалтерскийУчетСервер.СформироватьСуммуПрописью(СтруктураПеременных.ИтоговыеСуммы.ИтогоСумма, Шапка.ВалютаДокумента));
		ДанныеПечати.Вставить("ДатаДокументаДень", """" + Лев(ПолнаяДатаДокумента, ПервыйРазделитель -1 ) + """");
		ДанныеПечати.Вставить("ДатаДокументаМесяц", Сред(ПолнаяДатаДокумента, ПервыйРазделитель + 1, ВторойРазделитель - ПервыйРазделитель - 1));
		ДанныеПечати.Вставить("ДатаДокументаГод", Прав(ПолнаяДатаДокумента, ДлинаСтроки - ВторойРазделитель));
		ДанныеПечати.Вставить("ВсегоНаименованийПрописью", ЧислоПрописью(СтруктураПеременных.КоличествоСтрок, ,",,,,,,,,0"));
		ДанныеПечати.Вставить("ВсегоМестПрописью", ЧислоПрописью(ДанныеПечати.КоличествоМест, ,",,,,,,,,0"));
		
		ДанныеПечати.Вставить("ДоверенностьНомер", Шапка.ДоверенностьНомер);
		ДанныеПечати.Вставить("ДоверенностьДата", Шапка.ДоверенностьДата);
		ДанныеПечати.Вставить("ДоверенностьВыдана", Шапка.ДоверенностьВыдана);
		ДанныеПечати.Вставить("ДоверенностьЧерезКого", Шапка.ДоверенностьЧерезКого);
		
		МассаГрузаНетто = ТаблицаТовары.Итог("МассаГрузаНетто");
		ДанныеПечати.Вставить("МассаГрузаНетто", МассаГрузаНетто);

		// МассаГрузаНетто прописью в тоннах и килограммах.
		МассаГрузаЦелаяЧасть = Цел(МассаГрузаНетто);
		МассаГрузаДробнаяЧасть = (МассаГрузаНетто - Цел(МассаГрузаНетто)) * 1000;
		МассаГрузаДробнаяЧастьСтрокой = Строка(МассаГрузаДробнаяЧасть);
		
		Если МассаГрузаДробнаяЧасть <> 0 И СтрДлина(МассаГрузаДробнаяЧастьСтрокой) = 1 Тогда
			МассаГрузаДробнаяЧастьСтрокой = "00" + МассаГрузаДробнаяЧастьСтрокой;
		ИначеЕсли СтрДлина(МассаГрузаДробнаяЧастьСтрокой) = 2 Тогда
			МассаГрузаДробнаяЧастьСтрокой = "0" + МассаГрузаДробнаяЧастьСтрокой;
		КонецЕсли;	
		
		ДанныеПечати.Вставить("МассаГрузаНеттоПрописью", ЧислоПрописью(МассаГрузаЦелаяЧасть, ,"тонна, тонны, тонн, ж,,,,,0") 
									+ " " + МассаГрузаДробнаяЧастьСтрокой + " " + "кг");
									
		МассаГрузаБрутто = ТаблицаТовары.Итог("МассаГрузаБрутто");
		ДанныеПечати.Вставить("МассаГрузаБрутто", МассаГрузаБрутто);

		// МассаГрузаБрутто прописью в тоннах и килограммах.
		МассаГрузаЦелаяЧасть = Цел(МассаГрузаБрутто);
		МассаГрузаДробнаяЧасть = (МассаГрузаБрутто - Цел(МассаГрузаБрутто)) * 1000;
		МассаГрузаДробнаяЧастьСтрокой = Строка(МассаГрузаДробнаяЧасть);
		
		Если МассаГрузаДробнаяЧасть <> 0 И СтрДлина(МассаГрузаДробнаяЧастьСтрокой) = 1 Тогда
			МассаГрузаДробнаяЧастьСтрокой = "00" + МассаГрузаДробнаяЧастьСтрокой;
		ИначеЕсли СтрДлина(МассаГрузаДробнаяЧастьСтрокой) = 2 Тогда
			МассаГрузаДробнаяЧастьСтрокой = "0" + МассаГрузаДробнаяЧастьСтрокой;
		КонецЕсли;	
		
		ДанныеПечати.Вставить("МассаГрузаБуттоПрописью", ЧислоПрописью(МассаГрузаЦелаяЧасть, ,"тонна, тонны, тонн, ж,,,,,0") 
									+ " " + МассаГрузаДробнаяЧастьСтрокой + " " + "кг");
		
		// Подписи.
		ФамилияИОРуководителя = "";                    
		ФамилияИОГлавногоБухгалтера = "";
		ОтветственныеЛица = БухгалтерскийУчетСервер.ОтветственныеЛицаОрганизацийРуководители(Шапка.Организация, Шапка.Дата);
		БухгалтерскийУчетСервер.ФамилияИнициалыПоНаименованию(ФамилияИОРуководителя, ОтветственныеЛица.Руководитель);
		БухгалтерскийУчетСервер.ФамилияИнициалыПоНаименованию(ФамилияИОГлавногоБухгалтера, ОтветственныеЛица.ГлавныйБухгалтер);
		
		ДанныеПечати.Вставить("ФамилияИОРуководителя", ФамилияИОРуководителя);
		ДанныеПечати.Вставить("ФамилияИОГлавногоБухгалтера", ФамилияИОГлавногоБухгалтера);
		ДанныеПечати.Вставить("РуководительДолжность", ОтветственныеЛица.РуководительДолжность);
				
		ОбластьПодвала.Параметры.Заполнить(ДанныеПечати);
		ТабличныйДокумент.Вывести(ОбластьПодвала);
				
		МассивВыводимыхОбластей.Очистить();
		МассивВыводимыхОбластей.Добавить(ОбластьТранспортногоРаздела);
		МассивВыводимыхОбластей.Добавить(ОбластьСведенийОГрузе);
		МассивВыводимыхОбластей.Добавить(ОбластьПодвалаСведенийОГрузе);
		МассивВыводимыхОбластей.Добавить(ОбластьПогрузочныеОперации);
		МассивВыводимыхОбластей.Добавить(ОбластьПрочихСведений);
		
		Если Не ТабличныйДокумент.ПроверитьВывод(МассивВыводимыхОбластей) Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ОбластьТранспортногоРаздела.Параметры.Заполнить(ДанныеПечати);
		ОбластьТранспортногоРаздела.Параметры.Заполнить(Шапка);
		ТабличныйДокумент.Вывести(ОбластьТранспортногоРаздела);
		ТабличныйДокумент.Вывести(ОбластьСведенийОГрузе);
		ТабличныйДокумент.Вывести(ОбластьПодвалаСведенийОГрузе);
		ТабличныйДокумент.Вывести(ОбластьПогрузочныеОперации);
		ТабличныйДокумент.Вывести(ОбластьПрочихСведений);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

// Функция формирует табличный документ с печатной формой ЭТТН
//
// Возвращаемое значение:
//  Табличный документ - печатная форма
//
Функция ПечатьЭТТН(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ТТНИсходящая_ЭТТН";
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.АвтоМасштаб = Истина;

	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ЭТТНИсходящая.ПФ_MXL_ЭлектроннаяТТН");
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТаблицаДокумента.Ссылка КАК Ссылка,
		|	ТаблицаДокумента.Номер КАК Номер,
		|	ТаблицаДокумента.Дата КАК Дата,
		|	ТаблицаДокумента.Организация КАК Организация,
		|	ТаблицаДокумента.Контрагент КАК Контрагент,
		|	ТаблицаДокумента.Склад.Наименование КАК Склад,
		|	ТаблицаДокумента.СкладПолучатель.Наименование КАК СкладПолучатель,
		|	ВЫБОР
		|		КОГДА ТаблицаДокумента.СкладПолучатель ССЫЛКА Справочник.СкладыКонтрагентов
		|			ТОГДА ТаблицаДокумента.СкладПолучатель.Адрес
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК АдресДоставки,
		|	ТаблицаДокумента.СпособПеревозки КАК СпособПеревозки,
		|	ТаблицаДокумента.Водитель КАК Водитель,
		|	ТаблицаДокумента.Водитель.ИНН КАК ВодительИНН,
		|	ТаблицаДокумента.Автомобиль КАК Автомобиль,
		|	ТаблицаДокумента.Автомобиль.НомерТранспорта КАК НомерТранспорта,
		|	ТаблицаДокумента.НомерПрицепа КАК НомерПрицепа,
		|	ТаблицаДокумента.ДокументОснование КАК ДокументОснование,
		|	ТаблицаДокумента.Товары.(
		|		НомерСтроки КАК НомерСтроки,
		|		Номенклатура.НаименованиеПолное КАК НаименованиеТовара,
		|		Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|		Цена КАК Цена,
		|		Количество КАК Количество,
		|		Сумма КАК Сумма,
		|		КлючСвязи КАК КлючСвязи
		|	) КАК Товары,
		|	ТаблицаДокумента.КодыМаркировки.(
		|		КодМаркировки КАК КодМаркировки,
		|		КлючСвязи КАК КлючСвязи
		|	) КАК КодыМаркировки
		|ИЗ
		|	Документ.ЭТТНИсходящая КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка В(&СписокДокументов)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЭлектронныйСчетФактураВыписанныйТовары.ДокументОснование КАК ДокументОснование,
		|	ЭлектронныйСчетФактураВыписанныйТовары.Ссылка.ДатаПоставки КАК ДатаПоставки,
		|	ЭлектронныйСчетФактураВыписанныйТовары.Ссылка.КодПоставкиНДС.ВидПоставки.Наименование КАК ВидПоставки,
		|	ЭлектронныйСчетФактураВыписанныйТовары.Ссылка.НомерЭСФ КАК НомерЭСФ
		|ИЗ
		|	Документ.ЭлектронныйСчетФактураВыписанный.Товары КАК ЭлектронныйСчетФактураВыписанныйТовары
		|ГДЕ
		|	ЭлектронныйСчетФактураВыписанныйТовары.ДокументОснование В(&МассивДокументовОснования)
		|
		|СГРУППИРОВАТЬ ПО
		|	ЭлектронныйСчетФактураВыписанныйТовары.ДокументОснование,
		|	ЭлектронныйСчетФактураВыписанныйТовары.Ссылка.ДатаПоставки,
		|	ЭлектронныйСчетФактураВыписанныйТовары.Ссылка.КодПоставкиНДС.ВидПоставки.Наименование,
		|	ЭлектронныйСчетФактураВыписанныйТовары.Ссылка.НомерЭСФ";		

	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("СписокДокументов", МассивОбъектов);
	
	МассивДокументовОснования = Новый Массив();
	
	Для Каждого ДокументЭТТН Из МассивОбъектов Цикл
		МассивДокументовОснования.Добавить(ДокументЭТТН.ДокументОснование);		
	КонецЦикла;	
	
	Запрос.УстановитьПараметр("МассивДокументовОснования", МассивДокументовОснования);
	
	Результаты = Запрос.ВыполнитьПакет();
	ВыборкаЭСФ = Результаты[1].Выбрать();
	
	Шапка = Результаты[0].Выбрать();
	Пока Шапка.Следующий() Цикл
		Если ТабличныйДокумент.ВысотаТаблицы > 0 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Подготовка данных.
		ДанныеПечати = Новый Структура;
		
		ДанныеПечати.Вставить("ДатаОформления1", Сред(Шапка.Дата, 1, 1));
		ДанныеПечати.Вставить("ДатаОформления2", Сред(Шапка.Дата, 2, 1));
		ДанныеПечати.Вставить("ДатаОформления3", Сред(Шапка.Дата, 4, 1));
		ДанныеПечати.Вставить("ДатаОформления4", Сред(Шапка.Дата, 5, 1));
		ДанныеПечати.Вставить("ДатаОформления5", Сред(Шапка.Дата, 7, 1));
		ДанныеПечати.Вставить("ДатаОформления6", Сред(Шапка.Дата, 8, 1));
		ДанныеПечати.Вставить("ДатаОформления7", Сред(Шапка.Дата, 9, 1));
		ДанныеПечати.Вставить("ДатаОформления8", Сред(Шапка.Дата, 10, 1));

		СведенияОбОрганизации = БухгалтерскийУчетСервер.ПолучитьСведенияОбОрганизации(Шапка.Организация);
		
		ДанныеПечати.Вставить("ИННПоставщика1", Сред(СведенияОбОрганизации.ИНН, 1, 1));
		ДанныеПечати.Вставить("ИННПоставщика2", Сред(СведенияОбОрганизации.ИНН, 2, 1));
		ДанныеПечати.Вставить("ИННПоставщика3", Сред(СведенияОбОрганизации.ИНН, 3, 1));
		ДанныеПечати.Вставить("ИННПоставщика4", Сред(СведенияОбОрганизации.ИНН, 4, 1));
		ДанныеПечати.Вставить("ИННПоставщика5", Сред(СведенияОбОрганизации.ИНН, 5, 1));
		ДанныеПечати.Вставить("ИННПоставщика6", Сред(СведенияОбОрганизации.ИНН, 6, 1));
		ДанныеПечати.Вставить("ИННПоставщика7", Сред(СведенияОбОрганизации.ИНН, 7, 1));
		ДанныеПечати.Вставить("ИННПоставщика8", Сред(СведенияОбОрганизации.ИНН, 8, 1));
		ДанныеПечати.Вставить("ИННПоставщика9", Сред(СведенияОбОрганизации.ИНН, 9, 1));
		ДанныеПечати.Вставить("ИННПоставщика10", Сред(СведенияОбОрганизации.ИНН, 10, 1));
		ДанныеПечати.Вставить("ИННПоставщика11", Сред(СведенияОбОрганизации.ИНН, 11, 1));
		ДанныеПечати.Вставить("ИННПоставщика12", Сред(СведенияОбОрганизации.ИНН, 12, 1));
		ДанныеПечати.Вставить("ИННПоставщика13", Сред(СведенияОбОрганизации.ИНН, 13, 1));
		ДанныеПечати.Вставить("ИННПоставщика14", Сред(СведенияОбОрганизации.ИНН, 14, 1));
		
		ДанныеПечати.Вставить("НаименованиеОрганизацииПоставщика", СведенияОбОрганизации.НаименованиеПолное);
		ДанныеПечати.Вставить("АдресПоставщика", ?(СведенияОбОрганизации.АдрЮР = "", СведенияОбОрганизации.АдрФакт, СведенияОбОрганизации.АдрЮР));
		ДанныеПечати.Вставить("НаименованиеНалоговойПоставщика", СведенияОбОрганизации.ГНСНаименование);
		ДанныеПечати.Вставить("КодНалоговойПоставщика1", Сред(СведенияОбОрганизации.ГНСКод, 1, 1));
		ДанныеПечати.Вставить("КодНалоговойПоставщика2", Сред(СведенияОбОрганизации.ГНСКод, 2, 1));
		ДанныеПечати.Вставить("КодНалоговойПоставщика3", Сред(СведенияОбОрганизации.ГНСКод, 3, 1));
		
		Если ЗначениеЗаполнено(Шапка.Контрагент) Тогда
			СведенияОКонтрагенте = БухгалтерскийУчетСервер.ПолучитьСведенияОКонтрагенте(Шапка.Контрагент);
			
			ДанныеПечати.Вставить("ИННПокупателя1", Сред(СведенияОКонтрагенте.ИНН, 1, 1));
			ДанныеПечати.Вставить("ИННПокупателя2", Сред(СведенияОКонтрагенте.ИНН, 2, 1));
			ДанныеПечати.Вставить("ИННПокупателя3", Сред(СведенияОКонтрагенте.ИНН, 3, 1));
			ДанныеПечати.Вставить("ИННПокупателя4", Сред(СведенияОКонтрагенте.ИНН, 4, 1));
			ДанныеПечати.Вставить("ИННПокупателя5", Сред(СведенияОКонтрагенте.ИНН, 5, 1));
			ДанныеПечати.Вставить("ИННПокупателя6", Сред(СведенияОКонтрагенте.ИНН, 6, 1));
			ДанныеПечати.Вставить("ИННПокупателя7", Сред(СведенияОКонтрагенте.ИНН, 7, 1));
			ДанныеПечати.Вставить("ИННПокупателя8", Сред(СведенияОКонтрагенте.ИНН, 8, 1));
			ДанныеПечати.Вставить("ИННПокупателя9", Сред(СведенияОКонтрагенте.ИНН, 9, 1));
			ДанныеПечати.Вставить("ИННПокупателя10", Сред(СведенияОКонтрагенте.ИНН, 10, 1));
			ДанныеПечати.Вставить("ИННПокупателя11", Сред(СведенияОКонтрагенте.ИНН, 11, 1));
			ДанныеПечати.Вставить("ИННПокупателя12", Сред(СведенияОКонтрагенте.ИНН, 12, 1));
			ДанныеПечати.Вставить("ИННПокупателя13", Сред(СведенияОКонтрагенте.ИНН, 13, 1));
			ДанныеПечати.Вставить("ИННПокупателя14", Сред(СведенияОКонтрагенте.ИНН, 14, 1));
			
			ДанныеПечати.Вставить("НаименованиеОрганизацииПокупателя", СведенияОКонтрагенте.НаименованиеПолное);
			ДанныеПечати.Вставить("АдресПокупателя", ?(СведенияОКонтрагенте.АдрЮР = "", СведенияОКонтрагенте.АдрФакт, СведенияОКонтрагенте.АдрЮР));
			ДанныеПечати.Вставить("НаименованиеНалоговойПокупателя", СведенияОКонтрагенте.ГНСНаименование);
			ДанныеПечати.Вставить("КодНалоговойПокупателя1", Сред(СведенияОКонтрагенте.ГНСКод, 1, 1));
			ДанныеПечати.Вставить("КодНалоговойПокупателя2", Сред(СведенияОКонтрагенте.ГНСКод, 2, 1));
			ДанныеПечати.Вставить("КодНалоговойПокупателя3", Сред(СведенияОКонтрагенте.ГНСКод, 3, 1));
		КонецЕсли;	
		
		ДанныеПечати.Вставить("ОбъектОтправки",	Шапка.Склад);
		ДанныеПечати.Вставить("АдерсОтправки", 	"");
		ДанныеПечати.Вставить("ОбъектДоставки",	Шапка.СкладПолучатель);
		ДанныеПечати.Вставить("АдресДоставки", 	Шапка.АдресДоставки);
		
		Отбор = Новый Структура();
		Отбор.Вставить("ДокументОснование", Шапка.ДокументОснование);
		
		Если ВыборкаЭСФ.НайтиСледующий(Отбор) Тогда
			ДанныеПечати.Вставить("ВидПоставки", 		ВыборкаЭСФ.ВидПоставки);
			ДанныеПечати.Вставить("НомерСчетФактуры", 	ВыборкаЭСФ.НомерЭСФ);
			ДанныеПечати.Вставить("ДатаПоставки1", 		Сред(ВыборкаЭСФ.ДатаПоставки, 1, 1));
			ДанныеПечати.Вставить("ДатаПоставки2", 		Сред(ВыборкаЭСФ.ДатаПоставки, 2, 1));
			ДанныеПечати.Вставить("ДатаПоставки3", 		Сред(ВыборкаЭСФ.ДатаПоставки, 4, 1));
			ДанныеПечати.Вставить("ДатаПоставки4",		Сред(ВыборкаЭСФ.ДатаПоставки, 5, 1));
			ДанныеПечати.Вставить("ДатаПоставки5", 		Сред(ВыборкаЭСФ.ДатаПоставки, 7, 1));
			ДанныеПечати.Вставить("ДатаПоставки6", 		Сред(ВыборкаЭСФ.ДатаПоставки, 8, 1));
			ДанныеПечати.Вставить("ДатаПоставки7", 		Сред(ВыборкаЭСФ.ДатаПоставки, 9, 1));
			ДанныеПечати.Вставить("ДатаПоставки8", 		Сред(ВыборкаЭСФ.ДатаПоставки, 10, 1));	
		КонецЕсли;	
		
		ВыборкаЭСФ.Сбросить();
		
		Если Шапка.СпособПеревозки = 10 Тогда
			ДанныеПечати.Вставить("СпособПереовозки", "Автомобильный");
			
		ИначеЕсли Шапка.СпособПеревозки = 20 Тогда
			ДанныеПечати.Вставить("СпособПереовозки", "Ж/д транспорт");
			
		ИначеЕсли Шапка.СпособПеревозки = 30 Тогда
			ДанныеПечати.Вставить("СпособПереовозки", "Воздушный");
			
		ИначеЕсли Шапка.СпособПеревозки = 40 Тогда
			ДанныеПечати.Вставить("СпособПереовозки", "Иной");	
		КонецЕсли;	
			
		ДанныеПечати.Вставить("ФИОВодителя", 			Шапка.Водитель);
		ДанныеПечати.Вставить("МаркаАТС",				Шапка.Автомобиль);
		ДанныеПечати.Вставить("ГосномерАТС", 			Шапка.НомерТранспорта);
		ДанныеПечати.Вставить("ГосномерПрицепаВагона", 	Шапка.НомерПрицепа);
		
		ДанныеПечати.Вставить("ИННВодителя1", Сред(Шапка.ВодительИНН, 1, 1));
		ДанныеПечати.Вставить("ИННВодителя2", Сред(Шапка.ВодительИНН, 2, 1));
		ДанныеПечати.Вставить("ИННВодителя3", Сред(Шапка.ВодительИНН, 3, 1));
		ДанныеПечати.Вставить("ИННВодителя4", Сред(Шапка.ВодительИНН, 4, 1));
		ДанныеПечати.Вставить("ИННВодителя5", Сред(Шапка.ВодительИНН, 5, 1));
		ДанныеПечати.Вставить("ИННВодителя6", Сред(Шапка.ВодительИНН, 6, 1));
		ДанныеПечати.Вставить("ИННВодителя7", Сред(Шапка.ВодительИНН, 7, 1));
		ДанныеПечати.Вставить("ИННВодителя8", Сред(Шапка.ВодительИНН, 8, 1));
		ДанныеПечати.Вставить("ИННВодителя9", Сред(Шапка.ВодительИНН, 9, 1));
		ДанныеПечати.Вставить("ИННВодителя10", Сред(Шапка.ВодительИНН, 10, 1));
		ДанныеПечати.Вставить("ИННВодителя11", Сред(Шапка.ВодительИНН, 11, 1));
		ДанныеПечати.Вставить("ИННВодителя12", Сред(Шапка.ВодительИНН, 12, 1));
		ДанныеПечати.Вставить("ИННВодителя13", Сред(Шапка.ВодительИНН, 13, 1));
		ДанныеПечати.Вставить("ИННВодителя14", Сред(Шапка.ВодительИНН, 14, 1));
		
		ДанныеПечати.Вставить("КоличествоИтого", 0);
		ДанныеПечати.Вставить("СуммаИтого", 0);
		ДанныеПечати.Вставить("ИтогоПоЛисту", "ИТОГО ПО ПЕРВОМУ ЛИСТУ:");
		
		ФИОРуководителя = "";                    
		ОтветственныеЛица = БухгалтерскийУчетСервер.ОтветственныеЛицаОрганизацийРуководители(Шапка.Организация, Шапка.Дата);
		БухгалтерскийУчетСервер.ФамилияИнициалыПоНаименованию(ФИОРуководителя, ОтветственныеЛица.Руководитель);	
		ДанныеПечати.Вставить("ФИОРуководителя", ФИОРуководителя);
				
		КоличествоЛистов = КоличествоЛистовЭТТН(Макет, ДанныеПечати, Шапка);		
		ДанныеПечати.Вставить("Листов", КоличествоЛистов);
				
		ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
		ОбластьМакета.Параметры.Заполнить(ДанныеПечати);		
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбластьШапка 	= Макет.ПолучитьОбласть("Шапка");
		ОбластьСтрока 	= Макет.ПолучитьОбласть("Строка");
		ОбластьИтого 	= Макет.ПолучитьОбласть("Итого");
		ОбластьПодвал 	= Макет.ПолучитьОбласть("Подвал");
		
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		ВыборкаТовары = Шапка.Товары.Выбрать();
		ВыборкаКодыМаркировки = Шапка.КодыМаркировки.Выбрать();
			
		Отбор = Новый Структура();
		Отбор.Вставить("КлючСвязи", 0);
		
		ПустойКодМаркировки = Новый Структура();
		ПустойКодМаркировки.Вставить("КодМаркировки", "");
		
		НомерСтроки = 1;
		КоличествоСтрок = ВыборкаТовары.Количество();
		
		Пока ВыборкаТовары.Следующий() Цикл		
			ОбластьСтрока.Параметры.Заполнить(ВыборкаТовары);
			
			Отбор.КлючСвязи = ВыборкаТовары.КлючСвязи;
			
			Если ВыборкаКодыМаркировки.НайтиСледующий(Отбор) Тогда
				ОбластьСтрока.Параметры.Заполнить(ВыборкаКодыМаркировки);
			Иначе
				ОбластьСтрока.Параметры.Заполнить(ПустойКодМаркировки); 
			КонецЕсли;
			
			ВыборкаКодыМаркировки.Сбросить();
			
			ДанныеПечати.КоличествоИтого = ДанныеПечати.КоличествоИтого + ВыборкаТовары.Количество;
			ДанныеПечати.СуммаИтого = ДанныеПечати.СуммаИтого + ВыборкаТовары.Сумма;
			
			ОбластьИтого.Параметры.Заполнить(ДанныеПечати);
			
			ОбластиПроверки = Новый Массив();
			ОбластиПроверки.Добавить(ОбластьСтрока);
			ОбластиПроверки.Добавить(ОбластьИтого);
			
			Если НомерСтроки = КоличествоСтрок Тогда
				ОбластиПроверки.Добавить(ОбластьПодвал);			
			КонецЕсли;	
			
			Если НЕ ТабличныйДокумент.ПроверитьВывод(ОбластиПроверки) Тогда
				ДанныеПечати.КоличествоИтого = ДанныеПечати.КоличествоИтого - ВыборкаТовары.Количество;
				ДанныеПечати.СуммаИтого = ДанныеПечати.СуммаИтого - ВыборкаТовары.Сумма;				
				
				ОбластьИтого.Параметры.Заполнить(ДанныеПечати);				
				ТабличныйДокумент.Вывести(ОбластьИтого);
				
				ДанныеПечати.ИтогоПоЛисту = "ИТОГО ПО ЛИСТУ:";
				
				ДанныеПечати.КоличествоИтого = ВыборкаТовары.Количество;
				ДанныеПечати.СуммаИтого = ВыборкаТовары.Сумма;
							
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ТабличныйДокумент.Вывести(ОбластьШапка);
			КонецЕсли;	
			
			ТабличныйДокумент.Вывести(ОбластьСтрока);
			
			Если НомерСтроки = КоличествоСтрок Тогда
				ДанныеПечати.ИтогоПоЛисту = "ИТОГО ПО ПОСЛЕДНЕМУ ЛИСТУ:";
				ОбластьИтого.Параметры.Заполнить(ДанныеПечати);
				ТабличныйДокумент.Вывести(ОбластьИтого);				
			КонецЕсли;
			
			НомерСтроки = НомерСтроки + 1;	
		КонецЦикла;
		
		ОбластьПодвал.Параметры.Заполнить(ДанныеПечати);
		ТабличныйДокумент.Вывести(ОбластьПодвал);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;	
КонецФункции

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТТН") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
		"ТТН", НСтр("ru = 'ТТН'"), ПечатьТТН(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЭТТН") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
		"ЭТТН", НСтр("ru = 'ЭТТН'"), ПечатьЭТТН(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ТТН";
	КомандаПечати.Представление = НСтр("ru = 'ТТН'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Обработчик = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Порядок = 1;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ЭТТН";
	КомандаПечати.Представление = НСтр("ru = 'ЭТТН'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Обработчик = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Порядок = 2;
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор  = "РеестрТТНИсходящая";
	КомандаПечати.Представление  = НСтр("ru = 'Реестр документов'");
	КомандаПечати.ЗаголовокФормы = НСтр("ru = 'Реестр документов ""Реализация товаров и услуг""'");
	КомандаПечати.Обработчик     = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм     = "ФормаСписка";
	КомандаПечати.Порядок        = 99;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура заполняет строку макета ТТН (ТТН в валюте)
//
// Параметры:
//  СтруктураОбластейМакета - Структура - структура с областями;
//  МассивТаблицЗначений - Массив - массив таблиц значений ТЧ "Товары", "Услуги" и "ОС";
//	ТабличныйДокумент - ТабличныйДокумент - табличный документ, в который выводятся данные
//	ДанныеПечати - Структура - данные для печати
//	СтруктураПеременных - Структура - структура переменных, необходимых для подсчета или вычисления.
//
Процедура ЗаполнитьИВывестиСтрокиМакетаТТН(СтруктураОбластейМакета, ТаблицаЗначений, ТабличныйДокумент, ДанныеПечати, СтруктураПеременных)

	МассивВыводимыхОбластей = Новый Массив;
	НомерСтраницы = 1;
	
	Для Каждого СтрокаТаблицы Из ТаблицаЗначений Цикл
			
		ДанныеСтроки = СтруктураДанныеСтрокиТТН();
		СтруктураПеременных.НомерСтроки = СтруктураПеременных.НомерСтроки + 1;			
		ДанныеСтроки.Номер = СтруктураПеременных.НомерСтроки;

		СтруктураОбластейМакета.ОбластьМакета.Параметры.Заполнить(СтрокаТаблицы);
		СтруктураОбластейМакета.ОбластьМакета.Параметры.Заполнить(ДанныеСтроки);
		
		Если СтруктураПеременных.НомерСтроки = 1 Тогда // первая строка
			ДанныеПечати.НомерСтраницы = "Страница " + НомерСтраницы; 
			СтруктураОбластейМакета.ОбластьЗаголовокТаблицы.Параметры.Заполнить(ДанныеПечати);
			ТабличныйДокумент.Вывести(СтруктураОбластейМакета.ОбластьЗаголовокТаблицы);

		Иначе
			МассивВыводимыхОбластей.Очистить();
			МассивВыводимыхОбластей.Добавить(СтруктураОбластейМакета.ОбластьМакета);
			МассивВыводимыхОбластей.Добавить(СтруктураОбластейМакета.ОбластьИтоговПоСтранице);
			
			Если СтруктураПеременных.НомерСтроки = СтруктураПеременных.КоличествоСтрок Тогда
				МассивВыводимыхОбластей.Добавить(СтруктураОбластейМакета.ОбластьВсего);
				МассивВыводимыхОбластей.Добавить(СтруктураОбластейМакета.ОбластьПодвала);					
			КонецЕсли;				
			
			Если СтруктураПеременных.НомерСтроки <> 1 И Не ТабличныйДокумент.ПроверитьВывод(МассивВыводимыхОбластей) Тогда
				
				СтруктураОбластейМакета.ОбластьИтоговПоСтранице.Параметры.Заполнить(СтруктураПеременных.ИтоговыеСуммы);
				ТабличныйДокумент.Вывести(СтруктураОбластейМакета.ОбластьИтоговПоСтранице);
				
				 // Очистим итоги по странице.
				ОбнулитьИтогиПоСтраницеТТН(СтруктураПеременных.ИтоговыеСуммы);
				
				НомерСтраницы = НомерСтраницы + 1;
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				
				ДанныеПечати.НомерСтраницы = "Страница " + НомерСтраницы; 
				СтруктураОбластейМакета.ОбластьЗаголовокТаблицы.Параметры.Заполнить(ДанныеПечати);
				ТабличныйДокумент.Вывести(СтруктураОбластейМакета.ОбластьЗаголовокТаблицы);
			
			КонецЕсли;
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(СтруктураОбластейМакета.ОбластьМакета);
		РассчитатьИтоговыеСуммыТТН(СтруктураПеременных.ИтоговыеСуммы, СтрокаТаблицы);
					
	КонецЦикла;	

КонецПроцедуры

// Функция формирования структуры хранения данных строки.
//
// Возвращаемое значение:
//	Структура - Структура данных строки товаров
//
Функция СтруктураДанныеСтрокиТТН()
	
	Структура = Новый Структура;
	Структура.Вставить("Номер", 0);
	
	Возврат Структура;
	
КонецФункции // СтруктураДанныеСтроки()

// Функция формирования структуры хранения итоговых суммы.
//
// Возвращаемое значение:
//	Структура - Структура хранения итоговых сумм
//
Функция СтруктураИтоговыеСуммыТТН()
	
	Структура = Новый Структура;
	
	// Инициализация итогов по странице.
	Структура.Вставить("ИтогоКоличествоМестНаСтранице", 0);
	Структура.Вставить("ИтогоКоличествоНаСтранице", 0);
	Структура.Вставить("ИтогоСуммаНаСтранице", 0);
	Структура.Вставить("ИтогоМассаГрузаНеттоНаСтранице", 0);
	Структура.Вставить("ИтогоМассаГрузаБруттоНаСтранице", 0);
	Структура.Вставить("ИтогоКоличествоМестНаСтранице", 0);
	
	// Инициализация итогов по документу.
	Структура.Вставить("ИтогоКоличество", 0);
	Структура.Вставить("ИтогоСумма", 0);
	Структура.Вставить("ИтогоМассаГрузаНетто", 0);
	Структура.Вставить("ИтогоМассаГрузаБрутто", 0);
	Структура.Вставить("ИтогоКоличествоМест", 0);
	
	Структура.Вставить("КоличествоПорядковыхНомеровЗаписейПрописью", 0);
	Структура.Вставить("СуммаПрописью", "");
	
	Возврат Структура;
	
КонецФункции // СтруктураИтоговыеСуммы()

// Процедура рассчитывает итоговые суммы с учетом строки товаров.
//
// Параметры:
//	ИтоговыеСуммы - Структура - Структура итоговых сумм документа
//	СтрокаТаблицы - СтрокаТаблицыЗначений - Данные строки таблицы товаров
//
Процедура РассчитатьИтоговыеСуммыТТН(ИтоговыеСуммы, СтрокаТаблицы)
	
	// Увеличим итоги по странице.
	ИтоговыеСуммы.ИтогоКоличествоМестНаСтранице		= ИтоговыеСуммы.ИтогоКоличествоМестНаСтранице		+ СтрокаТаблицы.КоличествоМест;
	ИтоговыеСуммы.ИтогоКоличествоНаСтранице  		= ИтоговыеСуммы.ИтогоКоличествоНаСтранице  			+ СтрокаТаблицы.Количество;
	ИтоговыеСуммы.ИтогоСуммаНаСтранице       		= ИтоговыеСуммы.ИтогоСуммаНаСтранице       			+ СтрокаТаблицы.Сумма;
	ИтоговыеСуммы.ИтогоМассаГрузаНеттоНаСтранице	= ИтоговыеСуммы.ИтогоМассаГрузаНеттоНаСтранице 		+ СтрокаТаблицы.МассаГрузаНетто;
	ИтоговыеСуммы.ИтогоМассаГрузаБруттоНаСтранице	= ИтоговыеСуммы.ИтогоМассаГрузаБруттоНаСтранице 	+ СтрокаТаблицы.МассаГрузаБрутто;
	
	// Увеличим итоги по документу.
	ИтоговыеСуммы.ИтогоКоличество  			= ИтоговыеСуммы.ИтогоКоличество  		+ СтрокаТаблицы.Количество;
	ИтоговыеСуммы.ИтогоСумма       			= ИтоговыеСуммы.ИтогоСумма       		+ СтрокаТаблицы.Сумма;
	ИтоговыеСуммы.ИтогоМассаГрузаНетто		= ИтоговыеСуммы.ИтогоМассаГрузаНетто	+ СтрокаТаблицы.МассаГрузаНетто;
	ИтоговыеСуммы.ИтогоМассаГрузаБрутто		= ИтоговыеСуммы.ИтогоМассаГрузаБрутто	+ СтрокаТаблицы.МассаГрузаБрутто;
	ИтоговыеСуммы.ИтогоКоличествоМест  		= ИтоговыеСуммы.ИтогоКоличествоМест  	+ СтрокаТаблицы.КоличествоМест;
	
КонецПроцедуры // РассчитатьИтоговыеСуммы()

// Процедура обнуления итоговых сумм по странице.
//
Процедура ОбнулитьИтогиПоСтраницеТТН(ИтоговыеСуммы)
	
	ИтоговыеСуммы.ИтогоКоличествоМестНаСтранице = 0;
	ИтоговыеСуммы.ИтогоКоличествоНаСтранице = 0;
	ИтоговыеСуммы.ИтогоСуммаНаСтранице = 0;
	ИтоговыеСуммы.ИтогоМассаГрузаНеттоНаСтранице = 0;
	ИтоговыеСуммы.ИтогоМассаГрузаБруттоНаСтранице = 0;
	
КонецПроцедуры // ОбнулитьИтогиПоСтранице()

Функция КоличествоЛистовЭТТН(Макет, ДанныеПечати, Шапка)

	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ДанныеПечати);		
	ТабличныйДокумент.Вывести(ОбластьМакета);	

	ОбластьШапка 	= Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока 	= Макет.ПолучитьОбласть("Строка");
	ОбластьИтого 	= Макет.ПолучитьОбласть("Итого");
	ОбластьПодвал 	= Макет.ПолучитьОбласть("Подвал");
	
	ТабличныйДокумент.Вывести(ОбластьШапка);
	
	ВыборкаТовары = Шапка.Товары.Выбрать();
	ВыборкаКодыМаркировки = Шапка.КодыМаркировки.Выбрать();
		
	Отбор = Новый Структура();
	Отбор.Вставить("КлючСвязи", 0); 
	
	ПустойКодМаркировки = Новый Структура();
	ПустойКодМаркировки.Вставить("КодМаркировки", "");
	
	НомерСтроки = 1;
	КоличествоСтрок = ВыборкаТовары.Количество();
	
	КоличествоЛистов = 1;
	
	Пока ВыборкаТовары.Следующий() Цикл		
		ОбластьСтрока.Параметры.Заполнить(ВыборкаТовары);
		
		Отбор.КлючСвязи = ВыборкаТовары.КлючСвязи;
		
		Если ВыборкаКодыМаркировки.НайтиСледующий(Отбор) Тогда
			ОбластьСтрока.Параметры.Заполнить(ВыборкаКодыМаркировки);
		Иначе
			ОбластьСтрока.Параметры.Заполнить(ПустойКодМаркировки); 
		КонецЕсли;
		
		ВыборкаКодыМаркировки.Сбросить();
		
		ДанныеПечати.КоличествоИтого = ДанныеПечати.КоличествоИтого + ВыборкаТовары.Количество;
		ДанныеПечати.СуммаИтого = ДанныеПечати.СуммаИтого + ВыборкаТовары.Сумма;
		
		ОбластьИтого.Параметры.Заполнить(ДанныеПечати);
		
		ОбластиПроверки = Новый Массив();
		ОбластиПроверки.Добавить(ОбластьСтрока);
		ОбластиПроверки.Добавить(ОбластьИтого);
		
		Если НомерСтроки = КоличествоСтрок Тогда
			ОбластиПроверки.Добавить(ОбластьПодвал);			
		КонецЕсли;	
		
		Если НЕ ТабличныйДокумент.ПроверитьВывод(ОбластиПроверки) Тогда
			ДанныеПечати.КоличествоИтого = ДанныеПечати.КоличествоИтого - ВыборкаТовары.Количество;
			ДанныеПечати.СуммаИтого = ДанныеПечати.СуммаИтого - ВыборкаТовары.Сумма;				
			
			ОбластьИтого.Параметры.Заполнить(ДанныеПечати);				
			ТабличныйДокумент.Вывести(ОбластьИтого);
			
			ДанныеПечати.КоличествоИтого = ВыборкаТовары.Количество;
			ДанныеПечати.СуммаИтого = ВыборкаТовары.Сумма;
						
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ТабличныйДокумент.Вывести(ОбластьШапка);
			
			КоличествоЛистов = КоличествоЛистов + 1;
		КонецЕсли;	
		
		ТабличныйДокумент.Вывести(ОбластьСтрока);
		
		Если НомерСтроки = КоличествоСтрок Тогда
			ТабличныйДокумент.Вывести(ОбластьИтого);				
		КонецЕсли;
		
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;	
	
	ДанныеПечати.КоличествоИтого = 0;
	ДанныеПечати.СуммаИтого = 0;
	
	Возврат КоличествоЛистов;
КонецФункции

#КонецОбласти

#КонецЕсли