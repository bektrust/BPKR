#Область ОбработчикиСобытийФормы

// Процедура - обработчик события ПриСозданииНаСервере.
// В процедуре осуществляется
// - инициализация реквизитов формы,
// - установка параметров функциональных опций формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Установка реквизитов формы.
	ДатаДокумента = Объект.Дата;
	
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	Если Объект.КурсПрихода = 0 Тогда 
		Объект.КурсПрихода = 1;
	КонецЕсли;				
	Если Объект.КурсРасхода = 0 Тогда 
		Объект.КурсРасхода = 1;
	КонецЕсли;
	
	СуммаКурсовойРазницыВПроводке = ПолучитьСуммуКурсовойразницыИзПроводки();
	
	УказыватьСтатьиДДСДляВнутреннихОборотов = Константы.УказыватьСтатьиДДСДляВнутреннихОборотов.Получить();
	
	БухгалтерскийУчетКлиентСервер.УстановитьКартинкуДляКомментария(Элементы.СтраницаДополнительно, Объект.Комментарий);
	
	// Установить видимость и доступность элементов формы
	УстановитьВидимостьДоступностьЭлементов();
	
	// РаботаСФормами
	РаботаСФормамиСервер.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец РаботаСФормами
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.ГруппаВажныеКоманды;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

// Процедура - обработчик события ПриЧтенииНаСервере.
//
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменений
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменений
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом 
	
КонецПроцедуры

// Процедура - обработчик события ПриОткрытии.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПоказатьКурсПрихода();
	ПоказатьКурсРасхода();	
	СформироватьНадписьКурсоваяРазница();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	СуммаКурсовойРазницыВПроводке = ПолучитьСуммуКурсовойразницыИзПроводки();
	
	// РаботаСФормами
	РаботаСФормамиСервер.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец РаботаСФормами
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом

	// СтандартныеПодсистемы.КонтрольВеденияУчета
	КонтрольВеденияУчета.ПослеЗаписиНаСервере(ТекущийОбъект);
	// Конец СтандартныеПодсистемы.КонтрольВеденияУчета
	
КонецПроцедуры

// Процедура - обработчик события ПослеЗаписи.
//
&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	СформироватьНадписьКурсоваяРазница();

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Процедура - обработчик события ПриИзменении поля ввода Дата.
// В процедуре определяется ситуация, когда при изменении своей даты документ 
// оказывается в другом периоде нумерации документов, и в этом случае
// присваивает документу новый уникальный номер.
// Переопределяет соответствующий параметр формы.
//
&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	// Обработка события изменения даты.
	ДатаПередИзменением = ДатаДокумента;
	ДатаДокумента = Объект.Дата;
	Если Объект.Дата <> ДатаПередИзменением Тогда
		СтруктураДанные = ПолучитьДанныеДатаПриИзменении(ДатаПередИзменением);
		Если СтруктураДанные.РазностьДат <> 0 Тогда
			Объект.Номер = "";
		КонецЕсли;
	КонецЕсли;
	
	Объект.КурсПрихода 	= ПолучитьКурсВалюты(Объект.ВалютаПрихода, Объект.Дата);
	Объект.КурсРасхода 	= ПолучитьКурсВалюты(Объект.ВалютаРасхода, Объект.Дата);	
	
	ПересчетРасчетногоКурса();
	ПересчетКурсаОбмена();
	
	//ПересчетСуммыПрихода();
	ПересчетСуммыПриходаПоКурсуОбмена();
	
	ПересчетКурсовойРазницы();
	ПоказатьКурсПрихода();
	ПоказатьКурсРасхода();
	ПересчетУчетнойСуммы();
	
	// Установить видимость и доступность элементов формы
	УстановитьВидимостьДоступностьЭлементов();
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Организация.
//
&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	// Обработка события изменения организации.
	Объект.Номер = "";
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Комментарий.
//
&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	ПодключитьОбработчикОжидания("УстановитьПиктограммуКомментария", 0.1, Истина);
КонецПроцедуры

// Процедура - обработчик события НачалоВыбора поля ввода Комментарий.
//
&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода ПриходРасчетныйСчет.
//
&НаКлиенте
Процедура ПриходРасчетныйСчетПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Объект.ПриходРасчетныйСчет) Тогда
		СтруктураРасчетныйСчет 	= РасчетныйСчетПриИзмененииНаСервере(Объект.ПриходРасчетныйСчет);
		Объект.СчетПрихода 		= СтруктураРасчетныйСчет.СчетУчета;
		Объект.ВалютаПрихода 	= СтруктураРасчетныйСчет.ВалютаДенежныхСредств;
	КонецЕсли;
		
	Объект.КурсПрихода 			= ПолучитьКурсВалюты(Объект.ВалютаПрихода, Объект.Дата);
	ПересчетРасчетногоКурса();
	ПересчетКурсаОбмена();
	ПересчетКурсовойРазницы();
	ПоказатьКурсПрихода();
	ПересчетУчетнойСуммы();
	//ПересчетСуммыПрихода();
	ПересчетСуммыПриходаПоКурсуОбмена();
	
	ПроверкаПринадлежностиСчетовБанку();
	
	// Установить видимость и доступность элементов формы
	УстановитьВидимостьДоступностьЭлементов();
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода РасходРасчетныйСчет.
//
&НаКлиенте
Процедура РасходРасчетныйСчетПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Объект.РасходРасчетныйСчет) Тогда
		СтруктураРасчетныйСчет 	= РасчетныйСчетПриИзмененииНаСервере(Объект.РасходРасчетныйСчет);
		Объект.СчетРасходов 		= СтруктураРасчетныйСчет.СчетУчета;
		Объект.ВалютаРасхода 	= СтруктураРасчетныйСчет.ВалютаДенежныхСредств;
	КонецЕсли;

	Объект.КурсРасхода = ПолучитьКурсВалюты(Объект.ВалютаРасхода, Объект.Дата);
	ПересчетКурсаОбмена();
	ПересчетКурсовойРазницы();
	ПоказатьКурсРасхода();
	ПересчетУчетнойСуммы();
	//ПересчетСуммыПрихода();
	ПересчетСуммыПриходаПоКурсуОбмена();
	
	ПроверкаПринадлежностиСчетовБанку();
	
	// Установить видимость и доступность элементов формы
	УстановитьВидимостьДоступностьЭлементов();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода СуммаПрихода.
//
&НаКлиенте
Процедура СуммаПриходаПриИзменении(Элемент)
	ПриИзмененииСуммы();
	ПересчетКурсовойРазницы();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода СуммаРасхода.
//
&НаКлиенте
Процедура СуммаРасходаПриИзменении(Элемент)
	
	//ПересчетСуммыПрихода();
	ПересчетСуммыПриходаПоКурсуОбмена();
	ПересчетУчетнойСуммы();
	ПересчетКурсовойРазницы();
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода КурсОбмена.
//
&НаКлиенте
Процедура КурсОбменаПриИзменении(Элемент)
	
	ПересчетСуммыПриходаПоКурсуОбмена();
	ПересчетКурсовойРазницы();
	ПересчетРасчетногоКурса();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Пересчитать(Команда)
	КурсОбменаПриИзменении(Неопределено)
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура устанавливает видимость и доступность элементов.
//
&НаСервере
Процедура УстановитьВидимостьДоступностьЭлементов()
	
	Элементы.ДекорацияВалютКурсПрихода.Видимость = НЕ (Объект.ВалютаПрихода = ВалютаРегламентированногоУчета ИЛИ Объект.ВалютаПрихода = Справочники.Валюты.ПустаяСсылка());
	Элементы.ДекорацияВалютКурсРасхода.Видимость = НЕ (Объект.ВалютаРасхода = ВалютаРегламентированногоУчета ИЛИ Объект.ВалютаРасхода = Справочники.Валюты.ПустаяСсылка());
	Элементы.ДекорацияЧислоКурсПрихода.Видимость = НЕ (Объект.ВалютаПрихода = ВалютаРегламентированногоУчета ИЛИ Объект.ВалютаПрихода = Справочники.Валюты.ПустаяСсылка());
	Элементы.ДекорацияЧислоКурсРасхода.Видимость = НЕ (Объект.ВалютаРасхода = ВалютаРегламентированногоУчета ИЛИ Объект.ВалютаРасхода = Справочники.Валюты.ПустаяСсылка());
	
	Если ЗначениеЗаполнено(Объект.ВалютаПрихода) И ЗначениеЗаполнено(Объект.ВалютаРасхода)
		И Объект.ВалютаПрихода <> ВалютаРегламентированногоУчета И Объект.ВалютаРасхода <> ВалютаРегламентированногоУчета	Тогда
		Элементы.СуммаУчетная.Видимость = Истина;
		Элементы.ДекорацияВалютаУчетная.Видимость = Истина;
	Иначе
		Элементы.СуммаУчетная.Видимость = Ложь;
		Элементы.ДекорацияВалютаУчетная.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.СтатьяДвиженияДенежныхСредствПриход.Видимость = УказыватьСтатьиДДСДляВнутреннихОборотов;
	Элементы.СтатьяДвиженияДенежныхСредствРасход.Видимость = УказыватьСтатьиДДСДляВнутреннихОборотов;
КонецПроцедуры 

// Получает набор данных с сервера для процедуры ДатаПриИзменении.
//
&НаСервере
Функция ПолучитьДанныеДатаПриИзменении(ДатаПередИзменением)
	РазностьДат = БухгалтерскийУчетСервер.ПроверитьНомерДокумента(Объект.Ссылка, Объект.Дата, ДатаПередИзменением);
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("РазностьДат",	РазностьДат);
	
	Возврат СтруктураДанные;
КонецФункции // ПолучитьДанныеДатаПриИзменении()

// Процедура - Установить пиктограмму комментария.
//
&НаКлиенте
Процедура УстановитьПиктограммуКомментария()
	БухгалтерскийУчетКлиентСервер.УстановитьКартинкуДляКомментария(Элементы.СтраницаДополнительно, Объект.Комментарий);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьНаименованиеВалюты(Валюта)

	Возврат Валюта.Наименование	

КонецФункции // ПолучитьКодСчетаУчета()

// См. РаботаСКурсамиВалют.ПолучитьКурсВалюты.
&НаСервереБезКонтекста
Функция ПолучитьКурсВалюты(Валюта, Дата)
	КурсСтруктура 	= РаботаСКурсамиВалют.ПолучитьКурсВалюты(Валюта, Дата);
	Возврат КурсСтруктура.Курс;		
КонецФункции // ()

&НаКлиенте
Процедура СформироватьНадписьКурсоваяРазница()
	
	//Если Объект.ПрямойКурс Тогда
	//	Текст1 = "" + Объект.ВалютаРасхода + "/" + Объект.ВалютаПрихода + "  ";				
	//Иначе
	//    Текст1 = "" + Объект.ВалютаПрихода + "/" + Объект.ВалютаРасхода + "  ";
	//КонецЕсли;
	
	Если Объект.ВалютаРасхода = ВалютаРегламентированногоУчета И НЕ Объект.ВалютаПрихода = ВалютаРегламентированногоУчета Тогда
		 Текст1 = "" + ВалютаРегламентированногоУчета + "/" + Объект.ВалютаПрихода + "  ";
		
	ИначеЕсли НЕ Объект.ВалютаРасхода = ВалютаРегламентированногоУчета И Объект.ВалютаПрихода = ВалютаРегламентированногоУчета Тогда			
		Текст1 = "" + ВалютаРегламентированногоУчета + "/" + Объект.ВалютаРасхода + "  ";
	
	ИначеЕсли НЕ Объект.ВалютаРасхода = ВалютаРегламентированногоУчета И НЕ Объект.ВалютаПрихода = ВалютаРегламентированногоУчета Тогда
		Если Объект.ПрямойКурс Тогда
			Текст1 = "" + Объект.ВалютаРасхода + "/" + Объект.ВалютаПрихода + "  ";				
		Иначе
		    Текст1 = "" + Объект.ВалютаПрихода + "/" + Объект.ВалютаРасхода + "  ";
		КонецЕсли;
	Иначе
		Объект.КурсОбмена = 1;
	КонецЕсли;	
	
	Элементы.ВалютыКР.Заголовок = Текст1;
		
	СуммаУчетнаяПрихода = Окр(Объект.СуммаПрихода * Объект.КурсПрихода, 2);
	СуммаУчетнаяРасхода = Окр(Объект.СуммаРасхода * Объект.КурсРасхода, 2);
	СчетПрихода = Объект.СчетПрихода;
	СчетРасходов = Объект.СчетРасходов;
		
	СуммаКурсовойРазницы = СуммаУчетнаяПрихода - СуммаУчетнаяРасхода;
	Если Объект.КурсОбмена = Объект.КроссКурсНБКР Тогда
		СуммаКурсовойРазницы = 0;
		Если НЕ Объект.СуммаКР = 0 Тогда
			Объект.СуммаКР = 0;
		КонецЕсли;
	ИначеЕсли НЕ СуммаКурсовойРазницы = Объект.СуммаКР Тогда
		Объект.СуммаКР 	= СуммаКурсовойРазницы;
	КонецЕсли;
	
	// Нет курсовой разницы
	Если СуммаКурсовойРазницы = 0 Тогда
		Текст2 = "";	
		
	// Расход валюты, убыток
	ИначеЕсли НЕ СчетУчетаВалютный(СчетПрихода) И СчетУчетаВалютный(СчетРасходов) И СуммаКурсовойРазницы < 0 Тогда
		Текст2 = "Убыток";
		
		// Расход валюты, доход		
	ИначеЕсли НЕ СчетУчетаВалютный(СчетПрихода) И СчетУчетаВалютный(СчетРасходов) И СуммаКурсовойРазницы > 0 Тогда
		Текст2 = "Доход";	
		
		// Приход валюты, убыток	
	ИначеЕсли СчетУчетаВалютный(СчетПрихода) И НЕ СчетУчетаВалютный(СчетРасходов) И СуммаКурсовойРазницы < 0 Тогда
		Текст2 = "Убыток";		
		
		// Приход валюты, доход	
	ИначеЕсли СчетУчетаВалютный(СчетПрихода) И НЕ СчетУчетаВалютный(СчетРасходов) И СуммаКурсовойРазницы > 0 Тогда
		Текст2 = "Доход";
		
		// Валюта - Валюта, убыток	
	ИначеЕсли СчетУчетаВалютный(СчетПрихода) И СчетУчетаВалютный(СчетРасходов) И СуммаКурсовойРазницы < 0 Тогда
		Текст2 = "Убыток";			
		
		// Валюта - Валюта, доход		
	ИначеЕсли СчетУчетаВалютный(СчетПрихода) И СчетУчетаВалютный(СчетРасходов) И СуммаКурсовойРазницы > 0 Тогда
		Текст2 = "Доход";			
		
	Иначе
		Текст2 = "";
	КонецЕсли;
	
	Если Текст2 = "" Тогда
		Текст3 = "";
		Элементы.КурсоваяРазница.Заголовок = "";
	Иначе
		Текст3 = СтрШаблон(НСтр("ru = '%1 - %2 = %3'"), СуммаУчетнаяПрихода, СуммаУчетнаяРасхода, СуммаКурсовойРазницы);
		
		СуммаКурсовойРазницы = ?(СуммаКурсовойРазницы < 0, СуммаКурсовойРазницы * -1, СуммаКурсовойРазницы);
		
		Если СуммаКурсовойРазницы = СуммаКурсовойРазницыВПроводке Тогда
			ТекстЗаголовока = СтрШаблон(НСтр("ru = 'Операционная КР: %1 %2'"), Текст2, Текст3);
		Иначе
			ТекстЗаголовока = СтрШаблон(НСтр("ru = 'Операционная КР: %1 %2 (КУРС ИЗМЕНЕН)'"), Текст2, Текст3);
		КонецЕсли;
		
		Элементы.КурсоваяРазница.Заголовок = ТекстЗаголовока;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция СчетУчетаВалютный(СчетУчета)	
	Возврат СчетУчета.Валютный;	
КонецФункции

&НаСервереБезКонтекста
Функция РасчетныйСчетПриИзмененииНаСервере(РасчетныйСчет)
	СтруктураРасчетныйСчет = Новый Структура;
	СтруктураРасчетныйСчет.Вставить("СчетУчета", 				РасчетныйСчет.СчетУчета);
	СтруктураРасчетныйСчет.Вставить("ВалютаДенежныхСредств", 	РасчетныйСчет.ВалютаДенежныхСредств);
	Возврат СтруктураРасчетныйСчет;
	
КонецФункции

&НаКлиенте
Процедура ПересчетКурсаОбмена()
	//Если Объект.КурсРасхода >= Объект.КурсПрихода Тогда
	//	Объект.ПрямойКурс 		= Ложь;	
	//	Объект.КурсОбмена 		= Окр(Объект.КурсРасхода / Объект.КурсПрихода, 4);
	//	Объект.КроссКурсНБКР 	= Объект.КурсОбмена;
	//Иначе	
	//	Объект.ПрямойКурс 		= Истина;	
	//	Объект.КурсОбмена 		= Окр(Объект.КурсПрихода / Объект.КурсРасхода, 4);
	//	Объект.КроссКурсНБКР 	= Объект.КурсОбмена;	
	//КонецЕсли;

	Если Объект.ВалютаРасхода = ВалютаРегламентированногоУчета И НЕ Объект.ВалютаПрихода = ВалютаРегламентированногоУчета Тогда
		Объект.КурсОбмена = Объект.КурсПрихода;	
		Если Объект.КурсРасхода >= Объект.КурсПрихода Тогда
			Объект.ПрямойКурс 	= Ложь;
		Иначе	
		    Объект.ПрямойКурс 	= Истина;
		КонецЕсли;
		
	ИначеЕсли НЕ Объект.ВалютаРасхода = ВалютаРегламентированногоУчета И Объект.ВалютаПрихода = ВалютаРегламентированногоУчета Тогда			
		Объект.КурсОбмена = Объект.КурсРасхода;
		Если Объект.КурсРасхода >= Объект.КурсПрихода Тогда
			Объект.ПрямойКурс 	= Истина;
		Иначе	
		    Объект.ПрямойКурс 	= Ложь;
		КонецЕсли;
	
	ИначеЕсли НЕ Объект.ВалютаРасхода = ВалютаРегламентированногоУчета И НЕ Объект.ВалютаПрихода = ВалютаРегламентированногоУчета Тогда
		Если Объект.КурсРасхода >= Объект.КурсПрихода Тогда
			Объект.КурсОбмена 	= Окр(Объект.КурсРасхода / Объект.КурсПрихода, 4);
			Объект.ПрямойКурс 	= Ложь;
		Иначе	
			Объект.КурсОбмена 	= Окр(Объект.КурсПрихода / Объект.КурсРасхода, 4);
			Объект.ПрямойКурс 	= Истина;
		КонецЕсли;
	
	Иначе
		Объект.КурсОбмена = 1;
	КонецЕсли;	
	
	//Если Объект.КурсРасхода >= Объект.КурсПрихода Тогда
	//	Объект.ПрямойКурс 	= Ложь;
	//Иначе	
	//    Объект.ПрямойКурс 	= Истина;
	//КонецЕсли;	
	
	Объект.КроссКурсНБКР 	= Объект.КурсОбмена;
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчетКурсовойРазницы()

	Объект.СуммаКР = Объект.СуммаПрихода * Объект.КурсПрихода - Объект.СуммаРасхода * Объект.КурсРасхода;
	СформироватьНадписьКурсоваяРазница();

КонецПроцедуры // ПересчетКурсовойРазницы()

&НаКлиенте
Процедура ПоказатьКурсПрихода()

	Если ЗначениеЗаполнено(Объект.ВалютаПрихода) И ЗначениеЗаполнено(Объект.КурсПрихода) Тогда
		Элементы.ДекорацияЧислоКурсПрихода.Заголовок = Строка(Объект.КурсПрихода);
		Элементы.ДекорацияВалютКурсПрихода.Заголовок = " сом/" + ПолучитьНаименованиеВалюты(Объект.ВалютаПрихода);
	КонецЕсли;

КонецПроцедуры // ПоказатьКурсПрихода()

&НаКлиенте
Процедура ПоказатьКурсРасхода()

	Если ЗначениеЗаполнено(Объект.ВалютаРасхода) И ЗначениеЗаполнено(Объект.КурсРасхода) Тогда
		Элементы.ДекорацияЧислоКурсРасхода.Заголовок = Строка(Объект.КурсРасхода);
		Элементы.ДекорацияВалютКурсРасхода.Заголовок = " сом/" + ПолучитьНаименованиеВалюты(Объект.ВалютаРасхода);
	КонецЕсли;

КонецПроцедуры // ПоказатьКурсПрихода()

&НаКлиенте
Процедура ПриИзмененииСуммы()
	Если Объект.ВалютаРасхода = ВалютаРегламентированногоУчета 
		И НЕ Объект.ВалютаПрихода = ВалютаРегламентированногоУчета 		
		И ЗначениеЗаполнено(Объект.СуммаПрихода) Тогда	
		
		Объект.КурсОбмена = Окр(Объект.СуммаРасхода / Объект.СуммаПрихода, 2);	
		Если Объект.КурсРасхода >= Объект.КурсПрихода Тогда
			Объект.ПрямойКурс 	= Истина;
		Иначе	
		    Объект.ПрямойКурс 	= Ложь;
		КонецЕсли;
		
	ИначеЕсли НЕ Объект.ВалютаРасхода = ВалютаРегламентированногоУчета 
		И Объект.ВалютаПрихода = ВалютаРегламентированногоУчета 
		И ЗначениеЗаполнено(Объект.СуммаРасхода) Тогда
		
		Объект.КурсОбмена = Окр(Объект.СуммаПрихода / Объект.СуммаРасхода, 2);
		Если Объект.КурсРасхода >= Объект.КурсПрихода Тогда
			Объект.ПрямойКурс 	= Ложь;
		Иначе	
		    Объект.ПрямойКурс 	= Истина;
		КонецЕсли;
	
	ИначеЕсли НЕ Объект.ВалютаРасхода = ВалютаРегламентированногоУчета И НЕ Объект.ВалютаПрихода = ВалютаРегламентированногоУчета Тогда
		Если Объект.СуммаПрихода > Объект.СуммаРасхода Тогда
			Если ЗначениеЗаполнено(Объект.СуммаРасхода) Тогда
				Объект.КурсОбмена = Окр(Объект.СуммаПрихода / Объект.СуммаРасхода, 2);
			КонецЕсли;		
		Иначе
			Если ЗначениеЗаполнено(Объект.СуммаПрихода) Тогда
				Объект.КурсОбмена = Окр(Объект.СуммаРасхода / Объект.СуммаПрихода, 2);
			КонецЕсли;		
		КонецЕсли;
	
	Иначе
		Объект.КурсОбмена = 1;
	КонецЕсли;	
	

	
	//Если Объект.СуммаПрихода > Объект.СуммаРасхода Тогда
	//	Если ЗначениеЗаполнено(Объект.СуммаРасхода) Тогда
	//		Объект.КурсОбмена = Окр(Объект.СуммаПрихода / Объект.СуммаРасхода, 2);
	//	КонецЕсли;		
	//Иначе
	//	Если ЗначениеЗаполнено(Объект.СуммаПрихода) Тогда
	//		Объект.КурсОбмена = Окр(Объект.СуммаРасхода / Объект.СуммаПрихода, 2);
	//	КонецЕсли;		
	//КонецЕсли;
	ПересчетРасчетногоКурса();
КонецПроцедуры // ПриИзмененииСуммы()

&НаКлиенте
Процедура ПересчетРасчетногоКурса()
	Если Объект.СуммаПрихода = 0 Тогда
		Объект.КурсРасчетный = 0;	
	Иначе
		Объект.КурсРасчетный = Окр(Объект.СуммаРасхода * Объект.КурсРасхода / Объект.СуммаПрихода, 4);	
	КонецЕсли;		
	ПоказатьКурсПрихода();
	
КонецПроцедуры // ПересчетРасчетногоКурса()

&НаКлиенте
Процедура ПересчетУчетнойСуммы()

	Если ЗначениеЗаполнено(Объект.ВалютаПрихода) И ЗначениеЗаполнено(Объект.ВалютаРасхода)
		И Объект.ВалютаПрихода <> ВалютаРегламентированногоУчета И Объект.ВалютаРасхода <> ВалютаРегламентированногоУчета	Тогда
		Объект.СуммаУчетная = Объект.СуммаРасхода * Объект.КурсРасхода;	
	КонецЕсли;	

КонецПроцедуры

//&НаКлиенте
//Процедура ПересчетСуммыПрихода()
//	Если Объект.СуммаРасхода > 0 Тогда
//		Если Объект.ВалютаРасхода = ВалютаРегламентированногоУчета И НЕ Объект.ВалютаПрихода = ВалютаРегламентированногоУчета Тогда
//			Объект.СуммаПрихода = Объект.СуммаРасхода / Объект.КурсПрихода;	
//		ИначеЕсли НЕ Объект.ВалютаРасхода = ВалютаРегламентированногоУчета И Объект.ВалютаПрихода = ВалютаРегламентированногоУчета Тогда			
//			Объект.СуммаПрихода = Объект.СуммаРасхода * Объект.КурсРасхода;
//		ИначеЕсли НЕ Объект.ВалютаРасхода = ВалютаРегламентированногоУчета И НЕ Объект.ВалютаПрихода = ВалютаРегламентированногоУчета Тогда
//			Если Объект.ПрямойКурс Тогда
//				Объект.СуммаПрихода = Объект.СуммаРасхода * Объект.КурсОбмена;				
//			Иначе
//				Объект.СуммаПрихода = Объект.СуммаРасхода / ?(Объект.КурсОбмена = 0, 1, Объект.КурсОбмена);
//			КонецЕсли;
//			
//		ИначеЕсли Объект.ВалютаРасхода = ВалютаРегламентированногоУчета И Объект.ВалютаПрихода = ВалютаРегламентированногоУчета Тогда
//			Объект.СуммаПрихода = Объект.СуммаРасхода;		
//		КонецЕсли;
//		
//	Иначе
//		Объект.СуммаПрихода = 0;
//		ПересчетКурсаОбмена();
//		ПересчетКурсовойРазницы();
//	КонецЕсли;
//	
//	//Если Объект.СуммаРасхода > 0 Тогда
//	//	Если Объект.ВалютаРасхода = ВалютаРегламентированногоУчета И НЕ Объект.ВалютаПрихода = ВалютаРегламентированногоУчета Тогда
//	//		Объект.СуммаПрихода = Объект.СуммаРасхода / Объект.КурсПрихода;	
//	//	ИначеЕсли НЕ Объект.ВалютаРасхода = ВалютаРегламентированногоУчета И Объект.ВалютаПрихода = ВалютаРегламентированногоУчета Тогда			
//	//		Объект.СуммаПрихода = Объект.СуммаРасхода * Объект.КурсРасхода;
//	//	ИначеЕсли НЕ Объект.ВалютаРасхода = ВалютаРегламентированногоУчета И НЕ Объект.ВалютаПрихода = ВалютаРегламентированногоУчета Тогда
//	//		Если Объект.ПрямойКурс Тогда
//	//			Объект.СуммаПрихода = Объект.СуммаРасхода / ?(Объект.КурсОбмена = 0, 1, Объект.КурсОбмена);
//	//		Иначе
//	//			Объект.СуммаПрихода = Объект.СуммаРасхода * Объект.КурсОбмена;
//	//		КонецЕсли;
//	//		
//	//	ИначеЕсли Объект.ВалютаРасхода = ВалютаРегламентированногоУчета И Объект.ВалютаПрихода = ВалютаРегламентированногоУчета Тогда
//	//		Объект.СуммаПрихода = Объект.СуммаРасхода;		
//	//	КонецЕсли;
//	//	
//	//Иначе
//	//	Объект.СуммаПрихода = 0;
//	//	ПересчетКурсаОбмена();
//	//	ПересчетКурсовойРазницы();
//	//КонецЕсли;		

//КонецПроцедуры

&НаКлиенте
Процедура ПересчетСуммыПриходаПоКурсуОбмена()
	
	Если Объект.СуммаРасхода > 0 Тогда
		Если Объект.ВалютаРасхода = ВалютаРегламентированногоУчета И НЕ Объект.ВалютаПрихода = ВалютаРегламентированногоУчета Тогда
//			Если Объект.ПрямойКурс Тогда
				Объект.СуммаПрихода = Объект.СуммаРасхода / ?(Объект.КурсОбмена = 0, 1, Объект.КурсОбмена);
			//Иначе
			//    Объект.СуммаПрихода = Объект.СуммаРасхода * Объект.КурсОбмена;
			//КонецЕсли;
				
		ИначеЕсли НЕ Объект.ВалютаРасхода = ВалютаРегламентированногоУчета И Объект.ВалютаПрихода = ВалютаРегламентированногоУчета Тогда			
			//Если Объект.ПрямойКурс Тогда
				Объект.СуммаПрихода = Объект.СуммаРасхода * Объект.КурсОбмена;
			//Иначе
			//    Объект.СуммаПрихода = Объект.СуммаРасхода / ?(Объект.КурсОбмена = 0, 1, Объект.КурсОбмена);
			//КонецЕсли;
			
		ИначеЕсли НЕ Объект.ВалютаРасхода = ВалютаРегламентированногоУчета И НЕ Объект.ВалютаПрихода = ВалютаРегламентированногоУчета Тогда
			Если Объект.ПрямойКурс Тогда
				Объект.СуммаПрихода = Объект.СуммаРасхода / ?(Объект.КурсОбмена = 0, 1, Объект.КурсОбмена);				
			Иначе
				Объект.СуммаПрихода = Объект.СуммаРасхода * Объект.КурсОбмена;
			КонецЕсли;
			
		ИначеЕсли Объект.ВалютаРасхода = ВалютаРегламентированногоУчета И Объект.ВалютаПрихода = ВалютаРегламентированногоУчета Тогда
			Объект.СуммаПрихода = Объект.СуммаРасхода;		
		КонецЕсли;
		
	Иначе
		Объект.СуммаПрихода = 0;
		ПересчетКурсаОбмена();
		ПересчетКурсовойРазницы();
	КонецЕсли;	

КонецПроцедуры

&НаСервере
Процедура ПроверкаПринадлежностиСчетовБанку()

	Если ЗначениеЗаполнено(Объект.ПриходРасчетныйСчет) И ЗначениеЗаполнено(Объект.РасходРасчетныйСчет) Тогда
		Если Объект.ПриходРасчетныйСчет.Банк <> Объект.РасходРасчетныйСчет.Банк	Тогда
			ТекстСообщения = НСтр("ru = 'У выбранных счетов прихода и расхода разные банки.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,);	
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСуммуКурсовойразницыИзПроводки()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ХозрасчетныйОбороты.СуммаОборот КАК Сумма
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(
		|			&Период,
		|			&Период,
		|			Авто,
		|			Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДоходыОтОперационныхКурсовыхРазниц)
		|				ИЛИ Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.УбыткиОтОперационныхКурсовыхРазниц),
		|			,
		|			,
		|			,
		|			) КАК ХозрасчетныйОбороты
		|ГДЕ
		|	ХозрасчетныйОбороты.Регистратор = &Регистратор";	
	Запрос.УстановитьПараметр("Период", Объект.Дата);
	Запрос.УстановитьПараметр("Регистратор", Объект.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда	
		Возврат Выборка.Сумма;
	Иначе	
		Возврат 0;	
	КонецЕсли;	
КонецФункции

#КонецОбласти

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ПодключаемыеКоманды

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
     ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
     ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры
 
&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
     ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
     ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти
