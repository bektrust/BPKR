#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура - Распределить акциз
// Сумма акциза распределяется между 3-мя табличными частями по выбранному способу распределения.
//
Процедура РаспределитьАкциз() Экспорт
	// Массивы коэффициентов распределения
	Если СпособРаспределенияАкциза = "Сумма" Тогда
		// Массивы
		КоэффициентыРаспределенияТовары = Товары.ВыгрузитьКолонку("Сумма");
		КоэффициентыРаспределенияУслуги = Услуги.ВыгрузитьКолонку("Сумма");
		// Итоги
		ИтогТовары = Товары.Итог("Сумма");
		ИтогУслуги = Услуги.Итог("Сумма");
	ИначеЕсли СпособРаспределенияАкциза = "Количество" Тогда
		// Массивы
		КоэффициентыРаспределенияТовары = Товары.ВыгрузитьКолонку("Количество");
		КоэффициентыРаспределенияУслуги = Услуги.ВыгрузитьКолонку("Количество");
		// Итоги
		ИтогТовары = Товары.Итог("Количество");
		ИтогУслуги = Услуги.Итог("Количество");
	ИначеЕсли СпособРаспределенияАкциза = "Вес" Тогда
		// Массивы
		КоэффициентыРаспределенияТовары = Товары.ВыгрузитьКолонку("Вес");
		КоэффициентыРаспределенияУслуги = Услуги.ВыгрузитьКолонку("Вес");
		// Итоги
		ИтогТовары = Товары.Итог("Вес");
		ИтогУслуги = Услуги.Итог("Вес");
	Иначе // Обнуление
		Для Каждого СтрокаТабличнойЧасти Из Товары Цикл
			СтрокаТабличнойЧасти.Вес = 0;
			СтрокаТабличнойЧасти.СуммаАкциза = 0;
		КонецЦикла;
		Для Каждого СтрокаТабличнойЧасти Из Услуги Цикл
			СтрокаТабличнойЧасти.Вес = 0;
			СтрокаТабличнойЧасти.СуммаАкциза = 0;
		КонецЦикла;
		Возврат;
	КонецЕсли;		
	
	// Распределения общей суммы акциза
	МассивОбщихСумм = Новый Массив;
	МассивОбщихСумм.Добавить(ИтогТовары);
	МассивОбщихСумм.Добавить(ИтогУслуги);

	МассивРаспределениеМеждуТабличнымиЧастями = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(СуммаАкциза, МассивОбщихСумм);
	Если МассивРаспределениеМеждуТабличнымиЧастями = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СуммаАкцизаТовары = МассивРаспределениеМеждуТабличнымиЧастями[0];
	СуммаАкцизаУслуги = МассивРаспределениеМеждуТабличнымиЧастями[1];
	
	// Распределения суммы акциза табличной части
	// Товары
	Если СуммаАкцизаТовары > 0 Тогда  
		РезультатРаспределения = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(СуммаАкцизаТовары, КоэффициентыРаспределенияТовары);
		Если НЕ РезультатРаспределения = Неопределено Тогда
			ИндексСтроки = 0;
			Для Каждого ЭлементМассива Из РезультатРаспределения Цикл 
				Товары[ИндексСтроки].СуммаАкциза = ЭлементМассива;
				ИндексСтроки = ИндексСтроки + 1;
			КонецЦикла;	
		КонецЕсли;		
	КонецЕсли;
	// Услуги
	Если СуммаАкцизаУслуги > 0 Тогда  
		РезультатРаспределения = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(СуммаАкцизаУслуги, КоэффициентыРаспределенияУслуги);
		Если НЕ РезультатРаспределения = Неопределено Тогда
			ИндексСтроки = 0;
			Для Каждого ЭлементМассива Из РезультатРаспределения Цикл 
				Услуги[ИндексСтроки].СуммаАкциза = ЭлементМассива;
				ИндексСтроки = ИндексСтроки + 1;
			КонецЦикла;	
		КонецЕсли;		
	КонецЕсли;
	
КонецПроцедуры 

#КонецОбласти

#Область ПроцедурыЗаполненияДокумента

// Процедура заполнения документа на основании.
//
// Параметры:
//	ДанныеЗаполнения - ДокументСсылка.Доверенность - Данные заполнения документа.
//	
Процедура ЗаполнитьПоДоверенности(ДанныеЗаполнения) Экспорт
	ДокументОснование = ДанныеЗаполнения;
	
	Организация = ДанныеЗаполнения.Организация;
	Контрагент = ДанныеЗаполнения.Контрагент;
	ДоговорКонтрагента = ДанныеЗаполнения.ДоговорКонтрагента;
	ВалютаДокумента = ДоговорКонтрагента.ВалютаРасчетов;
	
	ВалютаРасчетовКурсКратность = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаДокумента, ТекущаяДатаСеанса());
	Курс      = ?(ВалютаРасчетовКурсКратность.Курс = 0, 1, ВалютаРасчетовКурсКратность.Курс);
	Кратность = ?(ВалютаРасчетовКурсКратность.Кратность = 0, 1, ВалютаРасчетовКурсКратность.Кратность);

	СуммаВключаетНалоги = ДоговорКонтрагента.СуммаВключаетНалоги;
	
	СчетаУчета = БухгалтерскийУчетСервер.ПолучитьСчетаРасчетовСКонтрагентом(Организация, Контрагент, ДоговорКонтрагента);
	СчетРасчетов = СчетаУчета.СчетРасчетовПоставщика;
		
	Для Каждого СтрокаТабличнойЧасти Из ДанныеЗаполнения.Товары Цикл
		НоваяСтрока = Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТабличнойЧасти);
		
		СчетаУчетаНоменклатуры = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСчетаУчетаНоменклатуры(Организация, НоваяСтрока.Номенклатура);
		НоваяСтрока.СчетУчета = СчетаУчетаНоменклатуры.СчетУчета;
	КонецЦикла;

	Для Каждого СтрокаТабличнойЧасти Из ДанныеЗаполнения.ОсновныеСредства Цикл
		НоваяСтрока = ОС.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТабличнойЧасти);
	КонецЦикла;

	ВидОперации = ПолучитьВидОперации();
	
КонецПроцедуры

// Процедура заполнения документа на основании.
//
// Параметры:
//	ДанныеЗаполнения - Структура - Данные заполнения документа.
//	
Процедура ЗаполнитьПоЭлектронныйСчетФактураПолученныйЗагрузка(СтрокаТабличнойЧастиДокументы, НайденныеСтроки) Экспорт
	ВидОперации = ?(СтрокаТабличнойЧастиДокументы.ВидОперации = Перечисления.ВидыОперацийЭСФ.ПоставкаТоваров, 
		Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Товары, 
		Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Услуги);
		
	Если СтрокаТабличнойЧастиДокументы.ФормаОплаты = Перечисления.ФормыОплаты.Безналичная Тогда 
		БезналичныйРасчет = Истина;	
	КонецЕсли;	
		
	Контрагент = СтрокаТабличнойЧастиДокументы.Контрагент;
	ДоговорКонтрагента = СтрокаТабличнойЧастиДокументы.ДоговорКонтрагента;
	ВалютаДокумента = ДоговорКонтрагента.ВалютаРасчетов;
	
	Курс      = СтрокаТабличнойЧастиДокументы.Курс;
	Кратность = 1;

	ЗначениеСтавкиНДС = СтрокаТабличнойЧастиДокументы.ЗначениеСтавкиНДС;
	
	СуммаВключаетНалоги = ДоговорКонтрагента.СуммаВключаетНалоги;
	
	СчетаУчета = БухгалтерскийУчетСервер.ПолучитьСчетаРасчетовСКонтрагентом(Организация, Контрагент, ДоговорКонтрагента);
	СчетРасчетов = СчетаУчета.СчетРасчетовПоставщика;
		
	ПараметрыРасчета = ПодготовитьПараметрыРасчета();
	
	Товары.Очистить();
	Услуги.Очистить();
	
	Если СтрокаТабличнойЧастиДокументы.ВидОперации = Перечисления.ВидыОперацийЭСФ.ПоставкаТоваров Тогда 
		Для Каждого СтрокаТабличнойЧасти Из НайденныеСтроки Цикл
			НоваяСтрока = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТабличнойЧасти);
			
			ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуИБазуНДССтрокиТабличнойЧастиПоступление(НоваяСтрока, ПараметрыРасчета, 
				ЗначениеЗаполнено(НоваяСтрока.Сумма));
			
			СчетаУчетаНоменклатуры = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСчетаУчетаНоменклатуры(Организация, НоваяСтрока.Номенклатура);
			НоваяСтрока.СчетУчета = СчетаУчетаНоменклатуры.СчетУчета;
		КонецЦикла;
	Иначе 	
		Для Каждого СтрокаТабличнойЧасти Из НайденныеСтроки Цикл
			НоваяСтрока = Услуги.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТабличнойЧасти);
			
			ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуИБазуНДССтрокиТабличнойЧастиПоступление(НоваяСтрока, ПараметрыРасчета,
				ЗначениеЗаполнено(НоваяСтрока.Сумма));
			
			СчетаУчетаНоменклатуры = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСчетаУчетаНоменклатуры(Организация, НоваяСтрока.Номенклатура);
			НоваяСтрока.СчетРасходов = СчетаУчетаНоменклатуры.СчетРасходов;
		КонецЦикла;
	КонецЕсли;
	
	СуммаДокумента = Товары.Итог("Всего") + Услуги.Итог("Всего") + ОС.Итог("Всего");

КонецПроцедуры

// Процедура заполнения документа на основании.
//
// Параметры:
//	ДанныеЗаполнения - ДокументСсылка.Доверенность - Данные заполнения документа.
//	
Процедура ЗаполнитьПоАвансовомуОтчету(ДанныеЗаполнения) Экспорт
	
	Организация = ДанныеЗаполнения.Организация;
	
	Если ДанныеЗаполнения.ОплатаПоставщикам.Количество() = 1 Тогда
		Контрагент = ДанныеЗаполнения.ОплатаПоставщикам[0].Контрагент;
		ДоговорКонтрагента = ДанныеЗаполнения.ОплатаПоставщикам[0].ДоговорКонтрагента;	
	КонецЕсли;	
	
КонецПроцедуры

// Процедура заполнения документа на основании.
//
// Параметры:
//	ДанныеЗаполнения - ДокументСсылка.СчетНаОплатуПоставщика - Данные заполнения документа.
//	
Процедура ЗаполнитьПоСчетуНаОплатуПоставщика(ДанныеЗаполнения) Экспорт
	ДокументОснование = ДанныеЗаполнения;
	
	Организация = ДанныеЗаполнения.Организация;
	Склад		= ДанныеЗаполнения.Склад;
	БезналичныйРасчет	= ДанныеЗаполнения.БезналичныйРасчет;
	// Сведения о контрагенте
	Контрагент  		= ДанныеЗаполнения.Контрагент;
	ДоговорКонтрагента 	= ДанныеЗаполнения.ДоговорКонтрагента;
	ВалютаДокумента 	= ДоговорКонтрагента.ВалютаРасчетов;
	
	ВалютаРасчетовКурсКратность = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаДокумента, ТекущаяДатаСеанса());
	Курс      = ?(ВалютаРасчетовКурсКратность.Курс = 0, 1, ВалютаРасчетовКурсКратность.Курс);
	Кратность = ?(ВалютаРасчетовКурсКратность.Кратность = 0, 1, ВалютаРасчетовКурсКратность.Кратность);

	СуммаВключаетНалоги = ДоговорКонтрагента.СуммаВключаетНалоги;
		
	Для Каждого СтрокаТабличнойЧасти Из ДанныеЗаполнения.Товары Цикл
		НоваяСтрока = Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТабличнойЧасти);
		
		СчетаУчетаНоменклатуры = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСчетаУчетаНоменклатуры(Организация, НоваяСтрока.Номенклатура);
		НоваяСтрока.СчетУчета = СчетаУчетаНоменклатуры.СчетУчета;
	КонецЦикла;

	Для Каждого СтрокаТабличнойЧасти Из ДанныеЗаполнения.Услуги Цикл
		НоваяСтрока = Услуги.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТабличнойЧасти);
	КонецЦикла;
	
	ВидОперации = ПолучитьВидОперации();

КонецПроцедуры

Функция ПодготовитьПараметрыРасчета(ПараметрыДляУслуг = Ложь) Экспорт 

	ПризнакСтраныЕАЭС 			= Контрагент.ПризнакСтраны = Перечисления.ПризнакиСтраны.ЕАЭС;
	ПризнакСтраныИмпортЭкспорт 	= Контрагент.ПризнакСтраны = Перечисления.ПризнакиСтраны.ИмпортЭкспорт;
	
	ПараметрыРасчета = ОбработкаТабличныхЧастейКлиентСервер.ШаблонПараметровРасчета();
	ПараметрыРасчета.ПризнакСтраныЕАЭС = ПризнакСтраныЕАЭС;
	ПараметрыРасчета.ЕстьАкциз = Истина;
	ПараметрыРасчета.СуммаВключаетНалоги = СуммаВключаетНалоги;
	ПараметрыРасчета.ЗначениеСтавкиНДС = ЗначениеСтавкиНДС;
	ПараметрыРасчета.БезналичныйРасчет = БезналичныйРасчет;
	
	Если Контрагент.ПризнакСтраны = Перечисления.ПризнакиСтраны.КР Тогда
		ПараметрыРасчета.КурсДокумента = 1;
		ПараметрыРасчета.КратностьДокумента = 1;
	Иначе		
		ПараметрыРасчета.КурсДокумента = Курс;
		ПараметрыРасчета.КратностьДокумента = Кратность;
	КонецЕсли;	
	
	Если ПараметрыДляУслуг Тогда
		ПараметрыРасчета.РассчитатьБазуНДС = ПризнакСтраныЕАЭС ИЛИ ПризнакСтраныИмпортЭкспорт;
		ПараметрыРасчета.РассчитатьОтБазыНДС = ПризнакСтраныЕАЭС ИЛИ ПризнакСтраныИмпортЭкспорт;
	Иначе
		ПараметрыРасчета.РассчитатьБазуНДС = ПризнакСтраныЕАЭС;	
		ПараметрыРасчета.РассчитатьОтБазыНДС = ПризнакСтраныЕАЭС;
	КонецЕсли;	
			
	ПараметрыРасчета.ПризнакСтраныИмпортЭкспорт = ПризнакСтраныИмпортЭкспорт;
	ПараметрыРасчета.Точность = ТочностьЦены;

	Возврат ПараметрыРасчета;
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

// Процедура - обработчик события ОбработкаЗаполнения объекта.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	СтратегияЗаполнения = Новый Соответствие;
	СтратегияЗаполнения[Тип("ДокументСсылка.Доверенность")] = "ЗаполнитьПоДоверенности";
	СтратегияЗаполнения[Тип("ДокументСсылка.ЭлектронныйСчетФактураПолученный")] = "ЗаполнитьПоЭлектронныйСчетФактураПолученный";
	СтратегияЗаполнения[Тип("ДокументСсылка.АвансовыйОтчет")] = "ЗаполнитьПоАвансовомуОтчету";
	СтратегияЗаполнения[Тип("ДокументСсылка.СчетНаОплатуПоставщика")] = "ЗаполнитьПоСчетуНаОплатуПоставщика";

	ЗаполнениеОбъектовБП.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения, СтратегияЗаполнения);
КонецПроцедуры

// Процедура - обработчик события ОбработкаПроверкиЗаполнения объекта.
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ДанныеУчетнойПолитики = БухгалтерскийУчетСервер.ПолучитьДанныеУчетнойПолитикиОрганизаций(Дата, Организация);
	
	МассивНепроверяемыхРеквизитов = Новый Массив;

	Если Товары.Количество() = 0
		И Услуги.Количество() = 0
		И ОС.Количество() = 0 Тогда	
		
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Не заполнен ни один список.'"),,,,Отказ)		
	КонецЕсли;
	
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.РасходыБудущихПериодов
		И НЕ ВалютаРегламентированногоУчета = ДоговорКонтрагента.ВалютаРасчетов Тогда	
		
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'При создании расходов будущих периодов необходимо указать договор в валюте регламентированного учета.'")
			,,,,Отказ)		
	КонецЕсли;
	
	Если НЕ (СуммаАкциза = Товары.Итог("СуммаАкциза") 
		+ Услуги.Итог("СуммаАкциза")) И ОС.Количество() = 0 Тогда
		
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Не верно распределена сумма акциза.'"),,,,Отказ)		
	КонецЕсли;	
	
	Если Товары.Количество() = 0 Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Склад");
	КонецЕсли;
	
	Если Курс = 0 Или Кратность = 0 Тогда 
		ТекстСообщения = НСтр("ru = 'Не заполнен курс валюты ""%1"". Откройте список валют (Банк и касса - Валюты) и проверьте,
			|что у валюты ""%1"" установлен курс на дату %2 или ранее.
			|Перевыберите договор и заново проведите документ.'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, ВалютаДокумента, Формат(Дата, "ДЛФ=D"));	
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ);
	КонецЕсли;
	
	// Контроль заполнения Контрагента ГНС.
	Если Контрагент.ПризнакСтраны = Перечисления.ПризнакиСтраны.ЕАЭС Тогда 
		КонтрагентГНС = Организация.КонтрагентГНС;
		Если НЕ ЗначениеЗаполнено(КонтрагентГНС) Тогда 
			ТекстСообщения = НСтр("ru = 'Не заполнен Контрагент ГНС. Откройте организацию и проверьте, что поле Контрагент ГНС заполнено.
				|Заново проведите документ.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "Объект.Организация",, Отказ);
		Иначе 
			СписокВидовДоговора = Новый СписокЗначений;
			СписокВидовДоговора.Добавить(Перечисления.ВидыДоговоровКонтрагентов.Прочее);
			ДоговорКонтрагентаГНС = Справочники.ДоговорыКонтрагентов.ПолучитьДоговорПоУмолчаниюПоОрганизацииВидуДоговора(КонтрагентГНС, Организация, СписокВидовДоговора);
			
			Если НЕ ЗначениеЗаполнено(ДоговорКонтрагентаГНС) Тогда 
				ТекстСообщения = НСтр("ru = 'Не установлен основной договор у Контрагента ГНС.
					|Откройте организацию и перейдите к указанному Контрагенту ГНС. На вкладке Договоры контрагента установите основной договор.
					|Вид договора должен быть Прочее, валюта договора KGS. Заново проведите документ.'");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "Объект.Организация",, Отказ);
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;	
	
	ЭтоПокупка = ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Покупка
		Или ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Товары
		Или ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Услуги
		Или ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ОсновныеСредства;
	
	Если ЗначениеЗаполнено(СерияБланкаСФ) И НЕ ЗначениеЗаполнено(НомерБланкаСФ) 
		И Контрагент.ПризнакСтраны = Перечисления.ПризнакиСтраны.КР
		И ЭтоПокупка 
		И ДанныеУчетнойПолитики.ПлательщикНДС Тогда
		ТекстСообщения = НСтр("ru = 'Необходимо заполнить поле ""Номер бланка СФ"" или очистить поле ""Серия бланка СФ"".'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "Объект.НомерБланкаСФ",, Отказ);	
	КонецЕсли;	
	
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ЗакупочныйАкт
		И НЕ ЗначениеЗаполнено(НомерАкта) Тогда
		
		ТекстСообщения = НСтр("ru = 'В поступлении с видом операции ""Закупочный акт"" необходимо заполнить поле ""Номер акта"".'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "Объект.НомерАкта",, Отказ);
	КонецЕсли;	
	
	//// Проверка договора в валюте регламентированного учета при признаке страны контрагента "КР".
	//ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	//
	//Если Контрагент.ПризнакСтраны = Перечисления.ПризнакиСтраны.КР 
	//	И ДоговорКонтрагента.ВалютаРасчетов <> ВалютаРегламентированногоУчета Тогда
	//	
	//	ТекстСообщения = СтрШаблон(НСтр("ru = 'Для контрагента с признаком страны ""КР"" необходимо выбрать договор с валютой %1.'"),
	//						ВалютаРегламентированногоУчета);
	//	ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "Объект.ДоговорКонтрагента",, Отказ);
	//КонецЕсли;	
	
	// Проверка признака страны контрагента и вида операции.
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ЗакупочныйАкт
		И Контрагент.ПризнакСтраны <> Перечисления.ПризнакиСтраны.КР Тогда
		
		ТекстСообщения = НСтр("ru = 'В видом операции ""Закупочный акт"" контрагент должен быть с признаком страны ""КР"".'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "Объект.Контрагент",, Отказ);
	КонецЕсли;	
	
	Если ЭтоПоступлениеБезНДС Тогда 
		ПроверяемыеРеквизиты.Добавить("НомерБланкаСФ");	
	КонецЕсли;	
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);

	// Выполнение предварительного контроля.
	ВыполнитьПредварительныйКонтроль(Отказ);	
	
КонецПроцедуры

// Процедура - обработчик события ПередЗаписью объекта.
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СуммаДокумента = Товары.Итог("Всего") + Услуги.Итог("Всего") + ОС.Итог("Всего");
	
	// В случае если очистили дату, но не очистили серию бланка СФ.
	// Дата нужна для записи в РС "РеестрПриобретенныхМатериальныхРесурсов".
	Если ЗначениеЗаполнено(СерияБланкаСФ)
		И НЕ ЗначениеЗаполнено(ДатаСф) Тогда
		ДатаСф = Дата;				
	КонецЕсли;
	
	Если Товары.Количество() = 0 Тогда 
		Склад = Справочники.Склады.ПустаяСсылка();
	КонецЕсли;
	
	// Переопределение вида операции Покупка (устарел).
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Покупка Тогда 
		ВидОперации = ПолучитьВидОперации();
	КонецЕсли;	
КонецПроцедуры

// Процедура - обработчик события ОбработкаПроведения объекта.
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ИнициализироватьДанные(Отказ, РежимПроведения);
	
	ОтразитьДвижения(Отказ, РежимПроведения);
	
	// Запись наборов записей.
	БухгалтерскийУчетСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
	
КонецПроцедуры // ОбработкаПроведения()

// Процедура - обработчик события ОбработкаУдаленияПроведения объекта.
//
Процедура ОбработкаУдаленияПроведения(Отказ)
	// Инициализация дополнительных свойств для удаления проведения документа
	БухгалтерскийУчетСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	БухгалтерскийУчетСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Запись наборов записей
	БухгалтерскийУчетСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ПриКопировании(ОбъектКопирования)
	СерияБланкаСФ = "";
	НомерБланкаСФ = "";
	НомерАкта = "";
	ДатаСФ = '00010101';
	
	СтруктураДанные = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ДоговорКонтрагента.ВалютаРасчетов, Дата);
	Курс      = ?(СтруктураДанные.Курс = 0, 1, СтруктураДанные.Курс);
	Кратность = ?(СтруктураДанные.Кратность = 0, 1, СтруктураДанные.Кратность);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииОбработкаПроведения

// Процедура инициализирует данные документа
// и подготавливает таблицы для движения в регистры
//
Процедура ИнициализироватьДанные(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа.
	БухгалтерскийУчетСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);

	// Инициализация данных документа.
	Документы.ПоступлениеТоваровУслуг.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей.
	БухгалтерскийУчетСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
КонецПроцедуры
	
// Процедура заполняет регистры данными
//
Процедура ОтразитьДвижения(Отказ, РежимПроведения)

	// Отражение в разделах учета.
	УчетТоваров.СформироватьДвиженияПоступлениеТоваров(ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаТовары,
		ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаРеквизиты, Движения, Отказ);
	УчетМБП.СформироватьДвиженияПоступлениеМБП(ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаМБП,
		ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаРеквизиты, Движения, Отказ);
	БухгалтерскийУчетСервер.ОтразитьХозрасчетный(ДополнительныеСвойства, Движения, Отказ);
	// ОС
	БухгалтерскийУчетСервер.ОтразитьПараметрыУчетаОС(ДополнительныеСвойства, Движения, Отказ);
	БухгалтерскийУчетСервер.ОтразитьСостоянияОС(ДополнительныеСвойства, Движения, Отказ);
	БухгалтерскийУчетСервер.ОтразитьСобытияОС(ДополнительныеСвойства, Движения, Отказ);	
	
	// Прочее
	БухгалтерскийУчетСервер.ОтразитьПоступлениеТоваров(ДополнительныеСвойства, Движения, Отказ);
	БухгалтерскийУчетСервер.ОтразитьРеестрВвезенных(ДополнительныеСвойства, Движения, Отказ);
	БухгалтерскийУчетСервер.ОтразитьРеестрПриобретенныхМатериальныхРесурсов(ДополнительныеСвойства, Движения, Отказ);
	БухгалтерскийУчетСервер.ОтразитьСведенияОПоступлении(ДополнительныеСвойства, Движения, Отказ);
	БухгалтерскийУчетСервер.ЗначенияСтавокНалогов(ДополнительныеСвойства, Движения, Отказ);
	БухгалтерскийУчетСервер.ОтразитьНДСНаИмпорт(ДополнительныеСвойства, Движения, Отказ);
	БухгалтерскийУчетСервер.ОтразитьСведенияПоПоказателямИмпорта(ДополнительныеСвойства, Движения, Отказ);
	БухгалтерскийУчетСервер.ОтразитьРеестрЗакупок(ДополнительныеСвойства, Движения, Отказ);
	БухгалтерскийУчетСервер.ОтразитьДанныеЗакупочныхАктов(ДополнительныеСвойства, Движения, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура выполняет контроль противоречий.
//
Процедура ВыполнитьПредварительныйКонтроль(Отказ)
	
	// Проверка ОС
	Если ОС.Количество() > 0  Тогда 
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
			|	ТаблицаДокумента.ОсновноеСредство,
			|	ТаблицаДокумента.Сумма
			|ПОМЕСТИТЬ ТаблицаДокумента
			|ИЗ
			|	&ТаблицаДокумента КАК ТаблицаДокумента
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	МАКСИМУМ(ТаблицаДокумента1.НомерСтроки) КАК НомерСтроки,
			|	ТаблицаДокумента1.ОсновноеСредство
			|ИЗ
			|	ТаблицаДокумента КАК ТаблицаДокумента1
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДокумента КАК ТаблицаДокумента2
			|		ПО ТаблицаДокумента1.НомерСтроки <> ТаблицаДокумента2.НомерСтроки
			|			И ТаблицаДокумента1.ОсновноеСредство = ТаблицаДокумента2.ОсновноеСредство
			|
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаДокумента1.ОсновноеСредство
			|
			|УПОРЯДОЧИТЬ ПО
			|	НомерСтроки
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
			|	ТаблицаДокумента.ОсновноеСредство,
			|	СостоянияОССрезПоследних.Состояние
			|ИЗ
			|	ТаблицаДокумента КАК ТаблицаДокумента
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияОС.СрезПоследних(
			|				&Период,
			|				Организация = &Организация
			|					И ОсновноеСредство В
			|						(ВЫБРАТЬ РАЗЛИЧНЫЕ
			|							ТаблицаДокумента.ОсновноеСредство
			|						ИЗ
			|							ТаблицаДокумента КАК ТаблицаДокумента)
			|					И НЕ Регистратор = &Ссылка) КАК СостоянияОССрезПоследних
			|		ПО ТаблицаДокумента.ОсновноеСредство = СостоянияОССрезПоследних.ОсновноеСредство
			|
			|УПОРЯДОЧИТЬ ПО
			|	НомерСтроки";
		Запрос.УстановитьПараметр("ТаблицаДокумента", ОС);
		Запрос.УстановитьПараметр("Период", Дата);
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		// Дубли строк ОС.
		Если НЕ МассивРезультатов[1].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[1].Выбрать();
			Пока ВыборкаИзРезультатаЗапроса.Следующий() Цикл
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Основное средство указывается повторно в строке %1 списка ""ОС"".'"), 
								ВыборкаИзРезультатаЗапроса.НомерСтроки);
				БухгалтерскийУчетСервер.СообщитьОбОшибке(
					ЭтотОбъект,
					ТекстСообщения,
					"ОС",
					ВыборкаИзРезультатаЗапроса.НомерСтроки,
					"ОсновноеСредство",
					Отказ);
			КонецЦикла;
		КонецЕсли;		
		
		// Проверка состояния ОС.
		Если НЕ МассивРезультатов[2].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[2].Выбрать();
			Пока ВыборкаИзРезультатаЗапроса.Следующий() Цикл
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Для основного средства ""%1"", указанного в строке %2 списка ""ОС"", текущее состояние ""%3"".'"), 
					ВыборкаИзРезультатаЗапроса.ОсновноеСредство,
					ВыборкаИзРезультатаЗапроса.НомерСтроки,
					ВыборкаИзРезультатаЗапроса.Состояние);
					
				БухгалтерскийУчетСервер.СообщитьОбОшибке(
					ЭтотОбъект,
					ТекстСообщения,
					"ОС",
					ВыборкаИзРезультатаЗапроса.НомерСтроки,
					"ОсновноеСредство",
					Отказ);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ВестиУчетОСПоКомплектам") Тогда
	 	КонтрольЗапретаПоступленияКомплектовОС(Отказ);
	КонецЕсли;
	
	// Проверка ставок НСП
	Если Дата >= Дата(2022,01,01) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Товары.ЗначениеСтавкиНСП КАК ЗначениеСтавкиНСП
		|ПОМЕСТИТЬ ВременнаяТаблицаТовары
		|ИЗ
		|	&Товары КАК Товары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Услуги.ЗначениеСтавкиНСП КАК ЗначениеСтавкиНСП
		|ПОМЕСТИТЬ ВременнаяТаблицаУслуги
		|ИЗ
		|	&Услуги КАК Услуги
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОС.ЗначениеСтавкиНСП КАК ЗначениеСтавкиНСП
		|ПОМЕСТИТЬ ВременнаяТаблицаОС
		|ИЗ
		|	&ОС КАК ОС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВременнаяТаблицаТовары.ЗначениеСтавкиНСП КАК ЗначениеСтавкиНСП
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВременнаяТаблицаУслуги.ЗначениеСтавкиНСП КАК ЗначениеСтавкиНСП
		|ИЗ
		|	ВременнаяТаблицаУслуги КАК ВременнаяТаблицаУслуги
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВременнаяТаблицаОС.ЗначениеСтавкиНСП КАК ЗначениеСтавкиНСП
		|ИЗ
		|	ВременнаяТаблицаОС КАК ВременнаяТаблицаОС";
		
		Запрос.УстановитьПараметр("Товары", Товары);
		Запрос.УстановитьПараметр("Услуги", Услуги);
		Запрос.УстановитьПараметр("ОС", 	ОС);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		// Товары
		Если НЕ МассивРезультатов[3].Пустой() И МассивРезультатов[3].Выбрать().Количество() >= 2 Тогда
			ТекстСообщения = НСтр("ru = 'На закладке ""Товары"" значения ставок НСП должны быть одинаковыми.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,, "Объект.Товары", Отказ);	
		КонецЕсли;
		
		// Услуги
		Если НЕ МассивРезультатов[4].Пустой() И МассивРезультатов[4].Выбрать().Количество() >= 2 Тогда
			ТекстСообщения = НСтр("ru = 'На закладке ""Услуги"" значения ставок НСП должны быть одинаковыми.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,, "Объект.Услуги", Отказ);	
		КонецЕсли;
		
		// ОС
		Если НЕ МассивРезультатов[5].Пустой() И МассивРезультатов[5].Выбрать().Количество() >= 2 Тогда
			ТекстСообщения = НСтр("ru = 'На закладке ""ОС"" значения ставок НСП должны быть одинаковыми.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,, "Объект.ОС", Отказ);	
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьПредварительныйКонтроль()

Процедура КонтрольЗапретаПоступленияКомплектовОС(Отказ)
	Для каждого СтрокаТабличнойЧасти Из ОС Цикл
		Если СтрокаТабличнойЧасти.ОсновноеСредство.ЭтоКомплект Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Поступление основного средства в строке %1 с признаком ""Это комплект"" запрещено.'"), 
								СтрокаТабличнойЧасти.НомерСтроки);
				БухгалтерскийУчетСервер.СообщитьОбОшибке(
					ЭтотОбъект,
					ТекстСообщения,
					"ОС",
					СтрокаТабличнойЧасти.НомерСтроки,
					"ОсновноеСредство",
					Отказ);
			КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьВидОперации()

	ВидОперацииПоУмолчанию = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Покупка;
	
	КоличествоСписков = 0;	
	Если Товары.Количество() > 0 Тогда 
		ВидОперацииПоУмолчанию = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Товары;
		КоличествоСписков = КоличествоСписков + 1;
	КонецЕсли;
	
	Если Услуги.Количество() > 0 Тогда 
		ВидОперацииПоУмолчанию = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Услуги;
		КоличествоСписков = КоличествоСписков + 1;
	КонецЕсли;
	
	Если ОС.Количество() > 0 Тогда 
		ВидОперацииПоУмолчанию = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ОсновныеСредства;
		КоличествоСписков = КоличествоСписков + 1;
	КонецЕсли;
	
	Если КоличествоСписков > 1 Тогда 
		ВидОперацииПоУмолчанию = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Покупка;
	КонецЕсли;	
	
	Возврат ВидОперацииПоУмолчанию;

КонецФункции

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли