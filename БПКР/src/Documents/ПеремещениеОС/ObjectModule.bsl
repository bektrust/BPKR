#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ЗаполненияДокумента

// Процедура заполнения документа на основании.
//
// Параметры:
//	ДанныеЗаполнения - ДокументСсылка.ПоступлениеТоваровУслуг - Данные заполнения документа.
//	
Процедура ЗаполнитьПоПоступлениюТоваров(ДанныеЗаполнения) Экспорт
	ДокументОснование = ДанныеЗаполнения;
	
	Организация = ДанныеЗаполнения.Организация;

	Для Каждого СтрокаТабличнойЧасти Из ДанныеЗаполнения.ОС Цикл
		НоваяСтрока = ОС.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТабличнойЧасти);
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

// Процедура - обработчик события ОбработкаЗаполнения объекта.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	СтратегияЗаполнения = Новый Соответствие;
	СтратегияЗаполнения[Тип("ДокументСсылка.ПоступлениеТоваровУслуг")] = "ЗаполнитьПоПоступлениюТоваров";
	
	ЗаполнениеОбъектовБП.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения, СтратегияЗаполнения);
	
КонецПроцедуры

// Процедура - обработчик события ОбработкаПроведения объекта.
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ИнициализироватьДанные(Отказ, РежимПроведения);
	
	ОтразитьДвижения(Отказ, РежимПроведения);

	// Запись наборов записей.
	БухгалтерскийУчетСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
	
КонецПроцедуры // ОбработкаПроведения()

// Процедура - обработчик события ОбработкаУдаленияПроведения объекта.
//
Процедура ОбработкаУдаленияПроведения(Отказ)
	
	// Инициализация дополнительных свойств для проведения документа
	БухгалтерскийУчетСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	БухгалтерскийУчетСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Запись наборов записей
	БухгалтерскийУчетСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
КонецПроцедуры // ОбработкаУдаленияПроведения()

// Процедура - обработчик события ОбработкаПроверкиЗаполнения объекта.
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если МОЛОтправитель = МОЛПолучатель 
		И ПодразделениеОтправитель = ПодразделениеПолучатель Тогда
		ТекстСообщения = НСтр("ru = 'Указан одинаковый МОЛ и одинаковое подразделение.'");
		БухгалтерскийУчетСервер.СообщитьОбОшибке(
			Ссылка,   
			ТекстСообщения,
			,
			,
			"Объект.МОЛПолучатель",
			Отказ);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ВестиУчетОСПоКомплектам") Тогда
		КонтрольКомплектов(Отказ);
	КонецЕсли;

	КонтрольМОЛИПодразделение(Отказ);	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииОбработкаПроведения

// Процедура инициализирует данные документа
// и подготавливает таблицы для движения в регистры
//
Процедура ИнициализироватьДанные(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа.
	БухгалтерскийУчетСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа.
	Документы.ПеремещениеОС.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей.
	БухгалтерскийУчетСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
КонецПроцедуры
	
// Процедура заполняет регистры данными
//
Процедура ОтразитьДвижения(Отказ, РежимПроведения)

	// Отражение в разделах учета. 	
	БухгалтерскийУчетСервер.ОтразитьМестонахождениеОС(ДополнительныеСвойства, Движения, Отказ);	
	БухгалтерскийУчетСервер.ОтразитьСобытияОС(ДополнительныеСвойства, Движения, Отказ); 
	БухгалтерскийУчетСервер.ОтразитьСоставОС(ДополнительныеСвойства, Движения, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура КонтрольКомплектов(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ПеремещениеОСОС.ОсновноеСредство КАК Справочник.ОсновныеСредства) КАК ОсновноеСредство,
		|	ПеремещениеОСОС.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ВременнаяТаблицаОС
		|ИЗ
		|	&ТаблицаОС КАК ПеремещениеОСОС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаОС.ОсновноеСредство КАК ОсновноеСредство
		|ПОМЕСТИТЬ ВременнаяТаблицаТабличнаяЧастьОС
		|ИЗ
		|	ВременнаяТаблицаОС КАК ВременнаяТаблицаОС
		|ГДЕ
		|	НЕ ВременнаяТаблицаОС.ОсновноеСредство.ЭтоКомплект
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СоставОССрезПоследних.ОсновноеСредство КАК ОсновноеСредство,
		|	СоставОССрезПоследних.ОсновноеСредствоВСоставеКомплекта КАК ОсновноеСредствоВСоставеКомплекта,
		|	ВременнаяТаблицаОС.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ВременнаяТаблицаОСТЧВходящиеВКомплекты
		|ИЗ
		|	РегистрСведений.СоставОС.СрезПоследних(
		|			&Период,
		|			ОсновноеСредствоВСоставеКомплекта В
		|					(ВЫБРАТЬ
		|						ВременнаяТаблицаТабличнаяЧастьОС.ОсновноеСредство КАК ОсновноеСредство
		|					ИЗ
		|						ВременнаяТаблицаТабличнаяЧастьОС КАК ВременнаяТаблицаТабличнаяЧастьОС)
		|				И НЕ Регистратор = &Ссылка) КАК СоставОССрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаОС КАК ВременнаяТаблицаОС
		|		ПО СоставОССрезПоследних.ОсновноеСредствоВСоставеКомплекта = ВременнаяТаблицаОС.ОсновноеСредство
		|ГДЕ
		|	(СоставОССрезПоследних.СостояниеВСоставеОС = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияОС.Комплектация)
		|					ИЛИ СоставОССрезПоследних.СостояниеВСоставеОС = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияОС.Добавление))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаОСТЧВходящиеВКомплекты.ОсновноеСредство КАК ОсновноеСредство
		|ПОМЕСТИТЬ ВременнаяТаблицаПроверяемыеКомплекты
		|ИЗ
		|	ВременнаяТаблицаОСТЧВходящиеВКомплекты КАК ВременнаяТаблицаОСТЧВходящиеВКомплекты
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаОСТЧВходящиеВКомплекты.ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СоставОССрезПоследних.ОсновноеСредство КАК ОсновноеСредство,
		|	СоставОССрезПоследних.ОсновноеСредствоВСоставеКомплекта КАК ОсновноеСредствоВСоставеКомплекта
		|ПОМЕСТИТЬ ВременнаяТаблицаСоставКомплектов
		|ИЗ
		|	ВременнаяТаблицаПроверяемыеКомплекты КАК ВременнаяТаблицаПроверяемыеКомплекты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоставОС.СрезПоследних(
		|				&Период,
		|				ОсновноеСредство В
		|						(ВЫБРАТЬ
		|							ВременнаяТаблицаПроверяемыеКомплекты.ОсновноеСредство КАК ОсновноеСредство
		|						ИЗ
		|							ВременнаяТаблицаПроверяемыеКомплекты КАК ВременнаяТаблицаПроверяемыеКомплекты)
		|					И НЕ Регистратор = &Ссылка) КАК СоставОССрезПоследних
		|		ПО ВременнаяТаблицаПроверяемыеКомплекты.ОсновноеСредство = СоставОССрезПоследних.ОсновноеСредство
		|ГДЕ
		|	(СоставОССрезПоследних.СостояниеВСоставеОС = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияОС.Комплектация)
		|					ИЛИ СоставОССрезПоследних.СостояниеВСоставеОС = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКомплектацияОС.Добавление))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаСоставКомплектов.ОсновноеСредство КАК ОсновноеСредство
		|ПОМЕСТИТЬ ВременнаяТаблицаИсключитьКомплектыДляПроверки
		|ИЗ
		|	ВременнаяТаблицаСоставКомплектов КАК ВременнаяТаблицаСоставКомплектов
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаОСТЧВходящиеВКомплекты КАК ВременнаяТаблицаОСТЧВходящиеВКомплекты
		|		ПО ВременнаяТаблицаСоставКомплектов.ОсновноеСредство = ВременнаяТаблицаОСТЧВходящиеВКомплекты.ОсновноеСредство
		|			И ВременнаяТаблицаСоставКомплектов.ОсновноеСредствоВСоставеКомплекта = ВременнаяТаблицаОСТЧВходящиеВКомплекты.ОсновноеСредствоВСоставеКомплекта
		|ГДЕ
		|	ВременнаяТаблицаОСТЧВходящиеВКомплекты.ОсновноеСредствоВСоставеКомплекта ЕСТЬ NULL
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаСоставКомплектов.ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаОСТЧВходящиеВКомплекты.ОсновноеСредство КАК ОсновноеСредство,
		|	ВременнаяТаблицаОСТЧВходящиеВКомплекты.ОсновноеСредствоВСоставеКомплекта КАК ОсновноеСредствоВСоставеКомплекта,
		|	ВременнаяТаблицаОСТЧВходящиеВКомплекты.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	ВременнаяТаблицаОСТЧВходящиеВКомплекты КАК ВременнаяТаблицаОСТЧВходящиеВКомплекты
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаИсключитьКомплектыДляПроверки КАК ВременнаяТаблицаИсключитьКомплектыДляПроверки
		|		ПО ВременнаяТаблицаОСТЧВходящиеВКомплекты.ОсновноеСредство = ВременнаяТаблицаИсключитьКомплектыДляПроверки.ОсновноеСредство
		|ГДЕ
		|	ВременнаяТаблицаИсключитьКомплектыДляПроверки.ОсновноеСредство ЕСТЬ NULL";
	
	Запрос.Параметры.Вставить("Период", 	Дата);	
	Запрос.Параметры.Вставить("Ссылка", 	Ссылка);
	Запрос.УстановитьПараметр("ТаблицаОС", 	ОС.Выгрузить());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Перемещаются все элементы комплекта ""%1"". Добавьте этот комплект в табличную часть, а основное средство ""%2"" удалите из табличной части.'"), 
							ВыборкаДетальныеЗаписи.ОсновноеСредство, ВыборкаДетальныеЗаписи.ОсновноеСредствоВСоставеКомплекта);
			БухгалтерскийУчетСервер.СообщитьОбОшибке(
				ЭтотОбъект,
				ТекстСообщения,
				"ОС",
				ВыборкаДетальныеЗаписи.НомерСтроки,
				"ОсновноеСредство",
				Отказ);
	КонецЦикла;
	
КонецПроцедуры

Процедура КонтрольМОЛИПодразделение(Отказ)

	КонтрольМОЛ 			= ПолучитьФункциональнуюОпцию("УчетДвиженияОСПоМОЛ");
	КонтрольПодразделение 	= ПолучитьФункциональнуюОпцию("УчетДвиженияОСПоПодразделениям");

	Если НЕ КонтрольМОЛ И НЕ КонтрольПодразделение Тогда
		Возврат;		
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПеремещениеОСОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ПеремещениеОСОС.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ВременнаяТаблицаОС
		|ИЗ
		|	&ТаблицаОС КАК ПеремещениеОСОС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ВременнаяТаблицаОС.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	ВременнаяТаблицаОС КАК ВременнаяТаблицаОС
		|ГДЕ
		|	ВременнаяТаблицаОС.ОсновноеСредство В
		|			(ВЫБРАТЬ
		|				МестонахождениеОССрезПоследних.ОсновноеСредство КАК ОсновноеСредство
		|			ИЗ
		|				РегистрСведений.МестонахождениеОС.СрезПоследних(&Период, Организация = &Организация
		|					И НЕ Регистратор = &Ссылка
		|					И ОсновноеСредство В
		|						(ВЫБРАТЬ
		|							ВременнаяТаблицаОС.ОсновноеСредство
		|						ИЗ
		|							ВременнаяТаблицаОС)) КАК МестонахождениеОССрезПоследних
		|			ГДЕ
		|				(НЕ МестонахождениеОССрезПоследних.МОЛ = &МОЛ
		|					ИЛИ НЕ МестонахождениеОССрезПоследних.Подразделение = &Подразделение))";
	Запрос.УстановитьПараметр("ТаблицаОС", 		ОС.Выгрузить());
	Запрос.УстановитьПараметр("Период", 		Дата);
	Запрос.УстановитьПараметр("Организация", 	Организация);
	Запрос.УстановитьПараметр("Ссылка", 		Ссылка);
	
	Если КонтрольМОЛ Тогда
		Запрос.УстановитьПараметр("МОЛ", МОЛОтправитель);
	Иначе	
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "НЕ МестонахождениеОССрезПоследних.МОЛ = &МОЛ", "ЛОЖЬ");	
	КонецЕсли;	
	
	Если КонтрольПодразделение Тогда
		Запрос.УстановитьПараметр("Подразделение", ПодразделениеОтправитель);
	Иначе	
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "НЕ МестонахождениеОССрезПоследних.Подразделение = &Подразделение", "ЛОЖЬ");	
	КонецЕсли;	
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл		
		
		Если КонтрольМОЛ И КонтрольПодразделение Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Перемещаемое ОС ""%1"" не находится в данном подразделении и/или у данного МОЛ.'"), 		
				Выборка.ОсновноеСредство);
				
		ИначеЕсли КонтрольМОЛ Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Перемещаемое ОС ""%1"" не находится у данного МОЛ.'"), 		
				Выборка.ОсновноеСредство);	
		Иначе
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Перемещаемое ОС ""%1"" не находится в данном подразделении.'"), 		
				Выборка.ОсновноеСредство);		
		КонецЕсли;
			
		БухгалтерскийУчетСервер.СообщитьОбОшибке(
			ЭтотОбъект,
			ТекстСообщения,
			"ОС",
			Выборка.НомерСтроки,
			"ОсновноеСредство",
			Отказ);		
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли