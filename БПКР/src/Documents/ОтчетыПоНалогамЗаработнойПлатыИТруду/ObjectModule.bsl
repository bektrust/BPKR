#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события ОбработкаЗаполнения объекта.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	ЗаполнениеОбъектовБП.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьНалоги(ДанныеУчетнойПолитики) Экспорт
	
	НачалоПериода = НачалоМесяца(Дата);
	КонецПериода = КонецМесяца(Дата);
	
	ТекстыЗапроса = Новый Массив;

	// Данные по сотрудникам
	
	//	Для случаев, когда 2 строки (дублирование) по сотруднику (Трудовое соглашение и Приказ), определяем приоритет записи:
	//	Если Принят и Не Совместитель = 1
	//	Если Принят и Совместитель = 2	
	//	Если Уволен и Не Совместитель = 3
	//	Если Уволен и Совместитель = 4
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ХозрасчетныйОборотыДтКт.СубконтоКт1 КАК ФизЛицо,
		|	ХозрасчетныйОборотыДтКт.Организация КАК Организация,
		|	ХозрасчетныйОборотыДтКт.СуммаОборот КАК СуммаПН
		|ПОМЕСТИТЬ ВременнаяТаблицаПрочиеФизЛица
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&НачалоПериода, &КонецПериода, , , , СчетКт = &СчетУчетаПоПНДляПрочихФизическихЛиц, ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СотрудникиОрганизаций), Организация = &Организация) КАК ХозрасчетныйОборотыДтКт
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СотрудникиСрезПоследних.ФизЛицо КАК ФизЛицо,
		|	ВЫБОР
		|		КОГДА СотрудникиСрезПоследних.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
		|			ТОГДА 3
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК Совместитель,
		|	СотрудникиСрезПоследних.ГрафикРаботы КАК ГрафикРаботы,
		|	СотрудникиСрезПоследних.Организация КАК Организация
		|ПОМЕСТИТЬ ВременнаяТаблицаСотрудникиПредв
		|ИЗ
		|	РегистрСведений.Сотрудники.СрезПоследних(
		|			&КонецПериода,
		|			Организация = &Организация
		|				И Подразделение В (&МассивПодразделений)) КАК СотрудникиСрезПоследних
		|
		|СГРУППИРОВАТЬ ПО
		|	СотрудникиСрезПоследних.ФизЛицо,
		|	ВЫБОР
		|		КОГДА СотрудникиСрезПоследних.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
		|			ТОГДА 3
		|		ИНАЧЕ 1
		|	КОНЕЦ,
		|	СотрудникиСрезПоследних.ГрафикРаботы,
		|	СотрудникиСрезПоследних.Организация
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СотрудникиПоТрудовымСоглашениямСрезПоследних.ФизЛицо,
		|	ВЫБОР
		|		КОГДА СотрудникиПоТрудовымСоглашениямСрезПоследних.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
		|			ТОГДА 4
		|		ИНАЧЕ 2
		|	КОНЕЦ,
		|	СотрудникиПоТрудовымСоглашениямСрезПоследних.ГрафикРаботы,
		|	СотрудникиПоТрудовымСоглашениямСрезПоследних.Организация
		|ИЗ
		|	РегистрСведений.СотрудникиПоТрудовымСоглашениям.СрезПоследних(
		|			&КонецПериода,
		|			Организация = &Организация
		|				И Подразделение В (&МассивПодразделений)) КАК СотрудникиПоТрудовымСоглашениямСрезПоследних
		|
		|СГРУППИРОВАТЬ ПО
		|	СотрудникиПоТрудовымСоглашениямСрезПоследних.ФизЛицо,
		|	ВЫБОР
		|		КОГДА СотрудникиПоТрудовымСоглашениямСрезПоследних.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
		|			ТОГДА 4
		|		ИНАЧЕ 2
		|	КОНЕЦ,
		|	СотрудникиПоТрудовымСоглашениямСрезПоследних.ГрафикРаботы,
		|	СотрудникиПоТрудовымСоглашениямСрезПоследних.Организация
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаПрочиеФизЛица.ФизЛицо,
		|	1,
		|	НЕОПРЕДЕЛЕНО,
		|	ВременнаяТаблицаПрочиеФизЛица.Организация
		|ИЗ
		|	ВременнаяТаблицаПрочиеФизЛица КАК ВременнаяТаблицаПрочиеФизЛица
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаСотрудникиПредв.ФизЛицо КАК ФизЛицо,
		|	МИНИМУМ(ВременнаяТаблицаСотрудникиПредв.Совместитель) КАК Совместитель,
		|	ВременнаяТаблицаСотрудникиПредв.ГрафикРаботы КАК ГрафикРаботы,
		|	ВременнаяТаблицаСотрудникиПредв.Организация КАК Организация
		|ПОМЕСТИТЬ ВременнаяТаблицаСотрудникиГрупп
		|ИЗ
		|	ВременнаяТаблицаСотрудникиПредв КАК ВременнаяТаблицаСотрудникиПредв
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаСотрудникиПредв.ФизЛицо,
		|	ВременнаяТаблицаСотрудникиПредв.Организация,
		|	ВременнаяТаблицаСотрудникиПредв.ГрафикРаботы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаСотрудникиГрупп.ФизЛицо КАК ФизЛицо,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаСотрудникиГрупп.Совместитель = 1
		|				ИЛИ ВременнаяТаблицаСотрудникиГрупп.Совместитель = 3
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК Совместитель,
		|	СУММА(КалендариГрафиковРабот.ЗначениеДней) КАК НормаДней,
		|	СтатусыСотрудниковСрезПоследних.Статус КАК Статус,
		|	СтатусыСотрудниковСрезПоследних.ПризнакРезидентства КАК ПризнакРезидентства
		|ПОМЕСТИТЬ ВременнаяТаблицаСотрудники
		|ИЗ
		|	ВременнаяТаблицаСотрудникиГрупп КАК ВременнаяТаблицаСотрудникиГрупп
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КалендариГрафиковРабот КАК КалендариГрафиковРабот
		|		ПО ВременнаяТаблицаСотрудникиГрупп.ГрафикРаботы = КалендариГрафиковРабот.ГрафикРаботы
		|			И (КалендариГрафиковРабот.Год = ГОД(&КонецПериода))
		|			И (КалендариГрафиковРабот.Дата МЕЖДУ &НачалоПериода И &КонецПериода)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыСотрудников.СрезПоследних КАК СтатусыСотрудниковСрезПоследних
		|		ПО ВременнаяТаблицаСотрудникиГрупп.Организация = СтатусыСотрудниковСрезПоследних.Организация
		|			И ВременнаяТаблицаСотрудникиГрупп.ФизЛицо = СтатусыСотрудниковСрезПоследних.ФизЛицо
		|
		|СГРУППИРОВАТЬ ПО
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаСотрудникиГрупп.Совместитель = 1
		|				ИЛИ ВременнаяТаблицаСотрудникиГрупп.Совместитель = 3
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ,
		|	СтатусыСотрудниковСрезПоследних.Статус,
		|	ВременнаяТаблицаСотрудникиГрупп.ФизЛицо,
		|	СтатусыСотрудниковСрезПоследних.ПризнакРезидентства";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// Выплаченная заработная плата
	// Зависит от настройки (Заполнять выплату ПН по начислениям)
	// Выборка по счету 3520 с корр.счетов Банк/Касса или 1520 (Под.отчет)
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ХозрасчетныйОбороты.Субконто1 КАК ФизЛицо,
		|	ХозрасчетныйОбороты.СуммаОборот КАК СуммаВыплачено
		|ПОМЕСТИТЬ ВременнаяТаблицаВыплата
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, , Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НачисленнаяЗаработнаяПлата), , Организация = &Организация, КорСчет В (&СчетаУчетаДенежныхСредств), ) КАК ХозрасчетныйОбороты
		|ГДЕ
		|	НЕ &ЗаполнятьВыплатуПНПоНачислениям
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// Соц.Фонд
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	СоциальныйФондОбороты.ФизЛицо КАК ФизЛицо,
		|	СоциальныйФондОбороты.ОблагаемаяСуммаДоходаОборот КАК ОДСФ,
		|	СоциальныйФондОбороты.ПФРОборот КАК СуммаПФР,
		|	СоциальныйФондОбороты.ГНПФРОборот КАК СуммаГНПФР,
		|	СоциальныйФондОбороты.ПФФОборот КАК СуммаПФФ,
		|	СоциальныйФондОбороты.МСФОборот КАК СуммаМСФ,
		|	СоциальныйФондОбороты.ФОТФОборот КАК СуммаФОТФ,
		|	СоциальныйФондОбороты.ПФФКорректировкаОборот КАК СуммаПФФКорректировка,
		|	СоциальныйФондОбороты.МСФКорректировкаОборот КАК СуммаМСФКорректировка,
		|	СоциальныйФондОбороты.ФОТФКорректировкаОборот КАК СуммаФОТФКорректировка,
		|	СоциальныйФондОбороты.ПриведеннаяСЗПОборот КАК ПриведеннаяСЗП
		|ПОМЕСТИТЬ ВременнаяТаблицаСФ
		|ИЗ
		|	РегистрНакопления.СоциальныйФонд.Обороты(&НачалоПериода, &КонецПериода, , Организация = &Организация) КАК СоциальныйФондОбороты
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// Подоходный налог
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ПодоходныйНалогОбороты.ФизЛицо КАК ФизЛицо,
		|	ПодоходныйНалогОбороты.ОДПНОборот КАК ОДПН,
		|	ПодоходныйНалогОбороты.ВычетыОборот КАК Вычеты,
		|	ПодоходныйНалогОбороты.ПНОборот КАК СуммаПНсСотрудника,
		|	ПодоходныйНалогОбороты.ДоплатаПНОборот КАК СуммаПНсМРД,
		|	ПодоходныйНалогОбороты.ПНОборот + ПодоходныйНалогОбороты.ДоплатаПНОборот КАК СуммаПН,
		|	ПодоходныйНалогОбороты.ПФРДляРасчетаПНОборот КАК СуммаПФРДляРасчетаПН,
		|	ПодоходныйНалогОбороты.ГНПФРДляРасчетаПНОборот КАК СуммаГНПФРДляРасчетаПН
		|ПОМЕСТИТЬ ВременнаяТаблицаПН
		|ИЗ
		|	РегистрНакопления.ПодоходныйНалог.Обороты(&НачалоПериода, &КонецПериода, , Организация = &Организация) КАК ПодоходныйНалогОбороты
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаПрочиеФизЛица.ФизЛицо,
		|	ВременнаяТаблицаПрочиеФизЛица.СуммаПН * 10,
		|	0,
		|	ВременнаяТаблицаПрочиеФизЛица.СуммаПН,
		|	0,
		|	ВременнаяТаблицаПрочиеФизЛица.СуммаПН,
		|	0,
		|	0
		|ИЗ
		|	ВременнаяТаблицаПрочиеФизЛица КАК ВременнаяТаблицаПрочиеФизЛица
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// Начисления
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Начисления.ФизЛицо КАК ФизЛицо,
		|	МИНИМУМ(Начисления.ПериодДействияНачало) КАК ДатаНачала,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА Начисления.ПериодДействияКонец > &КонецПериода
		|				ТОГДА &КонецПериода
		|			ИНАЧЕ Начисления.ПериодДействияКонец
		|		КОНЕЦ) КАК ДатаОкончания,
		|	СУММА(ВЫБОР
		|			КОГДА Начисления.ВидРасчета.ЗачетОтработанногоВремени
		|				ТОГДА Начисления.ОтработаноДней
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОтработаноДней,
		|	СУММА(ВЫБОР
		|			КОГДА Начисления.ВидРасчета.ЗачетОтработанногоВремени
		|				ТОГДА Начисления.ОтработаноЧасов
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОтработаноЧасов,
		|	СУММА(ВЫБОР
		|			КОГДА Начисления.ВидРасчета.ДополнительныйДоход
		|				ТОГДА 0
		|			ИНАЧЕ Начисления.Результат
		|		КОНЕЦ) КАК СуммаНачисленоБезДопДохода,
		|	СУММА(Начисления.Результат) КАК СуммаНачислено
		|ПОМЕСТИТЬ ВременнаяТаблицаНачисления
		|ИЗ
		|	РегистрРасчета.Начисления КАК Начисления
		|ГДЕ
		|	Начисления.Организация = &Организация
		|	И Начисления.ПериодРегистрации МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Начисления.Подразделение В(&МассивПодразделений)
		|
		|СГРУППИРОВАТЬ ПО
		|	Начисления.ФизЛицо
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		| 	ВременнаяТаблицаПрочиеФизЛица.ФизЛицо,
		| 	&НачалоПериода,
		| 	&КонецПериода,
		| 	0,
		| 	0,
		| 	ВременнаяТаблицаПрочиеФизЛица.СуммаПН * 10,
		| 	ВременнаяТаблицаПрочиеФизЛица.СуммаПН * 10
		|ИЗ
		|	ВременнаяТаблицаПрочиеФизЛица КАК ВременнаяТаблицаПрочиеФизЛица
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизЛицо";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
		
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаНачисления.ФизЛицо КАК ФизЛицо,
		|	ВременнаяТаблицаНачисления.ДатаНачала КАК ДатаНачала,
		|	ВременнаяТаблицаНачисления.ДатаОкончания КАК ДатаОкончания,
		|	ВЫБОР
		|		КОГДА НЕ ВременнаяТаблицаСотрудники.Статус ЕСТЬ NULL
		|			И ВременнаяТаблицаСотрудники.Статус.Категория = ЗНАЧЕНИЕ(Справочник.КатегорииСотрудников.Прочие)
		|			ТОГДА 0
		|		КОГДА ВременнаяТаблицаНачисления.ОтработаноДней = 0
		|			ТОГДА 1
		|		ИНАЧЕ ВременнаяТаблицаНачисления.ОтработаноДней
		|	КОНЕЦ КАК ОтработаноДней,
		|	ВременнаяТаблицаНачисления.ОтработаноЧасов КАК ОтработаноЧасов,
		|	ВременнаяТаблицаНачисления.СуммаНачислено КАК СуммаНачислено,
		|	ЕСТЬNULL(ВременнаяТаблицаСотрудники.Статус, НЕОПРЕДЕЛЕНО) КАК Статус,
		|	ЕСТЬNULL(ВременнаяТаблицаСотрудники.ПризнакРезидентства, """") КАК ПризнакРезидентства,
		|	ЕСТЬNULL(ВременнаяТаблицаСотрудники.Совместитель, ЛОЖЬ) КАК Совместитель,
		|	ЕСТЬNULL(ВременнаяТаблицаСотрудники.НормаДней, 0) КАК НормаДней,
		|	ЕСТЬNULL(ВременнаяТаблицаСФ.ОДСФ, 0) КАК ОДСФ,
		|	ЕСТЬNULL(ВременнаяТаблицаСФ.СуммаПФР, 0) КАК СуммаПФР,
		|	ЕСТЬNULL(ВременнаяТаблицаСФ.СуммаГНПФР, 0) КАК СуммаГНПФР,
		|	ЕСТЬNULL(ВременнаяТаблицаСФ.СуммаПФФ, 0) КАК СуммаПФФ,
		|	ЕСТЬNULL(ВременнаяТаблицаСФ.СуммаМСФ, 0) КАК СуммаМСФ,
		|	ЕСТЬNULL(ВременнаяТаблицаСФ.СуммаФОТФ, 0) КАК СуммаФОТФ,
		|	ЕСТЬNULL(ВременнаяТаблицаСФ.СуммаПФФКорректировка, 0) КАК СуммаПФФКорректировка,
		|	ЕСТЬNULL(ВременнаяТаблицаСФ.СуммаМСФКорректировка, 0) КАК СуммаМСФКорректировка,
		|	ЕСТЬNULL(ВременнаяТаблицаСФ.СуммаФОТФКорректировка, 0) КАК СуммаФОТФКорректировка,  
		|	ЕСТЬNULL(ВременнаяТаблицаСФ.ПриведеннаяСЗП, 0) КАК ПриведеннаяСЗП,
		|	ЕСТЬNULL(ВременнаяТаблицаПН.ОДПН, 0) КАК ОДПН,
		|	ЕСТЬNULL(ВременнаяТаблицаПН.СуммаПФРДляРасчетаПН, 0) КАК СуммаПФРДляРасчетаПН,
		|	ЕСТЬNULL(ВременнаяТаблицаПН.СуммаГНПФРДляРасчетаПН, 0) КАК СуммаГНПФРДляРасчетаПН,
		|	ЕСТЬNULL(ВременнаяТаблицаПН.Вычеты, 0) КАК Вычеты,
		|	ЕСТЬNULL(ВременнаяТаблицаПН.СуммаПНсСотрудника, 0) КАК СуммаПНсСотрудника,
		|	ЕСТЬNULL(ВременнаяТаблицаПН.СуммаПНсМРД, 0) КАК СуммаПНсМРД,
		|	ЕСТЬNULL(ВременнаяТаблицаПН.СуммаПН, 0) КАК СуммаПН,
		|	ЕСТЬNULL(ВременнаяТаблицаПН.СуммаПН, 0) КАК ОстатокПН,
		|	ВЫБОР
		|		КОГДА &ЗаполнятьВыплатуПНПоНачислениям
		|			ТОГДА ВременнаяТаблицаНачисления.СуммаНачисленоБезДопДохода - ЕСТЬNULL(ВременнаяТаблицаПН.СуммаПФРДляРасчетаПН, 0) - ЕСТЬNULL(ВременнаяТаблицаПН.СуммаГНПФРДляРасчетаПН, 0) - ЕСТЬNULL(ВременнаяТаблицаПН.СуммаПН, 0)
		|		ИНАЧЕ ЕСТЬNULL(ВременнаяТаблицаВыплата.СуммаВыплачено, 0)
		|	КОНЕЦ КАК СуммаВыплачено,
		|	ЕСТЬNULL(СтатусыСотрудниковСрезПоследних.ДопВычет, 0) КАК ДопВычет
		|ИЗ
		|	ВременнаяТаблицаНачисления КАК ВременнаяТаблицаНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСФ КАК ВременнаяТаблицаСФ
		|		ПО ВременнаяТаблицаНачисления.ФизЛицо = ВременнаяТаблицаСФ.ФизЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСотрудники КАК ВременнаяТаблицаСотрудники
		|		ПО ВременнаяТаблицаНачисления.ФизЛицо = ВременнаяТаблицаСотрудники.ФизЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаПН КАК ВременнаяТаблицаПН
		|		ПО ВременнаяТаблицаНачисления.ФизЛицо = ВременнаяТаблицаПН.ФизЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаВыплата КАК ВременнаяТаблицаВыплата
		|		ПО ВременнаяТаблицаНачисления.ФизЛицо = ВременнаяТаблицаВыплата.ФизЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыСотрудников.СрезПоследних(&КонецПериода, Организация = &Организация) КАК СтатусыСотрудниковСрезПоследних
		|		ПО ВременнаяТаблицаНачисления.ФизЛицо = СтатусыСотрудниковСрезПоследних.ФизЛицо";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Если НЕ ДанныеУчетнойПолитики.ПределыПоПодразделениям Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Начисления.Подразделение В(&МассивПодразделений)", "ИСТИНА");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Подразделение В (&МассивПодразделений)", "ИСТИНА");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);     
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);     
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ЗаполнятьВыплатуПНПоНачислениям", Константы.ЗаполнятьВыплатуПНПоНачислениям.Получить());
	
	СчетУчетаПоПНДляПрочихФизическихЛиц = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьДанныеУчетнойПолитикиПоПерсоналу(Дата, Организация).СчетУчетаПоПНДляПрочихФизическихЛиц;
	Запрос.УстановитьПараметр("СчетУчетаПоПНДляПрочихФизическихЛиц", СчетУчетаПоПНДляПрочихФизическихЛиц);
	
	// Определение отбора по подразделениям.
	Если ДанныеУчетнойПолитики.ПределыПоПодразделениям Тогда
		МассивПодразделений = Новый Массив;
		
		ЗапросПодразделения = Новый Запрос;
		ЗапросПодразделения.Текст = 
			"ВЫБРАТЬ
			|	ПределыПоПодразделениямСрезПоследних.Подразделение КАК Подразделение
			|ИЗ
			|	РегистрСведений.ПределыПоПодразделениям.СрезПоследних(
			|			&Период,
			|			Организация = &Организация) КАК ПределыПоПодразделениямСрезПоследних
			|ГДЕ
			|	ПределыПоПодразделениямСрезПоследних.УГНС = &Контрагент";
		ЗапросПодразделения.УстановитьПараметр("Период", 		КонецПериода);
		ЗапросПодразделения.УстановитьПараметр("Организация", 	Организация);
		ЗапросПодразделения.УстановитьПараметр("Контрагент", 	Контрагент);
		
		РезультатЗапроса = ЗапросПодразделения.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			МассивПодразделений.Добавить(ВыборкаДетальныеЗаписи.Подразделение);		
		КонецЦикла;

		Запрос.УстановитьПараметр("МассивПодразделений", МассивПодразделений);
	КонецЕсли;
	
	СчетаУчетаДенежныхСредств = БухгалтерскийУчетСервер.СчетаУчетаДенежныхСредств();
	СчетаУчетаДенежныхСредств.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицами); // 1520
	Запрос.УстановитьПараметр("СчетаУчетаДенежныхСредств", СчетаУчетаДенежныхСредств);
	
	Налоги.Загрузить(Запрос.Выполнить().Выгрузить());
	
	// Заполнение прочих расходов
	ПрочийРасход3 = Налоги.Итог("СуммаПФФ") + Налоги.Итог("СуммаМСФ") + Налоги.Итог("СуммаФОТФ")
		+ Налоги.Итог("СуммаПФФКорректировка") + Налоги.Итог("СуммаМСФКорректировка") + Налоги.Итог("СуммаФОТФКорректировка");
		
	// Заполнение суммы прямых начислений
	СуммаПрямыхНачислений = Налоги.Итог("СуммаНачислено");
КонецПроцедуры

Процедура ЗаполнитьКадры(ДанныеУчетнойПолитики) Экспорт
	
	КоличествоПринятых = 0;
	КоличествоВнешнихСовместителей = 0;
	КоличествоУволенныхПоСобственномуЖеланию = 0;
	КоличествоУволенныхПоСтатье = 0;
	
	ТекстыЗапроса = Новый Массив;

	// Прием на работу.
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СотрудникиСрезПоследних.ФизЛицо) КАК КоличествоПринятых
		|ИЗ
		|	РегистрСведений.Сотрудники.СрезПоследних(
		|			&КонецПериода,
		|			Организация = &Организация
		|				И ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Прием)
		|				И Подразделение В (&МассивПодразделений)) КАК СотрудникиСрезПоследних
		|ГДЕ
		|	СотрудникиСрезПоследних.Период МЕЖДУ &НачалоПериода И &КонецПериода";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// Внешние совместители.
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СотрудникиПоТрудовымСоглашениямСрезПоследних.ФизЛицо) КАК КоличествоВнешнихСовместителей
		|ИЗ
		|	РегистрСведений.СотрудникиПоТрудовымСоглашениям.СрезПоследних(&КонецПериода, Организация = &Организация И Подразделение В (&МассивПодразделений)) КАК СотрудникиПоТрудовымСоглашениямСрезПоследних
		|ГДЕ
		|	СотрудникиПоТрудовымСоглашениямСрезПоследних.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И НЕ СотрудникиПоТрудовымСоглашениямСрезПоследних.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// Увольнения.
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	СУММА(ВЫБОР
		|			КОГДА СотрудникиСрезПоследних.СтатьяТрудовогоКодекса = &СтатьяТрудовогоКодексаПоСобственномуЖеланию
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК КоличествоУволенныхПоСобственномуЖеланию,
		|	СУММА(ВЫБОР
		|			КОГДА СотрудникиСрезПоследних.СтатьяТрудовогоКодекса = &СтатьяТрудовогоКодексаПоСобственномуЖеланию
		|				ТОГДА 0
		|			ИНАЧЕ 1
		|		КОНЕЦ) КАК КоличествоУволенныхПоСтатье
		|ИЗ
		|	РегистрСведений.Сотрудники.СрезПоследних(
		|			&КонецПериода,
		|			Организация = &Организация
		|				И ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
		|				И Подразделение В (&МассивПодразделений)) КАК СотрудникиСрезПоследних
		|ГДЕ
		|	СотрудникиСрезПоследних.Период МЕЖДУ &НачалоПериода И &КонецПериода";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));     
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));     
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СтатьяТрудовогоКодексаПоСобственномуЖеланию", Справочники.СтатьиТрудовогоКодекса.ПолучитьСтатьюПоСобственномуЖеланию());
	
	Если НЕ ДанныеУчетнойПолитики.ПределыПоПодразделениям Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Подразделение В (&МассивПодразделений)", "ИСТИНА");
	КонецЕсли;
	
	// Определение отбора по подразделениям.
	Если ДанныеУчетнойПолитики.ПределыПоПодразделениям Тогда
		МассивПодразделений = Новый Массив;
		
		ЗапросПодразделения = Новый Запрос;
		ЗапросПодразделения.Текст = 
			"ВЫБРАТЬ
			|	ПределыПоПодразделениямСрезПоследних.Подразделение КАК Подразделение
			|ИЗ
			|	РегистрСведений.ПределыПоПодразделениям.СрезПоследних(
			|			&Период,
			|			Организация = &Организация) КАК ПределыПоПодразделениямСрезПоследних
			|ГДЕ
			|	ПределыПоПодразделениямСрезПоследних.УГНС = &Контрагент";
		ЗапросПодразделения.УстановитьПараметр("Период", КонецМесяца(Дата));
		ЗапросПодразделения.УстановитьПараметр("Организация", Организация);
		ЗапросПодразделения.УстановитьПараметр("Контрагент", Контрагент);
		
		РезультатЗапроса = ЗапросПодразделения.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			МассивПодразделений.Добавить(ВыборкаДетальныеЗаписи.Подразделение);		
		КонецЦикла;

		Запрос.УстановитьПараметр("МассивПодразделений", МассивПодразделений);
	КонецЕсли;
	
	МассивРезультатовЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаДетальныеЗаписи = МассивРезультатовЗапроса[0].Выбрать();
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда 
		КоличествоПринятых = ВыборкаДетальныеЗаписи.КоличествоПринятых;	
	КонецЕсли;	
	
	ВыборкаДетальныеЗаписи = МассивРезультатовЗапроса[1].Выбрать();
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда 
		КоличествоВнешнихСовместителей = ВыборкаДетальныеЗаписи.КоличествоВнешнихСовместителей;	
	КонецЕсли;	
	
	ВыборкаДетальныеЗаписи = МассивРезультатовЗапроса[2].Выбрать();
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда 
		КоличествоУволенныхПоСобственномуЖеланию = ВыборкаДетальныеЗаписи.КоличествоУволенныхПоСобственномуЖеланию;	
		КоличествоУволенныхПоСтатье = ВыборкаДетальныеЗаписи.КоличествоУволенныхПоСтатье;	
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли