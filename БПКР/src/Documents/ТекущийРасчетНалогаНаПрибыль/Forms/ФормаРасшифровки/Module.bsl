
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Организация	  = Параметры.Организация;
	НачалоПериода = Параметры.НачалоПериода;
	КонецПериода  = Параметры.КонецПериода;
	
	Заголовок = СтрШаблон(НСтр("ru = 'Отчет по текущему налогу на прибыль с %1 по %2'"), 
					Формат(Параметры.НачалоПериода, "ДЛФ=D"),
					Формат(Параметры.КонецПериода,  "ДЛФ=D"));
					
	ПредставлениеОрганизации = ?(Организация.НаименованиеПолное = "", 
									Организация.Наименование, Организация.НаименованиеПолное);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Хозрасчетный.Ссылка КАК Счет,
		|	Хозрасчетный.Родитель КАК СчетРодитель,
		|	Хозрасчетный.Наименование КАК Наименование,
		|	Хозрасчетный.Вид КАК Вид
		|ПОМЕСТИТЬ ВременнаяТаблицаСписокСчетов
		|ИЗ
		|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
		|ГДЕ
		|	Хозрасчетный.Временный
		|	И НЕ Хозрасчетный.Ссылка = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СводДоходовИРасходов)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаСписокСчетов.Счет КАК Счет,
		|	ВременнаяТаблицаСписокСчетов.Наименование КАК Наименование
		|ИЗ
		|	ВременнаяТаблицаСписокСчетов КАК ВременнаяТаблицаСписокСчетов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаСписокСчетов.СчетРодитель КАК СчетРодитель,
		|	ХозрасчетныйОборотыДтКт.СчетДт КАК Счет,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаСписокСчетов.Вид = ЗНАЧЕНИЕ(ВидСчета.Пассивный)
		|			ТОГДА ВЫБОР
		|					КОГДА ХозрасчетныйОборотыДтКт.СуммаОборот < 0
		|						ТОГДА -ХозрасчетныйОборотыДтКт.СуммаОборот
		|					ИНАЧЕ ХозрасчетныйОборотыДтКт.СуммаОборот
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаДоходов,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаСписокСчетов.Вид = ЗНАЧЕНИЕ(ВидСчета.Активный)
		|			ТОГДА ВЫБОР
		|					КОГДА ХозрасчетныйОборотыДтКт.СуммаОборот < 0
		|						ТОГДА -ХозрасчетныйОборотыДтКт.СуммаОборот
		|					ИНАЧЕ ХозрасчетныйОборотыДтКт.СуммаОборот
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаРасходов
		|ПОМЕСТИТЬ ВременнаяТаблицаОбороты
		|ИЗ
		|	ВременнаяТаблицаСписокСчетов КАК ВременнаяТаблицаСписокСчетов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(
		|				&НачалоПериода,
		|				&КонецПериода,
		|				,
		|				СчетДт В
		|						(ВЫБРАТЬ
		|							ВременнаяТаблицаСписокСчетов.Счет КАК Ссылка
		|						ИЗ
		|							ВременнаяТаблицаСписокСчетов КАК ВременнаяТаблицаСписокСчетов)
		|					И СчетДт <> ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СводДоходовИРасходов),
		|				,
		|				СчетКт <> ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СводДоходовИРасходов),
		|				,
		|				Организация = &Организация) КАК ХозрасчетныйОборотыДтКт
		|		ПО ВременнаяТаблицаСписокСчетов.Счет = ХозрасчетныйОборотыДтКт.СчетДт
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаСписокСчетов.СчетРодитель,
		|	ХозрасчетныйОборотыДтКт.СчетКт,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаСписокСчетов.Вид = ЗНАЧЕНИЕ(ВидСчета.Пассивный)
		|			ТОГДА ВЫБОР
		|					КОГДА ХозрасчетныйОборотыДтКт.СуммаОборот < 0
		|						ТОГДА -ХозрасчетныйОборотыДтКт.СуммаОборот
		|					ИНАЧЕ ХозрасчетныйОборотыДтКт.СуммаОборот
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаСписокСчетов.Вид = ЗНАЧЕНИЕ(ВидСчета.Активный)
		|			ТОГДА ВЫБОР
		|					КОГДА ХозрасчетныйОборотыДтКт.СуммаОборот < 0
		|						ТОГДА -ХозрасчетныйОборотыДтКт.СуммаОборот
		|					ИНАЧЕ ХозрасчетныйОборотыДтКт.СуммаОборот
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ
		|ИЗ
		|	ВременнаяТаблицаСписокСчетов КАК ВременнаяТаблицаСписокСчетов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(
		|				&НачалоПериода,
		|				&КонецПериода,
		|				,
		|				СчетДт <> ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СводДоходовИРасходов),
		|				,
		|				СчетКт В
		|						(ВЫБРАТЬ
		|							ВременнаяТаблицаСписокСчетов.Счет КАК Ссылка
		|						ИЗ
		|							ВременнаяТаблицаСписокСчетов КАК ВременнаяТаблицаСписокСчетов)
		|					И СчетКт <> ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасходыДоходыПоНалогуНаПрибыль),
		|				,
		|				Организация = &Организация) КАК ХозрасчетныйОборотыДтКт
		|		ПО ВременнаяТаблицаСписокСчетов.Счет = ХозрасчетныйОборотыДтКт.СчетКт
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаОбороты.СчетРодитель КАК СчетРодитель,
		|	ВременнаяТаблицаОбороты.Счет КАК Счет,
		|	ВременнаяТаблицаОбороты.СуммаДоходов КАК СуммаДоходов
		|ИЗ
		|	ВременнаяТаблицаОбороты КАК ВременнаяТаблицаОбороты
		|ГДЕ
		|	ВременнаяТаблицаОбороты.СуммаДоходов <> 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	СчетРодитель,
		|	Счет
		|ИТОГИ
		|	СУММА(СуммаДоходов)
		|ПО
		|	ОБЩИЕ,
		|	СчетРодитель,
		|	Счет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаОбороты.СчетРодитель КАК СчетРодитель,
		|	ВременнаяТаблицаОбороты.Счет КАК Счет,
		|	ВременнаяТаблицаОбороты.СуммаРасходов КАК СуммаРасходов
		|ИЗ
		|	ВременнаяТаблицаОбороты КАК ВременнаяТаблицаОбороты
		|ГДЕ
		|	ВременнаяТаблицаОбороты.СуммаРасходов <> 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	СчетРодитель,
		|	Счет
		|ИТОГИ
		|	СУММА(СуммаРасходов)
		|ПО
		|	ОБЩИЕ,
		|	СчетРодитель,
		|	Счет";
		
	Запрос.УстановитьПараметр("НачалоПериода", 	Параметры.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", 	Параметры.КонецПериода);
	Запрос.УстановитьПараметр("Организация", 	Параметры.Организация);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ТаблицаСчетовИНаименований = МассивРезультатов[1].Выгрузить();
	
	СуммаРасходов = 0;
	СуммаДоходов  = 0;	
	
	ВыборкаРасходы_1Уровень = МассивРезультатов[4].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаРасходы_1Уровень.Следующий() Цикл
		
		ДеревоЗначений = РеквизитФормыВЗначение("ТаблицаРасходы");
		
		СтрокаТаблицы_1Уровень = ДеревоЗначений.Строки.Добавить();
		СтрокаТаблицы_1Уровень.Наименование = НСтр("ru = 'Всего расходы'");
		СтрокаТаблицы_1Уровень.Сумма = ВыборкаРасходы_1Уровень.СуммаРасходов;
		СуммаРасходов = ВыборкаРасходы_1Уровень.СуммаРасходов;
		
		ВыборкаРасходы_2Уровень = ВыборкаРасходы_1Уровень.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаРасходы_2Уровень.Следующий() Цикл
			
			СтрокаТаблицы_2Уровень = СтрокаТаблицы_1Уровень.Строки.Добавить();
			СтрокаТаблицы_2Уровень.Счет = ВыборкаРасходы_2Уровень.СчетРодитель;
			СтрокаТаблицы_2Уровень.Наименование = ТаблицаСчетовИНаименований.Найти(ВыборкаРасходы_2Уровень.СчетРодитель, "Счет").Наименование;
			СтрокаТаблицы_2Уровень.Сумма = ВыборкаРасходы_2Уровень.СуммаРасходов;
			
			ВыборкаРасходы_3Уровень = ВыборкаРасходы_2Уровень.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаРасходы_3Уровень.Следующий() Цикл
				
				СтрокаТаблицы_3Уровень = СтрокаТаблицы_2Уровень.Строки.Добавить();
				СтрокаТаблицы_3Уровень.Счет = ВыборкаРасходы_3Уровень.Счет;
				СтрокаТаблицы_3Уровень.Наименование = ТаблицаСчетовИНаименований.Найти(ВыборкаРасходы_3Уровень.Счет, "Счет").Наименование;
				СтрокаТаблицы_3Уровень.Сумма = ВыборкаРасходы_3Уровень.СуммаРасходов;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ДеревоЗначений, "ТаблицаРасходы");
	
	ВыборкаДоходы_1Уровень = МассивРезультатов[3].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаДоходы_1Уровень.Следующий() Цикл
		
		ДеревоЗначений = РеквизитФормыВЗначение("ТаблицаДоходы");
		
		СтрокаТаблицы_1Уровень = ДеревоЗначений.Строки.Добавить();
		СтрокаТаблицы_1Уровень.Наименование = НСтр("ru = 'Всего Доходы'");
		СтрокаТаблицы_1Уровень.Сумма = ВыборкаДоходы_1Уровень.СуммаДоходов;
		СуммаДоходов = ВыборкаДоходы_1Уровень.СуммаДоходов;
		
		ВыборкаДоходы_2Уровень = ВыборкаДоходы_1Уровень.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаДоходы_2Уровень.Следующий() Цикл
			
			СтрокаТаблицы_2Уровень = СтрокаТаблицы_1Уровень.Строки.Добавить();
			СтрокаТаблицы_2Уровень.Счет = ВыборкаДоходы_2Уровень.СчетРодитель;
			СтрокаТаблицы_2Уровень.Наименование = ТаблицаСчетовИНаименований.Найти(ВыборкаДоходы_2Уровень.СчетРодитель, "Счет").Наименование;
			СтрокаТаблицы_2Уровень.Сумма = ВыборкаДоходы_2Уровень.СуммаДоходов;
			
			ВыборкаДоходы_3Уровень = ВыборкаДоходы_2Уровень.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаДоходы_3Уровень.Следующий() Цикл
				
				СтрокаТаблицы_3Уровень = СтрокаТаблицы_2Уровень.Строки.Добавить();
				СтрокаТаблицы_3Уровень.Счет = ВыборкаДоходы_3Уровень.Счет;
				СтрокаТаблицы_3Уровень.Наименование = ТаблицаСчетовИНаименований.Найти(ВыборкаДоходы_3Уровень.Счет, "Счет").Наименование;
				СтрокаТаблицы_3Уровень.Сумма = ВыборкаДоходы_3Уровень.СуммаДоходов;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ДеревоЗначений, "ТаблицаДоходы");
	
	Прибыль 				= СуммаДоходов - СуммаРасходов;
	СтавкаНалогаНаПрибыль 	= Строка(Параметры.СтавкаНалогаНаПрибыль) + "%";
	НалогНаПрибыль			= Прибыль / 100 * Параметры.СтавкаНалогаНаПрибыль;	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Печать(Команда)
	ТабличныйДокумент = Новый ТабличныйДокумент();
	
	ПечатьНаСервере(ТабличныйДокумент);
	
	ТабличныйДокумент.ОтображатьСетку = Ложь;
	ТабличныйДокумент.ОтображатьЗаголовки = Ложь;
	ТабличныйДокумент.Показать();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПечатьНаСервере(ТабличныйДокумент)
	
	Макет = Документы.ТекущийРасчетНалогаНаПрибыль.ПолучитьМакет("ПФ_MXL_Расшифровка");
	
	ДанныеПечати = Новый Структура();
	ДанныеПечати.Вставить("НачалоПериода", 			 Формат(НачалоПериода, "ДЛФ=D"));
	ДанныеПечати.Вставить("КонецПериода", 			 Формат(КонецПериода, "ДЛФ=D"));
	ДанныеПечати.Вставить("НаименованиеОрганизации", ?(Организация.НаименованиеПолное = "", 
									Организация.Наименование, Организация.НаименованиеПолное));
									
	ДанныеПечати.Вставить("НаименованиеТаблицы", "");
	ДанныеПечати.Вставить("ИтогЗаголовок", "");
	
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	ОбластьЗаголовокТаблицы 	= Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьШапкаТаблицы 		= Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьИтогТаблицы 			= Макет.ПолучитьОбласть("ИтогТаблицы");
	ОбластьИтогПоСчетуРодителю 	= Макет.ПолучитьОбласть("ИтогПоСчетуРодителю");
	ОбластьСтрока 				= Макет.ПолучитьОбласть("Строка");
	ОбластьПустаяСтрока 		= Макет.ПолучитьОбласть("ПустаяСтрока");
	
	ДанныеПечати.НаименованиеТаблицы = НСтр("ru = 'Расходы'");
	
	ОбластьЗаголовокТаблицы.Параметры.Заполнить(ДанныеПечати);
	ТабличныйДокумент.Вывести(ОбластьЗаголовокТаблицы);
	
	ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
	
	ДеревоЗначений = РеквизитФормыВЗначение("ТаблицаРасходы");
	
	Для Каждого СтрокаТаблицы Из ДеревоЗначений.Строки Цикл
		
		ДанныеПечати.ИтогЗаголовок = НСтр("ru = 'Всего расходы:'");
		ОбластьИтогТаблицы.Параметры.Заполнить(ДанныеПечати);
		ОбластьИтогТаблицы.Параметры.Заполнить(СтрокаТаблицы);
		ТабличныйДокумент.Вывести(ОбластьИтогТаблицы);
		
		Строки2Уровня = СтрокаТаблицы.Строки;	
		
		Для Каждого СтрокаТаблицы2Уровня Из Строки2Уровня Цикл
			
			ОбластьИтогПоСчетуРодителю.Параметры.Заполнить(СтрокаТаблицы2Уровня);
			ТабличныйДокумент.Вывести(ОбластьИтогПоСчетуРодителю);
			
			Строки3Уровня = СтрокаТаблицы2Уровня.Строки;
			
			Для Каждого СтрокаТаблицы3Уровня Из Строки3Уровня Цикл
		    	ОбластьСтрока.Параметры.Заполнить(СтрокаТаблицы3Уровня);
				ТабличныйДокумент.Вывести(ОбластьСтрока);
			КонецЦикла;	
		КонецЦикла;			
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ДеревоЗначений, "ТаблицаРасходы");
	
	ТабличныйДокумент.Вывести(ОбластьПустаяСтрока);
	
	ДанныеПечати.НаименованиеТаблицы = НСтр("ru = 'Доходы'");
	
	ОбластьЗаголовокТаблицы.Параметры.Заполнить(ДанныеПечати);
	ТабличныйДокумент.Вывести(ОбластьЗаголовокТаблицы);
	
	ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
	
	ДеревоЗначений = РеквизитФормыВЗначение("ТаблицаДоходы");
	
	Для Каждого СтрокаТаблицы Из ДеревоЗначений.Строки Цикл
		
		ДанныеПечати.ИтогЗаголовок = НСтр("ru = 'Всего доходы:'");
		ОбластьИтогТаблицы.Параметры.Заполнить(ДанныеПечати);
		ОбластьИтогТаблицы.Параметры.Заполнить(СтрокаТаблицы);
		ТабличныйДокумент.Вывести(ОбластьИтогТаблицы);
		
		Строки2Уровня = СтрокаТаблицы.Строки;	
		
		Для Каждого СтрокаТаблицы2Уровня Из Строки2Уровня Цикл
			
			ОбластьИтогПоСчетуРодителю.Параметры.Заполнить(СтрокаТаблицы2Уровня);
			ТабличныйДокумент.Вывести(ОбластьИтогПоСчетуРодителю);
			
			Строки3Уровня = СтрокаТаблицы2Уровня.Строки;
			
			Для Каждого СтрокаТаблицы3Уровня Из Строки3Уровня Цикл
		    	ОбластьСтрока.Параметры.Заполнить(СтрокаТаблицы3Уровня);
				ТабличныйДокумент.Вывести(ОбластьСтрока);
			КонецЦикла;	
		КонецЦикла;			
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ДеревоЗначений, "ТаблицаДоходы");
	
	ТабличныйДокумент.Вывести(ОбластьПустаяСтрока);
	
	ДанныеПечати.Вставить("СуммаПрибыли", 		  Прибыль);
	ДанныеПечати.Вставить("Ставка", 			  СтавкаНалогаНаПрибыль);
	ДанныеПечати.Вставить("СуммаНалогаНаПрибыль", НалогНаПрибыль);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
	ТабличныйДокумент.Вывести(ОбластьМакета);
КонецПроцедуры

#КонецОбласти
