#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПроцедурыПроведенияДокумента

// Формирует таблицу значений, содержащую данные для проведения по регистру бухгалтерии Хозрасчетный.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	// 1. Оплата поставщику, Возврат покупателю, Расчеты по займам, Оплата налогов
	// 2. Выдача подотчетнику
	// 3. Перевод на другой счет
	// 4. Выплата ЗП
	// 5. Прочий расход
	// 6. Комиссия банка
	
	// Оплата поставщику, Возврат покупателю, Расчеты по займам, Оплата налогов
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	1 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППИ.ОплатаНалогов)
		|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.СчетРасчетов
		|		ИНАЧЕ ВременнаяТаблицаШапка.СчетРасчетов
		|	КОНЕЦ КАК СчетДт,
		|	ВременнаяТаблицаШапка.БанковскийСчет.СчетУчета КАК СчетКт,
		|	ВременнаяТаблицаШапка.Контрагент КАК СубконтоДт1,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	ВременнаяТаблицаШапка.БанковскийСчет КАК СубконтоКт1,
		|	ВременнаяТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.ВалютаДокумента = ВременнаяТаблицаШапка.ВалютаРегламентированногоУчета
		|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа
		|		КОГДА ВременнаяТаблицаШапка.ВалютаРасчетов = ВременнаяТаблицаШапка.ВалютаРегламентированногоУчета
		|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов
		|		ИНАЧЕ ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов * ВременнаяТаблицаШапка.КурсВзаиморасчетовПоНацБанку / ВременнаяТаблицаШапка.КратностьВзаиморасчетовПоНацБанку
		|	КОНЕЦ КАК Сумма,
		|	ВременнаяТаблицаШапка.ВалютаРасчетов КАК ВалютаДт,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаКт,
		|	ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов КАК ВалютнаяСуммаДт,
		|	ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа КАК ВалютнаяСуммаКт,
		|	ПРЕДСТАВЛЕНИЕ(ВременнаяТаблицаШапка.ВидОперации) КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаРасшифровкаПлатежа КАК ВременнаяТаблицаРасшифровкаПлатежа
		|		ПО (ИСТИНА)
		|ГДЕ
		|	(ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППИ.ОплатаПоставщику)
		|			ИЛИ ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППИ.ВозвратПокупателю)
		|			ИЛИ ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППИ.РасчетыПоЗаймам)
		|			ИЛИ ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППИ.ОплатаНалогов))";
	ТекстЗапроса = ТекстЗапроса + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|";
	
	// Выдача подотчетнику.
	ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	2 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.СчетРасчетов КАК СчетДт,
		|	ВременнаяТаблицаШапка.БанковскийСчет.СчетУчета КАК СчетКт,
		|	ВременнаяТаблицаШапка.ФизЛицо КАК СубконтоДт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	ВременнаяТаблицаШапка.БанковскийСчет КАК СубконтоКт1,
		|	ВременнаяТаблицаРасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК Сумма,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаДт,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаКт,
		|	ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа КАК ВалютнаяСуммаДт,
		|	ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа КАК ВалютнаяСуммаКт,
		|	ПРЕДСТАВЛЕНИЕ(ВременнаяТаблицаШапка.ВидОперации) КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаРасшифровкаПлатежа КАК ВременнаяТаблицаРасшифровкаПлатежа
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППИ.ВыдачаПодотчетнику)";
	ТекстЗапроса = ТекстЗапроса + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|";
	
	// Перевод на другой счет.
	ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	3 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.БанковскийСчетПолучателя.СчетУчета КАК СчетДт,
		|	ВременнаяТаблицаШапка.БанковскийСчет.СчетУчета КАК СчетКт,
		|	ВременнаяТаблицаШапка.БанковскийСчетПолучателя КАК СубконтоДт1,
		|	&СтатьяДДСДляВнутреннихОборотов КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	ВременнаяТаблицаШапка.БанковскийСчет КАК СубконтоКт1,
		|	&СтатьяДДСДляВнутреннихОборотов КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК Сумма,
		|	ВременнаяТаблицаШапка.БанковскийСчетПолучателя.ВалютаДенежныхСредств КАК ВалютаДт,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаКт,
		|	ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа КАК ВалютнаяСуммаДт,
		|	ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа КАК ВалютнаяСуммаКт,
		|	ПРЕДСТАВЛЕНИЕ(ВременнаяТаблицаШапка.ВидОперации) КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаРасшифровкаПлатежа КАК ВременнаяТаблицаРасшифровкаПлатежа
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППИ.ПереводНаДругойСчет)";
	ТекстЗапроса = ТекстЗапроса + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|";
	
	// Выплата ЗП.
	ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	4 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППИ.ВыплатаЗП)
		|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НачисленнаяЗаработнаяПлата)
		|		ИНАЧЕ ВременнаяТаблицаШапка.СчетРасчетов
		|	КОНЕЦ КАК СчетДт,
		|	ВременнаяТаблицаШапка.БанковскийСчет.СчетУчета КАК СчетКт,
		|	ВременнаяТаблицаВыплатаЗаработнойПлаты.ФизЛицо КАК СубконтоДт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	ВременнаяТаблицаШапка.БанковскийСчет КАК СубконтоКт1,
		|	ВременнаяТаблицаШапка.СтатьяДвиженияДенежныхСредств КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППИ.ВыплатаЗП)
		|			ТОГДА ВременнаяТаблицаВыплатаЗаработнойПлаты.СуммаПлатежа * ВременнаяТаблицаШапка.КурсВзаиморасчетов / ВременнаяТаблицаШапка.КратностьВзаиморасчетов
		|		ИНАЧЕ ВременнаяТаблицаВыплатаЗаработнойПлаты.СуммаПлатежа * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность
		|	КОНЕЦ КАК Сумма,
		|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаКт,
		|	0 КАК ВалютнаяСуммаДт,
		|	ВременнаяТаблицаВыплатаЗаработнойПлаты.СуммаПлатежа КАК ВалютнаяСуммаКт,
		|	ПРЕДСТАВЛЕНИЕ(ВременнаяТаблицаШапка.ВидОперации) КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаВыплатаЗаработнойПлаты КАК ВременнаяТаблицаВыплатаЗаработнойПлаты
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППИ.ВыплатаЗП)
		|	ИЛИ ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППИ.ВыплатаДепонированнойЗП)";
	ТекстЗапроса = ТекстЗапроса + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|";
	
	// Прочий расход.
	ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	5 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаПрочиеПлатежи.СчетРасчетов КАК СчетДт,
		|	ВременнаяТаблицаШапка.БанковскийСчет.СчетУчета КАК СчетКт,
		|	ВременнаяТаблицаПрочиеПлатежи.Субконто1 КАК СубконтоДт1,
		|	ВременнаяТаблицаПрочиеПлатежи.Субконто2 КАК СубконтоДт2,
		|	ВременнаяТаблицаПрочиеПлатежи.Субконто3 КАК СубконтоДт3,
		|	ВременнаяТаблицаШапка.БанковскийСчет КАК СубконтоКт1,
		|	ВременнаяТаблицаПрочиеПлатежи.СтатьяДвиженияДенежныхСредств КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	ВременнаяТаблицаПрочиеПлатежи.СуммаПлатежа * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК Сумма,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаДт,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаКт,
		|	ВременнаяТаблицаПрочиеПлатежи.СуммаПлатежа КАК ВалютнаяСуммаДт,
		|	ВременнаяТаблицаПрочиеПлатежи.СуммаПлатежа КАК ВалютнаяСуммаКт,
		|	ПРЕДСТАВЛЕНИЕ(ВременнаяТаблицаШапка.ВидОперации) КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаПрочиеПлатежи КАК ВременнаяТаблицаПрочиеПлатежи
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППИ.ПрочийРасход)";
	ТекстЗапроса = ТекстЗапроса + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|";
	
	// Комиссия банка.
	ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	6 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.СчетЗатратКомиссияБанка КАК СчетДт,
		|	ВременнаяТаблицаШапка.БанковскийСчет.СчетУчета КАК СчетКт,
		|	ВременнаяТаблицаШапка.СтатьяЗатратКомиссияБанка КАК СубконтоДт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	ВременнаяТаблицаШапка.БанковскийСчет КАК СубконтоКт1,
		|	ВременнаяТаблицаШапка.СтатьяДвиженияДенежныхСредствКомиссияБанка КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	ВременнаяТаблицаШапка.СуммаКомиссияБанка * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК Сумма,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаДт,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаКт,
		|	ВременнаяТаблицаШапка.СуммаКомиссияБанка КАК ВалютнаяСуммаДт,
		|	ВременнаяТаблицаШапка.СуммаКомиссияБанка КАК ВалютнаяСуммаКт,
		|	&СодержаниеКомиссияБанка КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|ГДЕ
		|	ВременнаяТаблицаШапка.СниматьКомиссиюБанка
		|	И НЕ ВременнаяТаблицаШапка.СуммаКомиссияБанка = 0";
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("СтатьяДДСДляВнутреннихОборотов", БухгалтерскийУчетСервер.СтатьяДДСДляВнутреннихОборотов());
	Запрос.УстановитьПараметр("СодержаниеКомиссияБанка", НСтр("ru = 'Комиссия банка'")); 
	РезультатЗапроса = Запрос.Выполнить();	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаХозрасчетный", РезультатЗапроса.Выгрузить());
	
	// Подготовка таблицы для расчета операционных курсовых разниц.
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.Контрагент КАК Контрагент,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	ВременнаяТаблицаШапка.СчетРасчетов КАК СчетРасчетов,
		|	ВременнаяТаблицаШапка.БанковскийСчет КАК КассаБанковскийСчет,
		|	ВременнаяТаблицаШапка.БанковскийСчет.СчетУчета КАК ДенежныйСчет,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаДокумента,
		|	ВременнаяТаблицаШапка.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета,
		|	ВременнаяТаблицаШапка.ВалютаРасчетов КАК ВалютаРасчетов,
		|	ВременнаяТаблицаШапка.Курс КАК КурсДокумента,
		|	ВременнаяТаблицаШапка.Кратность КАК КратностьДокумента,
		|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа) КАК СуммаПлатежа,
		|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов) КАК СуммаВзаиморасчетов,
		|	ВременнаяТаблицаШапка.КурсВзаиморасчетовПоНацБанку КАК КурсВзаиморасчетовПоНацБанку,
		|	ВременнаяТаблицаШапка.КратностьВзаиморасчетовПоНацБанку КАК КратностьВзаиморасчетовПоНацБанку
		|ИЗ
		|	ВременнаяТаблицаРасшифровкаПлатежа КАК ВременнаяТаблицаРасшифровкаПлатежа
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СчетаУчетаСОсобымПорядкомПереоценки КАК СчетаУчетаСОсобымПорядкомПереоценки
		|		ПО (ВременнаяТаблицаШапка.БанковскийСчет.СчетУчета = СчетаУчетаСОсобымПорядкомПереоценки.СчетУчета)
		|ГДЕ
		|	НЕ ВременнаяТаблицаШапка.ВалютаРасчетов = ВременнаяТаблицаШапка.ВалютаДокумента
		|	И НЕ ВременнаяТаблицаШапка.КурсВзаиморасчетов = ВременнаяТаблицаШапка.КурсВзаиморасчетовПоНацБанку
		|	И НЕ ЕСТЬNULL(СчетаУчетаСОсобымПорядкомПереоценки.НеСчитатьОКР, ЛОЖЬ) = ИСТИНА
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.Контрагент,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента,
		|	ВременнаяТаблицаШапка.СчетРасчетов,
		|	ВременнаяТаблицаШапка.БанковскийСчет,
		|	ВременнаяТаблицаШапка.БанковскийСчет.СчетУчета,
		|	ВременнаяТаблицаШапка.ВалютаДокумента,
		|	ВременнаяТаблицаШапка.ВалютаРегламентированногоУчета,
		|	ВременнаяТаблицаШапка.ВалютаРасчетов,
		|	ВременнаяТаблицаШапка.Курс,
		|	ВременнаяТаблицаШапка.Кратность,
		|	ВременнаяТаблицаШапка.КурсВзаиморасчетовПоНацБанку,
		|	ВременнаяТаблицаШапка.КратностьВзаиморасчетовПоНацБанку
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.Контрагент КАК Контрагент,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	ВременнаяТаблицаШапка.СчетРасчетов КАК СчетРасчетов,
		|	ВременнаяТаблицаШапка.БанковскийСчет КАК КассаБанковскийСчет,
		|	ВременнаяТаблицаШапка.БанковскийСчет.СчетУчета КАК ДенежныйСчет,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаДокумента,
		|	ВременнаяТаблицаШапка.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета,
		|	ВременнаяТаблицаШапка.ВалютаРасчетов КАК ВалютаРасчетов,
		|	ВременнаяТаблицаШапка.КурсВзаиморасчетов КАК КурсДокумента,
		|	ВременнаяТаблицаШапка.КратностьВзаиморасчетов КАК КратностьДокумента,
		|	СУММА(ВременнаяТаблицаВыплатаЗаработнойПлаты.СуммаПлатежа) КАК СуммаПлатежа,
		|	СУММА(ВременнаяТаблицаВыплатаЗаработнойПлаты.СуммаПлатежа * ВременнаяТаблицаШапка.КурсВзаиморасчетов / ВременнаяТаблицаШапка.КратностьВзаиморасчетов) КАК СуммаВзаиморасчетов,
		|	ВременнаяТаблицаШапка.КурсВзаиморасчетовПоНацБанку КАК КурсВзаиморасчетовПоНацБанку,
		|	ВременнаяТаблицаШапка.КратностьВзаиморасчетовПоНацБанку КАК КратностьВзаиморасчетовПоНацБанку
		|ИЗ
		|	ВременнаяТаблицаВыплатаЗаработнойПлаты КАК ВременнаяТаблицаВыплатаЗаработнойПлаты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СчетаУчетаСОсобымПорядкомПереоценки КАК СчетаУчетаСОсобымПорядкомПереоценки
		|		ПО (ВременнаяТаблицаШапка.БанковскийСчет.СчетУчета = СчетаУчетаСОсобымПорядкомПереоценки.СчетУчета)
		|ГДЕ
		|	ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППИ.ВыплатаЗП)
		|	И НЕ ВременнаяТаблицаШапка.ВалютаРасчетов = ВременнаяТаблицаШапка.ВалютаДокумента
		|	И НЕ ВременнаяТаблицаШапка.КурсВзаиморасчетов = ВременнаяТаблицаШапка.КурсВзаиморасчетовПоНацБанку
		|	И НЕ ЕСТЬNULL(СчетаУчетаСОсобымПорядкомПереоценки.НеСчитатьОКР, ЛОЖЬ) = ИСТИНА
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.Контрагент,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента,
		|	ВременнаяТаблицаШапка.СчетРасчетов,
		|	ВременнаяТаблицаШапка.БанковскийСчет,
		|	ВременнаяТаблицаШапка.БанковскийСчет.СчетУчета,
		|	ВременнаяТаблицаШапка.ВалютаДокумента,
		|	ВременнаяТаблицаШапка.ВалютаРегламентированногоУчета,
		|	ВременнаяТаблицаШапка.ВалютаРасчетов,
		|	ВременнаяТаблицаШапка.КурсВзаиморасчетов,
		|	ВременнаяТаблицаШапка.КратностьВзаиморасчетов,
		|	ВременнаяТаблицаШапка.КурсВзаиморасчетовПоНацБанку,
		|	ВременнаяТаблицаШапка.КратностьВзаиморасчетовПоНацБанку";
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДляРасчетаОперационныхКурсовыхРазниц", Запрос.Выполнить().Выгрузить());
КонецПроцедуры // СформироватьТаблицаДенежныеСредства()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаВыплаченнаяЗарплата(ДокументСсылка, СтруктураДополнительныеСвойства)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =		     
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаВыплатаЗаработнойПлаты.ФизЛицо,
		|	ВЫРАЗИТЬ(ВременнаяТаблицаШапка.ДокументОснование КАК Документ.ВедомостьЗаработнойПлаты).ПериодРегистрации КАК ПериодРегистрации,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыВыплатыЗарплаты.ЧерезБанк) КАК ВидВыплаты,
		|	ВременнаяТаблицаВыплатаЗаработнойПлаты.СуммаПлатежа * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность КАК Сумма
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаВыплатаЗаработнойПлаты КАК ВременнаяТаблицаВыплатаЗаработнойПлаты
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППИ.ВыплатаЗП)
		|	ИЛИ ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППИ.ВыплатаДепонированнойЗП)";
	РезультатЗапроса = Запрос.Выполнить();
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаВыплаченнаяЗарплата", РезультатЗапроса.Выгрузить());
КонецПроцедуры // СформироватьТаблицаВыплаченнаяЗарплата()

// Формирует таблицу значений, содержащую данные для проведения по регистру накопления ДанныеРеестраГТД.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДанныеРеестраГТД(ДокументСсылка,СтруктураДополнительныеСвойства)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.ДокументОснование КАК Документ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.ВалютаДокумента = ВременнаяТаблицаШапка.ВалютаРегламентированногоУчета
		|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа
		|		КОГДА ВременнаяТаблицаШапка.ВалютаРасчетов = ВременнаяТаблицаШапка.ВалютаРегламентированногоУчета
		|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов
		|		ИНАЧЕ ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность
		|	КОНЕЦ КАК Сумма
		|ИЗ
		|	ВременнаяТаблицаРасшифровкаПлатежа КАК ВременнаяТаблицаРасшифровкаПлатежа
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППИ.ОплатаПоставщику)
		|	И ВременнаяТаблицаШапка.ДокументОснование ССЫЛКА Документ.ПоступлениеТоваровУслуг
		|	И НЕ ВременнаяТаблицаШапка.ДокументОснование = ЗНАЧЕНИЕ(Документ.ПоступлениеТоваровУслуг.ПустаяСсылка)
		|	И НЕ ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа = 0";
	РезультатЗапроса = Запрос.Выполнить();	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДанныеРеестраГТД", РезультатЗапроса.Выгрузить());
КонецПроцедуры

// Формирует таблицу значений, содержащую данные для проведения по регистру "АвансыПодотчетника".
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаАвансыПодотчетника(ДокументСсылка,СтруктураДополнительныеСвойства)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		// Удалить после обновления на 3.1.11
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.ФизЛицо КАК ФизЛицо,
		|	ВременнаяТаблицаШапка.СчетРасчетов КАК СчетУчета,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК Валюта,
		|	ВременнаяТаблицаШапка.Дата КАК ДатаАванса,
		|	ВременнаяТаблицаШапка.Ссылка КАК ДокументАванса,
		|	ВременнаяТаблицаШапка.СуммаДокумента КАК Сумма
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|ГДЕ
		|	ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППИ.ВыдачаПодотчетнику)";	
	РезультатЗапроса = Запрос.Выполнить();	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаАвансыПодотчетника", РезультатЗапроса.Выгрузить());
КонецПроцедуры

// Формирует таблицу значений, содержащую данные для проведения по регистру накопления ОборотыПоДаннымЕдиногоНалога.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаОборотыПоДаннымЕдиногоНалога(СтруктураДополнительныеСвойства)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаРасшифровкаПлатежа.ВидДеятельности КАК ВидДеятельности,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаШапка.ВалютаДокумента = ВременнаяТаблицаШапка.ВалютаРегламентированногоУчета
		|			ТОГДА -ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа
		|		КОГДА ВременнаяТаблицаШапка.ВалютаРасчетов = ВременнаяТаблицаШапка.ВалютаРегламентированногоУчета
		|			ТОГДА -ВременнаяТаблицаРасшифровкаПлатежа.СуммаВзаиморасчетов
		|		ИНАЧЕ -ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВременнаяТаблицаШапка.Курс / ВременнаяТаблицаШапка.Кратность
		|	КОНЕЦ КАК СуммаБезналичная
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаРасшифровкаПлатежа КАК ВременнаяТаблицаРасшифровкаПлатежа
		|		ПО (ИСТИНА)
		|ГДЕ
		|	&ПлательщикЕН
		|	И ВременнаяТаблицаШапка.ВидОперации В(&ВидыОпераций)";
	Запрос.УстановитьПараметр("ПлательщикЕН", СтруктураДополнительныеСвойства.УчетнаяПолитика.ПлательщикЕНКассовыйМетод);	
	Запрос.УстановитьПараметр("ВидыОпераций", ОбщегоНазначенияБПСервер.ВидыОперацийЕдиныйНалог());	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ОборотыПоДаннымЕдиногоНалога", РезультатЗапроса.Выгрузить());
КонецПроцедуры

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДокумента.Ссылка КАК Ссылка,
		|	ТаблицаДокумента.Дата КАК Дата,
		|	ТаблицаДокумента.Организация КАК Организация,
		|	ТаблицаДокумента.ВидОперации КАК ВидОперации,
		|	ТаблицаДокумента.БанковскийСчет КАК БанковскийСчет,
		|	ТаблицаДокумента.ВалютаДокумента КАК ВалютаДокумента,
		|	&Курс КАК Курс,
		|	&Кратность КАК Кратность,
		|	ТаблицаДокумента.Контрагент КАК Контрагент,
		|	ТаблицаДокумента.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	ТаблицаДокумента.ВалютаРасчетов КАК ВалютаРасчетов,
		|	&КурсВзаиморасчетовПоНацБанку КАК КурсВзаиморасчетовПоНацБанку,
		|	&КратностьВзаиморасчетовПоНацБанку КАК КратностьВзаиморасчетовПоНацБанку,
		|	ТаблицаДокумента.СчетРасчетов КАК СчетРасчетов,
		|	ТаблицаДокумента.КурсВзаиморасчетов КАК КурсВзаиморасчетов,
		|	ТаблицаДокумента.КратностьВзаиморасчетов КАК КратностьВзаиморасчетов,
		|	ТаблицаДокумента.ФизЛицо КАК ФизЛицо,
		|	ТаблицаДокумента.БанковскийСчетПолучателя КАК БанковскийСчетПолучателя,
		|	ТаблицаДокумента.СуммаДокумента КАК СуммаДокумента,
		|	ТаблицаДокумента.СниматьКомиссиюБанка КАК СниматьКомиссиюБанка,
		|	ТаблицаДокумента.СчетЗатратКомиссияБанка КАК СчетЗатратКомиссияБанка,
		|	ТаблицаДокумента.СтатьяЗатратКомиссияБанка КАК СтатьяЗатратКомиссияБанка,
		|	ТаблицаДокумента.СтатьяДвиженияДенежныхСредствКомиссияБанка КАК СтатьяДвиженияДенежныхСредствКомиссияБанка,
		|	ТаблицаДокумента.СуммаКомиссияБанка КАК СуммаКомиссияБанка,
		|	ТаблицаДокумента.ДокументОснование КАК ДокументОснование,
		|	ТаблицаДокумента.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|	&ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета
		|ПОМЕСТИТЬ ВременнаяТаблицаШапка
		|ИЗ
		|	Документ.ПлатежноеПоручениеИсходящее КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.ВидДеятельности КАК ВидДеятельности,
		|	ТаблицаДокумента.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|	ТаблицаДокумента.СуммаПлатежа КАК СуммаПлатежа,
		|	ТаблицаДокумента.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
		|	ТаблицаДокумента.СчетРасчетов КАК СчетРасчетов
		|ПОМЕСТИТЬ ВременнаяТаблицаРасшифровкаПлатежа
		|ИЗ
		|	Документ.ПлатежноеПоручениеИсходящее.РасшифровкаПлатежа КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|	ТаблицаДокумента.СуммаПлатежа КАК СуммаПлатежа,
		|	ТаблицаДокумента.СчетРасчетов КАК СчетРасчетов,
		|	ТаблицаДокумента.Субконто1 КАК Субконто1,
		|	ТаблицаДокумента.Субконто2 КАК Субконто2,
		|	ТаблицаДокумента.Субконто3 КАК Субконто3
		|ПОМЕСТИТЬ ВременнаяТаблицаПрочиеПлатежи
		|ИЗ
		|	Документ.ПлатежноеПоручениеИсходящее.ПрочиеПлатежи КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.ФизЛицо КАК ФизЛицо,
		|	ТаблицаДокумента.СуммаПлатежа КАК СуммаПлатежа
		|ПОМЕСТИТЬ ВременнаяТаблицаВыплатаЗаработнойПлаты
		|ИЗ
		|	Документ.ПлатежноеПоручениеИсходящее.ВыплатаЗаработнойПлаты КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка";				
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", СтруктураДополнительныеСвойства.ДляПроведения.ВалютаРегламентированногоУчета);	
	// Курсы.
	КурсВалютыДокумента = РаботаСКурсамиВалют.ПолучитьКурсВалюты(СтруктураДополнительныеСвойства.ДляПроведения.ВалютаДокумента, 		
		СтруктураДополнительныеСвойства.ДляПроведения.Дата);
	Запрос.УстановитьПараметр("Курс", ?(ЗначениеЗаполнено(КурсВалютыДокумента.Курс), КурсВалютыДокумента.Курс, 1));	
	Запрос.УстановитьПараметр("Кратность", ?(ЗначениеЗаполнено(КурсВалютыДокумента.Кратность), КурсВалютыДокумента.Кратность, 1));	
	
	ВалютаРасчетов =  
	?(СтруктураДополнительныеСвойства.ДляПроведения.ВалютаРасчетов = СтруктураДополнительныеСвойства.ДляПроведения.ВалютаРегламентированногоУчета,
		СтруктураДополнительныеСвойства.ДляПроведения.ВалютаДокумента, 
		СтруктураДополнительныеСвойства.ДляПроведения.ВалютаРасчетов);	

	КурсВалютыРасчетов = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаРасчетов, 		
		СтруктураДополнительныеСвойства.ДляПроведения.Дата);
	Запрос.УстановитьПараметр("КурсВзаиморасчетовПоНацБанку", ?(ЗначениеЗаполнено(КурсВалютыРасчетов.Курс), КурсВалютыРасчетов.Курс, 1));	
	Запрос.УстановитьПараметр("КратностьВзаиморасчетовПоНацБанку", ?(ЗначениеЗаполнено(КурсВалютыРасчетов.Кратность), КурсВалютыРасчетов.Кратность, 1));	

	Запрос.Выполнить();
	
	СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаВыплаченнаяЗарплата(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаДанныеРеестраГТД(ДокументСсылка,СтруктураДополнительныеСвойства);
	СформироватьТаблицаАвансыПодотчетника(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаОборотыПоДаннымЕдиногоНалога(СтруктураДополнительныеСвойства);	
	
КонецПроцедуры // ИнициализироватьДанныеДокумента()

#КонецОбласти

#Область ВерсионированиеОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
//@skip-warning
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область ИнтерфейсПечати

// Функция формирует табличный документ с печатной формой Платежное поручение
//
// Возвращаемое значение:
//  Табличный документ - печатная форма
//
Функция ПечатьПлатежноеПоручение(МассивОбъектов, ОбъектыПечати)
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПлатежноеПоручениеИсходящее.Ссылка КАК Ссылка,
		|	ПлатежноеПоручениеИсходящее.Номер КАК Номер,
		|	ПлатежноеПоручениеИсходящее.Дата КАК Дата,
		|	ПлатежноеПоручениеИсходящее.ВалютаДокумента КАК ВалютаДокумента,
		|	ПлатежноеПоручениеИсходящее.СуммаДокумента КАК СуммаДокумента,
		|	ПлатежноеПоручениеИсходящее.ВидОперации КАК ВидОперации,
		|	ПлатежноеПоручениеИсходящее.ФизЛицо КАК ФизЛицо,
		|	ПлатежноеПоручениеИсходящее.ФизЛицо.ИНН КАК ФизЛицоИНН,
		// Реквизиты организации.
		|	ПлатежноеПоручениеИсходящее.Организация КАК Организация,
		|	ПлатежноеПоручениеИсходящее.Организация.НаименованиеПолное КАК ОрганизацияНаименованиеПолное,
		|	ПлатежноеПоручениеИсходящее.Организация.ИНН КАК ОрганизацияИНН,
		|	ПлатежноеПоручениеИсходящее.Организация.КодПоОКПО КАК ОрганизацияКодПоОКПО,
		|	ПлатежноеПоручениеИсходящее.Организация.РегНомерСоцФонда КАК ОрганизацияРегНомерСоцФонда,
		// Реквизиты банковского счета организации.
		|	ПлатежноеПоручениеИсходящее.БанковскийСчет.НомерСчета КАК БанковскийСчетНомерСчета,
		|	ПлатежноеПоручениеИсходящее.БанковскийСчет.Банк.Наименование КАК БанковскийСчетБанкНаименование,
		|	ЕСТЬNULL(ПлатежноеПоручениеИсходящее.БанковскийСчет.Банк.Адрес, """") КАК БанковскийСчетАдрес,
		|	ПлатежноеПоручениеИсходящее.БанковскийСчет.Банк.Код КАК БанковскийСчетБанкБИК,
		// Реквизиты контрагента.
		|	ПлатежноеПоручениеИсходящее.Контрагент.НаименованиеПолное КАК КонтрагентНаименованиеПолное,
		|	ПлатежноеПоручениеИсходящее.Контрагент.ИНН КАК КонтрагентИНН,
		// Реквизиты банковского счета контрагента.
		|	ПлатежноеПоручениеИсходящее.БанковскийСчетПолучателя.НомерСчета КАК БанковскийСчетПолучателяНомерСчета,
		|	ПлатежноеПоручениеИсходящее.БанковскийСчетПолучателя.Банк.Наименование КАК БанковскийСчетПолучателяБанкНаименование,
		|	ЕСТЬNULL(ПлатежноеПоручениеИсходящее.БанковскийСчетПолучателя.Банк.Адрес, """") КАК БанковскийСчетПолучателяАдрес,
		|	ПлатежноеПоручениеИсходящее.БанковскийСчетПолучателя.Банк.Код КАК БанковскийСчетПолучателяСчетБанкБИК,
		// Реквизиты банковского счета ЗП проекта.
		|	ПлатежноеПоручениеИсходящее.БанковскийСчетЗППроекта КАК БанковскийСчетЗППроекта,
		|	ПлатежноеПоручениеИсходящее.БанковскийСчетЗППроекта.НомерСчета КАК БанковскийСчетЗППроектаНомерСчета,
		|	ПлатежноеПоручениеИсходящее.БанковскийСчетЗППроекта.Банк.Наименование КАК БанковскийСчетЗППроектаБанкНаименование,
		|	ЕСТЬNULL(ПлатежноеПоручениеИсходящее.БанковскийСчетЗППроекта.Банк.Адрес, """") КАК БанковскийСчетЗППроектаАдрес,
		|	ПлатежноеПоручениеИсходящее.БанковскийСчетЗППроекта.Банк.Код КАК БанковскийСчетЗППроектаСчетБанкБИК,
		// Реквизиты платежа.
		|	ПлатежноеПоручениеИсходящее.КодПлатежа КАК КодПлатежа,
		|	ПлатежноеПоручениеИсходящее.НазначениеПлатежа КАК НазначениеПлатежа,
		// Настройки печати.
		|	ПлатежноеПоручениеИсходящее.ПечатьВРамке КАК ПечатьВРамке,
		|	ПлатежноеПоручениеИсходящее.ВтораяПодписьНеПредусмотрена КАК ВтораяПодписьНеПредусмотрена,
		|	ПлатежноеПоручениеИсходящее.ПечатьНаОдномЛистеДваЭкземпляра КАК ПечатьНаОдномЛистеДваЭкземпляра,
		|	ПлатежноеПоручениеИсходящее.ВыплатаЗаработнойПлаты.(
		|		ФизЛицо КАК ФизЛицо,
		|		ФизЛицо.ИНН КАК ФизЛицоИНН,
		|		БанковскийСчет.НомерСчета КАК БанковскийСчетПолучателяНомерСчета,
		|		БанковскийСчет.Банк.Наименование КАК БанковскийСчетПолучателяБанкНаименование,
		|		ЕСТЬNULL(БанковскийСчет.Банк.Адрес, """") КАК БанковскийСчетПолучателяАдрес,
		|		БанковскийСчет.Банк.Код КАК БанковскийСчетПолучателяСчетБанкБИК,
		|	) КАК ВыплатаЗаработнойПлаты
		|ИЗ
		|	Документ.ПлатежноеПоручениеИсходящее КАК ПлатежноеПоручениеИсходящее
		|ГДЕ
		|	ПлатежноеПоручениеИсходящее.Ссылка В(&СписокДокументов)";		
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("СписокДокументов", МассивОбъектов);
	
	Шапка = Запрос.Выполнить().Выбрать();
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПлатежноеПоручениеИсходящее_ПлатежноеПоручение";

	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПлатежноеПоручениеИсходящее.ПФ_MXL_ПлатежноеПоручение");
	
	Пока Шапка.Следующий() Цикл
		Если ТабличныйДокумент.ВысотаТаблицы > 0 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Подготовка данных
		ДанныеПечати = Новый Структура;
		
		ТекстЗаголовка = СформироватьЗаголовокДокумента(Шапка, НСтр("ru = 'ПЛАТЕЖНОЕ ПОРУЧЕНИЕ'"));
		ДанныеПечати.Вставить("ТекстЗаголовка", ТекстЗаголовка);
		ДанныеПечати.Вставить("ДатаДокумента", Формат(Шапка.Дата, "ДЛФ=DD"));
		
		ДанныеПечати.Вставить("ОрганизацияНаименованиеПолное", Шапка.ОрганизацияНаименованиеПолное);
		ДанныеПечати.Вставить("ОрганизацияИНН", Шапка.ОрганизацияИНН);
		ДанныеПечати.Вставить("ОрганизацияКодПоОКПО", Шапка.ОрганизацияКодПоОКПО);
		ДанныеПечати.Вставить("ОрганизацияРегНомерСоцФонда", Шапка.ОрганизацияРегНомерСоцФонда);
		ДанныеПечати.Вставить("БанковскийСчетНомерСчета", Шапка.БанковскийСчетНомерСчета);
		ДанныеПечати.Вставить("БанковскийСчетБанкНаименование", Шапка.БанковскийСчетБанкНаименование + Шапка.БанковскийСчетАдрес);
		ДанныеПечати.Вставить("БанковскийСчетБанкБИК", Шапка.БанковскийСчетБанкБИК);
		
		Если Шапка.ВидОперации = Перечисления.ВидыОперацийППИ.ВыдачаПодотчетнику Тогда
			ДанныеПечати.Вставить("КонтрагентНаименованиеПолное", Шапка.ФизЛицо);
			ДанныеПечати.Вставить("КонтрагентИНН", Шапка.ФизЛицоИНН);
			ДанныеПечати.Вставить("БанковскийСчетПолучателяНомерСчета", Шапка.БанковскийСчетПолучателяНомерСчета);
			ДанныеПечати.Вставить("БанковскийСчетПолучателяБанкНаименование", "" 
									+ Шапка.БанковскийСчетПолучателяБанкНаименование 
									+ Шапка.БанковскийСчетПолучателяАдрес);
			ДанныеПечати.Вставить("БанковскийСчетПолучателяСчетБанкБИК", Шапка.БанковскийСчетПолучателяСчетБанкБИК);
			
		ИначеЕсли Шапка.ВидОперации = Перечисления.ВидыОперацийППИ.ВыплатаЗП
			ИЛИ Шапка.ВидОперации = Перечисления.ВидыОперацийППИ.ВыплатаДепонированнойЗП Тогда
			ТаблицаВыплатаЗаработнойПлаты = Шапка.ВыплатаЗаработнойПлаты.Выбрать();
			
			Если ТаблицаВыплатаЗаработнойПлаты.Количество() = 1 И НЕ ЗначениеЗаполнено(Шапка.БанковскийСчетЗППроекта) Тогда
				ТаблицаВыплатаЗаработнойПлаты.Следующий();
				ДанныеПечати.Вставить("КонтрагентНаименованиеПолное", ТаблицаВыплатаЗаработнойПлаты.ФизЛицо);
				ДанныеПечати.Вставить("КонтрагентИНН", ТаблицаВыплатаЗаработнойПлаты.ФизЛицоИНН);
				ДанныеПечати.Вставить("БанковскийСчетПолучателяНомерСчета", ТаблицаВыплатаЗаработнойПлаты.БанковскийСчетПолучателяНомерСчета);
				ДанныеПечати.Вставить("БанковскийСчетПолучателяБанкНаименование", "" 
										+ ТаблицаВыплатаЗаработнойПлаты.БанковскийСчетПолучателяБанкНаименование 
										+ ТаблицаВыплатаЗаработнойПлаты.БанковскийСчетПолучателяАдрес);
				ДанныеПечати.Вставить("БанковскийСчетПолучателяСчетБанкБИК", ТаблицаВыплатаЗаработнойПлаты.БанковскийСчетПолучателяСчетБанкБИК);
				
			ИначеЕсли ТаблицаВыплатаЗаработнойПлаты.Количество() > 0 И ЗначениеЗаполнено(Шапка.БанковскийСчетЗППроекта) Тогда
				ДанныеПечати.Вставить("КонтрагентНаименованиеПолное", Шапка.БанковскийСчетЗППроектаБанкНаименование);
				ДанныеПечати.Вставить("БанковскийСчетПолучателяНомерСчета", Шапка.БанковскийСчетЗППроектаНомерСчета);
				ДанныеПечати.Вставить("БанковскийСчетПолучателяБанкНаименование", "" 
										+ Шапка.БанковскийСчетЗППроектаБанкНаименование 
										+ Шапка.БанковскийСчетЗППроектаАдрес);
				ДанныеПечати.Вставить("БанковскийСчетПолучателяСчетБанкБИК", Шапка.БанковскийСчетЗППроектаСчетБанкБИК);
			КонецЕсли;	
			
		ИначеЕсли Шапка.ВидОперации = Перечисления.ВидыОперацийППИ.ПереводНаДругойСчет Тогда
			ДанныеПечати.Вставить("КонтрагентНаименованиеПолное", Шапка.ОрганизацияНаименованиеПолное);
			ДанныеПечати.Вставить("КонтрагентИНН", Шапка.КонтрагентИНН);
			ДанныеПечати.Вставить("БанковскийСчетПолучателяНомерСчета", Шапка.БанковскийСчетПолучателяНомерСчета);
			ДанныеПечати.Вставить("БанковскийСчетПолучателяБанкНаименование", "" 
									+ Шапка.БанковскийСчетПолучателяБанкНаименование 
									+ Шапка.БанковскийСчетПолучателяАдрес);
			ДанныеПечати.Вставить("БанковскийСчетПолучателяСчетБанкБИК", Шапка.БанковскийСчетПолучателяСчетБанкБИК);
			
		Иначе	
			ДанныеПечати.Вставить("КонтрагентНаименованиеПолное", Шапка.КонтрагентНаименованиеПолное);
			ДанныеПечати.Вставить("КонтрагентИНН", Шапка.КонтрагентИНН);
			ДанныеПечати.Вставить("БанковскийСчетПолучателяНомерСчета", Шапка.БанковскийСчетПолучателяНомерСчета);
			ДанныеПечати.Вставить("БанковскийСчетПолучателяБанкНаименование", "" 
									+ Шапка.БанковскийСчетПолучателяБанкНаименование 
									+ Шапка.БанковскийСчетПолучателяАдрес);
			ДанныеПечати.Вставить("БанковскийСчетПолучателяСчетБанкБИК", Шапка.БанковскийСчетПолучателяСчетБанкБИК);
		КонецЕсли;
		
		ДанныеПечати.Вставить("КодПлатежа", Шапка.КодПлатежа);
		ДанныеПечати.Вставить("НазначениеПлатежа", Шапка.НазначениеПлатежа);
		
		ДанныеПечати.Вставить("СуммаДокумента", Формат(Шапка.СуммаДокумента, "ЧЦ=15; ЧДЦ=2; ЧРД=-; ЧГ=0"));
		ДанныеПечати.Вставить("СуммаПрописью", БухгалтерскийУчетСервер.СформироватьСуммуПрописью(Шапка.СуммаДокумента, Шапка.ВалютаДокумента));
		
		// Подписи
		ФамилияИОРуководителя = "";
		ФамилияИОГлавногоБухгалтера = "";
		ОтветственныеЛица = БухгалтерскийУчетСервер.ОтветственныеЛицаОрганизацийРуководители(Шапка.Организация, Шапка.Дата);
		БухгалтерскийУчетСервер.ФамилияИнициалыПоНаименованию(ФамилияИОРуководителя, ОтветственныеЛица.Руководитель);
		ДанныеПечати.Вставить("Руководитель", ФамилияИОРуководителя);
		
		Если НЕ Шапка.ВтораяПодписьНеПредусмотрена Тогда 
			БухгалтерскийУчетСервер.ФамилияИнициалыПоНаименованию(ФамилияИОГлавногоБухгалтера, ОтветственныеЛица.ГлавныйБухгалтер);
			ДанныеПечати.Вставить("ВтораяПодпись", СтрШаблон(НСтр("ru = 'Главный бухгалтер: %1'"), ФамилияИОГлавногоБухгалтера));
		Иначе
			ДанныеПечати.Вставить("ВтораяПодпись", НСтр("ru = 'Вторая подпись не предусмотрена'"));
		КонецЕсли;		
			
		// Области
		МассивОбластейМакета = Новый Массив;
		Если Шапка.ПечатьВРамке Тогда 
			МассивОбластейМакета.Добавить("ЗаголовокРамка");
			МассивОбластейМакета.Добавить("ПодписиРамка");
		Иначе 
			МассивОбластейМакета.Добавить("Заголовок");
			МассивОбластейМакета.Добавить("Подписи");
		КонецЕсли;
		
		Если Шапка.ПечатьНаОдномЛистеДваЭкземпляра Тогда 
			МассивОбластейМакета.Добавить("ЛинияРазреза");
			МассивОбластейМакета.Добавить(МассивОбластейМакета[0]);
			МассивОбластейМакета.Добавить(МассивОбластейМакета[1]);
		КонецЕсли;

		Для Каждого ИмяОбласти Из МассивОбластейМакета Цикл
			ОбластьМакета = Макет.ПолучитьОбласть(ИмяОбласти);
			ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, ДанныеПечати);
			ТабличныйДокумент.Вывести(ОбластьМакета);
		КонецЦикла;

		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Устанавливаем признак доступности печати покомплектно.
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	// Проверяем, нужно ли для макета ППИ формировать табличный документ.
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПлатежноеПоручение") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
			"ПлатежноеПоручение", НСтр("ru = 'Платежное поручение'"), ПечатьПлатежноеПоручение(МассивОбъектов, ОбъектыПечати),,
			"Документ.ПлатежноеПоручениеИсходящее.ПФ_MXL_ПлатежноеПоручение");
	КонецЕсли;
		
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПлатежноеПоручение";
	КомандаПечати.Представление = НСтр("ru = 'Печать ППИ'");
	КомандаПечати.СписокФорм = "ФормаДокумента,ФормаСписка";
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Обработчик = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Порядок = 1;
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор  = "РеестрПлатежноеПоручениеИсходящее";
	КомандаПечати.Представление  = НСтр("ru = 'Реестр документов'");
	КомандаПечати.ЗаголовокФормы = НСтр("ru = 'Реестр документов ""Платежное поручение исходящее""'");
	КомандаПечати.Обработчик     = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм     = "ФормаСписка";
	КомандаПечати.Порядок        = 100;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает заголовок документа для печатной формы.
//
// Параметры:
//  Шапка - любая структура с полями:
//           Номер         - Строка или Число - номер документа;
//           Представление - Строка - (необязательный) платформенное представление ссылки на документ.
//                                    Если параметр НазваниеДокумента не задан, то название документа будет вычисляться
//                                    из этого параметра.
//  НазваниеДокумента - Строка - название документа (например, "Счет на оплату").
//
// Возвращаемое значение:
//  Строка - заголовок документа.
//
Функция СформироватьЗаголовокДокумента(Шапка, Знач НазваниеДокумента = "")
	
	ДанныеДокумента = Новый Структура("Номер,Представление");
	ЗаполнитьЗначенияСвойств(ДанныеДокумента, Шапка);
	
	// Если название документа не передано, получим название по представлению документа.
	Если ПустаяСтрока(НазваниеДокумента) И ЗначениеЗаполнено(ДанныеДокумента.Представление) Тогда
		ПоложениеНомера = СтрНайти(ДанныеДокумента.Представление, ДанныеДокумента.Номер);
		Если ПоложениеНомера > 0 Тогда
			НазваниеДокумента = СокрЛП(Лев(ДанныеДокумента.Представление, ПоложениеНомера - 1));
		КонецЕсли;
	КонецЕсли;

	НомерНаПечать = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокумента.Номер);
	Возврат СтрШаблон(НСтр("ru = '%1 № %2'"), НазваниеДокумента, НомерНаПечать);
	
КонецФункции

#КонецОбласти

#КонецЕсли