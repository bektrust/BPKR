#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ЗаполненияДокумента

Процедура ЗаполнитьПоСтруктуре(ДанныеЗаполнения) Экспорт
	
	Если ДанныеЗаполнения.Свойство("ВедомостьЗаработнойПлаты") Тогда 
		ЗаполнитьПоВедомостиЗП(ДанныеЗаполнения, 2);
	КонецЕсли;
	
КонецПроцедуры

// Процедура заполнения документа на основании.
//
// Параметры:
//	ДанныеЗаполнения - Структура - Данные заполнения документа.
//	
Процедура ЗаполнитьПоВедомостиЗП(ДанныеЗаполнения, ВидДокументаВыплаты = 1) Экспорт
	// Общий 
	Если ДанныеЗаполнения.Свойство("ТаблицаЗначений") Тогда 
		ДокументОснование = ДанныеЗаполнения.ВедомостьЗаработнойПлаты;
		ВидОперации = Перечисления.ВидыОперацийППИ.ВыплатаЗП;
		КурсВзаиморасчетов = ДанныеЗаполнения.КурсВыплаты;
		КратностьВзаиморасчетов = ДанныеЗаполнения.КратностьВыплаты;
		
		// Заполнение шапки
		Организация 					= ДанныеЗаполнения.Организация;
		БанковскийСчет 					= ДанныеЗаполнения.БанковскийСчет;
		БанковскийСчетЗППроекта 		= ДанныеЗаполнения.БанковскийСчетЗППроекта;
		СтатьяДвиженияДенежныхСредств 	= ДанныеЗаполнения.СтатьяДвиженияДенежныхСредств;
		ВалютаДокумента					= ДанныеЗаполнения.БанковскийСчет.ВалютаДенежныхСредств;
		ОбщийППИ 						= Истина;
		
		ОбщаяСуммаДокумента = 0;
		// Заполнение Табличной части
		ВыплатаЗаработнойПлаты.Очистить();
		Для Каждого СтрокаТаблицы Из ДанныеЗаполнения.ТаблицаЗначений Цикл
			СтрокаТабличнойЧасти = ВыплатаЗаработнойПлаты.Добавить();
			СтрокаТабличнойЧасти.ФизЛицо 		= СтрокаТаблицы.ФизЛицо;
			СтрокаТабличнойЧасти.СуммаПлатежа 	= СтрокаТаблицы.СуммаПоКартСчету;
			ОбщаяСуммаДокумента 				= ОбщаяСуммаДокумента + СтрокаТаблицы.СуммаПоКартСчету;
		КонецЦикла;
		СуммаДокумента = ОбщаяСуммаДокумента;
		
	// Индивидуальные	
	Иначе 
		ДокументОснование = ДанныеЗаполнения.ВедомостьЗаработнойПлаты;
		ВидОперации = Перечисления.ВидыОперацийППИ.ВыплатаЗП; 
		КурсВзаиморасчетов = ДанныеЗаполнения.КурсВыплаты;
		КратностьВзаиморасчетов = ДанныеЗаполнения.КратностьВыплаты;
		
		// Заполнение шапки
		Организация 					= ДанныеЗаполнения.Организация;
		БанковскийСчет 					= ДанныеЗаполнения.БанковскийСчет;
		СуммаДокумента 					= ДанныеЗаполнения.СуммаКВыплате;
		СтатьяДвиженияДенежныхСредств 	= ДанныеЗаполнения.СтатьяДвиженияДенежныхСредств;
		ВалютаДокумента					= ДанныеЗаполнения.БанковскийСчет.ВалютаДенежныхСредств;
		
		ВыплатаЗаработнойПлаты.Очистить();
		СтрокаТабличнойЧасти = ВыплатаЗаработнойПлаты.Добавить();
		СтрокаТабличнойЧасти.ФизЛицо 		= ДанныеЗаполнения.ФизЛицо;
		СтрокаТабличнойЧасти.СуммаПлатежа 	= ДанныеЗаполнения.СуммаКВыплате;
		СтрокаТабличнойЧасти.БанковскийСчет = ДанныеЗаполнения.БанковскийСчетПолучателя;
	КонецЕсли;	
КонецПроцедуры

// Процедура заполнения документа на основании.
//
// Параметры:
//	ДанныеЗаполнения - Структура - Данные заполнения документа.
//	
Процедура ЗаполнитьПоВыплатеЗП(ДанныеЗаполнения) Экспорт
	
	// Общий 
	Если ДанныеЗаполнения.Свойство("ТаблицаЗначений") Тогда 
		ДокументОснование = ДанныеЗаполнения.ВыплатаЗП;
		ВидОперации = Перечисления.ВидыОперацийППИ.ВыплатаЗП; 
		
		// Заполнение шапки
		Организация = ДанныеЗаполнения.Организация;
		БанковскийСчет = ДанныеЗаполнения.БанковскийСчет;
		
		ОбщаяСуммаДокумента = 0;
		// Заполнение Табличной части
		ВыплатаЗаработнойПлаты.Очистить();
		Для Каждого СтрокаТаблицы Из ДанныеЗаполнения.ТаблицаЗначений Цикл
			СтрокаТабличнойЧасти = ВыплатаЗаработнойПлаты.Добавить();
			СтрокаТабличнойЧасти.ФизЛицо = СтрокаТаблицы.ФизЛицо;
			СтрокаТабличнойЧасти.СуммаПлатежа = СтрокаТаблицы.СуммаКВыплате;
			СтрокаТабличнойЧасти.БанковскийСчет = СтрокаТаблицы.БанковскийСчет;
			ОбщаяСуммаДокумента = ОбщаяСуммаДокумента + СтрокаТаблицы.СуммаКВыплате;
		КонецЦикла;
		СуммаДокумента = ОбщаяСуммаДокумента;
		
	// Индивидуальные	
	Иначе 
		ДокументОснование = ДанныеЗаполнения.ВыплатаЗП;
		ВидОперации = Перечисления.ВидыОперацийППИ.ВыплатаЗП; 
		
		// Заполнение шапки
		Организация = ДанныеЗаполнения.Организация;
		БанковскийСчет = ДанныеЗаполнения.БанковскийСчет;
		СуммаДокумента = ДанныеЗаполнения.СуммаКВыплате;
		
		ВыплатаЗаработнойПлаты.Очистить();
		СтрокаТабличнойЧасти = ВыплатаЗаработнойПлаты.Добавить();
		СтрокаТабличнойЧасти.ФизЛицо = ДанныеЗаполнения.ФизЛицо;
		СтрокаТабличнойЧасти.СуммаПлатежа = ДанныеЗаполнения.СуммаКВыплате;
		СтрокаТабличнойЧасти.БанковскийСчет = ДанныеЗаполнения.БанковскийСчетПолучателя;
	КонецЕсли;	
КонецПроцедуры

// Процедура заполнения документа на основании.
//
// Параметры:
//	ДанныеЗаполнения - ДокументСсылка.ПоступлениеТоваровУслуг - Данные заполнения документа.
//	
Процедура ЗаполнитьПоПоступлениюТоваровУслуг(ДанныеЗаполнения) Экспорт
	ДокументОснование = ДанныеЗаполнения;	
	
	// Шапка
	ВидОперации = Перечисления.ВидыОперацийППИ.ОплатаПоставщику;
	
	// Реквизиты организации.
	Организация = ДанныеЗаполнения.Организация;
	БанковскийСчет = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьОсновнойБанковскийСчетОрганизации(Организация, ДанныеЗаполнения.ВалютаДокумента);
	ВалютаДокумента = БанковскийСчет.ВалютаДенежныхСредств;
	
	ДатаДокумента = ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса());
	КурсСтруктура = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаДокумента, ДатаДокумента);
	Если ЗначениеЗаполнено(КурсСтруктура.Курс) Тогда
		Курс = КурсСтруктура.Курс;
		Кратность = КурсСтруктура.Кратность; 
	Иначе
		Курс = 1;
		Кратность = 1;
	КонецЕсли;		
	
	// Реквизиты контрагента.
	Контрагент = ДанныеЗаполнения.Контрагент;
	БанковскийСчетПолучателя = Контрагент.ОсновнойБанковскийСчет;
	ДоговорКонтрагента = ДанныеЗаполнения.ДоговорКонтрагента;
	ВалютаРасчетов = ДоговорКонтрагента.ВалютаРасчетов;

	СчетаРасчетовСКонтрагентом = БухгалтерскийУчетСервер.ПолучитьСчетаРасчетовСКонтрагентом(Организация, Контрагент, ДоговорКонтрагента);
	СчетРасчетов = СчетаРасчетовСКонтрагентом.СчетАвансовПоставщика;

	СуммаДокумента = ДанныеЗаполнения.Товары.Итог("Всего") + ДанныеЗаполнения.Услуги.Итог("Всего") + ДанныеЗаполнения.ОС.Итог("Всего");

	// Расшифровка.
	РасшифровкаПлатежа.Очистить();
	СтрокаТабличнойЧасти = РасшифровкаПлатежа.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ДанныеЗаполнения);
	
	Если ВалютаРасчетов = ВалютаДокумента Тогда 
		КурсВзаиморасчетов = Курс;
		КратностьВзаиморасчетов = Кратность;
		СтрокаТабличнойЧасти.СуммаПлатежа = СуммаДокумента;
		СтрокаТабличнойЧасти.СуммаВзаиморасчетов = СуммаДокумента;
	Иначе 
		ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
		КурсСтруктура = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаРасчетов, ДатаДокумента);

		ДанныеДокумента = Новый Структура;
		ДанныеДокумента.Вставить("Валюта", ВалютаДокумента);
		ДанныеДокумента.Вставить("Курс", Курс);
		ДанныеДокумента.Вставить("Кратность", Кратность);
		ДанныеДокумента.Вставить("ПрямойКурс", Ложь);
		
		ДанныеВзаиморасчетов = Новый Структура;
		ДанныеВзаиморасчетов.Вставить("Валюта", ВалютаРасчетов);
		ДанныеВзаиморасчетов.Вставить("Курс", КурсСтруктура.Курс);
		ДанныеВзаиморасчетов.Вставить("Кратность", КурсСтруктура.Кратность);

		УчетДенежныхСредствКлиентСервер.УстановитьКурсыВзаиморасчетов(ЭтотОбъект, ДанныеДокумента, ДанныеВзаиморасчетов);
			
		СтрокаТабличнойЧасти.СуммаПлатежа = ?(ВалютаДокумента = ВалютаРегламентированногоУчета,
			СуммаДокумента * КурсВзаиморасчетов / КратностьВзаиморасчетов,
			СуммаДокумента * КратностьВзаиморасчетов / КурсВзаиморасчетов);			
			
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуВзаиморасчетовТабличнойЧасти(
			ЭтотОбъект, "РасшифровкаПлатежа", ДанныеДокумента, ДанныеВзаиморасчетов, ВалютаРегламентированногоУчета);
	КонецЕсли;
		
КонецПроцедуры

// Процедура заполнения документа на основании.
//
// Параметры:
//	ДанныеЗаполнения - ДокументСсылка.ДополнительныеРасходы - Данные заполнения документа.
//	
Процедура ЗаполнитьПоДополнительнымРасходам(ДанныеЗаполнения) Экспорт
	ДокументОснование = ДанныеЗаполнения;
	
	// Шапка
	ВидОперации = Перечисления.ВидыОперацийППИ.ОплатаПоставщику;
	
	// Реквизиты организации.
	Организация = ДанныеЗаполнения.Организация;
	БанковскийСчет = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьОсновнойБанковскийСчетОрганизации(Организация);
	ВалютаДокумента = БанковскийСчет.ВалютаДенежныхСредств;
	
	ДатаДокумента = ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса());
	КурсСтруктура = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаДокумента, ДатаДокумента);
	Если ЗначениеЗаполнено(КурсСтруктура.Курс) Тогда
		Курс = КурсСтруктура.Курс;
		Кратность = КурсСтруктура.Кратность; 
	Иначе
		Курс = 1;
		Кратность = 1;
	КонецЕсли;		
	
	// Реквизиты контрагента.
	Контрагент 	= ДанныеЗаполнения.Контрагент;
	БанковскийСчетПолучателя = Контрагент.ОсновнойБанковскийСчет;
	ДоговорКонтрагента = ДанныеЗаполнения.ДоговорКонтрагента;
	ВалютаРасчетов = ДоговорКонтрагента.ВалютаРасчетов;
	
	СчетаРасчетовСКонтрагентом = БухгалтерскийУчетСервер.ПолучитьСчетаРасчетовСКонтрагентом(Организация, Контрагент, ДоговорКонтрагента);
	СчетРасчетов = СчетаРасчетовСКонтрагентом.СчетАвансовПоставщика;

	СуммаДокумента = ДанныеЗаполнения.СуммаДопРасходов;

	// Расшифровка платежа.
	РасшифровкаПлатежа.Очистить();
	СтрокаТабличнойЧасти = РасшифровкаПлатежа.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ДанныеЗаполнения);
	
	Если ВалютаРасчетов = ВалютаДокумента Тогда 
		КурсВзаиморасчетов = Курс;
		КратностьВзаиморасчетов = Кратность;
		СтрокаТабличнойЧасти.СуммаПлатежа = СуммаДокумента;
		СтрокаТабличнойЧасти.СуммаВзаиморасчетов = СуммаДокумента;
	Иначе 
		ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
		КурсСтруктура = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаРасчетов, ДатаДокумента);

		ДанныеДокумента = Новый Структура;
		ДанныеДокумента.Вставить("Валюта", ВалютаДокумента);
		ДанныеДокумента.Вставить("Курс", Курс);
		ДанныеДокумента.Вставить("Кратность", Кратность);
		ДанныеДокумента.Вставить("ПрямойКурс", Ложь);

		ДанныВзаиморасчетов = Новый Структура;
		ДанныеДокумента.Вставить("Валюта", ВалютаРасчетов);
		ДанныеДокумента.Вставить("Курс", КурсСтруктура.Курс);
		ДанныеДокумента.Вставить("Кратность", КурсСтруктура.Кратность);
		
		УчетДенежныхСредствКлиентСервер.УстановитьКурсыВзаиморасчетов(ЭтотОбъект, ДанныеДокумента, ДанныВзаиморасчетов);
			
		СтрокаТабличнойЧасти.СуммаПлатежа = ?(ВалютаДокумента = ВалютаРегламентированногоУчета,
			СуммаДокумента * КурсВзаиморасчетов / КратностьВзаиморасчетов,
			СуммаДокумента * КратностьВзаиморасчетов / КурсВзаиморасчетов);			
			
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуВзаиморасчетовТабличнойЧасти(
			ЭтотОбъект, "РасшифровкаПлатежа", ДанныеДокумента, ДанныВзаиморасчетов, ВалютаРегламентированногоУчета);
	КонецЕсли;
	
КонецПроцедуры

// Процедура заполнения документа на основании.
//
// Параметры:
//	ДанныеЗаполнения - ДокументСсылка.ВозвратТоваровОтПокупателя - Данные заполнения документа.
//	
Процедура ЗаполнитьПоВозвратуОтПокупателя(ДанныеЗаполнения) Экспорт
	ДокументОснование = ДанныеЗаполнения;
	
	// Шапка
	ВидОперации = Перечисления.ВидыОперацийППИ.ВозвратПокупателю;
	
	// Реквизиты организации.
	Организация = ДанныеЗаполнения.Организация;
	БанковскийСчет = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьОсновнойБанковскийСчетОрганизации(Организация);
	ВалютаДокумента = БанковскийСчет.ВалютаДенежныхСредств;
	
	ДатаДокумента = ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса());
	КурсСтруктура = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаДокумента, ДатаДокумента);
	Если ЗначениеЗаполнено(КурсСтруктура.Курс) Тогда
		Курс = КурсСтруктура.Курс;
		Кратность = КурсСтруктура.Кратность; 
	Иначе
		Курс = 1;
		Кратность = 1;
	КонецЕсли;		
	
	// Реквизиты контрагента.
	Контрагент 	= ДанныеЗаполнения.Контрагент;
	БанковскийСчетПолучателя = Контрагент.ОсновнойБанковскийСчет;
	ДоговорКонтрагента = ДанныеЗаполнения.ДоговорКонтрагента;
	ВалютаРасчетов = ДоговорКонтрагента.ВалютаРасчетов;
	
	СчетаРасчетовСКонтрагентом = БухгалтерскийУчетСервер.ПолучитьСчетаРасчетовСКонтрагентом(Организация, Контрагент, ДоговорКонтрагента);
	СчетРасчетов = СчетаРасчетовСКонтрагентом.СчетРасчетовПокупателя;
	
	СуммаДокумента = ДанныеЗаполнения.Товары.Итог("Сумма");
	
	// Расшифровка
	РасшифровкаПлатежа.Очистить();
	СтрокаТабличнойЧасти = РасшифровкаПлатежа.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ДанныеЗаполнения);
	
	Если ВалютаРасчетов = ВалютаДокумента Тогда 
		КурсВзаиморасчетов = Курс;
		КратностьВзаиморасчетов = Кратность;
		СтрокаТабличнойЧасти.СуммаПлатежа = СуммаДокумента;
		СтрокаТабличнойЧасти.СуммаВзаиморасчетов = СуммаДокумента;
	Иначе 
		ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
		КурсСтруктура = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаРасчетов, ДатаДокумента);

		ДанныеДокумента = Новый Структура;
		ДанныеДокумента.Вставить("Валюта", ВалютаДокумента);
		ДанныеДокумента.Вставить("Курс", Курс);
		ДанныеДокумента.Вставить("Кратность", Кратность);
		ДанныеДокумента.Вставить("ПрямойКурс", Ложь);
		
		ДанныВзаиморасчетов = Новый Структура;
		ДанныВзаиморасчетов.Вставить("Валюта", ВалютаРасчетов);
		ДанныВзаиморасчетов.Вставить("Курс", КурсСтруктура.Курс);
		ДанныВзаиморасчетов.Вставить("Кратность", КурсСтруктура.Кратность);
		
		УчетДенежныхСредствКлиентСервер.УстановитьКурсыВзаиморасчетов(ЭтотОбъект, ДанныеДокумента, ДанныВзаиморасчетов);
			
		СтрокаТабличнойЧасти.СуммаПлатежа = ?(ВалютаДокумента = ВалютаРегламентированногоУчета,
			СуммаДокумента * КурсВзаиморасчетов / КратностьВзаиморасчетов,
			СуммаДокумента * КратностьВзаиморасчетов / КурсВзаиморасчетов);			
			
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуВзаиморасчетовТабличнойЧасти(
			ЭтотОбъект, "РасшифровкаПлатежа", ДанныеДокумента, ДанныВзаиморасчетов, ВалютаРегламентированногоУчета);
	КонецЕсли;
	
КонецПроцедуры

// Процедура заполнения документа на основании.
//
// Параметры:
//	ДанныеЗаполнения - ДокументСсылка.ТекущийРасчетНалогаНаПрибыль - Данные заполнения документа.
//	
Процедура ЗаполнитьПоТекущемуРасчетуНалогаНаПрибыль(ДанныеЗаполнения) Экспорт
	ДокументОснование = ДанныеЗаполнения;
	
	// Шапка
	ВидОперации = Перечисления.ВидыОперацийППИ.ОплатаНалогов;
	
	// Реквизиты организации.
	Организация = ДанныеЗаполнения.Организация;
	БанковскийСчет = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьОсновнойБанковскийСчетОрганизации(Организация);
	ВалютаДокумента = БанковскийСчет.ВалютаДенежныхСредств;
	
	СуммаДокумента = ДанныеЗаполнения.СуммаНачислено;
	
	КурсВзаиморасчетов = 1;
	КратностьВзаиморасчетов = 1;	
	
	// Расшифровка
	РасшифровкаПлатежа.Очистить();
	СтрокаТабличнойЧасти = РасшифровкаПлатежа.Добавить();
	СтрокаТабличнойЧасти.СуммаПлатежа = СуммаДокумента;
	СтрокаТабличнойЧасти.СуммаВзаиморасчетов = СуммаДокумента;	
	СтрокаТабличнойЧасти.НалоговыйПериодНачало = НачалоКвартала(ДокументОснование.Дата);
	СтрокаТабличнойЧасти.НалоговыйПериодКонец = КонецКвартала(ДокументОснование.Дата);
	
КонецПроцедуры

// Процедура заполнения документа на основании.
//
// Параметры:
//	ДанныеЗаполнения - ДокументСсылка.ВозвратТоваровОтПокупателя - Данные заполнения документа.
//	
Процедура ЗаполнитьПоКорректировкеСтоимостиРеализации(ДанныеЗаполнения) Экспорт
	ДокументОснование = ДанныеЗаполнения;
	
	// Шапка
	ВидОперации = Перечисления.ВидыОперацийППИ.ВозвратПокупателю;
	
	// Реквизиты организации.
	Организация = ДанныеЗаполнения.Организация;
	БанковскийСчет = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьОсновнойБанковскийСчетОрганизации(Организация);
	ВалютаДокумента = БанковскийСчет.ВалютаДенежныхСредств;
	
	ДатаДокумента = ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса());
	КурсСтруктура = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаДокумента, ДатаДокумента);
	Если ЗначениеЗаполнено(КурсСтруктура.Курс) Тогда
		Курс = КурсСтруктура.Курс;
		Кратность = КурсСтруктура.Кратность; 
	Иначе
		Курс = 1;
		Кратность = 1;
	КонецЕсли;		
	
	// Реквизиты контрагента.
	Контрагент 	= ДанныеЗаполнения.Контрагент;
	БанковскийСчетПолучателя = Контрагент.ОсновнойБанковскийСчет;
	ДоговорКонтрагента = ДанныеЗаполнения.ДоговорКонтрагента;
	ВалютаРасчетов = ДоговорКонтрагента.ВалютаРасчетов;
	
	СчетаРасчетовСКонтрагентом = БухгалтерскийУчетСервер.ПолучитьСчетаРасчетовСКонтрагентом(Организация, Контрагент, ДоговорКонтрагента);
	СчетРасчетов = СчетаРасчетовСКонтрагентом.СчетРасчетовПокупателя;
	
	СуммаДокумента = 0;
	
	Если ДанныеЗаполнения.БезналичныйРасчет Тогда
		ТабличныеЧасти = Новый Массив();
		ТабличныеЧасти.Добавить(ДанныеЗаполнения.Товары);
		ТабличныеЧасти.Добавить(ДанныеЗаполнения.Услуги);
		ТабличныеЧасти.Добавить(ДанныеЗаполнения.ОС);
		
		Для Каждого ТабличнаяЧасть Из ТабличныеЧасти Цикл
			
			Для Каждого СтрокаТабличнойЧасти Из ТабличнаяЧасть Цикл	
				
				Если СтрокаТабличнойЧасти.Всего < 0 Тогда
					СуммаДокумента = СуммаДокумента + СтрокаТабличнойЧасти.Всего;	
				КонецЕсли;	
			КонецЦикла;
		КонецЦикла;	
		
		СуммаДокумента = -СуммаДокумента;
	КонецЕсли;
	
	// Расшифровка
	РасшифровкаПлатежа.Очистить();
	СтрокаТабличнойЧасти = РасшифровкаПлатежа.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ДанныеЗаполнения);
	
	Если ВалютаРасчетов = ВалютаДокумента Тогда 
		КурсВзаиморасчетов = Курс;
		КратностьВзаиморасчетов = Кратность;
		СтрокаТабличнойЧасти.СуммаПлатежа = СуммаДокумента;
		СтрокаТабличнойЧасти.СуммаВзаиморасчетов = СуммаДокумента;
	Иначе 
		ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
		КурсСтруктура = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаРасчетов, ДатаДокумента);

		ДанныеДокумента = Новый Структура;
		ДанныеДокумента.Вставить("Валюта", ВалютаДокумента);
		ДанныеДокумента.Вставить("Курс", Курс);
		ДанныеДокумента.Вставить("Кратность", Кратность);
		ДанныеДокумента.Вставить("ПрямойКурс", Ложь);
		
		ДанныВзаиморасчетов = Новый Структура;
		ДанныВзаиморасчетов.Вставить("Валюта", ВалютаРасчетов);
		ДанныВзаиморасчетов.Вставить("Курс", КурсСтруктура.Курс);
		ДанныВзаиморасчетов.Вставить("Кратность", КурсСтруктура.Кратность);
		
		УчетДенежныхСредствКлиентСервер.УстановитьКурсыВзаиморасчетов(ЭтотОбъект, ДанныеДокумента, ДанныВзаиморасчетов);
			
		СтрокаТабличнойЧасти.СуммаПлатежа = ?(ВалютаДокумента = ВалютаРегламентированногоУчета,
			СуммаДокумента * КурсВзаиморасчетов / КратностьВзаиморасчетов,
			СуммаДокумента * КратностьВзаиморасчетов / КурсВзаиморасчетов);			
			
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуВзаиморасчетовТабличнойЧасти(
			ЭтотОбъект, "РасшифровкаПлатежа", ДанныеДокумента, ДанныВзаиморасчетов, ВалютаРегламентированногоУчета);
	КонецЕсли;
	
КонецПроцедуры

// Процедура заполнения документа на основании.
//
// Параметры:
//	ДанныеЗаполнения - Структура - Данные заполнения документа.
//	
Процедура ЗаполнитьПоВедомостиДепонированнойЗарплаты(ДанныеЗаполнения) Экспорт
	
	ДокументОснование = ДанныеЗаполнения;
	ВидОперации = Перечисления.ВидыОперацийППИ.ВыплатаДепонированнойЗП;
	
	// Заполнение шапки
	Организация 					= ДанныеЗаполнения.Организация;
	СтатьяДвиженияДенежныхСредств 	= ДанныеЗаполнения.СтатьяДвиженияДенежныхСредств;
	СчетРасчетов 					= ДанныеЗаполнения.СчетУчета;
	
	ОбщаяСуммаДокумента = 0;
	// Заполнение Табличной части
	ВыплатаЗаработнойПлаты.Очистить();
	Для Каждого СтрокаТаблицы Из ДанныеЗаполнения.Зарплата Цикл
		СтрокаТабличнойЧасти = ВыплатаЗаработнойПлаты.Добавить();
		СтрокаТабличнойЧасти.ФизЛицо 		= СтрокаТаблицы.ФизЛицо;
		СтрокаТабличнойЧасти.СуммаПлатежа 	= СтрокаТаблицы.Сумма;
		ОбщаяСуммаДокумента 				= ОбщаяСуммаДокумента + СтрокаТаблицы.Сумма;
	КонецЦикла;
	СуммаДокумента = ОбщаяСуммаДокумента;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

// Процедура - обработчик события ОбработкаЗаполнения объекта.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка) Экспорт
	
	СтратегияЗаполнения = Новый Соответствие;
	СтратегияЗаполнения[Тип("Структура")] = "ЗаполнитьПоСтруктуре";
	СтратегияЗаполнения[Тип("ДокументСсылка.ВедомостьЗаработнойПлаты")] = "ЗаполнитьПоВедомостиЗП";
	СтратегияЗаполнения[Тип("ДокументСсылка.ПоступлениеТоваровУслуг")] = "ЗаполнитьПоПоступлениюТоваровУслуг";
	СтратегияЗаполнения[Тип("ДокументСсылка.ДополнительныеРасходы")] = "ЗаполнитьПоДополнительнымРасходам";
	СтратегияЗаполнения[Тип("ДокументСсылка.ВозвратТоваровОтПокупателя")] = "ЗаполнитьПоВозвратуОтПокупателя";
	СтратегияЗаполнения[Тип("ДокументСсылка.ТекущийРасчетНалогаНаПрибыль")] = "ЗаполнитьПоТекущемуРасчетуНалогаНаПрибыль";
	СтратегияЗаполнения[Тип("ДокументСсылка.КорректировкаСтоимостиРеализации")] = "ЗаполнитьПоКорректировкеСтоимостиРеализации";
	СтратегияЗаполнения[Тип("ДокументСсылка.ВедомостьДепонированнойЗарплаты")] = "ЗаполнитьПоВедомостиДепонированнойЗарплаты";
	
	ЗаполнениеОбъектовБП.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения, СтратегияЗаполнения);
	
	// Комиссия банка.
	Если ЗначениеЗаполнено(БанковскийСчет) 
		И ЗначениеЗаполнено(БанковскийСчет.СуммаКомиссияБанка)
		И НЕ ЗначениеЗаполнено(СуммаКомиссияБанка) Тогда
		
		СниматьКомиссиюБанка = Истина;
		СуммаКомиссияБанка = БанковскийСчет.СуммаКомиссияБанка;
		
		Если НЕ ЗначениеЗаполнено(СчетЗатратКомиссияБанка) Тогда
			СчетЗатратКомиссияБанка = ПланыСчетов.Хозрасчетный.ПрочиеОбщиеИАдминистративныеРасходы;	
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(БанковскийСчет)
		И НЕ ЗначениеЗаполнено(ВалютаДокумента) Тогда
		ВалютаДокумента = БанковскийСчет.ВалютаДенежныхСредств;
	КонецЕсли;	
	
	Если НЕ ЗначениеЗаполнено(КурсВзаиморасчетов)  
		ИЛИ НЕ ЗначениеЗаполнено(КратностьВзаиморасчетов) Тогда 
		
		КурсВзаиморасчетов = 1;
		КратностьВзаиморасчетов = 1;
	КонецЕсли;	
	
КонецПроцедуры

// Процедура - обработчик события ПередЗаписью объекта.
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ВидОперации = Перечисления.ВидыОперацийППИ.ПрочийРасход
		И НЕ (РасшифровкаПлатежа.Количество() = 0
		И ВыплатаЗаработнойПлаты.Количество() = 0) Тогда 
		РасшифровкаПлатежа.Очистить();
		ВыплатаЗаработнойПлаты.Очистить();
	ИначеЕсли (ВидОперации = Перечисления.ВидыОперацийППИ.ВыплатаЗП
		ИЛИ ВидОперации = Перечисления.ВидыОперацийППИ.ВыплатаДепонированнойЗП)
		И НЕ (РасшифровкаПлатежа.Количество() = 0
		И ПрочиеПлатежи.Количество() = 0) Тогда 
		РасшифровкаПлатежа.Очистить();
		ПрочиеПлатежи.Очистить();
	ИначеЕсли НЕ (ВидОперации = Перечисления.ВидыОперацийППИ.ПрочийРасход
		ИЛИ ВидОперации = Перечисления.ВидыОперацийППИ.ВыплатаЗП
		ИЛИ ВидОперации = Перечисления.ВидыОперацийППИ.ВыплатаДепонированнойЗП)
		И НЕ (ВыплатаЗаработнойПлаты.Количество() = 0
		И ПрочиеПлатежи.Количество() = 0) Тогда 
		ВыплатаЗаработнойПлаты.Очистить();
		ПрочиеПлатежи.Очистить();
	КонецЕсли;

	СуммаДокумента = РасшифровкаПлатежа.Итог("СуммаПлатежа") + ПрочиеПлатежи.Итог("СуммаПлатежа") + ВыплатаЗаработнойПлаты.Итог("СуммаПлатежа");

КонецПроцедуры

// Процедура - обработчик события ОбработкаПроверкиЗаполнения объекта.
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ПроверяемыеРеквизиты.Добавить("РасшифровкаПлатежа");

	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если ВидОперации = Перечисления.ВидыОперацийППИ.ПрочийРасход Тогда 
		МассивНепроверяемыхРеквизитов.Добавить("Контрагент");
		МассивНепроверяемыхРеквизитов.Добавить("ДоговорКонтрагента");
		МассивНепроверяемыхРеквизитов.Добавить("ВалютаРасчетов");
		МассивНепроверяемыхРеквизитов.Добавить("СчетРасчетов");
		МассивНепроверяемыхРеквизитов.Добавить("БанковскийСчетПолучателя");
		МассивНепроверяемыхРеквизитов.Добавить("ФизЛицо");
		МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа");
		МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа.СуммаПлатежа");
		МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа.СуммаВзаиморасчетов");
		МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа.СчетРасчетов");
		
		ПроверяемыеРеквизиты.Добавить("ПрочиеПлатежи");
		
		// Контроль заполнения субконто Договор.
		ОписаниеТиповДоговор = Новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов");
		Для Каждого СтрокаТабличнойЧасти Из ПрочиеПлатежи Цикл
			
			СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаТабличнойЧасти.СчетРасчетов);
			Если СвойстваСчета.ВидСубконто1ТипЗначения = ОписаниеТиповДоговор
					И НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Субконто1)
				Или СвойстваСчета.ВидСубконто2ТипЗначения = ОписаниеТиповДоговор
					И НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Субконто2)	
				Или СвойстваСчета.ВидСубконто3ТипЗначения = ОписаниеТиповДоговор
					И НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Субконто3) Тогда 
					
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("КОЛОНКА",, НСтр("ru = 'Субконто'"), СтрокаТабличнойЧасти.НомерСтроки, "Прочие платежи");
				ПолеСообщения = СтрШаблон("Объект.ПрочиеПлатежи[%1].Субконто2", СтрокаТабличнойЧасти.НомерСтроки-1);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, ПолеСообщения,, Отказ);
			
			// Валюта документа и договора должны быть одинаковы.
			ИначеЕсли СвойстваСчета.ВидСубконто1ТипЗначения = ОписаниеТиповДоговор
					И НЕ СтрокаТабличнойЧасти.Субконто1.ВалютаРасчетов = ВалютаДокумента
				Или СвойстваСчета.ВидСубконто2ТипЗначения = ОписаниеТиповДоговор
					И НЕ СтрокаТабличнойЧасти.Субконто2.ВалютаРасчетов = ВалютаДокумента	
				Или СвойстваСчета.ВидСубконто3ТипЗначения = ОписаниеТиповДоговор
					И НЕ СтрокаТабличнойЧасти.Субконто3.ВалютаРасчетов = ВалютаДокумента Тогда 	
			
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("КОЛОНКА", 
					"КОРРЕКТНОСТЬ", 
					НСтр("ru = 'Субконто'"), 
					СтрокаТабличнойЧасти.НомерСтроки, 
					"Прочие платежи",
					НСтр("ru = 'Валюта договора должна совпадать с валютой документа.'"));
				ПолеСообщения = СтрШаблон("Объект.ПрочиеПлатежи[%1].Субконто2", СтрокаТабличнойЧасти.НомерСтроки-1);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, ПолеСообщения,, Отказ);		
			КонецЕсли;	
		КонецЦикла;
		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийППИ.ПереводНаДругойСчет Тогда 
		МассивНепроверяемыхРеквизитов.Добавить("Контрагент");
		МассивНепроверяемыхРеквизитов.Добавить("ДоговорКонтрагента");
		МассивНепроверяемыхРеквизитов.Добавить("ВалютаРасчетов");
		МассивНепроверяемыхРеквизитов.Добавить("СчетРасчетов");
		МассивНепроверяемыхРеквизитов.Добавить("ФизЛицо");
		МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа.СчетРасчетов");
		
		ПроверяемыеРеквизиты.Добавить("БанковскийСчетПолучателя");
		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийППИ.ВыплатаЗП
		ИЛИ ВидОперации = Перечисления.ВидыОперацийППИ.ВыплатаДепонированнойЗП Тогда 
		МассивНепроверяемыхРеквизитов.Добавить("Контрагент");
		МассивНепроверяемыхРеквизитов.Добавить("ДоговорКонтрагента");
		МассивНепроверяемыхРеквизитов.Добавить("ВалютаРасчетов");
		МассивНепроверяемыхРеквизитов.Добавить("ФизЛицо");
		МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа");
		МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа.СуммаПлатежа");
		МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа.СуммаВзаиморасчетов");
		МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа.СчетРасчетов");
		
		Если ВидОперации = Перечисления.ВидыОперацийППИ.ВыплатаЗП Тогда
			МассивНепроверяемыхРеквизитов.Добавить("СчетРасчетов");
		КонецЕсли;			
			
		ПроверяемыеРеквизиты.Добавить("ВыплатаЗаработнойПлаты");
		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийППИ.ВыдачаПодотчетнику Тогда 
		МассивНепроверяемыхРеквизитов.Добавить("Контрагент");
		МассивНепроверяемыхРеквизитов.Добавить("ДоговорКонтрагента");
		МассивНепроверяемыхРеквизитов.Добавить("ВалютаРасчетов");
		МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа.СчетРасчетов");

	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийППИ.ОплатаПоставщику
		Или ВидОперации = Перечисления.ВидыОперацийППИ.РасчетыПоЗаймам
		Или ВидОперации = Перечисления.ВидыОперацийППИ.ВозвратПокупателю Тогда 
		МассивНепроверяемыхРеквизитов.Добавить("ФизЛицо");
		МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа.СчетРасчетов");
		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийППИ.ОплатаНалогов Тогда 
		МассивНепроверяемыхРеквизитов.Добавить("ФизЛицо");
		МассивНепроверяемыхРеквизитов.Добавить("ДоговорКонтрагента");
		МассивНепроверяемыхРеквизитов.Добавить("СчетРасчетов");
	КонецЕсли;	
	
	// Вид деятельности.
	ВидыОперацийЕдиныйНалог = ОбщегоНазначенияБПСервер.ВидыОперацийЕдиныйНалог();
	Если ВидыОперацийЕдиныйНалог.Найти(ВидОперации) = Неопределено Тогда 
		МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа.ВидДеятельности");
	КонецЕсли;	
	
	ОбщегоНазначенияБПСервер.ДобавитьНепроверяемыеРеквизитыПоПараметрамФункциональныхОпций(МассивНепроверяемыхРеквизитов, Организация, Дата);
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
КонецПроцедуры

// Процедура - обработчик события ОбработкаПроведения объекта.
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ИнициализироватьДанные(Отказ, РежимПроведения);
	
	ОтразитьДвижения(Отказ, РежимПроведения);
	
	// Запись наборов записей.
	БухгалтерскийУчетСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
	
КонецПроцедуры // ОбработкаПроведения()

// Процедура - обработчик события ОбработкаУдаленияПроведения объекта.
//
Процедура ОбработкаУдаленияПроведения(Отказ)
	
	// Инициализация дополнительных свойств для проведения документа
	БухгалтерскийУчетСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	БухгалтерскийУчетСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Запись наборов записей
	БухгалтерскийУчетСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
КонецПроцедуры // ОбработкаУдаленияПроведения()

// Процедура - обработчик события ПриУстановкеНовогоНомера объекта.
//
Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если ЗначениеЗаполнено(Префикс) Тогда 
		Возврат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПрефиксКассыИБанковскогоСчета") Тогда
		Префикс = ?(БанковскийСчет.Префикс = "", "0", БанковскийСчет.Префикс);
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииОбработкаПроведения

// Процедура инициализирует данные документа
// и подготавливает таблицы для движения в регистры
//
Процедура ИнициализироватьДанные(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа.
	БухгалтерскийУчетСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа.
	Документы.ПлатежноеПоручениеИсходящее.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей.
	БухгалтерскийУчетСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
КонецПроцедуры
	
// Процедура заполняет регистры данными
//
Процедура ОтразитьДвижения(Отказ, РежимПроведения)

	// Отражение в разделах учета.
	БухгалтерскийУчетСервер.ОтразитьХозрасчетный(ДополнительныеСвойства, Движения, Отказ);
	УчетДенежныхСредств.СформироватьДвиженияОперационнаяКурсоваяРазница(ДополнительныеСвойства, Движения, Отказ, Ложь);
	БухгалтерскийУчетСервер.ОтразитьВыплаченнаяЗарплата(ДополнительныеСвойства, Движения, Отказ);
	БухгалтерскийУчетСервер.ОтразитьДанныеРеестраГТД(ДополнительныеСвойства, Движения, Отказ);
	БухгалтерскийУчетСервер.ОтразитьАвансыПодотчетника(ДополнительныеСвойства, Движения, Отказ);
	БухгалтерскийУчетСервер.ОтразитьОборотыПоДаннымЕдиногоНалога(ДополнительныеСвойства, Движения, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли