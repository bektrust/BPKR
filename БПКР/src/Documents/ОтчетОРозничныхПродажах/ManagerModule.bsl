#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПроцедурыПроведенияДокумента

// Формирует таблицу значений, содержащую данные для проведения по регистру бухгалтерии Хозрасчетный.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаХозрасчетный(ДокументСсылка,СтруктураДополнительныеСвойства, Отказ)
	
	// Подготовка данных.
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Ссылка КАК Регистратор,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.Склад КАК Склад,
		|	НЕОПРЕДЕЛЕНО КАК Контрагент,
		|	&Содержание КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	""Товары"" КАК ИмяСписка,
		|	&СинонимСписка КАК СинонимСписка,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаТовары.НомерСтроки КАК НомерСтроки,
		|	ВременнаяТаблицаТовары.СчетУчета КАК СчетУчета,
		|	ВременнаяТаблицаТовары.Номенклатура КАК Номенклатура,
		|	ВременнаяТаблицаШапка.Склад КАК Склад,
		|	НЕОПРЕДЕЛЕНО КАК ДокументОприходования,
		|	0 КАК Себестоимость,
		|	ВременнаяТаблицаТовары.Количество КАК Количество,
		|	ВременнаяТаблицаТовары.СчетСебестоимости КАК КорСчетСписания,
		|	ВременнаяТаблицаТовары.СтатьяРасходов КАК КорСубконто1,
		|	НЕОПРЕДЕЛЕНО КАК КорСубконто2,
		|	НЕОПРЕДЕЛЕНО КАК КорСубконто3
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаТовары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.МБП)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	""Товары"" КАК ИмяСписка,
		|	&СинонимСписка КАК СинонимСписка,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаТовары.НомерСтроки КАК НомерСтроки,
		|	ВременнаяТаблицаТовары.СчетУчета КАК СчетУчета,
		|	ВременнаяТаблицаТовары.Номенклатура КАК Номенклатура,
		|	ВременнаяТаблицаШапка.Склад КАК Склад,
		|	НЕОПРЕДЕЛЕНО КАК ДокументОприходования,
		|	0 КАК Себестоимость,
		|	ВременнаяТаблицаТовары.Количество КАК Количество,
		|	ВременнаяТаблицаТовары.СчетСебестоимости КАК КорСчетСписания,
		|	ВременнаяТаблицаТовары.СтатьяРасходов КАК КорСубконто1,
		|	НЕОПРЕДЕЛЕНО КАК КорСубконто2,
		|	НЕОПРЕДЕЛЕНО КАК КорСубконто3
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаТовары.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.МБП)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	Запрос.УстановитьПараметр("Содержание", НСтр("ru = 'Списание товаров (чек)'")); 
	Запрос.УстановитьПараметр("СинонимСписка", НСтр("ru = 'Товары'"));
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ТаблицаРеквизитыТовары = МассивРезультатов[0].Выгрузить();
	ТаблицаТовары = МассивРезультатов[1].Выгрузить();
	ТаблицаМБП = МассивРезультатов[2].Выгрузить();
	
	//ТаблицаСписанныеТовары = УчетТоваров.ПодготовитьТаблицуСписанныеТовары(ТаблицаТовары, ТаблицаРеквизиты, Отказ);
	//ТаблицаСписанныеМБП = УчетМБП.ПодготовитьТаблицуСписанныеМБП(ТаблицаМБП, ТаблицаРеквизитыТовары, Отказ);
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРеквизиты", ТаблицаРеквизитыТовары);
	//СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСписанныеТовары", ТаблицаСписанныеТовары);
	//СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСписанныеМБП", ТаблицаСписанныеМБП);
	
	// Возврат товаров.
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Ссылка КАК Регистратор,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	НЕОПРЕДЕЛЕНО КАК Контрагент,
		|	ВременнаяТаблицаШапка.Склад КАК Склад,
		|	ВременнаяТаблицаВозвраты.Сделка КАК Сделка,
		|	ВременнаяТаблицаВозвраты.ВозвратТекущейСмены КАК ВозвратТекущейСмены,
		|	ВременнаяТаблицаВозвраты.Партия КАК Партия,
		|	ВременнаяТаблицаВозвраты.ДатаРеализации КАК ПартияДата,
		|	""ОтчетОРозничныхПродажах"" КАК ВидОперации,
		|	ИСТИНА КАК ЭтоВозврат,
		|	ВременнаяТаблицаВозвраты.УказанДокументОтгрузки КАК УказанДокументОтгрузки,
		|	ВременнаяТаблицаВозвраты.ДатаРеализации <> ДАТАВРЕМЯ(1, 1, 1) КАК УказанаДатаРеализации,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаВозвраты.УказанДокументОтгрузки
		|			ТОГДА ВременнаяТаблицаВозвраты.ДатаРеализации
		|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
		|	КОНЕЦ КАК ДатаДокументаРеализации,
		|	&Содержание КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаВозвраты КАК ВременнаяТаблицаВозвраты
		|		ПО (ИСТИНА)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВременнаяТаблицаВозвраты.СчетУчета КАК СчетУчета,
		|	ВременнаяТаблицаВозвраты.Сделка КАК Сделка
		|ИЗ
		|	ВременнаяТаблицаВозвраты КАК ВременнаяТаблицаВозвраты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВременнаяТаблицаВозвраты.Номенклатура КАК Номенклатура,
		|	ВременнаяТаблицаВозвраты.Сделка КАК Сделка
		|ИЗ
		|	ВременнаяТаблицаВозвраты КАК ВременнаяТаблицаВозвраты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	""Товары"" КАК ИмяСписка,
		|	&СинонимСписка КАК СинонимСписка,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаВозвраты.НомерСтроки КАК НомерСтроки,
		|	ВременнаяТаблицаВозвраты.СчетУчета КАК СчетУчета,
		|	ВременнаяТаблицаВозвраты.Номенклатура КАК Номенклатура,
		|	ВременнаяТаблицаШапка.Склад КАК Склад,
		|	НЕОПРЕДЕЛЕНО КАК Партия,
		|	0 КАК Себестоимость,
		|	ВременнаяТаблицаВозвраты.Количество КАК Количество,
		|	ВременнаяТаблицаВозвраты.СчетСебестоимости КАК КорСчетСписания,
		|	ВременнаяТаблицаВозвраты.СтатьяРасходов КАК КорСубконто1,
		|	НЕОПРЕДЕЛЕНО КАК КорСубконто2,
		|	НЕОПРЕДЕЛЕНО КАК КорСубконто3,
		|	0 КАК СуммаСписания,
		|	ВременнаяТаблицаВозвраты.Сделка КАК Сделка
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаВозвраты КАК ВременнаяТаблицаВозвраты
		|		ПО (ИСТИНА)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка); 
	Запрос.УстановитьПараметр("Содержание", НСтр("ru = 'Возврат товаров (чек)'")); 
	Запрос.УстановитьПараметр("СинонимСписка", НСтр("ru = 'Товары'"));
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ТаблицаРеквизиты = МассивРезультатов[0].Выгрузить();
	ТаблицаСчетаУчетаТоваров = МассивРезультатов[1].Выгрузить();
	ТаблицаСписокНоменклатуры = МассивРезультатов[2].Выгрузить();
	ТаблицаТоварыВозвраты = МассивРезультатов[3].Выгрузить();
	
	// Таблица возвращаемых товаров с указанием партии возврата и себестоимости.
	ТаблицаВозвращенныеСписанныеТовары = ПодготовитьТаблицуСписанныеТоварыОтПокупателя(ТаблицаТоварыВозвраты, ТаблицаСчетаУчетаТоваров, ТаблицаСписокНоменклатуры, ТаблицаРеквизиты, Отказ);   
	
	// Возвраты текущей смены себестоимость для целей списания товаров не формируют. 
	ТаблицаВозвращенныеСписанныеТоварыБезУчетаТекущейСмены = ТаблицаВозвращенныеСписанныеТовары.Скопировать(Новый Структура("ВозвратТекущейСмены",Ложь));
	
	// Таблица списанных товаров.
	ТаблицаСписанныеТовары = УчетТоваров.ПодготовитьТаблицуСписанныеТоварыСУчетомВозврата(ТаблицаТовары, ТаблицаВозвращенныеСписанныеТоварыБезУчетаТекущейСмены, ТаблицаРеквизитыТовары, Отказ);
	ЗаполнитьПартииВозвратаТекущейСмены(ТаблицаВозвращенныеСписанныеТовары, ТаблицаСписанныеТовары, Отказ);
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСписанныеТовары", ТаблицаСписанныеТовары);
	//СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСписанныеМБП", ТаблицаСписанныеМБП);
	
	// СТОРНО
	СтруктураПолейДляСторно = Новый Структура("Количество, Себестоимость, СуммаСписания");
	УчетТоваров.ПроставитьСторноТаблицы(ТаблицаВозвращенныеСписанныеТовары, СтруктураПолейДляСторно, -1);
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРеквизитыВозвраты", ТаблицаРеквизиты);
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСписанныеТоварыВозвраты", ТаблицаВозвращенныеСписанныеТовары);
	//СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСписанныеМБП", ТаблицаСписанныеМБП);
	
	// 1. Доход от реализации товаров
	// 2. НДС товары
	// 3. НСП товары
	
	// 4. Доход от реализации услуг
	// 5. НДС услуги
	// 6. НСП услуги
	
	
	
	
	// 7. Безналичный расчет.
	// 8. Возврат безналичного расчета.

	
	ТекстыЗапроса = Новый Массив;
	
	// Доход от реализации товаров.
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	1 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.СчетРасчетовДт КАК СчетДт,
		|	ВременнаяТаблицаТовары.СчетДоходов КАК СчетКт,
		|	ВременнаяТаблицаШапка.Контрагент КАК СубконтоДт1,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	ВременнаяТаблицаТовары.СтатьяДоходов КАК СубконтоКт1,
		|	ВременнаяТаблицаТовары.НоменклатурнаяГруппа КАК СубконтоКт2,
		|	ВременнаяТаблицаТовары.Номенклатура КАК СубконтоКт3,
		|	ВременнаяТаблицаТовары.Сумма КАК Сумма,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаДт,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаКт,
		|	ВременнаяТаблицаТовары.Сумма КАК ВалютнаяСуммаДт,
		|	ВременнаяТаблицаТовары.Сумма КАК ВалютнаяСуммаКт,
		|	ВЫРАЗИТЬ(&СодержаниеТоварыДоход КАК СТРОКА(150)) КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаТовары.Сумма = 0";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// НДС товары.
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	2 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.СчетРасчетовДт КАК СчетДт,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСКОплате) КАК СчетКт,
		|	ВременнаяТаблицаШапка.Контрагент КАК СубконтоДт1,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	ВременнаяТаблицаТовары.СуммаНДС КАК Сумма,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаДт,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаКт,
		|	ВременнаяТаблицаТовары.СуммаНДС КАК ВалютнаяСуммаДт,
		|	ВременнаяТаблицаТовары.СуммаНДС КАК ВалютнаяСуммаКт,
		|	&СодержаниеТоварыНДС КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|		ПО (ИСТИНА)
		|ГДЕ
		|	&ПлательщикНДС
		|	И НЕ ВременнаяТаблицаТовары.СуммаНДС = 0";
	ТекстыЗапроса.Добавить(ТекстЗапроса);

	// НСП товары.
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	3 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.СчетРасчетовДт КАК СчетДт,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НалогСПродаж) КАК СчетКт,
		|	ВременнаяТаблицаШапка.Контрагент КАК СубконтоДт1,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	ВременнаяТаблицаТовары.СуммаНСП КАК Сумма,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаДт,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаКт,
		|	ВременнаяТаблицаТовары.СуммаНСП КАК ВалютнаяСуммаДт,
		|	ВременнаяТаблицаТовары.СуммаНСП КАК ВалютнаяСуммаКт,
		|	&СодержаниеТоварыНСП КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаТовары.СуммаНСП = 0";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// Доход от реализации услуг.
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	4 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.СчетРасчетовДт КАК СчетДт,
		|	ВременнаяТаблицаУслуги.СчетДоходов КАК СчетКт,
		|	ВременнаяТаблицаШапка.Контрагент КАК СубконтоДт1,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	ВременнаяТаблицаУслуги.СтатьяДоходов КАК СубконтоКт1,
		|	ВременнаяТаблицаУслуги.Номенклатура.НоменклатурнаяГруппа КАК СубконтоКт2,
		|	ВременнаяТаблицаУслуги.Номенклатура КАК СубконтоКт3,
		|	ВременнаяТаблицаУслуги.Сумма КАК Сумма,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаДт,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаКт,
		|	ВременнаяТаблицаУслуги.Сумма КАК ВалютнаяСуммаДт,
		|	ВременнаяТаблицаУслуги.Сумма КАК ВалютнаяСуммаКт,
		|	&СодержаниеУслугиДоход КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаУслуги КАК ВременнаяТаблицаУслуги
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаУслуги.Сумма = 0";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// НДС услуги.
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	5 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.СчетРасчетовДт КАК СчетДт,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСКОплате) КАК СчетКт,
		|	ВременнаяТаблицаШапка.Контрагент КАК СубконтоДт1,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	ВременнаяТаблицаУслуги.СуммаНДС КАК Сумма,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаДт,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаКт,
		|	ВременнаяТаблицаУслуги.СуммаНДС КАК ВалютнаяСуммаДт,
		|	ВременнаяТаблицаУслуги.СуммаНДС КАК ВалютнаяСуммаКт,
		|	&СодержаниеУслугиНДС КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаУслуги КАК ВременнаяТаблицаУслуги
		|		ПО (ИСТИНА)
		|ГДЕ
		|	&ПлательщикНДС
		|	И НЕ ВременнаяТаблицаУслуги.СуммаНДС = 0";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// НСП услуги.
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	6 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.СчетРасчетовДт КАК СчетДт,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НалогСПродаж) КАК СчетКт,
		|	ВременнаяТаблицаШапка.Контрагент КАК СубконтоДт1,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	ВременнаяТаблицаУслуги.СуммаНСП КАК Сумма,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаДт,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаКт,
		|	ВременнаяТаблицаУслуги.СуммаНСП КАК ВалютнаяСуммаДт,
		|	ВременнаяТаблицаУслуги.СуммаНСП КАК ВалютнаяСуммаКт,
		|	&СодержаниеУслугиНСП КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаУслуги КАК ВременнаяТаблицаУслуги
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаУслуги.СуммаНСП = 0";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// Сторно дохода от реализации товаров.
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	1 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.СчетРасчетовДт КАК СчетДт,
		|	ВременнаяТаблицаВозвраты.СчетДоходов КАК СчетКт,
		|	ВременнаяТаблицаШапка.Контрагент КАК СубконтоДт1,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
		|	ВременнаяТаблицаВозвраты.Номенклатура КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	-ВременнаяТаблицаВозвраты.Сумма КАК Сумма,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаДт,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаКт,
		|	-ВременнаяТаблицаВозвраты.Сумма КАК ВалютнаяСуммаДт,
		|	-ВременнаяТаблицаВозвраты.Сумма КАК ВалютнаяСуммаКт,
		|	&СодержаниеВозвратТоварыДоход КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаВозвраты КАК ВременнаяТаблицаВозвраты
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаВозвраты.Сумма = 0";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// Сторно НДС товары.
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	2 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.СчетРасчетовДт КАК СчетДт,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСКОплате) КАК СчетКт,
		|	ВременнаяТаблицаШапка.Контрагент КАК СубконтоДт1,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	-ВременнаяТаблицаВозвраты.СуммаНДС КАК Сумма,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаДт,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаКт,
		|	-ВременнаяТаблицаВозвраты.СуммаНДС КАК ВалютнаяСуммаДт,
		|	-ВременнаяТаблицаВозвраты.СуммаНДС КАК ВалютнаяСуммаКт,
		|	&СодержаниеВозвратТоварыНДС КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаВозвраты КАК ВременнаяТаблицаВозвраты
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаВозвраты.СуммаНДС = 0";
	ТекстыЗапроса.Добавить(ТекстЗапроса);

	// Сторно НСП товары.
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	3 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.СчетРасчетовДт КАК СчетДт,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСКОплате) КАК СчетКт,
		|	ВременнаяТаблицаШапка.Контрагент КАК СубконтоДт1,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	-ВременнаяТаблицаВозвраты.СуммаНСП КАК Сумма,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаДт,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаКт,
		|	-ВременнаяТаблицаВозвраты.СуммаНСП КАК ВалютнаяСуммаДт,
		|	-ВременнаяТаблицаВозвраты.СуммаНСП КАК ВалютнаяСуммаКт,
		|	&СодержаниеВозвратТоварыНСП КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаВозвраты КАК ВременнаяТаблицаВозвраты
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаВозвраты.СуммаНСП = 0";	
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// Сторно дохода от реализации услуг.
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	4 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.СчетРасчетовДт КАК СчетДт,
		|	ВременнаяТаблицаВозвратУслуг.СчетДоходов КАК СчетКт,
		|	ВременнаяТаблицаШапка.Контрагент КАК СубконтоДт1,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	ВременнаяТаблицаВозвратУслуг.СтатьяДоходов КАК СубконтоКт1,
		|	ВременнаяТаблицаВозвратУслуг.Номенклатура.НоменклатурнаяГруппа КАК СубконтоКт2,
		|	ВременнаяТаблицаВозвратУслуг.Номенклатура КАК СубконтоКт3,
		|	-ВременнаяТаблицаВозвратУслуг.Сумма КАК Сумма,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаДт,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаКт,
		|	-ВременнаяТаблицаВозвратУслуг.Сумма КАК ВалютнаяСуммаДт,
		|	-ВременнаяТаблицаВозвратУслуг.Сумма КАК ВалютнаяСуммаКт,
		|	&СодержаниеВозвратУслугиДоход КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаВозвратУслуг КАК ВременнаяТаблицаВозвратУслуг
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаВозвратУслуг.Сумма = 0";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// Сторно НДС услуги.
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	5 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.СчетРасчетовДт КАК СчетДт,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДСКОплате) КАК СчетКт,
		|	ВременнаяТаблицаШапка.Контрагент КАК СубконтоДт1,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	-ВременнаяТаблицаВозвратУслуг.СуммаНДС КАК Сумма,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаДт,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаКт,
		|	-ВременнаяТаблицаВозвратУслуг.СуммаНДС КАК ВалютнаяСуммаДт,
		|	-ВременнаяТаблицаВозвратУслуг.СуммаНДС КАК ВалютнаяСуммаКт,
		|	&СодержаниеВозвратУслугиНДС КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаВозвратУслуг КАК ВременнаяТаблицаВозвратУслуг
		|		ПО (ИСТИНА)
		|ГДЕ
		|	&ПлательщикНДС
		|	И НЕ ВременнаяТаблицаВозвратУслуг.СуммаНДС = 0";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// Сторно НСП услуги.
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	6 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.СчетРасчетовДт КАК СчетДт,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НалогСПродаж) КАК СчетКт,
		|	ВременнаяТаблицаШапка.Контрагент КАК СубконтоДт1,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	-ВременнаяТаблицаВозвратУслуг.СуммаНСП КАК Сумма,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаДт,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаКт,
		|	-ВременнаяТаблицаВозвратУслуг.СуммаНСП КАК ВалютнаяСуммаДт,
		|	-ВременнаяТаблицаВозвратУслуг.СуммаНСП КАК ВалютнаяСуммаКт,
		|	&СодержаниеВозвратУслугиНСП КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаВозвратУслуг КАК ВременнаяТаблицаВозвратУслуг
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаВозвратУслуг.СуммаНСП = 0";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// Безналичный расчет.
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	7 КАК Порядок,
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПродажиПоДоговорамЭквайринга) КАК СчетДт,
		|	ВременнаяТаблицаШапка.СчетРасчетовКт КАК СчетКт,
		|	ВременнаяТаблицаОплата.ВидОплаты КАК СубконтоДт1,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
		|	ВременнаяТаблицаШапка.Контрагент КАК СубконтоКт1,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента КАК СубконтоКт2,
		|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
		|	ВременнаяТаблицаОплата.СуммаОплаты КАК Сумма,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаДт,
		|	ВременнаяТаблицаШапка.ВалютаДокумента КАК ВалютаКт,
		|	ВременнаяТаблицаОплата.СуммаОплаты КАК ВалютнаяСуммаДт,
		|	ВременнаяТаблицаОплата.СуммаОплаты КАК ВалютнаяСуммаКт,
		|	ВЫРАЗИТЬ(&СодержаниеБезналичнаяОплата КАК СТРОКА(150)) КАК Содержание
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаОплата КАК ВременнаяТаблицаОплата
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаОплата.СуммаОплаты = 0";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст = СтрСоединить(ТекстыЗапроса, Символы.ПС + " ОБЪЕДИНИТЬ ВСЕ " + Символы.ПС);
	
	Запрос.УстановитьПараметр("СодержаниеТоварыДоход", НСтр("ru = 'Доход от реализации товаров'")); 
	Запрос.УстановитьПараметр("СодержаниеТоварыНДС", НСтр("ru = 'НДС (товары)'")); 
	Запрос.УстановитьПараметр("СодержаниеТоварыНСП", НСтр("ru = 'НСП (товары)'")); 
	
	Запрос.УстановитьПараметр("СодержаниеУслугиДоход", НСтр("ru = 'Доход от реализации услуг'")); 
	Запрос.УстановитьПараметр("СодержаниеУслугиНДС", НСтр("ru = 'НДС (услуги)'")); 
	Запрос.УстановитьПараметр("СодержаниеУслугиНСП", НСтр("ru = 'НСП (услуги)'")); 
	
	Запрос.УстановитьПараметр("СодержаниеВозвратТоварыДоход", НСтр("ru = 'Возврат дохода от реализации товаров'")); 
	Запрос.УстановитьПараметр("СодержаниеВозвратТоварыНДС", НСтр("ru = 'Возврат НДС (товары)'")); 
	Запрос.УстановитьПараметр("СодержаниеВозвратТоварыНСП", НСтр("ru = 'Возврат НСП (товары)'")); 
	
	Запрос.УстановитьПараметр("СодержаниеВозвратУслугиДоход", НСтр("ru = 'Возврат дохода от реализации услуг'")); 
	Запрос.УстановитьПараметр("СодержаниеВозвратУслугиНДС", НСтр("ru = 'Возврат НДС (услуги)'")); 
	Запрос.УстановитьПараметр("СодержаниеВозвратУслугиНСП", НСтр("ru = 'Возврат НСП (услуги)'")); 
	
	Запрос.УстановитьПараметр("СодержаниеБезналичнаяОплата", НСтр("ru = 'Безналичная оплата'")); 

	Запрос.УстановитьПараметр("ПлательщикНДС", СтруктураДополнительныеСвойства.УчетнаяПолитика.ПлательщикНДС);
	
	РезультатЗапроса = Запрос.Выполнить();	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаХозрасчетный", РезультатЗапроса.Выгрузить());	
	
КонецПроцедуры // СформироватьТаблицаХозрасчетный()

// Формирует таблицу значений, содержащую данные для проведения по регистру бухгалтерии Хозрасчетный.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаПродажи(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаШапка.Ссылка КАК ДокументРеализации,
		|	ВременнаяТаблицаШапка.Склад КАК Склад,
		|	ВременнаяТаблицаШапка.Контрагент КАК Контрагент,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента КАК Договор,
		|	ВременнаяТаблицаТовары.БезналичныйРасчет КАК БезналичныйРасчет,
		|	ВременнаяТаблицаТовары.СтавкаНДС КАК СтавкаНДС,
		|	ВременнаяТаблицаТовары.СтавкаНСП КАК СтавкаНСП,
		|	ВременнаяТаблицаТовары.Номенклатура КАК Номенклатура,
		|	ВременнаяТаблицаТовары.Количество КАК Количество,
		|	ВременнаяТаблицаТовары.Всего - ВременнаяТаблицаТовары.СуммаНДС - ВременнаяТаблицаТовары.СуммаНСП КАК Сумма,
		|	ВременнаяТаблицаТовары.СуммаНДС КАК СуммаНДС,
		|	ВременнаяТаблицаТовары.СуммаНСП КАК СуммаНСП,
		|	ВременнаяТаблицаТовары.Всего КАК СуммаДохода,
		|	0 КАК Себестоимость
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаТовары.Всего = 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.Ссылка,
		|	ВременнаяТаблицаШапка.Склад,
		|	ВременнаяТаблицаШапка.Контрагент,
		|	ВременнаяТаблицаШапка.ДоговорКонтрагента,
		|	ВременнаяТаблицаУслуги.БезналичныйРасчет,
		|	ВременнаяТаблицаУслуги.СтавкаНДС,
		|	ВременнаяТаблицаУслуги.СтавкаНСП,
		|	ВременнаяТаблицаУслуги.Номенклатура,
		|	ВременнаяТаблицаУслуги.Количество,
		|	ВременнаяТаблицаУслуги.Всего - ВременнаяТаблицаУслуги.СуммаНДС - ВременнаяТаблицаУслуги.СуммаНСП,
		|	ВременнаяТаблицаУслуги.СуммаНДС,
		|	ВременнаяТаблицаУслуги.СуммаНСП,
		|	ВременнаяТаблицаУслуги.Всего,
		|	0
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаУслуги КАК ВременнаяТаблицаУслуги
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НЕ ВременнаяТаблицаУслуги.Всего = 0";
	РезультатЗапроса = Запрос.Выполнить();	
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПродажи", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаРеализацияТоваров(ДокументСсылка, СтруктураДополнительныеСвойства) 

// Формирует таблицу значений, содержащую данные для проведения по регистру накопления ОборотыПоДаннымЕдиногоНалога.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаОборотыПоДаннымЕдиногоНалога(СтруктураДополнительныеСвойства)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаТовары.ВидДеятельности КАК ВидДеятельности,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаТовары.БезналичныйРасчет
		|			ТОГДА 0
		|		ИНАЧЕ ВременнаяТаблицаТовары.Всего
		|	КОНЕЦ КАК СуммаНаличная,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаТовары.БезналичныйРасчет
		|			ТОГДА ВременнаяТаблицаТовары.Всего
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаБезналичная
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
		|		ПО (ИСТИНА)
		|ГДЕ
		|	&ПлательщикЕН
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаУслуги.ВидДеятельности,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаУслуги.БезналичныйРасчет
		|			ТОГДА 0
		|		ИНАЧЕ ВременнаяТаблицаУслуги.Всего
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаУслуги.БезналичныйРасчет
		|			ТОГДА ВременнаяТаблицаУслуги.Всего
		|		ИНАЧЕ 0
		|	КОНЕЦ
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаУслуги КАК ВременнаяТаблицаУслуги
		|		ПО (ИСТИНА)
		|ГДЕ
		|	&ПлательщикЕН
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаВозвраты.ВидДеятельности,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаВозвраты.БезналичныйРасчет
		|			ТОГДА 0
		|		ИНАЧЕ ВременнаяТаблицаВозвраты.Всего
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаВозвраты.БезналичныйРасчет
		|			ТОГДА ВременнаяТаблицаВозвраты.Всего
		|		ИНАЧЕ 0
		|	КОНЕЦ
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаВозвраты КАК ВременнаяТаблицаВозвраты
		|		ПО (ИСТИНА)
		|ГДЕ
		|	&ПлательщикЕН
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата,
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаВозвратУслуг.ВидДеятельности,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаВозвратУслуг.БезналичныйРасчет
		|			ТОГДА 0
		|		ИНАЧЕ ВременнаяТаблицаВозвратУслуг.Всего
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаВозвратУслуг.БезналичныйРасчет
		|			ТОГДА ВременнаяТаблицаВозвратУслуг.Всего
		|		ИНАЧЕ 0
		|	КОНЕЦ
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаВозвратУслуг КАК ВременнаяТаблицаВозвратУслуг
		|		ПО (ИСТИНА)
		|ГДЕ
		|	&ПлательщикЕН";
	Запрос.УстановитьПараметр("ПлательщикЕН", СтруктураДополнительныеСвойства.УчетнаяПолитика.ПлательщикЕНМетодНачисления);
	РезультатЗапроса = Запрос.Выполнить();	
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ОборотыПоДаннымЕдиногоНалога", РезультатЗапроса.Выгрузить());
КонецПроцедуры

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства, Отказ) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаДокумента.Ссылка КАК Ссылка,
		|	ТаблицаДокумента.Дата КАК Дата,
		|	ТаблицаДокумента.Номер КАК Номер,
		|	ТаблицаДокумента.Организация КАК Организация,
		|	ТаблицаДокумента.Склад КАК Склад,
		|	ТаблицаДокумента.ВалютаДокумента КАК ВалютаДокумента,
		|	ТаблицаДокумента.Контрагент КАК Контрагент,
		|	ТаблицаДокумента.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	ТаблицаДокумента.СчетРасчетовДт КАК СчетРасчетовДт,
		|	ТаблицаДокумента.СчетРасчетовКт КАК СчетРасчетовКт
		|ПОМЕСТИТЬ ВременнаяТаблицаШапка
		|ИЗ
		|	Документ.ОтчетОРозничныхПродажах КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РанееОтраженныеОтчетыОРозничныхПродажах.Ссылка КАК СделкаСсылка,
		|	ТаблицаВозвраты.Номенклатура КАК Номенклатура,
		|	ТаблицаВозвраты.ДатаРеализации КАК ДатаРеализации
		|ПОМЕСТИТЬ ВременнаяТаблицаВозвратыРанееОтраженныеСделки
		|ИЗ
		|	Документ.ОтчетОРозничныхПродажах.Возвраты КАК ТаблицаВозвраты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах КАК РанееОтраженныеОтчетыОРозничныхПродажах
		|		ПО (РанееОтраженныеОтчетыОРозничныхПродажах.Организация = &Организация)
		|			И (РанееОтраженныеОтчетыОРозничныхПродажах.Склад = &Склад)
		|			И (РанееОтраженныеОтчетыОРозничныхПродажах.Дата МЕЖДУ НАЧАЛОПЕРИОДА(ТаблицаВозвраты.ДатаРеализации, ДЕНЬ) И КОНЕЦПЕРИОДА(ТаблицаВозвраты.ДатаРеализации, ДЕНЬ))
		|			И (РанееОтраженныеОтчетыОРозничныхПродажах.Проведен)
		|ГДЕ
		|	ТаблицаВозвраты.Ссылка = &Ссылка
		|	И НЕ РанееОтраженныеОтчетыОРозничныхПродажах.Ссылка ЕСТЬ NULL
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	СделкаСсылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(Сделка.Ссылка) КАК СделкаСсылка,
		|	ВременнаяТаблицаВозвратыРанееОтраженныеСделки.Номенклатура КАК Номенклатура,
		|	ВременнаяТаблицаВозвратыРанееОтраженныеСделки.ДатаРеализации КАК ДатаРеализации
		|ПОМЕСТИТЬ ВременнаяТаблицаВозвратыСделки
		|ИЗ
		|	ВременнаяТаблицаВозвратыРанееОтраженныеСделки КАК ВременнаяТаблицаВозвратыРанееОтраженныеСделки
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах.Товары КАК Сделка
		|		ПО ВременнаяТаблицаВозвратыРанееОтраженныеСделки.СделкаСсылка = Сделка.Ссылка
		|			И ВременнаяТаблицаВозвратыРанееОтраженныеСделки.Номенклатура = Сделка.Номенклатура
		|ГДЕ
		|	НЕ Сделка.Ссылка ЕСТЬ NULL
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаВозвратыРанееОтраженныеСделки.Номенклатура,
		|	ВременнаяТаблицаВозвратыРанееОтраженныеСделки.ДатаРеализации
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	ДатаРеализации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаОплата.Ссылка КАК Ссылка,
		|	ТаблицаОплата.НомерСтроки КАК НомерСтроки,
		|	ТаблицаОплата.ВидОплаты.ТипОплаты КАК ТипОплаты,
		|	ТаблицаОплата.СуммаОплаты КАК СуммаОплаты,
		|	ТаблицаОплата.ВидОплаты.Контрагент КАК Контрагент,
		|	ТаблицаОплата.ВидОплаты.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	ТаблицаОплата.ВидОплаты.ДоговорКонтрагента.ВидДоговора КАК ВидДоговора,
		|	ТаблицаОплата.ВидОплаты.ДоговорКонтрагента.ВалютаРасчетов КАК ВалютаВзаиморасчетов,
		|	ТаблицаОплата.ВидОплаты.СчетУчетаРасчетов КАК СчетУчетаРасчетов
		|ПОМЕСТИТЬ ВременнаяТаблицаВозвратыОплаты
		|ИЗ
		|	Документ.ОтчетОРозничныхПродажах.ВозвратОплаты КАК ТаблицаОплата
		|ГДЕ
		|	ТаблицаОплата.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
		|	ТаблицаДокумента.Номенклатура КАК Номенклатура,
		|	ТаблицаДокумента.Номенклатура.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
		|	ТаблицаДокумента.Количество КАК Количество,
		|	ТаблицаДокумента.Всего КАК Всего,
		|	ТаблицаДокумента.Сумма КАК Сумма,
		|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
		|	ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
		|	ТаблицаДокумента.СтавкаНСП КАК СтавкаНСП,
		|	ТаблицаДокумента.СуммаНСП КАК СуммаНСП,
		|	ТаблицаДокумента.СчетУчета КАК СчетУчета,
		|	ТаблицаДокумента.СчетДоходов КАК СчетДоходов,
		|	ТаблицаДокумента.СтатьяДоходов КАК СтатьяДоходов,
		|	ТаблицаДокумента.СчетСебестоимости КАК СчетСебестоимости,
		|	ТаблицаДокумента.СтатьяРасходов КАК СтатьяРасходов,
		|	ТаблицаДокумента.БезналичныйРасчет КАК БезналичныйРасчет,
		|	ТаблицаДокумента.ВидДеятельности КАК ВидДеятельности
		|ПОМЕСТИТЬ ВременнаяТаблицаТовары
		|ИЗ
		|	Документ.ОтчетОРозничныхПродажах.Товары КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
		|	ТаблицаДокумента.Номенклатура КАК Номенклатура,
		|	ТаблицаДокумента.Номенклатура.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
		|	ТаблицаДокумента.Количество КАК Количество,
		|	ТаблицаДокумента.Всего КАК Всего,
		|	ТаблицаДокумента.Сумма КАК Сумма,
		|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
		|	ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
		|	ТаблицаДокумента.СтавкаНСП КАК СтавкаНСП,
		|	ТаблицаДокумента.СуммаНСП КАК СуммаНСП,
		|	ТаблицаДокумента.СчетДоходов КАК СчетДоходов,
		|	ТаблицаДокумента.СтатьяДоходов КАК СтатьяДоходов,
		|	ТаблицаДокумента.БезналичныйРасчет КАК БезналичныйРасчет,
		|	ТаблицаДокумента.ВидДеятельности КАК ВидДеятельности
		|ПОМЕСТИТЬ ВременнаяТаблицаУслуги
		|ИЗ
		|	Документ.ОтчетОРозничныхПродажах.Услуги КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
		|	ТаблицаДокумента.Номенклатура КАК Номенклатура,
		|	ТаблицаДокумента.Номенклатура.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
		|	ТаблицаДокумента.Количество КАК Количество,
		|	ТаблицаДокумента.Всего КАК Всего,
		|	ТаблицаДокумента.Сумма КАК Сумма,
		|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
		|	ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
		|	ТаблицаДокумента.СтавкаНСП КАК СтавкаНСП,
		|	ТаблицаДокумента.СуммаНСП КАК СуммаНСП,
		|	ТаблицаДокумента.СчетУчета КАК СчетУчета,
		|	ТаблицаДокумента.СчетДоходов КАК СчетДоходов,
		|	ТаблицаДокумента.СтатьяДоходов КАК СтатьяДоходов,
		|	ТаблицаДокумента.СчетСебестоимости КАК СчетСебестоимости,
		|	ТаблицаДокумента.СтатьяРасходов КАК СтатьяРасходов,
		|	ТаблицаДокумента.ДатаРеализации КАК ДатаРеализации,
		|	ТаблицаДокумента.БезналичныйРасчет КАК БезналичныйРасчет,
		|	ТаблицаДокумента.ВидДеятельности КАК ВидДеятельности,
		|	ЕСТЬNULL(ВременнаяТаблицаВозвратыСделки.СделкаСсылка, НЕОПРЕДЕЛЕНО) КАК Сделка,
		|	ЕСТЬNULL(ВременнаяТаблицаВозвратыСделки.СделкаСсылка = &Ссылка, ЛОЖЬ) КАК ВозвратТекущейСмены,
		|	ЕСТЬNULL(ВременнаяТаблицаВозвратыСделки.СделкаСсылка, НЕОПРЕДЕЛЕНО) КАК Партия,
		|	НЕ ВременнаяТаблицаВозвратыСделки.СделкаСсылка ЕСТЬ NULL КАК УказанДокументОтгрузки
		|ПОМЕСТИТЬ ВременнаяТаблицаВозвраты
		|ИЗ
		|	Документ.ОтчетОРозничныхПродажах.Возвраты КАК ТаблицаДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаВозвратыСделки КАК ВременнаяТаблицаВозвратыСделки
		|		ПО ТаблицаДокумента.ДатаРеализации = ВременнаяТаблицаВозвратыСделки.ДатаРеализации
		|			И ТаблицаДокумента.Номенклатура = ВременнаяТаблицаВозвратыСделки.Номенклатура
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
		|	ТаблицаДокумента.Номенклатура КАК Номенклатура,
		|	ТаблицаДокумента.Номенклатура.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
		|	ТаблицаДокумента.Количество КАК Количество,
		|	ТаблицаДокумента.Всего КАК Всего,
		|	ТаблицаДокумента.Сумма КАК Сумма,
		|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
		|	ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
		|	ТаблицаДокумента.СтавкаНСП КАК СтавкаНСП,
		|	ТаблицаДокумента.СуммаНСП КАК СуммаНСП,
		|	ТаблицаДокумента.СчетДоходов КАК СчетДоходов,
		|	ТаблицаДокумента.СтатьяДоходов КАК СтатьяДоходов,
		|	ТаблицаДокумента.БезналичныйРасчет КАК БезналичныйРасчет,
		|	ТаблицаДокумента.ВидДеятельности КАК ВидДеятельности
		|ПОМЕСТИТЬ ВременнаяТаблицаВозвратУслуг
		|ИЗ
		|	Документ.ОтчетОРозничныхПродажах.ВозвратУслуг КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОтчетОРозничныхПродажахОплата.НомерСтроки КАК НомерСтроки,
		|	ОтчетОРозничныхПродажахОплата.ВидОплаты КАК ВидОплаты,
		|	ОтчетОРозничныхПродажахОплата.СуммаОплаты КАК СуммаОплаты
		|ПОМЕСТИТЬ ВременнаяТаблицаОплата
		|ИЗ
		|	Документ.ОтчетОРозничныхПродажах.Оплата КАК ОтчетОРозничныхПродажахОплата
		|ГДЕ
		|	ОтчетОРозничныхПродажахОплата.Ссылка = &Ссылка";		
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("Склад", ДокументСсылка.Склад);
	Запрос.Выполнить();
	
	СформироватьТаблицаХозрасчетный(ДокументСсылка, СтруктураДополнительныеСвойства, Отказ);
	СформироватьТаблицаПродажи(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаОборотыПоДаннымЕдиногоНалога(СтруктураДополнительныеСвойства);
	
КонецПроцедуры

#КонецОбласти	

#Область СлужебныеПроцедурыИФункции

#Область Возвраты

// Подготавливаем таблицу партий на которые будет возвращен ранее реализованный товар
//
Функция ПодготовитьТаблицуСписанныеТоварыОтПокупателя(ТаблицаВозвращенныеСписанныеТовары, СписаниеТоваровСчетаУчета, СписаниеТоваровНоменклатура, ТаблицаРеквизиты, Отказ)
	ТаблицаСписанныеТоварыОтПокупателя = УчетТоваров.ПолучитьПустуюТаблицуСписанныеТовары();
	ТаблицаСписанныеТоварыОтПокупателя.Колонки.Добавить("ВозвратТекущейСмены");
	
	Если ТаблицаРеквизиты <> Неопределено Тогда
		// таблица реквизиты содержит строки в разрезе документов списания
		Для Каждого СтрокаРеквизиты Из ТаблицаРеквизиты Цикл
			// подбираем партии по каждому документу списания
			ОтборПоДокументуРеализации = Новый Структура("Сделка", СтрокаРеквизиты.Сделка);
			
			Если СтрокаРеквизиты.УказанаДатаРеализации
				И НЕ СтрокаРеквизиты.ВозвратТекущейСмены
				И НЕ ЗначениеЗаполнено(СтрокаРеквизиты.Сделка) Тогда
				
				ТаблицаТоваровПоСделке = ТаблицаВозвращенныеСписанныеТовары.НайтиСтроки(ОтборПоДокументуРеализации);
				Для каждого СтрокаТовара Из ТаблицаТоваровПоСделке Цикл
					ТекстСообщения = НСтр("ru = 'За ""%1"" было реализовано ""%2"" на ""%3"" ед. меньше, чем возвращается.'");
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, 
						Формат(СтрокаРеквизиты.ПартияДата, "ДФ=dd.MM.yyyy"), СтрокаТовара.Номенклатура, СтрокаТовара.Количество);
					
					ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Корректность",
						НСтр("ru = 'Дата реализации'"), СтрокаТовара.НомерСтроки, НСтр("ru = 'Возвраты'"), ТекстСообщения);

					Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Возвраты", СтрокаТовара.НомерСтроки, "ДатаРеализации");
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, СтрокаРеквизиты.Регистратор, Поле, "Объект", Отказ);
				КонецЦикла; 
				
				Продолжить;
			КонецЕсли; 
			
			РеквизитыВозвраты 				= ТаблицаРеквизиты.Скопировать(ОтборПоДокументуРеализации);
			ТаблицаТоваровПоСделке 			= ТаблицаВозвращенныеСписанныеТовары.Скопировать(ОтборПоДокументуРеализации);
			ТаблицаСчетаУчетаПоСделке 		= СписаниеТоваровСчетаУчета.Скопировать(ОтборПоДокументуРеализации);
			ТаблицаНоменклатураПоСделке 	= СписаниеТоваровНоменклатура.Скопировать(ОтборПоДокументуРеализации);
			
			Если СтрокаРеквизиты.ВозвратТекущейСмены Тогда
				ТаблицаСписанныеТоварыОтПокупателяПоСделке = УчетТоваров.ПолучитьПустуюТаблицуСписанныеТовары();
				ОбщегоНазначенияБПВызовСервера.ЗагрузитьВТаблицуЗначений(ТаблицаТоваровПоСделке, ТаблицаСписанныеТоварыОтПокупателяПоСделке);
			Иначе
				ТаблицаСписанныеТоварыОтПокупателяПоСделке = УчетТоваров.ПодготовитьТаблицуВозвращенныеСписанныеТовары(
					ТаблицаТоваровПоСделке, 
					ТаблицаСчетаУчетаПоСделке,
					ТаблицаНоменклатураПоСделке, 
					РеквизитыВозвраты, Отказ);
			КонецЕсли;
			
			ТаблицаСписанныеТоварыОтПокупателяПоСделке.Колонки.Добавить("ВозвратТекущейСмены");
			ТаблицаСписанныеТоварыОтПокупателяПоСделке.ЗаполнитьЗначения(СтрокаРеквизиты.ВозвратТекущейСмены, "ВозвратТекущейСмены");
			
			// загружаем подобранные партии по сделке в результирующую таблицу
			ОбщегоНазначенияБПВызовСервера.ЗагрузитьВТаблицуЗначений(ТаблицаСписанныеТоварыОтПокупателяПоСделке, ТаблицаСписанныеТоварыОтПокупателя);
			
		КонецЦикла; 
		
	КонецЕсли;
	
	Возврат ТаблицаСписанныеТоварыОтПокупателя;
КонецФункции 

Процедура ЗаполнитьПартииВозвратаТекущейСмены(ТаблицаВозвраты, ТаблицаРеализации, Отказ) Экспорт
	ТаблицаРезультата = ТаблицаВозвраты.Скопировать(Новый Структура("ВозвратТекущейСмены", Ложь));
	
	ТаблицаОтгрузок = ТаблицаРеализации.Скопировать();
	
	ВозвратыТекущейСмены = ТаблицаВозвраты.НайтиСтроки(Новый Структура("ВозвратТекущейСмены", Истина));
	Для Каждого СтрокаВозврата Из ВозвратыТекущейСмены Цикл

		КоличествоНеПодобраноПартии = СтрокаВозврата.Количество;

		ОтгруженныеПартии = ТаблицаОтгрузок.НайтиСтроки(Новый Структура("Номенклатура, СчетУчета", СтрокаВозврата.Номенклатура, СтрокаВозврата.СчетУчета));
		Для Каждого СтрокаОтгрузки Из ОтгруженныеПартии Цикл
			
			НоваяСтрока = ТаблицаРезультата.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаВозврата);
			НоваяСтрока.Партия = СтрокаОтгрузки.Партия;
			
			КоличествоВозвращено = Мин(КоличествоНеПодобраноПартии, СтрокаОтгрузки.Количество);

			Если СтрокаОтгрузки.Количество = КоличествоВозвращено Тогда
				// Текущий возврат в совокупности с предыдущими привел к полному возврату всей партии.
				НоваяСтрока.Количество         = КоличествоВозвращено;
				НоваяСтрока.СуммаСписания      = СтрокаОтгрузки.СуммаСписания;
				
			ИначеЕсли КоличествоВозвращено > СтрокаОтгрузки.Количество Тогда
				// Текущий возврат больше, чем очередная партия, т.е. фактически возвращают несколько разных партий.
				НоваяСтрока.Количество         = СтрокаОтгрузки.Количество;
				НоваяСтрока.СуммаСписания      = СтрокаОтгрузки.СуммаСписания;
			Иначе
				// Частичный возврат партии.
				НоваяСтрока.Количество         = КоличествоВозвращено;
				Коэфф                          = КоличествоВозвращено / СтрокаОтгрузки.Количество;
				НоваяСтрока.СуммаСписания      = Окр(СтрокаОтгрузки.СуммаСписания * Коэфф, 2);
			КонецЕсли;
			
			КоличествоНеПодобраноПартии = КоличествоНеПодобраноПартии - НоваяСтрока.Количество;
			
			СтрокаОтгрузки.Количество = СтрокаОтгрузки.Количество - НоваяСтрока.Количество;
			СтрокаОтгрузки.СуммаСписания = СтрокаОтгрузки.СуммаСписания - НоваяСтрока.СуммаСписания;

			Если КоличествоНеПодобраноПартии = 0 Тогда
				Прервать;
			КонецЕсли;

		КонецЦикла;

		// Если в документе указан документ реализации по которому было отгружено больше,
		// чем возвращается, то эту разницу отнесем на пустую партию.
		Если КоличествоНеПодобраноПартии > 0 Тогда
			ТекстСообщения = НСтр("ru = 'Документом ""%1"" было реализовано ""%2"" на ""%3"" ед. меньше, чем возвращается.'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ТекстСообщения,
				СокрЛП(СтрокаВозврата.Сделка),
				СтрокаВозврата.Номенклатура,
				КоличествоНеПодобраноПартии);
				
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка", "Корректность",
				НСтр("ru = 'Количество'"), СтрокаВозврата.НомерСтроки, НСтр("ru = 'Возвраты'"), ТекстСообщения);

			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Возвраты", СтрокаВозврата.НомерСтроки, "Количество");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, СтрокаВозврата.Сделка, Поле, "Объект", Отказ);
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаВозвраты = ТаблицаРезультата;
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ВерсионированиеОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
//@skip-warning
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#КонецЕсли
