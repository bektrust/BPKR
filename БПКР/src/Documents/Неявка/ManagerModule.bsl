#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПроцедурыПроведенияДокумента

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаНачисления(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	// Определение минимальной и максимальной даты.
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	МИНИМУМ(ТаблицаДокумента.ДатаНачала) КАК ДатаНачала,
		|	МАКСИМУМ(ТаблицаДокумента.ДатаОкончания) КАК ДатаОкончания
		|ИЗ
		|	ВременнаяТаблицаНачисления КАК ТаблицаДокумента";
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	ДатаНачала = Выборка.ДатаНачала;
	ДатаОкончания = Выборка.ДатаОкончания;
	
	// Распределение по периодам действия(месяц).
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КалендариГрафиковРабот.Дата КАК Дата,
		|	КалендариГрафиковРабот.ГрафикРаботы КАК ГрафикРаботы,
		|	КалендариГрафиковРабот.ЗначениеДней КАК ЗначениеДней
		|ПОМЕСТИТЬ ВременнаяТаблицаКалендарь
		|ИЗ
		|	РегистрСведений.КалендариГрафиковРабот КАК КалендариГрафиковРабот
		|ГДЕ
		|	КалендариГрафиковРабот.ГрафикРаботы В
		|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				ВременнаяТаблицаНачисления.ГрафикРаботы
		|			ИЗ
		|				ВременнаяТаблицаНачисления КАК ВременнаяТаблицаНачисления)
		|	И КалендариГрафиковРабот.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И КалендариГрафиковРабот.Год МЕЖДУ &ГодНачалоПериода И &ГодКонецПериода
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МИНИМУМ(НАЧАЛОПЕРИОДА(ВременнаяТаблицаШапка.Дата, МЕСЯЦ)) КАК ПериодРегистрации,
		|	МИНИМУМ(ВременнаяТаблицаШапка.Организация) КАК Организация,
		|	ВременнаяТаблицаНачисления.ФизЛицо КАК ФизЛицо,
		|	МИНИМУМ(ВременнаяТаблицаНачисления.ВидРасчета) КАК ВидРасчета,
		|	МИНИМУМ(ВременнаяТаблицаНачисления.ГрафикРаботы) КАК ГрафикРаботы,
		|	МИНИМУМ(ВременнаяТаблицаКалендарь.Дата) КАК ПериодДействияНачало,
		|	МАКСИМУМ(КОНЕЦПЕРИОДА(ВременнаяТаблицаКалендарь.Дата, ДЕНЬ)) КАК ПериодДействияКонец,
		|	СУММА(ВременнаяТаблицаКалендарь.ЗначениеДней) КАК ОтработаноДней,
		|	НАЧАЛОПЕРИОДА(ВременнаяТаблицаКалендарь.Дата, МЕСЯЦ) КАК ПериодДействия
		|ИЗ
		|	ВременнаяТаблицаНачисления КАК ВременнаяТаблицаНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКалендарь КАК ВременнаяТаблицаКалендарь
		|		ПО ВременнаяТаблицаНачисления.ГрафикРаботы = ВременнаяТаблицаКалендарь.ГрафикРаботы
		|			И (ВременнаяТаблицаКалендарь.Дата МЕЖДУ ВременнаяТаблицаНачисления.ДатаНачала И ВременнаяТаблицаНачисления.ДатаОкончания)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаШапка.ВидНеявки = 0
		|
		|СГРУППИРОВАТЬ ПО
		|	НАЧАЛОПЕРИОДА(ВременнаяТаблицаКалендарь.Дата, МЕСЯЦ),
		|	ВременнаяТаблицаНачисления.ФизЛицо";
	Запрос.УстановитьПараметр("ГодНачалоПериода", Год(ДатаНачала));
	Запрос.УстановитьПараметр("ГодКонецПериода", Год(ДатаОкончания));
	Запрос.УстановитьПараметр("НачалоПериода", ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", ДатаОкончания);

	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаНачисления = РезультатЗапроса.Выгрузить();
	ТаблицаНачисления.Очистить();
	
	Выборка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока Выборка.Следующий() Цикл 
		НоваяСтрокаТаблицы = ТаблицаНачисления.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаТаблицы, Выборка);
	КонецЦикла;	
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаНачисления", ТаблицаНачисления);
КонецПроцедуры // СформироватьТаблицаНачисления()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТабельПоЧасам(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Дата КАК Дата,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ВременнаяТаблицаНачисления.ФизЛицо КАК ФизЛицо,
		|	ВременнаяТаблицаНачисления.ВидРасчета КАК ВидРасчета,
		|	ВременнаяТаблицаНачисления.Часов КАК ЧасовНеявки
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНачисления КАК ВременнаяТаблицаНачисления
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ВременнаяТаблицаШапка.ВидНеявки = 1";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаТабельПоЧасам", РезультатЗапроса.Выгрузить());
КонецПроцедуры // СформироватьТабельПоЧасам()

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДокумента.Дата КАК Дата,
		|	ТаблицаДокумента.Организация КАК Организация,
		|	ТаблицаДокумента.ВидНеявки КАК ВидНеявки
		|ПОМЕСТИТЬ ВременнаяТаблицаШапка
		|ИЗ
		|	Документ.Неявка КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДокумента.ФизЛицо КАК ФизЛицо,
		|	ТаблицаДокумента.ВидОтсутствия КАК ВидРасчета,
		|	ТаблицаДокумента.ДатаНачала КАК ДатаНачала,
		|	ТаблицаДокумента.ДатаОкончания КАК ДатаОкончания,
		|	ТаблицаДокумента.Дней КАК Дней,
		|	ТаблицаДокумента.Часов КАК Часов,
		|	ТаблицаДокумента.ГрафикРаботы КАК ГрафикРаботы
		|ПОМЕСТИТЬ ВременнаяТаблицаНачисления
		|ИЗ
		|	Документ.Неявка.Сотрудники КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Выполнить();
	
	СформироватьТаблицаНачисления(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТабельПоЧасам(ДокументСсылка, СтруктураДополнительныеСвойства);
КонецПроцедуры // ИнициализироватьДанныеДокумента()

#КонецОбласти

#Область ВерсионированиеОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
//@skip-warning
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#КонецЕсли
