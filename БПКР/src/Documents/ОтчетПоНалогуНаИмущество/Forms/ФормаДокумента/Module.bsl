#Область ОбработчикиСобытийФормы

// Процедура - обработчик события ПриСозданииНаСервере.
// В процедуре осуществляется
// - инициализация реквизитов формы,
// - установка параметров функциональных опций формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Установка реквизитов формы.
	ДатаДокумента = Объект.Дата;
	
	ЗаполнитьИтоги();
	
	// РаботаСФормами
	РаботаСФормамиСервер.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец РаботаСФормами
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.ГруппаВажныеКоманды;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

// Процедура - обработчик события ПриЧтенииНаСервере.
//
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменений
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменений
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом 
	
КонецПроцедуры

// Процедура - обработчик события ПриОткрытии.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

// Процедура - обработчик события ПослеЗаписиНаСервере.
//
&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)	
	
	// РаботаСФормами
	РаботаСФормамиСервер.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец РаботаСФормами
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.КонтрольВеденияУчета
	КонтрольВеденияУчета.ПослеЗаписиНаСервере(ТекущийОбъект);
	// Конец СтандартныеПодсистемы.КонтрольВеденияУчета
	
КонецПроцедуры

// Процедура - обработчик события ПослеЗаписи.
//
&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Процедура - обработчик события ПриИзменении поля ввода Дата.
// В процедуре определяется ситуация, когда при изменении своей даты документ 
// оказывается в другом периоде нумерации документов, и в этом случае
// присваивает документу новый уникальный номер.
// Переопределяет соответствующий параметр формы.
//
&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	Объект.Дата = КонецКвартала(Объект.Дата);
	Если ЕстьДокументВУказанныйПериод(Объект.Дата) Тогда
		ТекстСообщения = НСтр("ru = 'Для указанного периода уже существует документ ""Отчет по налогу на имущество"".'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, "Объект.Дата");
		// Возвращаем значение до изменения
		Объект.Дата	= ДатаДокумента;
	    Возврат;
	КонецЕсли;

	// Обработка события изменения даты.
	ДатаПередИзменением = ДатаДокумента;
	ДатаДокумента = Объект.Дата;
	Если Объект.Дата <> ДатаПередИзменением Тогда		
		Объект.Дата = КонецМесяца(Объект.Дата);		
		СтруктураДанные = ПолучитьДанныеДатаПриИзменении(ДатаПередИзменением);
		Если СтруктураДанные.РазностьДат <> 0 Тогда
			Объект.Номер = "";
		КонецЕсли;				
	КонецЕсли;

КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Организация.
//
&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	// Обработка события изменения организации.
	Объект.Номер = "";
	БухгалтерскийУчетВызовСервера.УчетнаяПолитикаСуществует(Объект.Организация, ДатаДокумента, Истина, Объект.Ссылка);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	
	Если Объект.Недвижимость.Количество() > 0 ИЛИ Объект.Недвижимость.Количество() > 0 Тогда 
		ОписаниеОповещения = Новый ОписаниеОповещения("ОтветНаВопросЗаполнить", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Данные табличной части документа будут перезаполнены. Продолжить выполнение операции?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60);
	Иначе
		ЗаполнитьНаСервере();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиРезультатовИнтерактивныхДействий

&НаКлиенте
Процедура ОтветНаВопросЗаполнить(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗаполнитьНаСервере();
	КонецЕсли; 
КонецПроцедуры	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Получает набор данных с сервера для процедуры ДатаПриИзменении.
//
&НаСервере
Функция ПолучитьДанныеДатаПриИзменении(ДатаПередИзменением)
	РазностьДат = БухгалтерскийУчетСервер.ПроверитьНомерДокумента(Объект.Ссылка, Объект.Дата, ДатаПередИзменением);
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("РазностьДат",	РазностьДат);
	
	Возврат СтруктураДанные;
КонецФункции // ПолучитьДанныеДатаПриИзменении()

&НаСервере
Функция ЕстьДокументВУказанныйПериод(Дата)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОтчетПоНалогуНаИмущество.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ОтчетПоНалогуНаИмущество КАК ОтчетПоНалогуНаИмущество
		|ГДЕ
		|	НЕ ОтчетПоНалогуНаИмущество.ПометкаУдаления
		|	И КОНЕЦПЕРИОДА(ОтчетПоНалогуНаИмущество.Дата, КВАРТАЛ) = &Дата";
	
	Запрос.УстановитьПараметр("Дата", Дата);		
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Выборка.Количество() > 0 	

КонецФункции 

&НаСервере
Процедура ЗаполнитьНаСервере()
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗакрытиеМесяцаАмортизацияОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ЗакрытиеМесяцаАмортизацияОС.Сумма КАК Сумма
		|ИЗ
		|	Документ.ЗакрытиеМесяца.АмортизацияОС КАК ЗакрытиеМесяцаАмортизацияОС
		|ГДЕ
		|	КонецПериода(ЗакрытиеМесяцаАмортизацияОС.Ссылка.Дата, МЕСЯЦ) = КонецПериода(&Дата, МЕСЯЦ)
		|	И ЗакрытиеМесяцаАмортизацияОС.Ссылка.Проведен";
	
	Запрос.УстановитьПараметр("Дата", Объект.Дата);
	
	АмортизацияОС =Запрос.Выполнить().Выгрузить();
	
	// 
	НомерМесяца = Месяц(Объект.Дата);
	Если НомерМесяца = 12 Тогда
		НачалоПериода = НачалоГода(Объект.Дата);
	Иначе
	    НачалоПериода = НачалоМесяца(Объект.Дата);
	КонецЕсли;
	
	МассивРезультатов = Документы.ЗакрытиеМесяца.ПолучитьДанныеПоНалогуНаИмущество(Объект.Организация, НачалоПериода, КонецМесяца(Объект.Дата));
	
	Объект.Недвижимость.Очистить();
	Объект.Транспорт.Очистить();	
	
	// Недвижимость.
	Выборка = МассивРезультатов[8].Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Стоимость = 0;
		Результат = 0;
		
		// Проверка заполнения показателей.
		ТекстСообщения = "";
		Если Выборка.ОбщаяПлощадь = 0 Тогда 
			ТекстСообщения = НСтр("ru = 'Расчет налога на имущество. Общая площадь равна 0. Основное средство ""%1"".'");
		ИначеЕсли Выборка.ОценочнаяСтоимость = 0 Тогда 
			ТекстСообщения = НСтр("ru = 'Расчет налога на имущество. Оценочная стоимость равна 0. Основное средство ""%1"".'");
		ИначеЕсли Выборка.РегиональныйКоэффициент = 0 Тогда 
			ТекстСообщения = НСтр("ru = 'Расчет налога на имущество. Региональный Коэффициент равен 0. Основное средство ""%1"".'");
		ИначеЕсли Выборка.ЗональныйКоэффициент = 0 Тогда 
			ТекстСообщения = НСтр("ru = 'Расчет налога на имущество. Зональный Коэффициент равен 0. Основное средство ""%1"".'");
		ИначеЕсли Выборка.ОтраслевойКоэффициент = 0 Тогда 
			ТекстСообщения = НСтр("ru = 'Расчет налога на имущество. Отраслевой Коэффициент равен 0. Основное средство ""%1"".'");
		ИначеЕсли Выборка.Ставка = 0 Тогда 
			ТекстСообщения = НСтр("ru = 'Расчет налога на имущество. Ставка налога на имущество равна 0. Основное средство ""%1"".'");
		ИначеЕсли Выборка.КоличествоМесяцевЭксплуатации = 0 Тогда 
			// Не ошибка. Снято с учета в первом месяце года.
			Продолжить;
		ИначеЕсли Выборка.ДатаНачисленияНалогаНаИмущество > КонецМесяца(Объект.Дата) Тогда
			Продолжить;			
		Иначе 
			Стоимость = Выборка.ОбщаяПлощадь * Выборка.ОценочнаяСтоимость * Выборка.РегиональныйКоэффициент * Выборка.ЗональныйКоэффициент * Выборка.ОтраслевойКоэффициент;
			Результат = Выборка.КоличествоМесяцевЭксплуатации / 12 * (Стоимость * Выборка.Ставка / 100);
		КонецЕсли;	
		
		Если НЕ ТекстСообщения = "" Тогда 
			ТекстСообщения = СтрШаблон(ТекстСообщения, Выборка.ОсновноеСредствоНаименование);
			//ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, Неопределено, ТекстСообщения, Неопределено);
		КонецЕсли;	
		
		СтрокаТабличнойЧасти = Объект.Недвижимость.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, Выборка);
		СтрокаТабличнойЧасти.Кр = Выборка.РегиональныйКоэффициент;
		СтрокаТабличнойЧасти.Кз = Выборка.ЗональныйКоэффициент;
		СтрокаТабличнойЧасти.Ко = Выборка.ОтраслевойКоэффициент;
		СтрокаТабличнойЧасти.СуммаНалога = Результат;
		//СтрокаТабличнойЧасти.СтоимостьНУ = Стоимость;
	КонецЦикла; 
	
	// Транспорт.
	Выборка = МассивРезультатов[16].Выбрать();
	
	НесамоходныеТС = Справочники.ВидыТранспортаНалогНаИмущество.НайтиПоНаименованию("Несамоходные");
	ДругиеТС = Справочники.ВидыТранспортаНалогНаИмущество.НайтиПоНаименованию("Другие");
	
	Пока Выборка.Следующий() Цикл
		Результат = 0;
		
		// Проверка заполнения показателей.
		ТекстСообщения = "";	
		
		Если Выборка.ВидТранспорта = НесамоходныеТС ИЛИ Выборка.ВидТранспорта = ДругиеТС Тогда
			
			СтрокаТабличнойЧасти = АмортизацияОС.Найти(Выборка.ОсновноеСредство, "ОсновноеСредство");
			
			Если СтрокаТабличнойЧасти <> Неопределено Тогда
				Результат = (Выборка.ПервоначальнаяСтоимость - Выборка.НакопленныйИзнос - СтрокаТабличнойЧасти.Сумма) * Выборка.Ставка / 100;
			Иначе	
				Результат = (Выборка.ПервоначальнаяСтоимость - Выборка.НакопленныйИзнос) * Выборка.Ставка / 100;
			КонецЕсли;
			
		Иначе		
			Если Выборка.ОбъемДвигателя = 0 Тогда 
				ТекстСообщения = НСтр("ru = 'Расчет налога на имущество. Объем двигателя равен 0. Основное средство ""%1"".'");
			ИначеЕсли Выборка.ОценочнаяСтоимость = 0 Тогда 
				ТекстСообщения = НСтр("ru = 'Расчет налога на имущество. Оценочная стоимость равна 0. Основное средство ""%1"".'");	
			Иначе
				Результат = Выборка.ОбъемДвигателя * Выборка.ОценочнаяСтоимость;
			КонецЕсли;
		КонецЕсли;
		
		Если Выборка.ДатаНачисленияНалогаНаИмущество > КонецМесяца(Объект.Дата) Тогда
			Продолжить;	
		КонецЕсли;
		
		Если НЕ ТекстСообщения = "" Тогда 
			ТекстСообщения = СтрШаблон(ТекстСообщения, Выборка.ОсновноеСредствоНаименование);
			//ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, Неопределено, ТекстСообщения, Неопределено);
		КонецЕсли;	
		
		СтрокаТабличнойЧасти = Объект.Транспорт.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, Выборка);
		СтрокаТабличнойЧасти.СуммаНалога = Результат;
		СтрокаТабличнойЧасти.АБВ = Выборка.ВидТранспортаКатегорияАБВ;
	КонецЦикла;
	
	СтруктураСуммНалога = Новый Структура();
	СтруктураСуммНалога.Вставить("Март", 	 0);
	СтруктураСуммНалога.Вставить("Июнь", 	 0);
	СтруктураСуммНалога.Вставить("Сентябрь", 0);
	СтруктураСуммНалога.Вставить("Декабрь",  0);
	
	СуммаНалога = 0;
	
	СтруктураОтбора = Новый Структура();
	СтруктураОтбора.Вставить("ГруппаИмущества", 0);	
	
	Объект.Приложение.Очистить();
	
	Для Индекс = 2 По 3 Цикл
		ГруппаИмущества = Справочники.ГруппыИмущества.НайтиПоНаименованию(Строка(Индекс) + " группа");
		
		СтруктураОтбора.ГруппаИмущества = ГруппаИмущества;
		
		МассивСтрок = Объект.Недвижимость.НайтиСтроки(СтруктураОтбора);
		
		Для Каждого СтрокаМассива Из МассивСтрок Цикл
			СуммаНалога = СуммаНалога + СтрокаМассива.СуммаНалога - СтрокаМассива.ЗемельныйНалог;			
		КонецЦикла;	
		
		МассивКоэффициентов = Новый Массив();
		МассивКоэффициентов.Добавить(1);
		МассивКоэффициентов.Добавить(1);
		МассивКоэффициентов.Добавить(1);
		МассивКоэффициентов.Добавить(1);
		
		МассивСуммКорректировки = ОбщегоНазначенияКлиентСервер.РаспределитьСуммуПропорциональноКоэффициентам(СуммаНалога, МассивКоэффициентов);
		
		Если МассивСуммКорректировки <> Неопределено Тогда
			СтруктураСуммНалога.Март 	 = МассивСуммКорректировки[0];
			СтруктураСуммНалога.Июнь 	 = МассивСуммКорректировки[1];
			СтруктураСуммНалога.Сентябрь = МассивСуммКорректировки[2];
			СтруктураСуммНалога.Декабрь  = МассивСуммКорректировки[3];
			
			СтрокаТабличнойЧасти = Объект.Приложение.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, СтруктураСуммНалога);
			СтрокаТабличнойЧасти.ГруппаИмущества = ГруппаИмущества;
		КонецЕсли;
		
		СуммаНалога = 0;
	КонецЦикла;
	
	ЗаполнитьИтоги();
	
КонецПроцедуры // ЗаполнитьНаСервере()

&НаСервере
Процедура ЗаполнитьИтоги()
	НачисленоГИ23 = Объект.Недвижимость.Итог("СуммаНалога");
	НачисленоГИ4 = Объект.Транспорт.Итог("СуммаНалога");	
	АвансГИ23 = 0;
	АвансГИ4 = 0;
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ХозрасчетныйОбороты.СуммаОборотДт КАК СуммаАванс
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(НАЧАЛОПЕРИОДА(&Дата, ГОД), КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ), , Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НалогНаИмущество), , , , ) КАК ХозрасчетныйОбороты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ХозрасчетныйОбороты.СуммаОборотДт КАК СуммаАванс
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты(НАЧАЛОПЕРИОДА(&Дата, ГОД), КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ), , Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НалогНаТранспортныеСредства), , , , ) КАК ХозрасчетныйОбороты";
	
	Запрос.УстановитьПараметр("Дата", ДатаДокумента);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Если НЕ РезультатЗапроса[0].Пустой() Тогда
		ВыборкаДетальныеЗаписи = РезультатЗапроса[0].Выбрать();
		ВыборкаДетальныеЗаписи.Следующий();
		АвансГИ23 = ВыборкаДетальныеЗаписи.СуммаАванс;	
	КонецЕсли;
	Если НЕ РезультатЗапроса[0].Пустой() Тогда
		ВыборкаДетальныеЗаписи = РезультатЗапроса[1].Выбрать();
		ВыборкаДетальныеЗаписи.Следующий();
		АвансГИ4 = ВыборкаДетальныеЗаписи.СуммаАванс;	
	КонецЕсли;
	
	ИтогоГИ23 = НачисленоГИ23 - АвансГИ23;
	ИтогоГИ4 = НачисленоГИ4 - АвансГИ4;

КонецПроцедуры // ЗаполнитьИтоги()

#КонецОбласти

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ПодключаемыеКоманды

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
     ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
     ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры
 
&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
     ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
     ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

