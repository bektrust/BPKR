#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события ОбработкаЗаполнения объекта.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеОбъектовБП.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения);
	
КонецПроцедуры

// Процедура - обработчик события ПередЗаписью объекта.
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ВидимостьКомандыСличительнаяВедомость = ОС.Итог("ИзлишекСумма") > 0 Или ОС.Итог("НедостачаСумма") > 0;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура заполняет табличную часть
//
Процедура ЗаполнитьПоОстаткамОС() Экспорт 
	
	// 1. Параметры учета, Местонахождение, Период принятия к учету
	// 2. Остатки по счетам учета и амортизации
	
	ТекстЗапроса =	
		"ВЫБРАТЬ
		|	МестонахождениеОССрезПоследних.ОсновноеСредство КАК ОсновноеСредство,
		|	МестонахождениеОССрезПоследних.Подразделение КАК Подразделение,
		|	МестонахождениеОССрезПоследних.МОЛ КАК МОЛ,
		|	ПараметрыУчетаОССрезПоследних.СчетУчета КАК СчетУчета,
		|	ПараметрыУчетаОССрезПоследних.ИнвентарныйНомер КАК ИнвентарныйНомер
		|ПОМЕСТИТЬ ВременнаяТаблицаОС
		|ИЗ
		|	РегистрСведений.ПараметрыУчетаОС.СрезПоследних(&Период, Организация = &Организация) КАК ПараметрыУчетаОССрезПоследних
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОС.СрезПоследних(&Период, Организация = &Организация) КАК МестонахождениеОССрезПоследних
		|		ПО ПараметрыУчетаОССрезПоследних.ОсновноеСредство = МестонахождениеОССрезПоследних.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияОС.СрезПоследних(
		|				&Период,
		|				Организация = &Организация
		|					И Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийОС.ПринятоКУчету)) КАК СостоянияОССрезПоследних
		|		ПО ПараметрыУчетаОССрезПоследних.ОсновноеСредство = СостоянияОССрезПоследних.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияОС.СрезПоследних(&Период, Организация = &Организация) КАК СостоянияОССрезПоследних_СнятыеСУчета
		|		ПО ПараметрыУчетаОССрезПоследних.ОсновноеСредство = СостоянияОССрезПоследних_СнятыеСУчета.ОсновноеСредство
		|			И (СостоянияОССрезПоследних_СнятыеСУчета.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийОС.СнятоСУчета)
		|				ИЛИ СостоянияОССрезПоследних_СнятыеСУчета.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийОС.Передано))
		|ГДЕ
		|	МестонахождениеОССрезПоследних.Подразделение В ИЕРАРХИИ(&Подразделение)
		|	И МестонахождениеОССрезПоследних.МОЛ = &МОЛ
		|	И СостоянияОССрезПоследних_СнятыеСУчета.ОсновноеСредство ЕСТЬ NULL
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство,
		|	СчетУчета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ВременнаяТаблицаОС.СчетУчета КАК СчетУчета,
		|	ВременнаяТаблицаОС.Подразделение КАК Подразделение,
		|	ВременнаяТаблицаОС.МОЛ КАК МОЛ,
		|	ИСТИНА КАК НаличиеПоДаннымУчета,
		|	ИСТИНА КАК НаличиеФактическое,
		|	ЕСТЬNULL(ТиповойОстатки.СуммаОстатокДт, 0) КАК СтоимостьПоДаннымУчета,
		|	ЕСТЬNULL(ТиповойОстатки.СуммаОстатокДт, 0) КАК СтоимостьФактическая,
		|	ВременнаяТаблицаОС.ИнвентарныйНомер КАК ИнвентарныйНомер
		|ИЗ
		|	ВременнаяТаблицаОС КАК ВременнаяТаблицаОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(
		|				&Период,
		|				Счет В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						ВременнаяТаблицаОС.СчетУчета
		|					ИЗ
		|						ВременнаяТаблицаОС КАК ВременнаяТаблицаОС),
		|				ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства),
		|				Организация = &Организация
		|					И Субконто1 В
		|						(ВЫБРАТЬ
		|							ВременнаяТаблицаОС.ОсновноеСредство
		|						ИЗ
		|							ВременнаяТаблицаОС КАК ВременнаяТаблицаОС)) КАК ТиповойОстатки
		|		ПО ВременнаяТаблицаОС.ОсновноеСредство = ТиповойОстатки.Субконто1";	
	Если НЕ ЗначениеЗаполнено(Подразделение) Тогда 
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "МестонахождениеОССрезПоследних.Подразделение В ИЕРАРХИИ(&Подразделение)", "ИСТИНА");
	КонецЕсли;			
	
	Если НЕ ЗначениеЗаполнено(МОЛ) Тогда 
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "МестонахождениеОССрезПоследних.МОЛ = &МОЛ", "ИСТИНА");
	КонецЕсли;			
	
	Запрос = Новый Запрос(ТекстЗапроса);	
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("МОЛ", МОЛ);	
	
	ОС.Загрузить(Запрос.Выполнить().Выгрузить());
КонецПроцедуры // ЗаполнитьПоОстаткамОСНаСервере()

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли