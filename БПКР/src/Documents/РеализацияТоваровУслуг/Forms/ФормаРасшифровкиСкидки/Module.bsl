
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИтоговыеДанные = Новый Структура();
	ИтоговыеДанные.Вставить("ВсегоБезСкидки", 	0);
	ИтоговыеДанные.Вставить("ДоходБезСкидки", 	0);
	ИтоговыеДанные.Вставить("НДСБезСкидки", 	0);
	ИтоговыеДанные.Вставить("НСПБезСкидки", 	0);
	ИтоговыеДанные.Вставить("ВсегоСкидка", 		0);
	ИтоговыеДанные.Вставить("ДоходСкидка", 		0);
	ИтоговыеДанные.Вставить("НДССкидка", 		0);
	ИтоговыеДанные.Вставить("НСПСкидка", 		0);
	ИтоговыеДанные.Вставить("ВсегоСоСкидкой", 	0);
	ИтоговыеДанные.Вставить("ДоходСоСкидкой", 	0);
	ИтоговыеДанные.Вставить("НДССоСкидкой", 	0);
	ИтоговыеДанные.Вставить("НСПСоСкидкой", 	0);
	
	НомерСтроки = Параметры.НомерСтроки;
	ОтдельнаяПроводкаПоСкидке = Константы.ОтдельнаяПроводкаПоСкидке.Получить();
	
	Для Каждого СтрокаТабличнойЧасти Из Параметры.Объект[Параметры.ИмяТаблицы] Цикл
		
		Если НомерСтроки = СтрокаТабличнойЧасти.НомерСтроки Тогда
			ТекущаяСтрокаДокумента = Истина;	
		Иначе
			ТекущаяСтрокаДокумента = Ложь;
		КонецЕсли;
		
		СтрокаСписка = Список.Добавить();
		
		Если Параметры.ЭтоНоменклатура Тогда
			СтрокаСписка.ВидСтроки = СтрокаТабличнойЧасти.Номенклатура;
		Иначе
			СтрокаСписка.ВидСтроки = СтрокаТабличнойЧасти.ОсновноеСредство;
		КонецЕсли;	
		
		СтрокаСписка.Всего 					= СтрокаТабличнойЧасти.Всего;
		
		Если Параметры.Объект.ДоговорКонтрагента.СуммаВключаетНалоги Тогда
			СтрокаСписка.Доход 				= СтрокаТабличнойЧасти.СуммаДохода 	+ СтрокаТабличнойЧасти.СуммаДоходаСкидки;
		Иначе
			СтрокаСписка.Доход 				= СтрокаТабличнойЧасти.СуммаДохода;	
		КонецЕсли;	
			
		СтрокаСписка.НДС 					= СтрокаТабличнойЧасти.СуммаНДС 	+ СтрокаТабличнойЧасти.СуммаНДССкидки;
		СтрокаСписка.НСП 					= СтрокаТабличнойЧасти.СуммаНСП 	+ СтрокаТабличнойЧасти.СуммаНСПСкидки;
		
		Если ОтдельнаяПроводкаПоСкидке Тогда
			СтрокаСписка.Проводка 			= НСтр("ru = 'Доход всего: 1410 - 6110'");
		КонецЕсли;	
			
		СтрокаСписка.ТекущаяСтрокаДокумента	= ТекущаяСтрокаДокумента;
		
		ИтоговыеДанные.ВсегоБезСкидки 	= ИтоговыеДанные.ВсегоБезСкидки + СтрокаСписка.Всего;
		ИтоговыеДанные.ДоходБезСкидки 	= ИтоговыеДанные.ДоходБезСкидки + СтрокаСписка.Доход;
		ИтоговыеДанные.НДСБезСкидки 	= ИтоговыеДанные.НДСБезСкидки + СтрокаСписка.НДС;
		ИтоговыеДанные.НСПБезСкидки 	= ИтоговыеДанные.НСПБезСкидки + СтрокаСписка.НСП;
		
		СтрокаСписка = Список.Добавить();
		СтрокаСписка.ВидСтроки 				= НСтр("ru = 'Скидка'");
		СтрокаСписка.Всего 					= СтрокаТабличнойЧасти.СуммаСкидки;
		СтрокаСписка.Доход 					= СтрокаТабличнойЧасти.СуммаДоходаСкидки;
		СтрокаСписка.НДС 					= СтрокаТабличнойЧасти.СуммаНДССкидки;
		СтрокаСписка.НСП 					= СтрокаТабличнойЧасти.СуммаНСПСкидки;
		
		Если ОтдельнаяПроводкаПоСкидке Тогда
			СтрокаСписка.Проводка 			= НСтр("ru = 'Доход скидка: 6120 - 1410'");
		КонецЕсли;
		
		СтрокаСписка.ТекущаяСтрокаДокумента	= ТекущаяСтрокаДокумента;
		
		ИтоговыеДанные.ВсегоСкидка 	= ИтоговыеДанные.ВсегоСкидка + СтрокаСписка.Всего;
		ИтоговыеДанные.ДоходСкидка 	= ИтоговыеДанные.ДоходСкидка + СтрокаСписка.Доход;
		ИтоговыеДанные.НДССкидка 	= ИтоговыеДанные.НДССкидка + СтрокаСписка.НДС;
		ИтоговыеДанные.НСПСкидка 	= ИтоговыеДанные.НСПСкидка + СтрокаСписка.НСП;
				
		СтрокаСписка = Список.Добавить();		
		СтрокаСписка.ВидСтроки 				= НСтр("ru = 'Итого со скидкой'");
		СтрокаСписка.Всего 					= СтрокаТабличнойЧасти.Всего - СтрокаТабличнойЧасти.СуммаСкидки;
		
		Если Параметры.Объект.ДоговорКонтрагента.СуммаВключаетНалоги Тогда
			СтрокаСписка.Доход 				= СтрокаТабличнойЧасти.СуммаДохода;
		Иначе
			СтрокаСписка.Доход 				= СтрокаТабличнойЧасти.СуммаДохода - СтрокаТабличнойЧасти.СуммаДоходаСкидки;	
		КонецЕсли;
		
		СтрокаСписка.НДС 					= СтрокаТабличнойЧасти.СуммаНДС;
		СтрокаСписка.НСП 					= СтрокаТабличнойЧасти.СуммаНСП;
		
		Если ОтдельнаяПроводкаПоСкидке Тогда	
			СтрокаСписка.Проводка 			= НСтр("ru = 'НДС и НСП итоги: 1410 - 3430, 3480'");
		Иначе
			СтрокаСписка.Проводка 			= НСтр("ru = 'Доход, НДС и НСП итоги: (1410-6110, 3430, 3480)'");
		КонецЕсли;
		
		СтрокаСписка.ТекущаяСтрокаДокумента	= ТекущаяСтрокаДокумента;
		
		ИтоговыеДанные.ВсегоСоСкидкой 	= ИтоговыеДанные.ВсегоСоСкидкой + СтрокаСписка.Всего;
		ИтоговыеДанные.ДоходСоСкидкой 	= ИтоговыеДанные.ДоходСоСкидкой + СтрокаСписка.Доход;
		ИтоговыеДанные.НДССоСкидкой 	= ИтоговыеДанные.НДССоСкидкой + СтрокаСписка.НДС;
		ИтоговыеДанные.НСПСоСкидкой 	= ИтоговыеДанные.НСПСоСкидкой + СтрокаСписка.НСП;
	КонецЦикла;
	
	СтрокаСписка = Список.Добавить();
	СтрокаСписка.ВидСтроки 		= НСтр("ru = 'Итого Товар'");
	СтрокаСписка.Всего 			= ИтоговыеДанные.ВсегоБезСкидки;
	СтрокаСписка.Доход 			= ИтоговыеДанные.ДоходБезСкидки;
	СтрокаСписка.НДС 			= ИтоговыеДанные.НДСБезСкидки;
	СтрокаСписка.НСП 			= ИтоговыеДанные.НСПБезСкидки;
	СтрокаСписка.ИтоговаяСтрока = Истина;
	
	СтрокаСписка = Список.Добавить();
	СтрокаСписка.ВидСтроки 		= НСтр("ru = 'Итого скидка'");
	СтрокаСписка.Всего 			= ИтоговыеДанные.ВсегоСкидка;
	СтрокаСписка.Доход 			= ИтоговыеДанные.ДоходСкидка;
	СтрокаСписка.НДС 			= ИтоговыеДанные.НДССкидка;
	СтрокаСписка.НСП 			= ИтоговыеДанные.НСПСкидка;
	СтрокаСписка.ИтоговаяСтрока = Истина;
	
	СтрокаСписка = Список.Добавить();
	СтрокаСписка.ВидСтроки 		= НСтр("ru = 'Итог общий со скидкой'");
	СтрокаСписка.Всего 			= ИтоговыеДанные.ВсегоСоСкидкой;
	СтрокаСписка.Доход 			= ИтоговыеДанные.ДоходСоСкидкой;
	СтрокаСписка.НДС 			= ИтоговыеДанные.НДССоСкидкой;
	СтрокаСписка.НСП 			= ИтоговыеДанные.НСПСоСкидкой;
	СтрокаСписка.ИтоговаяСтрока = Истина;
	
	УстановитьВидимостьДоступностьЭлементов(Параметры);
	
	ВсеСтроки = Истина;
	УстановитьОтборСтрок();
	
	// Условное оформление
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("Список");

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ИтоговаяСтрока");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЦветаСтиля.ИзмененноеЗначениеРеквизитаФон);
	
	// РаботаСФормами
	РаботаСФормамиСервер.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец РаботаСФормами	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимостьДоступностьЭлементов(Параметры)

	Элементы.Список.ТолькоПросмотр = Истина;

	Если Параметры.Объект.БезналичныйРасчет Тогда
		Элементы.СписокНСП.Видимость = Ложь;
	Иначе
		Элементы.СписокНСП.Видимость = Параметры.ДанныеУчетнойПолитики.ПлательщикНСП;
	КонецЕсли;	

	Если Параметры.СтавкаНДС = Справочники.СтавкиНДС.Нулевая
		ИЛИ Параметры.СтавкаНДС = Справочники.СтавкиНДС.Освобожденная
		ИЛИ Параметры.СтавкаНДС = Справочники.СтавкиНДС.Необлагаемая Тогда
		Элементы.СписокНДС.Видимость = Ложь;
	Иначе
		Элементы.СписокНДС.Видимость = Параметры.ДанныеУчетнойПолитики.ПлательщикНДС;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборСтрок()

	Если ВсеСтроки Тогда
		СтруктураОтбора = Новый ФиксированнаяСтруктура();	
	Иначе
		СтруктураОтбора = Новый ФиксированнаяСтруктура("ТекущаяСтрокаДокумента", Истина);
	КонецЕсли;	

	Элементы.Список.ОтборСтрок = СтруктураОтбора;
КонецПроцедуры

&НаКлиенте
Процедура ВсеСтрокиПриИзменении(Элемент)
	УстановитьОтборСтрок();
КонецПроцедуры

#КонецОбласти