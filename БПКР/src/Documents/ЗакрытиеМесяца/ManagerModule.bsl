#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция - собирает данные, с помощью запроса, для расчета налога на имущество.
//
// Возвращаемое значение:
//  Массив - массив результатов запроса.
//
Функция ПолучитьДанныеПоНалогуНаИмущество(Организация, НачалоПериода, КонецПериода, ДополнительныйВариантНИ = Ложь, КонецПериодаДляОстатков = Неопределено) Экспорт
	
	// Начальные данные.
	
	// 1. Все основные средства принятые к учету на начало расчетного года
	//    +
	// 	  все основные средства принятые к в течение расчетного года.
	// 2. 1 пункт с отбором по дате начисления налога на имущество.
	// 3. Все основные средства снятые с учета в течение расчетного года.
	// 4. Все основные средства для расчета налога.
	
	// Недвижимость.
	
	// 5. Сведения об имуществе для групп 1,2,3.
	// 6. Шкала оценки по виду материалов стен.
	// 7. Ставки налога на имущество.
	// 8. Результат для расчета (временная таблица).	
	// 9. Результат для расчета в док. "Закрытие месяца".
	// 10. Данные для док. "Отчет по налогу на имущество".	
	
	// Транспорт.
	
	// 11. Сведения о транспорте для групп 4.
	// 12. Шкала оценки по виду материалов стен.
	// 13. Корректировочный коэффициент.
	// 14. Накопленная амортизация.
	// 15. Результат для расчета (временная таблица).	
	// 16. Результат для расчета в док. "Закрытие месяца".
	// 17. Данные для док. "Отчет по налогу на имущество".
	ТекстыЗапроса = Новый Массив;
	
	Запрос = Новый Запрос;
	
	Если ДополнительныйВариантНИ Тогда
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	1,
			|	СостоянияОС.ОсновноеСредство КАК ОсновноеСредство
			|ПОМЕСТИТЬ ВременнаяТаблицаСостоянияОСПринятоКУчету
			|ИЗ
			|	РегистрСведений.СостоянияОС КАК СостоянияОС
			|ГДЕ
			|	СостоянияОС.Период > &НачалоПериода
			|	И СостоянияОС.Период <= &КонецПериода
			|	И СостоянияОС.Организация = &Организация
			|	И СостоянияОС.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийОС.ПринятоКУчету)"
	Иначе
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	1 КАК Порядок,
			|	СостоянияОССрезПоследних.ОсновноеСредство КАК ОсновноеСредство
			|ПОМЕСТИТЬ ВременнаяТаблицаСостоянияОСПринятоКУчету
			|ИЗ
			|	РегистрСведений.СостоянияОС.СрезПоследних(ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&КонецПериода, ГОД), СЕКУНДА, -1), Организация = &Организация) КАК СостоянияОССрезПоследних
			|ГДЕ
			|	СостоянияОССрезПоследних.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийОС.ПринятоКУчету)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	1,
			|	СостоянияОС.ОсновноеСредство
			|ИЗ
			|	РегистрСведений.СостоянияОС КАК СостоянияОС
			|ГДЕ
			|	СостоянияОС.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийОС.ПринятоКУчету)
			|	И СостоянияОС.Период МЕЖДУ НАЧАЛОПЕРИОДА(&КонецПериода, ГОД) И &КонецПериода";		
	КонецЕсли;
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	// Количество месяцев эксплуатации:
	// 1. Дата ввода в эксплуатацию - текущий год, дата выбытия - не текущий год, ДатаНачисленияНалогаНаИмущество - при любой дате: Количество месяцев эксплуатации = 12
	// 2. Дата ввода в эксплуатацию - не текущий год, дата выбытия - текущий год, ДатаНачисленияНалогаНаИмущество - при любой дате: Количество месяцев эксплуатации = месяц выбытия - 1
	// 3. Дата ввода в эксплуатацию - текущий год, дата выбытия - не текущий год, ДатаНачисленияНалогаНаИмущество - не заполнено: Количество месяцев эксплуатации = 12 - месяц ввода в эксплуатацию
	// 4. Дата ввода в эксплуатацию - текущий год, дата выбытия - текущий год, ДатаНачисленияНалогаНаИмущество - не заполнено: Количество месяцев эксплуатации = месяц выбытия - месяц ввода в эксплуатацию
	// 5. Дата ввода в эксплуатацию - текущий год, дата выбытия - текущий год, ДатаНачисленияНалогаНаИмущество - текущий год: Количество месяцев эксплуатации = месяц выбытия - месяц Даты НачисленияНалогаНаИмущество + 1
	// 6. Дата ввода в эксплуатацию - текущий год, дата выбытия - не текущий год, ДатаНачисленияНалогаНаИмущество - текущий год: Количество месяцев эксплуатации = 12 - месяц Даты НачисленияНалогаНаИмущество + 1
	// 7. Дата ввода в эксплуатацию - при любой дате, дата выбытия - при любой дате, ДатаНачисленияНалогаНаИмущество - следующий год: Количество месяцев эксплуатации = 0
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	2 КАК Порядок,
		|	ВременнаяТаблицаСостоянияОСПринятоКУчету.ОсновноеСредство КАК ОсновноеСредство,
		|	ПараметрыУчетаОССрезПоследних.ИнвентарныйНомер КАК ИнвентарныйНомер,
		|	ПараметрыУчетаОССрезПоследних.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
		|	ПараметрыУчетаОССрезПоследних.ДатаНачисленияНалогаНаИмущество КАК ДатаНачисленияНалогаНаИмущество,
		|	ПараметрыУчетаОССрезПоследних.ДатаВводаВЭксплуатацию КАК ДатаВводаВЭксплуатацию,
		|	МЕСЯЦ(ПараметрыУчетаОССрезПоследних.ДатаВводаВЭксплуатацию) КАК МесяцВводаВЭксплуатацию,
		|	МЕСЯЦ(ПараметрыУчетаОССрезПоследних.ДатаНачисленияНалогаНаИмущество) КАК МесяцДатыНачисленияНалогаНаИмущество,
		|	ВЫБОР
		|		КОГДА МЕСЯЦ(ПараметрыУчетаОССрезПоследних.ДатаНачисленияНалогаНаИмущество) = МЕСЯЦ(ПараметрыУчетаОССрезПоследних.ДатаВводаВЭксплуатацию)
		|				ИЛИ МЕСЯЦ(ПараметрыУчетаОССрезПоследних.ДатаНачисленияНалогаНаИмущество) < МЕСЯЦ(ПараметрыУчетаОССрезПоследних.ДатаВводаВЭксплуатацию)
		|				ИЛИ МЕСЯЦ(ПараметрыУчетаОССрезПоследних.ДатаНачисленияНалогаНаИмущество) = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ВЫБОР
		|					КОГДА МЕСЯЦ(ПараметрыУчетаОССрезПоследних.ДатаНачисленияНалогаНаИмущество) + 1 = 13
		|						ТОГДА 1
		|					ИНАЧЕ МЕСЯЦ(ПараметрыУчетаОССрезПоследних.ДатаНачисленияНалогаНаИмущество) + 1
		|				КОНЕЦ
		|		ИНАЧЕ МЕСЯЦ(ПараметрыУчетаОССрезПоследних.ДатаНачисленияНалогаНаИмущество)
		|	КОНЕЦ КАК МесяцНачалаРасчета,
		|	ВЫБОР
		|		КОГДА МЕСЯЦ(ПараметрыУчетаОССрезПоследних.ДатаНачисленияНалогаНаИмущество) = МЕСЯЦ(ПараметрыУчетаОССрезПоследних.ДатаВводаВЭксплуатацию)
		|				ИЛИ МЕСЯЦ(ПараметрыУчетаОССрезПоследних.ДатаНачисленияНалогаНаИмущество) < МЕСЯЦ(ПараметрыУчетаОССрезПоследних.ДатаВводаВЭксплуатацию)
		|				ИЛИ МЕСЯЦ(ПараметрыУчетаОССрезПоследних.ДатаНачисленияНалогаНаИмущество) = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ВЫБОР
		|					КОГДА МЕСЯЦ(ПараметрыУчетаОССрезПоследних.ДатаНачисленияНалогаНаИмущество) + 1 = 13
		|						ТОГДА ГОД(ПараметрыУчетаОССрезПоследних.ДатаНачисленияНалогаНаИмущество) + 1
		|					ИНАЧЕ ГОД(ПараметрыУчетаОССрезПоследних.ДатаНачисленияНалогаНаИмущество)
		|				КОНЕЦ
		|		ИНАЧЕ ГОД(ПараметрыУчетаОССрезПоследних.ДатаНачисленияНалогаНаИмущество)
		|	КОНЕЦ КАК ГодНачалаРасчета
		|ПОМЕСТИТЬ ВременнаяТаблицаСостоянияОСПринятоКУчетуСДатойНачисленияНИ
		|ИЗ
		|	ВременнаяТаблицаСостоянияОСПринятоКУчету КАК ВременнаяТаблицаСостоянияОСПринятоКУчету
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыУчетаОС.СрезПоследних(
		|				&КонецПериода,
		|				Организация = &Организация
		|					И ОсновноеСредство В
		|						(ВЫБРАТЬ
		|							ВременнаяТаблицаСостоянияОСПринятоКУчету.ОсновноеСредство КАК ОсновноеСредство
		|						ИЗ
		|							ВременнаяТаблицаСостоянияОСПринятоКУчету КАК ВременнаяТаблицаСостоянияОСПринятоКУчету)) КАК ПараметрыУчетаОССрезПоследних
		|		ПО ВременнаяТаблицаСостоянияОСПринятоКУчету.ОсновноеСредство = ПараметрыУчетаОССрезПоследних.ОсновноеСредство";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	3 КАК Порядок,
		|	СостоянияОССрезПервых.ОсновноеСредство КАК ОсновноеСредство,
		|	СостоянияОССрезПервых.Период КАК ПериодСнятоСУчета,
		|	МЕСЯЦ(СостоянияОССрезПервых.Период) КАК МесяцВыбытия
		|ПОМЕСТИТЬ ВременнаяТаблицаСостоянияОССнятоСУчета
		|ИЗ
		|	РегистрСведений.СостоянияОС.СрезПоследних(
		|			&КонецПериода,
		|			Организация = &Организация
		|				И ОсновноеСредство В
		|					(ВЫБРАТЬ
		|						ВременнаяТаблицаСостоянияОСПринятоКУчетуСДатойНачисленияНИ.ОсновноеСредство КАК ОсновноеСредство
		|					ИЗ
		|						ВременнаяТаблицаСостоянияОСПринятоКУчетуСДатойНачисленияНИ КАК ВременнаяТаблицаСостоянияОСПринятоКУчетуСДатойНачисленияНИ)) КАК СостоянияОССрезПервых
		|ГДЕ
		|	(СостоянияОССрезПервых.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийОС.СнятоСУчета)
		|			ИЛИ СостоянияОССрезПервых.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийОС.Передано))";
	ТекстыЗапроса.Добавить(ТекстЗапроса);

	ТекстЗапроса =
		"ВЫБРАТЬ
		|	4 КАК Порядок,
		|	ВременнаяТаблицаСостоянияОСПринятоКУчетуСДатойНачисленияНИ.ОсновноеСредство КАК ОсновноеСредство,
		|	ВременнаяТаблицаСостоянияОСПринятоКУчетуСДатойНачисленияНИ.ГодНачалаРасчета КАК ГодНачалаРасчета,
		// Расчет количества месяцев эксплуатации.	
		|	ВЫБОР
		// 1. Дата ввода в эксплуатацию - не текущий год, дата выбытия - не текущий год, ДатаНачисленияНалогаНаИмущество - при любой дате: Количество месяцев эксплуатации = 12
		|		КОГДА НЕ ГОД(ВременнаяТаблицаСостоянияОСПринятоКУчетуСДатойНачисленияНИ.ДатаВводаВЭксплуатацию) = ГОД(&НачалоПериода)
		|				И НЕ ГОД(ЕСТЬNULL(ВременнаяТаблицаСостоянияОССнятоСУчета.ПериодСнятоСУчета, ДАТАВРЕМЯ(1, 1, 1))) = ГОД(&НачалоПериода)
		|			ТОГДА 12		
		// 2. Дата ввода в эксплуатацию - не текущий год, дата выбытия - текущий год, ДатаНачисленияНалогаНаИмущество - при любой дате: Количество месяцев эксплуатации = месяц выбытия - 1
		|		КОГДА НЕ ГОД(ВременнаяТаблицаСостоянияОСПринятоКУчетуСДатойНачисленияНИ.ДатаВводаВЭксплуатацию) = ГОД(&НачалоПериода)
		|				И ГОД(ЕСТЬNULL(ВременнаяТаблицаСостоянияОССнятоСУчета.ПериодСнятоСУчета, ДАТАВРЕМЯ(1, 1, 1))) = ГОД(&НачалоПериода)
		|			ТОГДА ВременнаяТаблицаСостоянияОССнятоСУчета.МесяцВыбытия - 1		
		// 3. Дата ввода в эксплуатацию - текущий год, дата выбытия - не текущий год, ДатаНачисленияНалогаНаИмущество - не заполнено: Количество месяцев эксплуатации = 12 - месяц ввода в эксплуатацию
		|		КОГДА ГОД(ВременнаяТаблицаСостоянияОСПринятоКУчетуСДатойНачисленияНИ.ДатаВводаВЭксплуатацию) = ГОД(&НачалоПериода)
		|				И НЕ ГОД(ЕСТЬNULL(ВременнаяТаблицаСостоянияОССнятоСУчета.ПериодСнятоСУчета, ДАТАВРЕМЯ(1, 1, 1))) = ГОД(&НачалоПериода)
		|				И ВременнаяТаблицаСостоянияОСПринятоКУчетуСДатойНачисленияНИ.ДатаНачисленияНалогаНаИмущество = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА 12 - ВременнаяТаблицаСостоянияОСПринятоКУчетуСДатойНачисленияНИ.МесяцВводаВЭксплуатацию				
		// 4. Дата ввода в эксплуатацию - текущий год, дата выбытия - текущий год, ДатаНачисленияНалогаНаИмущество - не заполнено: Количество месяцев эксплуатации = месяц выбытия - месяц ввода в эксплуатацию - 1
		|		КОГДА ГОД(ВременнаяТаблицаСостоянияОСПринятоКУчетуСДатойНачисленияНИ.ДатаВводаВЭксплуатацию) = ГОД(&НачалоПериода)
		|				И ГОД(ЕСТЬNULL(ВременнаяТаблицаСостоянияОССнятоСУчета.ПериодСнятоСУчета, ДАТАВРЕМЯ(1, 1, 1))) = ГОД(&НачалоПериода)
		|				И ВременнаяТаблицаСостоянияОСПринятоКУчетуСДатойНачисленияНИ.ДатаНачисленияНалогаНаИмущество = ДАТАВРЕМЯ(1, 1, 1)
		|				И ВременнаяТаблицаСостоянияОССнятоСУчета.МесяцВыбытия - ВременнаяТаблицаСостоянияОСПринятоКУчетуСДатойНачисленияНИ.МесяцВводаВЭксплуатацию - 1 >= -1 
		|			ТОГДА 
		|				ВЫБОР
		|					КОГДА ВременнаяТаблицаСостоянияОССнятоСУчета.МесяцВыбытия - ВременнаяТаблицаСостоянияОСПринятоКУчетуСДатойНачисленияНИ.МесяцВводаВЭксплуатацию - 1 > 0
		|						ТОГДА ВременнаяТаблицаСостоянияОССнятоСУчета.МесяцВыбытия - ВременнаяТаблицаСостоянияОСПринятоКУчетуСДатойНачисленияНИ.МесяцВводаВЭксплуатацию - 1
		|       			ИНАЧЕ 0
		| 			КОНЕЦ
		// 5. Дата ввода в эксплуатацию - текущий год, дата выбытия - текущий год, ДатаНачисленияНалогаНаИмущество - текущий год: Количество месяцев эксплуатации = месяц выбытия - месяц Даты НачисленияНалогаНаИмущество + 1		
		|		КОГДА ГОД(ВременнаяТаблицаСостоянияОСПринятоКУчетуСДатойНачисленияНИ.ДатаВводаВЭксплуатацию) = ГОД(&НачалоПериода)
		|				И ГОД(ЕСТЬNULL(ВременнаяТаблицаСостоянияОССнятоСУчета.ПериодСнятоСУчета, ДАТАВРЕМЯ(1, 1, 1))) = ГОД(&НачалоПериода)
		|				И ГОД(ВременнаяТаблицаСостоянияОСПринятоКУчетуСДатойНачисленияНИ.ДатаНачисленияНалогаНаИмущество) = ГОД(&НачалоПериода)
		|			ТОГДА ВременнаяТаблицаСостоянияОССнятоСУчета.МесяцВыбытия - ВременнаяТаблицаСостоянияОСПринятоКУчетуСДатойНачисленияНИ.МесяцДатыНачисленияНалогаНаИмущество					
		// 6. Дата ввода в эксплуатацию - текущий год, дата выбытия - не текущий год, ДатаНачисленияНалогаНаИмущество - текущий год: Количество месяцев эксплуатации = 12 - месяц Даты НачисленияНалогаНаИмущество + 1
		|		КОГДА ГОД(ВременнаяТаблицаСостоянияОСПринятоКУчетуСДатойНачисленияНИ.ДатаВводаВЭксплуатацию) = ГОД(&НачалоПериода)
		|				И НЕ ГОД(ЕСТЬNULL(ВременнаяТаблицаСостоянияОССнятоСУчета.ПериодСнятоСУчета, ДАТАВРЕМЯ(1, 1, 1))) = ГОД(&НачалоПериода)
		|				И ГОД(ВременнаяТаблицаСостоянияОСПринятоКУчетуСДатойНачисленияНИ.ДатаНачисленияНалогаНаИмущество) = ГОД(&НачалоПериода)
		|			ТОГДА 12 - ВременнаяТаблицаСостоянияОСПринятоКУчетуСДатойНачисленияНИ.МесяцДатыНачисленияНалогаНаИмущество	+ 1					
		// 7. Дата ввода в эксплуатацию - при любой дате, дата выбытия - при любой дате, ДатаНачисленияНалогаНаИмущество - следующий год: Количество месяцев эксплуатации = 0
		|		КОГДА ГОД(ВременнаяТаблицаСостоянияОСПринятоКУчетуСДатойНачисленияНИ.ДатаНачисленияНалогаНаИмущество) - ГОД(&НачалоПериода) = 1
		|			ТОГДА 0					
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КоличествоМесяцевЭксплуатации,
		|	ВременнаяТаблицаСостоянияОСПринятоКУчетуСДатойНачисленияНИ.ИнвентарныйНомер КАК ИнвентарныйНомер,
		|	ВременнаяТаблицаСостоянияОСПринятоКУчетуСДатойНачисленияНИ.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
		|	ВременнаяТаблицаСостоянияОСПринятоКУчетуСДатойНачисленияНИ.ДатаНачисленияНалогаНаИмущество КАК ДатаНачисленияНалогаНаИмущество,
		|	ВременнаяТаблицаСостоянияОССнятоСУчета.ПериодСнятоСУчета КАК ПериодСнятоСУчета
		|ПОМЕСТИТЬ ВременнаяТаблицаСостоянияОС
		|ИЗ
		|	ВременнаяТаблицаСостоянияОСПринятоКУчетуСДатойНачисленияНИ КАК ВременнаяТаблицаСостоянияОСПринятоКУчетуСДатойНачисленияНИ
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСостоянияОССнятоСУчета КАК ВременнаяТаблицаСостоянияОССнятоСУчета
		|		ПО ВременнаяТаблицаСостоянияОСПринятоКУчетуСДатойНачисленияНИ.ОсновноеСредство = ВременнаяТаблицаСостоянияОССнятоСУчета.ОсновноеСредство
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	5 КАК Порядок,
		|	СведенияОбИмуществеСрезПоследних.ГруппаИмущества КАК ГруппаИмущества,
		|	СведенияОбИмуществеСрезПоследних.ОсновноеСредство КАК ОсновноеСредство,
		|	СведенияОбИмуществеСрезПоследних.МатериалСтен КАК МатериалСтен,
		|	СведенияОбИмуществеСрезПоследних.ДатаВводаВЭксплуатацию КАК ДатаВводаВЭксплуатацию,
		|	СведенияОбИмуществеСрезПоследних.РегиональныйКоэффициент.Коэффициент КАК РегиональныйКоэффициент,
		|	СведенияОбИмуществеСрезПоследних.ЗональныйКоэффициент.Коэффициент КАК ЗональныйКоэффициент,
		|	СведенияОбИмуществеСрезПоследних.ОтраслевойКоэффициент.Коэффициент КАК ОтраслевойКоэффициент,
		|	СведенияОбИмуществеСрезПоследних.КодНазначенияИмущества КАК КодНазначенияИмущества,
		|	СведенияОбИмуществеСрезПоследних.КодПользователяИмущества КАК КодПользователяИмущества,
		|	СведенияОбИмуществеСрезПоследних.КодИмущества КАК КодИмущества,
		|	СведенияОбИмуществеСрезПоследних.КатегорияОбъектаИмущества КАК КатегорияОбъектаИмущества,
		|	СведенияОбИмуществеСрезПоследних.ЖилаяПлощадь КАК ЖилаяПлощадь,
		|	СведенияОбИмуществеСрезПоследних.НежилаяПлощадь КАК НежилаяПлощадь,
		|	СведенияОбИмуществеСрезПоследних.ЖилаяПлощадь + СведенияОбИмуществеСрезПоследних.НежилаяПлощадь КАК ОбщаяПлощадь,
		|	ГОД(&КонецПериода) - ГОД(ВременнаяТаблицаСостоянияОС.ОсновноеСредство.ДатаВыпуска) + 1 КАК КоличествоЛетЭксплуатации,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаСостоянияОС.КоличествоМесяцевЭксплуатации < 0
		|			ТОГДА 0
		|		ИНАЧЕ ВременнаяТаблицаСостоянияОС.КоличествоМесяцевЭксплуатации
		|	КОНЕЦ КАК КоличествоМесяцевЭксплуатации,
		|	ВременнаяТаблицаСостоянияОС.ПериодСнятоСУчета КАК ПериодСнятоСУчета,
		|	ВременнаяТаблицаСостоянияОС.ИнвентарныйНомер КАК ИнвентарныйНомер
		|ПОМЕСТИТЬ ВременнаяТаблицаИмущество
		|ИЗ
		|	ВременнаяТаблицаСостоянияОС КАК ВременнаяТаблицаСостоянияОС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбИмуществе.СрезПоследних(
		|				&КонецПериода,
		|				Организация = &Организация
		|					И ОсновноеСредство В
		|						(ВЫБРАТЬ
		|							ВременнаяТаблицаСостоянияОС.ОсновноеСредство КАК ОсновноеСредство
		|						ИЗ
		|							ВременнаяТаблицаСостоянияОС КАК ВременнаяТаблицаСостоянияОС)) КАК СведенияОбИмуществеСрезПоследних
		|		ПО ВременнаяТаблицаСостоянияОС.ОсновноеСредство = СведенияОбИмуществеСрезПоследних.ОсновноеСредство
		|ГДЕ
		|	(СведенияОбИмуществеСрезПоследних.ГруппаИмущества = ЗНАЧЕНИЕ(Справочник.ГруппыИмущества.ГИ2)
		|			ИЛИ СведенияОбИмуществеСрезПоследних.ГруппаИмущества = ЗНАЧЕНИЕ(Справочник.ГруппыИмущества.ГИ3))";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	6 КАК Порядок,
		|	ШкалаОценкиСтоимостиМакс.ОсновноеСредство КАК ОсновноеСредство,
		|	ШкалаОценкиСтоимостиВидовМатериаловСтенСрезПоследних.Ставка КАК ОценочнаяСтоимость
		|ПОМЕСТИТЬ ВременнаяТаблицаОценкиСтоимостиНедвижимость
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВременнаяТаблицаИмущество.ОсновноеСредство КАК ОсновноеСредство,
		|		ВременнаяТаблицаИмущество.МатериалСтен КАК МатериалСтен,
		|		МАКСИМУМ(ШкалаОценкиСтоимостиВидовМатериаловСтенСрезПоследних.НижняяГраница) КАК НижняяГраница
		|	ИЗ
		|		ВременнаяТаблицаИмущество КАК ВременнаяТаблицаИмущество
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШкалаОценкиСтоимостиВидовМатериаловСтен.СрезПоследних(&КонецПериода, ) КАК ШкалаОценкиСтоимостиВидовМатериаловСтенСрезПоследних
		|			ПО ВременнаяТаблицаИмущество.МатериалСтен = ШкалаОценкиСтоимостиВидовМатериаловСтенСрезПоследних.ВидМатериалаСтен
		|				И ВременнаяТаблицаИмущество.КоличествоЛетЭксплуатации >= ШкалаОценкиСтоимостиВидовМатериаловСтенСрезПоследних.НижняяГраница
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ВременнаяТаблицаИмущество.ОсновноеСредство,
		|		ВременнаяТаблицаИмущество.МатериалСтен) КАК ШкалаОценкиСтоимостиМакс
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ШкалаОценкиСтоимостиВидовМатериаловСтен.СрезПоследних(&КонецПериода, ) КАК ШкалаОценкиСтоимостиВидовМатериаловСтенСрезПоследних
		|		ПО ШкалаОценкиСтоимостиМакс.МатериалСтен = ШкалаОценкиСтоимостиВидовМатериаловСтенСрезПоследних.ВидМатериалаСтен
		|			И ШкалаОценкиСтоимостиМакс.НижняяГраница = ШкалаОценкиСтоимостиВидовМатериаловСтенСрезПоследних.НижняяГраница";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	//|ВЫБРАТЬ
		//|	7 КАК Порядок,
		//|	ВременнаяТаблицаИмущество.ОсновноеСредство КАК ОсновноеСредство,
		//|	ЕСТЬNULL(КоэффициентыНалогаНаИмуществоРегиональные.Коэффициент, 0) КАК РегиональныйКоэффициент,
		//|	ЕСТЬNULL(КоэффициентыНалогаНаИмуществоЗональные.Коэффициент, 0) КАК ЗональныйКоэффициент,
		//|	ЕСТЬNULL(КоэффициентыНалогаНаИмуществоОтраслевые.Коэффициент, 0) КАК ОтраслевойКоэффициент
		//|ПОМЕСТИТЬ ВременнаяТаблицаКоэффициенты
		//|ИЗ
		//|	ВременнаяТаблицаИмущество КАК ВременнаяТаблицаИмущество
		//|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КоэффициентыНалогаНаИмущество.СрезПоследних(
		//|				&КонецПериода,
		//|				Классификация В
		//|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		//|						ВременнаяТаблицаИмущество.РегиональныйКоэффициент КАК РегиональныйКоэффициент
		//|					ИЗ
		//|						ВременнаяТаблицаИмущество КАК ВременнаяТаблицаИмущество)) КАК КоэффициентыНалогаНаИмуществоРегиональные
		//|		ПО ВременнаяТаблицаИмущество.РегиональныйКоэффициент = КоэффициентыНалогаНаИмуществоРегиональные.Классификация
		//|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КоэффициентыНалогаНаИмущество.СрезПоследних(
		//|				&КонецПериода,
		//|				Классификация В
		//|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		//|						ВременнаяТаблицаИмущество.ЗональныйКоэффициент КАК ЗональныйКоэффициент
		//|					ИЗ
		//|						ВременнаяТаблицаИмущество КАК ВременнаяТаблицаИмущество)) КАК КоэффициентыНалогаНаИмуществоЗональные
		//|		ПО ВременнаяТаблицаИмущество.ЗональныйКоэффициент = КоэффициентыНалогаНаИмуществоЗональные.Классификация
		//|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КоэффициентыНалогаНаИмущество.СрезПоследних(
		//|				&КонецПериода,
		//|				Классификация В
		//|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		//|						ВременнаяТаблицаИмущество.ОтраслевойКоэффициент КАК ОтраслевойКоэффициент
		//|					ИЗ
		//|						ВременнаяТаблицаИмущество КАК ВременнаяТаблицаИмущество)) КАК КоэффициентыНалогаНаИмуществоОтраслевые
		//|		ПО ВременнаяТаблицаИмущество.ОтраслевойКоэффициент = КоэффициентыНалогаНаИмуществоОтраслевые.Классификация
		//|;
		//|
		
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	7 КАК Порядок,
		|	ВременнаяТаблицаИмущество.ОсновноеСредство КАК ОсновноеСредство,
		|	ЕСТЬNULL(СтавкиНалогаНаИмуществоСрезПоследних.Ставка, 0) КАК Ставка
		|ПОМЕСТИТЬ ВременнаяТаблицаСтавки
		|ИЗ
		|	ВременнаяТаблицаИмущество КАК ВременнаяТаблицаИмущество
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНалогаНаИмущество.СрезПоследних(
		|				&КонецПериода,
		|				ГруппаНалогаНаИмущество В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						ВременнаяТаблицаИмущество.ГруппаИмущества КАК ГруппаИмущества
		|					ИЗ
		|						ВременнаяТаблицаИмущество КАК ВременнаяТаблицаИмущество)) КАК СтавкиНалогаНаИмуществоСрезПоследних
		|		ПО ВременнаяТаблицаИмущество.ГруппаИмущества = СтавкиНалогаНаИмуществоСрезПоследних.ГруппаНалогаНаИмущество";
	ТекстыЗапроса.Добавить(ТекстЗапроса);

	ТекстЗапроса =
		"ВЫБРАТЬ
		|	8 КАК Порядок,
		|	ВременнаяТаблицаИмущество.ОсновноеСредство КАК ОсновноеСредство,
		|	ВременнаяТаблицаИмущество.ИнвентарныйНомер КАК ИнвентарныйНомер,
		|	ВременнаяТаблицаИмущество.ОсновноеСредство.Наименование КАК ОсновноеСредствоНаименование,
		|	ВременнаяТаблицаИмущество.ГруппаИмущества КАК ГруппаИмущества,
		|	ВременнаяТаблицаИмущество.ЖилаяПлощадь КАК ЖилаяПлощадь,
		|	ВременнаяТаблицаИмущество.ОбщаяПлощадь КАК ОбщаяПлощадь,
		|	ВременнаяТаблицаИмущество.КоличествоМесяцевЭксплуатации КАК КоличествоМесяцевЭксплуатации,
		|	ВременнаяТаблицаИмущество.КоличествоЛетЭксплуатации КАК КоличествоЛетЭксплуатации,
		|	ВременнаяТаблицаИмущество.МатериалСтен КАК МатериалСтен,
		|	ВременнаяТаблицаИмущество.ДатаВводаВЭксплуатацию КАК ДатаВводаВЭксплуатацию,
		|	ВременнаяТаблицаИмущество.ПериодСнятоСУчета КАК ПериодСнятоСУчета,
		|	ЕСТЬNULL(ВременнаяТаблицаОценкиСтоимости.ОценочнаяСтоимость, 0) КАК ОценочнаяСтоимость,
		|	ВременнаяТаблицаИмущество.РегиональныйКоэффициент КАК РегиональныйКоэффициент,
		|	ВременнаяТаблицаИмущество.ЗональныйКоэффициент КАК ЗональныйКоэффициент,
		|	ВременнаяТаблицаИмущество.ОтраслевойКоэффициент КАК ОтраслевойКоэффициент,
		|	ЕСТЬNULL(ВременнаяТаблицаСтавки.Ставка, 0) КАК Ставка,
		|	ЕСТЬNULL(ЗемельныйНалогОбороты.СуммаЗемельногоНалогаПодОбъектомИмуществаОборот, 0) КАК ЗемельныйНалог
		|ПОМЕСТИТЬ ВременнаяТаблицаИмуществоГотовая
		|ИЗ
		|	ВременнаяТаблицаИмущество КАК ВременнаяТаблицаИмущество
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаОценкиСтоимостиНедвижимость КАК ВременнаяТаблицаОценкиСтоимости
		|		ПО ВременнаяТаблицаИмущество.ОсновноеСредство = ВременнаяТаблицаОценкиСтоимости.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСтавки КАК ВременнаяТаблицаСтавки
		|		ПО ВременнаяТаблицаИмущество.ОсновноеСредство = ВременнаяТаблицаСтавки.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗемельныйНалог.Обороты(НАЧАЛОПЕРИОДА(&НачалоПериода, ГОД), &КонецПериода, , Организация = &Организация) КАК ЗемельныйНалогОбороты
		|		ПО ВременнаяТаблицаИмущество.ОсновноеСредство = ЗемельныйНалогОбороты.ОсновноеСредство";
	ТекстыЗапроса.Добавить(ТекстЗапроса);

	ТекстЗапроса =
		"ВЫБРАТЬ
		|	9 КАК Порядок,
		|	ВременнаяТаблицаИмуществоГотовая.ОсновноеСредство КАК ОсновноеСредство,
		|	ВременнаяТаблицаИмуществоГотовая.ИнвентарныйНомер КАК ИнвентарныйНомер,
		|	ВременнаяТаблицаИмуществоГотовая.ОсновноеСредство.Наименование КАК ОсновноеСредствоНаименование,
		|	ВременнаяТаблицаИмуществоГотовая.ГруппаИмущества КАК ГруппаИмущества,
		|	ВременнаяТаблицаИмуществоГотовая.ЖилаяПлощадь КАК ЖилаяПлощадь,
		|	ВременнаяТаблицаИмуществоГотовая.ОбщаяПлощадь КАК ОбщаяПлощадь,
		|	ВременнаяТаблицаИмуществоГотовая.МатериалСтен КАК МатериалСтен,
		|	ВременнаяТаблицаИмуществоГотовая.КоличествоМесяцевЭксплуатации КАК КоличествоМесяцевЭксплуатации,
		|	ВременнаяТаблицаИмуществоГотовая.КоличествоЛетЭксплуатации КАК КоличествоЛетЭксплуатации,
		|	ВременнаяТаблицаИмуществоГотовая.ОценочнаяСтоимость КАК ОценочнаяСтоимость,
		|	ВременнаяТаблицаИмуществоГотовая.РегиональныйКоэффициент КАК РегиональныйКоэффициент,
		|	ВременнаяТаблицаИмуществоГотовая.ЗональныйКоэффициент КАК ЗональныйКоэффициент,
		|	ВременнаяТаблицаИмуществоГотовая.ОтраслевойКоэффициент КАК ОтраслевойКоэффициент,
		|	ВременнаяТаблицаИмуществоГотовая.Ставка КАК Ставка,
		|	ВременнаяТаблицаИмуществоГотовая.ЗемельныйНалог КАК ЗемельныйНалог,
		|	ВременнаяТаблицаИмуществоГотовая.ДатаВводаВЭксплуатацию КАК ДатаВводаВЭксплуатацию,
		|	ВременнаяТаблицаИмуществоГотовая.ПериодСнятоСУчета КАК ДатаВыбытия,
		|	ВременнаяТаблицаСостоянияОС.ДатаНачисленияНалогаНаИмущество КАК ДатаНачисленияНалогаНаИмущество
		|ИЗ
		|	ВременнаяТаблицаИмуществоГотовая КАК ВременнаяТаблицаИмуществоГотовая
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСостоянияОС КАК ВременнаяТаблицаСостоянияОС
		|		ПО ВременнаяТаблицаИмуществоГотовая.ОсновноеСредство = ВременнаяТаблицаСостоянияОС.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияОС.СрезПоследних(&КонецПериода, Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийОС.Поступило)) КАК СостоянияОССрезПоследних
		|		ПО ВременнаяТаблицаИмуществоГотовая.ОсновноеСредство = СостоянияОССрезПоследних.ОсновноеСредство
		|
		|УПОРЯДОЧИТЬ ПО
		|	ИнвентарныйНомер";
	ТекстыЗапроса.Добавить(ТекстЗапроса);

	ТекстЗапроса =
		"ВЫБРАТЬ
		|	10 КАК Порядок,
		|	СведенияПоНалогуНаИмущество.ОсновноеСредство КАК ОсновноеСредство,
		|	СведенияПоНалогуНаИмущество.СуммаНалога КАК СуммаНалога,
		|	ВременнаяТаблицаИмуществоГотовая.ГруппаИмущества КАК ГруппаИмущества,
		|	ВременнаяТаблицаИмуществоГотовая.ОбщаяПлощадь КАК ОбщаяПлощадь,
		|	ВременнаяТаблицаИмуществоГотовая.КоличествоМесяцевЭксплуатации КАК КоличествоМесяцев,
		|	ВременнаяТаблицаИмуществоГотовая.ОценочнаяСтоимость КАК ОценочнаяСтоимость,
		|	ВременнаяТаблицаИмуществоГотовая.РегиональныйКоэффициент КАК Кр,
		|	ВременнаяТаблицаИмуществоГотовая.ЗональныйКоэффициент КАК Кз,
		|	ВременнаяТаблицаИмуществоГотовая.ОтраслевойКоэффициент КАК Ко,
		|	ВременнаяТаблицаИмуществоГотовая.Ставка КАК Ставка,
		|	ВременнаяТаблицаИмуществоГотовая.ЗемельныйНалог КАК ЗемельныйНалог,
		|	ТаблицаИмущество.ДатаВводаВЭксплуатацию КАК ДатаВводаВЭксплуатацию,
		|	ТаблицаИмущество.ЖилаяПлощадь КАК ЖилаяПлощадь,
		|	ТаблицаИмущество.НежилаяПлощадь КАК НежилаяПлощадь,
		|	ТаблицаИмущество.МатериалСтен КАК МатериалСтен,
		|	ТаблицаИмущество.КодНазначенияИмущества КАК КодНазначенияИмущества,
		|	ТаблицаИмущество.КодПользователяИмущества КАК КодПользователяИмущества,
		|	ТаблицаИмущество.КодИмущества КАК КодИмущества,
		|	ТаблицаИмущество.КатегорияОбъектаИмущества КАК КатегорияОбъектаИмущества
		|ИЗ
		|	РегистрСведений.СведенияПоНалогуНаИмущество КАК СведенияПоНалогуНаИмущество
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаИмуществоГотовая КАК ВременнаяТаблицаИмуществоГотовая
		|		ПО СведенияПоНалогуНаИмущество.ОсновноеСредство = ВременнаяТаблицаИмуществоГотовая.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаИмущество КАК ТаблицаИмущество
		|		ПО СведенияПоНалогуНаИмущество.ОсновноеСредство = ТаблицаИмущество.ОсновноеСредство
		|ГДЕ
		|	СведенияПоНалогуНаИмущество.Год = &КонецПериода
		|	И СведенияПоНалогуНаИмущество.Организация = &Организация
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОсновноеСредство";
	ТекстыЗапроса.Добавить(ТекстЗапроса);

	ТекстЗапроса =
		"ВЫБРАТЬ
		|	11 КАК Порядок,
		|	СведенияОТранспортеСрезПоследних.ОсновноеСредство КАК ОсновноеСредство,
		|	СведенияОТранспортеСрезПоследних.ГруппаИмущества КАК ГруппаИмущества,
		|	СведенияОТранспортеСрезПоследних.ВидТранспорта КАК ВидТранспорта,
		|	СведенияОТранспортеСрезПоследних.ВидТранспорта.КатегорияАБВ КАК ВидТранспортаКатегорияАБВ,
		|	СведенияОТранспортеСрезПоследних.ОбъемДвигателя КАК ОбъемДвигателя,
		|	СведенияОТранспортеСрезПоследних.ГосНомер КАК ГосНомер,
		|	СведенияОТранспортеСрезПоследних.КодПользователяИмущества КАК КодПользователяИмущества,
		|	СведенияОТранспортеСрезПоследних.ДатаВводаВЭксплуатацию КАК ДатаВводаВЭксплуатацию,
		|	ВременнаяТаблицаСостоянияОС.ПериодСнятоСУчета КАК ПериодСнятоСУчета,
		|	СведенияОТранспортеСрезПоследних.МаркаТранспортногоСредства КАК МаркаТранспортногоСредства,
		|	ГОД(&КонецПериода) - ГОД(ВременнаяТаблицаСостоянияОС.ОсновноеСредство.ДатаВыпуска) + 1 КАК КоличествоЛетЭксплуатации,
		|	ВременнаяТаблицаСостоянияОС.КоличествоМесяцевЭксплуатации КАК КоличествоМесяцевЭксплуатации,
		|	ВременнаяТаблицаСостоянияОС.ИнвентарныйНомер КАК ИнвентарныйНомер,
		|	ВременнаяТаблицаСостоянияОС.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
		|	ВременнаяТаблицаСостоянияОС.ДатаНачисленияНалогаНаИмущество КАК ДатаНачисленияНалогаНаИмущество
		|ПОМЕСТИТЬ ВременнаяТаблицаТранспорт
		|ИЗ
		|	ВременнаяТаблицаСостоянияОС КАК ВременнаяТаблицаСостоянияОС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОТранспорте.СрезПоследних(
		|				&КонецПериода,
		|				Организация = &Организация
		|					И ОсновноеСредство В
		|						(ВЫБРАТЬ
		|							ВременнаяТаблицаСостоянияОС.ОсновноеСредство КАК ОсновноеСредство
		|						ИЗ
		|							ВременнаяТаблицаСостоянияОС КАК ВременнаяТаблицаСостоянияОС)) КАК СведенияОТранспортеСрезПоследних
		|		ПО ВременнаяТаблицаСостоянияОС.ОсновноеСредство = СведенияОТранспортеСрезПоследних.ОсновноеСредство
		|ГДЕ
		|	СведенияОТранспортеСрезПоследних.ГруппаИмущества = ЗНАЧЕНИЕ(Справочник.ГруппыИмущества.ГИ4с)
		|	ИЛИ СведенияОТранспортеСрезПоследних.ГруппаИмущества = ЗНАЧЕНИЕ(Справочник.ГруппыИмущества.ГИ4нс)";
	ТекстыЗапроса.Добавить(ТекстЗапроса);

	ТекстЗапроса =
		"ВЫБРАТЬ
		|	12 КАК Порядок,
		|	ШкалаОценкиСтоимостиМакс.ОсновноеСредство КАК ОсновноеСредство,
		|	ШкалаОценкиСтоимостиВидовТранспортаСрезПоследних.Ставка КАК ОценочнаяСтоимость
		|ПОМЕСТИТЬ ВременнаяТаблицаОценкиСтоимостиТранспорт
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВременнаяТаблицаТранспорт.ОсновноеСредство КАК ОсновноеСредство,
		|		ВременнаяТаблицаТранспорт.ВидТранспорта КАК ВидТранспорта,
		|		МАКСИМУМ(ШкалаОценкиСтоимостиВидовТранспортаСрезПоследних.НижняяГраница) КАК НижняяГраница
		|	ИЗ
		|		ВременнаяТаблицаТранспорт КАК ВременнаяТаблицаТранспорт
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШкалаОценкиСтоимостиВидовТранспорта.СрезПоследних(&КонецПериода, ) КАК ШкалаОценкиСтоимостиВидовТранспортаСрезПоследних
		|			ПО ВременнаяТаблицаТранспорт.ВидТранспорта = ШкалаОценкиСтоимостиВидовТранспортаСрезПоследних.ВидТранспорта
		|				И ВременнаяТаблицаТранспорт.КоличествоЛетЭксплуатации >= ШкалаОценкиСтоимостиВидовТранспортаСрезПоследних.НижняяГраница
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ВременнаяТаблицаТранспорт.ОсновноеСредство,
		|		ВременнаяТаблицаТранспорт.ВидТранспорта) КАК ШкалаОценкиСтоимостиМакс
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ШкалаОценкиСтоимостиВидовТранспорта.СрезПоследних(&КонецПериода, ) КАК ШкалаОценкиСтоимостиВидовТранспортаСрезПоследних
		|		ПО ШкалаОценкиСтоимостиМакс.ВидТранспорта = ШкалаОценкиСтоимостиВидовТранспортаСрезПоследних.ВидТранспорта
		|			И ШкалаОценкиСтоимостиМакс.НижняяГраница = ШкалаОценкиСтоимостиВидовТранспортаСрезПоследних.НижняяГраница";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	13 КАК Порядок,
		|	КорректировочныеКоэффициентыНалогаНаИмуществоМакс.ОсновноеСредство КАК ОсновноеСредство,
		|	КорректировочныеКоэффициентыНалогаНаИмуществоСрезПоследних.Коэффициент КАК КорректировочныйКоэффициент
		|ПОМЕСТИТЬ ВременнаяТаблицаКорректировочныеКоэффициенты
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВременнаяТаблицаТранспорт.ОсновноеСредство КАК ОсновноеСредство,
		|		МАКСИМУМ(КорректировочныеКоэффициентыНалогаНаИмуществоСрезПоследних.НижняяГраницаОбъемаДвигателя) КАК НижняяГраницаОбъемаДвигателя,
		|		МАКСИМУМ(КорректировочныеКоэффициентыНалогаНаИмуществоСрезПоследних.НижняяГраницаПериодаЭксплуатации) КАК НижняяГраницаПериодаЭксплуатации
		|	ИЗ
		|		ВременнаяТаблицаТранспорт КАК ВременнаяТаблицаТранспорт
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КорректировочныеКоэффициентыНалогаНаИмущество.СрезПоследних(&КонецПериода, ) КАК КорректировочныеКоэффициентыНалогаНаИмуществоСрезПоследних
		|			ПО ВременнаяТаблицаТранспорт.ОбъемДвигателя >= КорректировочныеКоэффициентыНалогаНаИмуществоСрезПоследних.НижняяГраницаОбъемаДвигателя
		|				И ВременнаяТаблицаТранспорт.КоличествоЛетЭксплуатации >= КорректировочныеКоэффициентыНалогаНаИмуществоСрезПоследних.НижняяГраницаПериодаЭксплуатации
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ВременнаяТаблицаТранспорт.ОсновноеСредство) КАК КорректировочныеКоэффициентыНалогаНаИмуществоМакс
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КорректировочныеКоэффициентыНалогаНаИмущество.СрезПоследних(&КонецПериода, ) КАК КорректировочныеКоэффициентыНалогаНаИмуществоСрезПоследних
		|		ПО КорректировочныеКоэффициентыНалогаНаИмуществоМакс.НижняяГраницаОбъемаДвигателя = КорректировочныеКоэффициентыНалогаНаИмуществоСрезПоследних.НижняяГраницаОбъемаДвигателя
		|			И КорректировочныеКоэффициентыНалогаНаИмуществоМакс.НижняяГраницаПериодаЭксплуатации = КорректировочныеКоэффициентыНалогаНаИмуществоСрезПоследних.НижняяГраницаПериодаЭксплуатации";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	14 КАК Порядок,
		|	ХозрасчетныйОстатки.Счет КАК Счет,
		|	ХозрасчетныйОстатки.Субконто1 КАК ОсновноеСредство,
		|	ВЫБОР
		|		КОГДА ХозрасчетныйОстатки.СуммаОстаток < 0
		|			ТОГДА ХозрасчетныйОстатки.СуммаОстаток * -1
		|		ИНАЧЕ ХозрасчетныйОстатки.СуммаОстаток
		|	КОНЕЦ КАК НакопленныйИзнос
		|ПОМЕСТИТЬ ВременнаяТаблицаНакопленнаяАмортизация
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(
		|			&КонецПериодаОстатков,
		|			Счет В (&СписокСчетовНакопленнойАмортизации),
		|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства),
		|			Организация = &Организация
		|				И Субконто1 В
		|					(ВЫБРАТЬ
		|						ВременнаяТаблицаСостоянияОС.ОсновноеСредство
		|					ИЗ
		|						ВременнаяТаблицаСостоянияОС КАК ВременнаяТаблицаСостоянияОС)) КАК ХозрасчетныйОстатки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	15 КАК Порядок,
		|	ВременнаяТаблицаТранспорт.ОсновноеСредство КАК ОсновноеСредство,
		|	ВременнаяТаблицаТранспорт.ГруппаИмущества КАК ГруппаИмущества,
		|	ЕСТЬNULL(СтавкиНалогаНаИмуществоСрезПоследних.Ставка, 0) КАК Ставка
		|ПОМЕСТИТЬ ВременнаяТаблицаСтавкиНалогаНаИмущество
		|	ИЗ
		|		ВременнаяТаблицаТранспорт КАК ВременнаяТаблицаТранспорт
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНалогаНаИмущество.СрезПоследних(&КонецПериода, ) КАК СтавкиНалогаНаИмуществоСрезПоследних
		|			ПО ВременнаяТаблицаТранспорт.ГруппаИмущества = СтавкиНалогаНаИмуществоСрезПоследних.ГруппаНалогаНаИмущество";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	16 КАК Порядок,
		|	ВременнаяТаблицаТранспорт.ОсновноеСредство КАК ОсновноеСредство,
		|	ВременнаяТаблицаТранспорт.ИнвентарныйНомер КАК ИнвентарныйНомер,
		|	ВременнаяТаблицаТранспорт.ОсновноеСредство.Наименование КАК ОсновноеСредствоНаименование,
		|	ВременнаяТаблицаТранспорт.ГруппаИмущества КАК ГруппаИмущества,
		|	ВременнаяТаблицаТранспорт.ВидТранспорта КАК ВидТранспорта,
		|	ВременнаяТаблицаТранспорт.ВидТранспортаКатегорияАБВ КАК ВидТранспортаКатегорияАБВ,
		|	ВременнаяТаблицаТранспорт.ОбъемДвигателя КАК ОбъемДвигателя,
		|	ВременнаяТаблицаТранспорт.ГосНомер КАК ГосНомер,
		|	ВременнаяТаблицаТранспорт.КодПользователяИмущества КАК КодПользователяИмущества,
		|	ВременнаяТаблицаТранспорт.МаркаТранспортногоСредства КАК МаркаТранспортногоСредства,
		|	ЕСТЬNULL(ВременнаяТаблицаОценкиСтоимости.ОценочнаяСтоимость, 0) КАК ОценочнаяСтоимость,
		|	ВременнаяТаблицаТранспорт.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
		|	ВременнаяТаблицаТранспорт.КоличествоМесяцевЭксплуатации КАК КоличествоМесяцевЭксплуатации,
		|	ВременнаяТаблицаТранспорт.КоличествоЛетЭксплуатации КАК КоличествоЛетЭксплуатации,
		|	ЕСТЬNULL(ВременнаяТаблицаНакопленнаяАмортизация.НакопленныйИзнос, 0) КАК НакопленныйИзнос,
		|	ВременнаяТаблицаТранспорт.ДатаВводаВЭксплуатацию КАК ДатаВводаВЭксплуатацию,
		|	ВременнаяТаблицаТранспорт.ПериодСнятоСУчета КАК ПериодСнятоСУчета,
		|	ВременнаяТаблицаТранспорт.ДатаНачисленияНалогаНаИмущество КАК ДатаНачисленияНалогаНаИмущество,
		|	ЕСТЬNULL(ВременнаяТаблицаКорректировочныеКоэффициенты.КорректировочныйКоэффициент, 1) КАК КорректировочныйКоэффициент,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаКорректировочныеКоэффициенты.КорректировочныйКоэффициент ЕСТЬ NULL    
		|			И НЕ ВременнаяТаблицаТранспорт.ВидТранспортаКатегорияАБВ = ""В""
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК КорректировочныйКоэффициентЕсть,
		|	ВременнаяТаблицаСтавкиНалогаНаИмущество.Ставка КАК СтавкаНалогаНаИмущество
		|ПОМЕСТИТЬ ВременнаяТаблицаТранспортГотовая
		|ИЗ
		|	ВременнаяТаблицаТранспорт КАК ВременнаяТаблицаТранспорт
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаОценкиСтоимостиТранспорт КАК ВременнаяТаблицаОценкиСтоимости
		|		ПО ВременнаяТаблицаТранспорт.ОсновноеСредство = ВременнаяТаблицаОценкиСтоимости.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНакопленнаяАмортизация КАК ВременнаяТаблицаНакопленнаяАмортизация
		|		ПО ВременнаяТаблицаТранспорт.ОсновноеСредство = ВременнаяТаблицаНакопленнаяАмортизация.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКорректировочныеКоэффициенты КАК ВременнаяТаблицаКорректировочныеКоэффициенты
		|		ПО ВременнаяТаблицаТранспорт.ОсновноеСредство = ВременнаяТаблицаКорректировочныеКоэффициенты.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСтавкиНалогаНаИмущество КАК ВременнаяТаблицаСтавкиНалогаНаИмущество
		|		ПО ВременнаяТаблицаТранспорт.ОсновноеСредство = ВременнаяТаблицаСтавкиНалогаНаИмущество.ОсновноеСредство
		|		И ВременнаяТаблицаТранспорт.ГруппаИмущества = ВременнаяТаблицаСтавкиНалогаНаИмущество.ГруппаИмущества";
	ТекстыЗапроса.Добавить(ТекстЗапроса);

	ТекстЗапроса =
		"ВЫБРАТЬ
		|	17 КАК Порядок,
		|	ВременнаяТаблицаТранспортГотовая.ОсновноеСредство КАК ОсновноеСредство,
		|	ВременнаяТаблицаТранспортГотовая.ИнвентарныйНомер КАК ИнвентарныйНомер,
		|	ВременнаяТаблицаТранспортГотовая.ОсновноеСредствоНаименование КАК ОсновноеСредствоНаименование,
		|	ВременнаяТаблицаТранспортГотовая.ГруппаИмущества КАК ГруппаИмущества,
		|	ВременнаяТаблицаТранспортГотовая.ВидТранспорта КАК ВидТранспорта,
		|	ВременнаяТаблицаТранспортГотовая.ВидТранспортаКатегорияАБВ КАК ВидТранспортаКатегорияАБВ,
		|	ВременнаяТаблицаТранспортГотовая.МаркаТранспортногоСредства КАК МаркаТранспортногоСредства,
		|	ВременнаяТаблицаТранспортГотовая.ОбъемДвигателя КАК ОбъемДвигателя,
		|	ВременнаяТаблицаТранспортГотовая.ГосНомер КАК ГосНомер,
		|	ВременнаяТаблицаТранспортГотовая.КодПользователяИмущества КАК КодПользователяИмущества,
		|	ВременнаяТаблицаТранспортГотовая.ОценочнаяСтоимость КАК ОценочнаяСтоимость,
		|	ВременнаяТаблицаТранспортГотовая.ОценочнаяСтоимость КАК Ставка,
		|	ВременнаяТаблицаТранспортГотовая.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
		|	ВременнаяТаблицаТранспортГотовая.КоличествоМесяцевЭксплуатации КАК КоличествоМесяцевЭксплуатации,
		|	ВременнаяТаблицаТранспортГотовая.КоличествоЛетЭксплуатации КАК КоличествоЛетЭксплуатации,
		|	ВременнаяТаблицаТранспортГотовая.НакопленныйИзнос КАК НакопленныйИзнос,
		|	ВременнаяТаблицаТранспортГотовая.ДатаВводаВЭксплуатацию КАК ДатаВводаВЭксплуатацию,
		|	ВременнаяТаблицаТранспортГотовая.ПериодСнятоСУчета КАК ДатаВыбытия,
		|	ВременнаяТаблицаТранспортГотовая.ДатаНачисленияНалогаНаИмущество КАК ДатаНачисленияНалогаНаИмущество,
		|	ВременнаяТаблицаТранспортГотовая.КорректировочныйКоэффициент КАК КорректировочныйКоэффициент,
		|	ВременнаяТаблицаТранспортГотовая.КорректировочныйКоэффициентЕсть КАК КорректировочныйКоэффициентЕсть,
		|	ВременнаяТаблицаТранспортГотовая.СтавкаНалогаНаИмущество КАК СтавкаНалогаНаИмущество
		|ИЗ
		|	ВременнаяТаблицаТранспортГотовая КАК ВременнаяТаблицаТранспортГотовая
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВидТранспортаКатегорияАБВ,
		|	ИнвентарныйНомер";
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	Запрос.УстановитьПараметр("Организация", Организация);			   	   
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);			   
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	Запрос.УстановитьПараметр("КонецПериодаОстатков", ?(КонецПериодаДляОстатков <> Неопределено, КонецПериодаДляОстатков, КонецПериода)); 	
	
	СписокСчетовНакопленнойАмортизации = Новый СписокЗначений;
	СписокСчетовНакопленнойАмортизации.Добавить(ПланыСчетов.Хозрасчетный.АмортизацияЗданияСооружения);
	СписокСчетовНакопленнойАмортизации.Добавить(ПланыСчетов.Хозрасчетный.НакопленнаяАмортизацияОборудование);
	СписокСчетовНакопленнойАмортизации.Добавить(ПланыСчетов.Хозрасчетный.НакопленнаяАмортизацияКонторскоеОборудование);
	СписокСчетовНакопленнойАмортизации.Добавить(ПланыСчетов.Хозрасчетный.НакопленнаяАмортизацияМебельИПринадлежности);
	СписокСчетовНакопленнойАмортизации.Добавить(ПланыСчетов.Хозрасчетный.НакопленнаяАмортизацияТранспортныеСредства);
	СписокСчетовНакопленнойАмортизации.Добавить(ПланыСчетов.Хозрасчетный.НакопленнаяАмортизацияБлагоустройствоАрендованнойСобственности);
	СписокСчетовНакопленнойАмортизации.Добавить(ПланыСчетов.Хозрасчетный.НакопленнаяАмортизацияБлагоустройствоЗемельныхУчастков);
	Запрос.УстановитьПараметр("СписокСчетовНакопленнойАмортизации",СписокСчетовНакопленнойАмортизации);
	
	Возврат Запрос.ВыполнитьПакет();
КонецФункции
	
#КонецОбласти
	
#Область ПроцедурыПроведенияДокумента

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаАвансыДоотгрузка(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаДоотгрузка.Контрагент КАК Контрагент,
		|	ТаблицаДоотгрузка.Договор КАК Договор,
		|	ТаблицаДоотгрузка.ДокументАванса КАК ДокументА,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	ТаблицаДоотгрузка.Сумма КАК Сумма,
		|	ТаблицаДоотгрузка.СуммаНДС КАК СуммаНДС,
		|	ТаблицаДоотгрузка.СуммаНСП КАК СуммаНСП
		|ПОМЕСТИТЬ ВременнаяТаблицаАвансыДоотгрузка
		|ИЗ
		|	ВременнаяТаблицаДоотгрузкаРасшифровка КАК ТаблицаДоотгрузка
		|ГДЕ
		|	ТаблицаДоотгрузка.Сумма <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаАвансы.Контрагент,
		|	ТаблицаАвансы.Договор,
		|	ТаблицаАвансы.Документ,
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
		|	ТаблицаАвансы.Сумма,
		|	ТаблицаАвансы.СуммаНДС,
		|	ТаблицаАвансы.СуммаНСП
		|ИЗ
		|	ВременнаяТаблицаАвансы КАК ТаблицаАвансы
		|ГДЕ
		|	ТаблицаАвансы.Документ <> НЕОПРЕДЕЛЕНО
		|	И ТаблицаАвансы.СуммаДокумента > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаШапка.Период КАК Период,
		|	ТаблицаШапка.Организация КАК Организация,
		|	ТаблицаАвансыДоотгрузка.Контрагент КАК Контрагент,
		|	ТаблицаАвансыДоотгрузка.Договор КАК Договор,
		|	ТаблицаАвансыДоотгрузка.ДокументА КАК ДокументА,
		|	ТаблицаАвансыДоотгрузка.ВидДвижения КАК ВидДвижения,
		|	ТаблицаАвансыДоотгрузка.Сумма КАК Сумма,
		|	ТаблицаАвансыДоотгрузка.СуммаНДС КАК СуммаНДС,
		|	ТаблицаАвансыДоотгрузка.СуммаНСП КАК СуммаНСП
		|ИЗ
		|	ВременнаяТаблицаАвансыДоотгрузка КАК ТаблицаАвансыДоотгрузка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)";
	
	РезультатЗапроса = Запрос.Выполнить();
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаАвансыДоотгрузка", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаАвансыДоотгрузкаРасшифровка(ДокументСсылка, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаДоотгрузка.Контрагент КАК Контрагент,
		|	ТаблицаДоотгрузка.Договор КАК Договор,
		|	ТаблицаДоотгрузка.ДокументАванса КАК ДокументА,
		|	-ТаблицаДоотгрузка.Сумма КАК Сумма,
		|	-ТаблицаДоотгрузка.СуммаНДС КАК СуммаНДС,
		|	-ТаблицаДоотгрузка.СуммаНСП КАК СуммаНСП,
		|	ТаблицаДоотгрузка.ДокументОперации КАК ДокументОтгрузки
		|ПОМЕСТИТЬ ВременнаяТаблицаАвансыДоотгрузкаРасшифровка
		|ИЗ
		|	ВременнаяТаблицаДоотгрузкаРасшифровка КАК ТаблицаДоотгрузка
		|ГДЕ
		|	ТаблицаДоотгрузка.Сумма <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаАвансы.Контрагент,
		|	ТаблицаАвансы.Договор,
		|	ТаблицаАвансы.Документ,
		|	ТаблицаАвансы.Сумма,
		|	ТаблицаАвансы.СуммаНДС,
		|	ТаблицаАвансы.СуммаНСП,
		|	НЕОПРЕДЕЛЕНО
		|ИЗ
		|	ВременнаяТаблицаАвансы КАК ТаблицаАвансы
		|ГДЕ
		|	ТаблицаАвансы.Документ <> НЕОПРЕДЕЛЕНО
		|	И ТаблицаАвансы.СуммаДокумента > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаШапка.Период КАК Период,
		|	ТаблицаШапка.Организация КАК Организация,
		|	ТаблицаАвансыДоотгрузка.Контрагент КАК Контрагент,
		|	ТаблицаАвансыДоотгрузка.Договор КАК Договор,
		|	ТаблицаАвансыДоотгрузка.ДокументА КАК ДокументА,
		|	ТаблицаАвансыДоотгрузка.Сумма КАК Сумма,
		|	ТаблицаАвансыДоотгрузка.СуммаНДС КАК СуммаНДС,
		|	ТаблицаАвансыДоотгрузка.СуммаНСП КАК СуммаНСП,
		|	ТаблицаАвансыДоотгрузка.ДокументОтгрузки КАК ДокументОтгрузки
		|ИЗ
		|	ВременнаяТаблицаАвансыДоотгрузкаРасшифровка КАК ТаблицаАвансыДоотгрузка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ТаблицаШапка
		|		ПО (ИСТИНА)";
	
	РезультатЗапроса = Запрос.Выполнить();
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаАвансыДоотгрузкаРасшифровка", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаСведенияПоНалогуНаИмущество(ДокументСсылка, СтруктураДополнительныеСвойства)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	КОНЕЦПЕРИОДА(ВременнаяТаблицаШапка.Период, ГОД) КАК Год,
		|	ЗакрытиеМесяцаНедвижимость.ОсновноеСредство КАК ОсновноеСредство,
		|	ЗакрытиеМесяцаНедвижимость.СуммаНалога КАК СуммаНалога
		|ИЗ
		|	ВременнаяТаблицаНедвижимость КАК ЗакрытиеМесяцаНедвижимость
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	МЕСЯЦ(ВременнаяТаблицаШапка.Период) = 12
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Организация,
		|	КОНЕЦПЕРИОДА(ВременнаяТаблицаШапка.Период, ГОД),
		|	ЗакрытиеМесяцаТранспорт.ОсновноеСредство,
		|	ЗакрытиеМесяцаТранспорт.СуммаНалога
		|ИЗ
		|	ВременнаяТаблицаТранспорт КАК ЗакрытиеМесяцаТранспорт
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ПО (ИСТИНА)
		|ГДЕ
		|	МЕСЯЦ(ВременнаяТаблицаШапка.Период) = 12";
	РезультатЗапроса = Запрос.Выполнить();
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСведенияПоНалогуНаИмущество", РезультатЗапроса.Выгрузить());
КонецПроцедуры

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаНалоговаяАмортизация(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	НАЧАЛОПЕРИОДА(ВременнаяТаблицаШапка.Период, ГОД) КАК Год,
		|	ВременнаяТаблицаНалоговаяАмортизация.ГруппаНУ КАК ГруппаНУ,
		|	ВременнаяТаблицаНалоговаяАмортизация.ОсновноеСредство КАК ОсновноеСредство,
		|	ВременнаяТаблицаНалоговаяАмортизация.БалансоваяСтоимостьНаНачалоГода КАК БалансоваяСтоимостьНаНачалоГода,
		|	ВременнаяТаблицаНалоговаяАмортизация.УвеличениеСтоимости КАК СуммаПоступления,
		|	ВременнаяТаблицаНалоговаяАмортизация.УвеличениеСтоимостиНУ КАК СуммаПоступленияНУ,
		|	ВременнаяТаблицаНалоговаяАмортизация.УвеличениеСтоимости - ВременнаяТаблицаНалоговаяАмортизация.УвеличениеСтоимостиНУ КАК СуммаПоступленияОстаток,
		|	ВременнаяТаблицаНалоговаяАмортизация.СуммаВыбытия КАК СуммаВыбытия,
		|	ВременнаяТаблицаНалоговаяАмортизация.СуммаВыбытияНУ КАК СуммаВыбытияНУ,
		|	ВременнаяТаблицаНалоговаяАмортизация.СуммаВыбытия - ВременнаяТаблицаНалоговаяАмортизация.СуммаВыбытияНУ КАК СуммаВыбытияОстаток,
		|	ВременнаяТаблицаНалоговаяАмортизация.ЗатратыНаРемонт КАК ЗатратыНаРемонт,
		|	ВременнаяТаблицаНалоговаяАмортизация.НормаНаРемонт КАК НормаНаРемонт,
		|	ВременнаяТаблицаНалоговаяАмортизация.ПревышениеНормыНаРемонт КАК ПревышениеЗатратНаРемонт,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаНалоговаяАмортизация.БалансоваяСтоимостьИтого < 0
		|			ТОГДА 0
		|		ИНАЧЕ ВременнаяТаблицаНалоговаяАмортизация.БалансоваяСтоимостьИтого
		|	КОНЕЦ КАК БалансоваяСтоимостьНаКонецГода,
		|	ВременнаяТаблицаНалоговаяАмортизация.БалансоваяСтоимостьИтого КАК БалансоваяСтоимостьИтого,
		|	ВременнаяТаблицаНалоговаяАмортизация.НормаАмортизации КАК НАО,
		|	ВременнаяТаблицаНалоговаяАмортизация.Амортизация КАК АмортизацияЗаГод,
		|	ВременнаяТаблицаНалоговаяАмортизация.УвеличениеСОД КАК УвеличениеСОД,
		|	ВременнаяТаблицаНалоговаяАмортизация.ВычетМеньше100РП КАК ВычетМеньше100РП,
		|	ВременнаяТаблицаНалоговаяАмортизация.ДатаВводаВЭксплуатацию КАК ДатаВводаВЭксплуатацию,
		|	ВременнаяТаблицаНалоговаяАмортизация.КоличествоЛетЭксплуатации КАК КоличествоЛетЭксплуатации,
		|	ВременнаяТаблицаНалоговаяАмортизация.БазаПрямолинейнойАмортизации КАК БазаПрямолинейнойАмортизации
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалоговаяАмортизация КАК ВременнаяТаблицаНалоговаяАмортизация
		|		ПО (ИСТИНА)";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаНалоговаяАмортизация", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры 

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаНалоговаяВыверка(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Организация,
		|	ВременнаяТаблицаШапка.Период КАК Год,
		|	ВременнаяТаблицаНалоговаяВыверка.НомерСтроки КАК Строка,
		|	ВременнаяТаблицаНалоговаяВыверка.Сумма,
		|	ВременнаяТаблицаНалоговаяВыверка.НаименованиеСтроки
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаНалоговаяВыверка КАК ВременнаяТаблицаНалоговаяВыверка
		|		ПО (ИСТИНА)";
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаНалоговаяВыверка", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры 

Процедура СформироватьТаблицаУбыткиПрошлыхЛет(ДокументСсылка, СтруктураДополнительныеСвойства) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Период КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация,
		|	ЗНАЧЕНИЕ(ВидДВиженияНакопления.Расход) КАК ВидДвижения,
		|	НАЧАЛОПЕРИОДА(ВременнаяТаблицаУбыткиПрошлыхЛет.ГОД, ГОД) КАК Год,
		|	ВременнаяТаблицаУбыткиПрошлыхЛет.Сумма КАК Сумма
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаУбыткиПрошлыхЛет КАК ВременнаяТаблицаУбыткиПрошлыхЛет
		|		ПО (ИСТИНА)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Период,
		|	ВременнаяТаблицаШапка.Организация,
		|	ЗНАЧЕНИЕ(ВидДВиженияНакопления.Приход),
		|	НАЧАЛОПЕРИОДА(ВременнаяТаблицаШапка.Период, ГОД),
		|	- &СуммаНалогооблагаемаяПрибыль
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
		|ГДЕ
		|	&СуммаНалогооблагаемаяПрибыль < 0";
	
	Запрос.УстановитьПараметр("СуммаНалогооблагаемаяПрибыль", ДокументСсылка.СуммаНалогооблагаемаяПрибыль);
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаУбыткиПрошлыхЛет", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры 

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, СтруктураДополнительныеСвойства) Экспорт 		
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДокумента.Ссылка КАК Ссылка,
		|	ТаблицаДокумента.Дата КАК Период,
		|	ТаблицаДокумента.Организация КАК Организация
		|ПОМЕСТИТЬ ВременнаяТаблицаШапка
		|ИЗ
		|	Документ.ЗакрытиеМесяца КАК ТаблицаДокумента
		|ГДЕ
		|	ТаблицаДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаДоотгрузкаРасшифровка.Контрагент КАК Контрагент,
		|	ЗакрытиеМесяцаДоотгрузкаРасшифровка.Договор КАК Договор,
		|	ЗакрытиеМесяцаДоотгрузкаРасшифровка.ДокументАванса КАК ДокументАванса,
		|	ЗакрытиеМесяцаДоотгрузкаРасшифровка.Сумма КАК Сумма,
		|	ЗакрытиеМесяцаДоотгрузкаРасшифровка.СуммаНДС КАК СуммаНДС,
		|	ЗакрытиеМесяцаДоотгрузкаРасшифровка.СуммаНСП КАК СуммаНСП,
		|	ЗакрытиеМесяцаДоотгрузкаРасшифровка.ДокументОперации КАК ДокументОперации
		|ПОМЕСТИТЬ ВременнаяТаблицаДоотгрузкаРасшифровка
		|ИЗ
		|	Документ.ЗакрытиеМесяца.ДоотгрузкаРасшифровка КАК ЗакрытиеМесяцаДоотгрузкаРасшифровка
		|ГДЕ
		|	ЗакрытиеМесяцаДоотгрузкаРасшифровка.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаАвансы.Контрагент КАК Контрагент,
		|	ЗакрытиеМесяцаАвансы.Договор КАК Договор,
		|	ЗакрытиеМесяцаАвансы.Сумма КАК Сумма,
		|	ЗакрытиеМесяцаАвансы.СуммаНДС КАК СуммаНДС,
		|	ЗакрытиеМесяцаАвансы.СуммаНСП КАК СуммаНСП,
		|	ЗакрытиеМесяцаАвансы.Документ КАК Документ,
		|	ЗакрытиеМесяцаАвансы.СуммаДокумента КАК СуммаДокумента
		|ПОМЕСТИТЬ ВременнаяТаблицаАвансы
		|ИЗ
		|	Документ.ЗакрытиеМесяца.АвансыРасшифровка КАК ЗакрытиеМесяцаАвансы
		|ГДЕ
		|	ЗакрытиеМесяцаАвансы.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаКорректировкаНДС.СчетДт КАК СчетДт,
		|	ЗакрытиеМесяцаКорректировкаНДС.СчетКт КАК СчетКт,
		|	ЗакрытиеМесяцаКорректировкаНДС.Сумма КАК Сумма,
		|	ЗакрытиеМесяцаКорректировкаНДС.Содержание КАК Содержание
		|ПОМЕСТИТЬ ВременнаяТаблицаКорректировкаНДС
		|ИЗ
		|	Документ.ЗакрытиеМесяца.КорректировкаНДС КАК ЗакрытиеМесяцаКорректировкаНДС
		|ГДЕ
		|	ЗакрытиеМесяцаКорректировкаНДС.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаНедвижимость.Ссылка КАК Ссылка,
		|	ЗакрытиеМесяцаНедвижимость.ОсновноеСредство КАК ОсновноеСредство,
		|	ЗакрытиеМесяцаНедвижимость.ГруппаИмущества КАК ГруппаИмущества,
		|	ЗакрытиеМесяцаНедвижимость.КоличествоМесяцевЭксплуатации КАК КоличествоМесяцев,
		|	ЗакрытиеМесяцаНедвижимость.Ставка КАК Ставка,
		|	ЗакрытиеМесяцаНедвижимость.СуммаНалога КАК СуммаНалога,
		|	ЗакрытиеМесяцаНедвижимость.ОценочнаяСтоимость КАК ОценочнаяСтоимость,
		|	ЗакрытиеМесяцаНедвижимость.СрокЭксплуатации КАК СрокЭксплуатации,
		|	ЗакрытиеМесяцаНедвижимость.ОбщаяПлощадь КАК ОбщаяПлощадь,
		|	ЗакрытиеМесяцаНедвижимость.Кр КАК Кр,
		|	ЗакрытиеМесяцаНедвижимость.Кз КАК Кз,
		|	ЗакрытиеМесяцаНедвижимость.Ко КАК Ко,
		|	ЗакрытиеМесяцаНедвижимость.СтоимостьНУ КАК СтоимостьНУ,
		|	ЗакрытиеМесяцаНедвижимость.МатериалСтен КАК МатериалСтен,
		|	ЗакрытиеМесяцаНедвижимость.ЗемельныйНалог КАК ЗемельныйНалог,
		|	ЗакрытиеМесяцаНедвижимость.ОтЛет КАК ОтЛет,
		|	ЗакрытиеМесяцаНедвижимость.ДоЛет КАК ДоЛет,
		|	ЗакрытиеМесяцаНедвижимость.ЖилаяПлощадь КАК ЖилаяПлощадь,
		|	ЗакрытиеМесяцаНедвижимость.НежилаяПлощадь КАК НежилаяПлощадь,
		|	ЗакрытиеМесяцаНедвижимость.НалогСУчетомЗемельногоНалога КАК НалогСУчетомЗемельногоНалога,
		|	ЗакрытиеМесяцаНедвижимость.Объект КАК Объект
		|ПОМЕСТИТЬ ВременнаяТаблицаНедвижимость
		|ИЗ
		|	Документ.ЗакрытиеМесяца.Недвижимость КАК ЗакрытиеМесяцаНедвижимость
		|ГДЕ
		|	ЗакрытиеМесяцаНедвижимость.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаТранспорт.Ссылка КАК Ссылка,
		|	ЗакрытиеМесяцаТранспорт.ОсновноеСредство КАК ОсновноеСредство,
		|	ЗакрытиеМесяцаТранспорт.ГруппаИмущества КАК ГруппаИмущества,
		|	ЗакрытиеМесяцаТранспорт.ВидТранспортаКатегорияАБВ КАК ВидТранспортаКатегорияАБВ,
		|	ЗакрытиеМесяцаТранспорт.КоличествоМесяцевЭксплуатации КАК КоличествоМесяцев,
		|	ЗакрытиеМесяцаТранспорт.ОбъемДвигателя КАК ОбъемДвигателя,
		|	ЗакрытиеМесяцаТранспорт.Ставка КАК Ставка,
		|	ЗакрытиеМесяцаТранспорт.СуммаНалога КАК СуммаНалога,
		|	ЗакрытиеМесяцаТранспорт.ОтЛет КАК ОтЛет,
		|	ЗакрытиеМесяцаТранспорт.ДоЛет КАК ДоЛет
		|ПОМЕСТИТЬ ВременнаяТаблицаТранспорт
		|ИЗ
		|	Документ.ЗакрытиеМесяца.Транспорт КАК ЗакрытиеМесяцаТранспорт
		|ГДЕ
		|	ЗакрытиеМесяцаТранспорт.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаНалоговаяАмортизация.ГруппаНУ КАК ГруппаНУ,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.ОсновноеСредство КАК ОсновноеСредство,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.БалансоваяСтоимостьНаНачалоГода КАК БалансоваяСтоимостьНаНачалоГода,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.УвеличениеСтоимости КАК УвеличениеСтоимости,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.УвеличениеСтоимостиНУ КАК УвеличениеСтоимостиНУ,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.ПоступлениеПрошлыйГод КАК ПоступлениеПрошлыйГод,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.СуммаВыбытия КАК СуммаВыбытия,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.СуммаВыбытияНУ КАК СуммаВыбытияНУ,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.ВыбытиеПрошлыйГод КАК ВыбытиеПрошлыйГод,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.ЗатратыНаРемонт КАК ЗатратыНаРемонт,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.НормаНаРемонт КАК НормаНаРемонт,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.ПревышениеНормыНаРемонт КАК ПревышениеНормыНаРемонт,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.БалансоваяСтоимостьНаКонецГода КАК БалансоваяСтоимостьНаКонецГода,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.БалансоваяСтоимостьИтого КАК БалансоваяСтоимостьИтого,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.НормаАмортизации КАК НормаАмортизации,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.Амортизация КАК Амортизация,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.УвеличениеСОД КАК УвеличениеСОД,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.ВычетМеньше100РП КАК ВычетМеньше100РП,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.ДатаВводаВЭксплуатацию КАК ДатаВводаВЭксплуатацию,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.КоличествоЛетЭксплуатации КАК КоличествоЛетЭксплуатации,
		|	ЗакрытиеМесяцаНалоговаяАмортизация.БазаПрямолинейнойАмортизации КАК БазаПрямолинейнойАмортизации
		|ПОМЕСТИТЬ ВременнаяТаблицаНалоговаяАмортизация
		|ИЗ
		|	Документ.ЗакрытиеМесяца.НалоговаяАмортизация КАК ЗакрытиеМесяцаНалоговаяАмортизация
		|ГДЕ
		|	ЗакрытиеМесяцаНалоговаяАмортизация.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаНалоговаяВыверка.Ссылка КАК Ссылка,
		|	ЗакрытиеМесяцаНалоговаяВыверка.НомерСтроки КАК НомерСтроки,
		|	ЗакрытиеМесяцаНалоговаяВыверка.Сумма КАК Сумма,
		|	ЗакрытиеМесяцаНалоговаяВыверка.НаименованиеСтроки КАК НаименованиеСтроки
		|ПОМЕСТИТЬ ВременнаяТаблицаНалоговаяВыверка
		|ИЗ
		|	Документ.ЗакрытиеМесяца.НалоговаяВыверка КАК ЗакрытиеМесяцаНалоговаяВыверка
		|ГДЕ
		|	ЗакрытиеМесяцаНалоговаяВыверка.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаАмортизацияОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ЗакрытиеМесяцаАмортизацияОС.Сумма КАК Сумма
		|ПОМЕСТИТЬ ВременнаяТаблицаАмортизацияОС
		|ИЗ
		|	Документ.ЗакрытиеМесяца.АмортизацияОС КАК ЗакрытиеМесяцаАмортизацияОС
		|ГДЕ
		|	ЗакрытиеМесяцаАмортизацияОС.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакрытиеМесяцаУбыткиПрошлыхЛет.Год КАК Год,
		|	ЗакрытиеМесяцаУбыткиПрошлыхЛет.УбытокПодлежащийПереносу КАК Сумма
		|ПОМЕСТИТЬ ВременнаяТаблицаУбыткиПрошлыхЛет
		|ИЗ
		|	Документ.ЗакрытиеМесяца.УбыткиПрошлыхЛет КАК ЗакрытиеМесяцаУбыткиПрошлыхЛет
		|ГДЕ
		|	ЗакрытиеМесяцаУбыткиПрошлыхЛет.Ссылка = &Ссылка
		|	И ЗакрытиеМесяцаУбыткиПрошлыхЛет.УбытокПодлежащийПереносу > 0";
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Выполнить();    	
	                                  
	СформироватьТаблицаАвансыДоотгрузка(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаАвансыДоотгрузкаРасшифровка(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаСведенияПоНалогуНаИмущество(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаНалоговаяАмортизация(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаНалоговаяВыверка(ДокументСсылка, СтруктураДополнительныеСвойства);
	СформироватьТаблицаУбыткиПрошлыхЛет(ДокументСсылка, СтруктураДополнительныеСвойства);

	// Подготовка данных	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВременнаяТаблицаШапка.Ссылка КАК Регистратор,
		|	ВременнаяТаблицаШапка.Период КАК Период,
		|	ВременнаяТаблицаШапка.Организация КАК Организация
		|ИЗ
		|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка";
	МассивРезультатов = Запрос.ВыполнитьПакет();
	ТаблицаРеквизиты = МассивРезультатов[0].Выгрузить();
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРеквизиты", ТаблицаРеквизиты);

КонецПроцедуры // ИнициализироватьДанныеДокумента()

#КонецОбласти

#Область ИнтерфейсПечати

// Функция формирует табличный документ с печатной формой НалоговаяАмортизация
//
// Возвращаемое значение:
//  Табличный документ - печатная форма
//
Функция ПечатьНалоговойАмортизации(МассивОбъектов, ОбъектыПечати)
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ЗакрытиеМесяца.Ссылка КАК Ссылка,
		|	ЗакрытиеМесяца.Номер КАК Номер,
		|	ЗакрытиеМесяца.Дата КАК Дата,
		|	ЗакрытиеМесяца.Организация КАК Организация,
		|	ЗакрытиеМесяца.Организация.НаименованиеПолное КАК ОрганизацияНаименованиеПолное,
		|	ЗакрытиеМесяца.НалоговаяАмортизация.(
		|		Ссылка КАК Ссылка,
		|		НомерСтроки КАК НомерСтроки,
		|		ГруппаНУ КАК ГруппаНУ,
		|		ОсновноеСредство КАК ОсновноеСредство,
		|		БалансоваяСтоимостьНаКонецПрошлыйГод КАК БалансоваяСтоимостьНаКонецПрошлыйГод,
		|		АмортизацияЗаПрошлыйГод КАК АмортизацияЗаПрошлыйГод,
		|		БалансоваяСтоимостьНаНачалоГода КАК БалансоваяСтоимостьНаНачалоГода,
		|		УвеличениеСтоимости КАК УвеличениеСтоимости,
		|		УвеличениеСтоимостиНУ КАК УвеличениеСтоимостиНУ,
		|		ПоступлениеПрошлыйГод КАК ПоступлениеПрошлыйГод,
		|		СуммаВыбытия КАК СуммаВыбытия,
		|		СуммаВыбытияНУ КАК СуммаВыбытияНУ,
		|		ВыбытиеПрошлыйГод КАК ВыбытиеПрошлыйГод,
		|		ЗатратыНаРемонт КАК ЗатратыНаРемонт,
		|		НормаНаРемонт КАК НормаНаРемонт,
		|		ПревышениеНормыНаРемонт КАК ПревышениеНормыНаРемонт,
		|		БалансоваяСтоимостьНаКонецГода КАК БалансоваяСтоимостьНаКонецГода,
		|		БалансоваяСтоимостьИтого КАК БалансоваяСтоимостьИтого,
		|		НормаАмортизации КАК НормаАмортизации,
		|		Амортизация КАК Амортизация,
		|		УвеличениеСОД КАК УвеличениеСОД
		|	) КАК НалоговаяАмортизация
		|ИЗ
		|	Документ.ЗакрытиеМесяца КАК ЗакрытиеМесяца
		|ГДЕ
		|	ЗакрытиеМесяца.Ссылка В(&СписокДокументов)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";		
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("СписокДокументов", МассивОбъектов);
	РезультатЗапроса = Запрос.Выполнить();
	
	Шапка = РезультатЗапроса.Выбрать();
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ЗакрытиеМесяца_НалоговаяАмортизация";

	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ЗакрытиеМесяца.ПФ_MXL_НалоговаяАмортизация");
	
	Пока Шапка.Следующий() Цикл
		Если ТабличныйДокумент.ВысотаТаблицы > 0 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Подготовка данных
		ДанныеПечати = Новый Структура;
		
		ТекстЗаголовка = СформироватьЗаголовокДокумента(Шапка, НСтр("ru = 'Налоговая амортизация'"));
		ДанныеПечати.Вставить("ТекстЗаголовка", ТекстЗаголовка);
		ДанныеПечати.Вставить("ОрганизацияПредставление", Шапка.ОрганизацияНаименованиеПолное);
		
		Руководители = БухгалтерскийУчетСервер.ОтветственныеЛицаОрганизацийРуководители(Шапка.Организация, Шапка.Дата);
		ДанныеПечати.Вставить("ФИОРуководителя", 			Руководители.Руководитель);
		ДанныеПечати.Вставить("ФИОГлавногоБухгалтера", 		Руководители.ГлавныйБухгалтер);
		
		ТаблицаСтрокиТаблицы = Шапка.НалоговаяАмортизация.Выгрузить();
		
		ДанныеПечати.Вставить("Всего", ТаблицаСтрокиТаблицы.Итог("Амортизация"));
		ДанныеПечати.Вставить("СуммаПрописью", БухгалтерскийУчетСервер.СформироватьСуммуПрописью(ДанныеПечати.Всего));
				
		// Области
		МассивОбластейМакета = Новый Массив;
		МассивОбластейМакета.Добавить("Заголовок");
		МассивОбластейМакета.Добавить("ШапкаТаблицы");
		МассивОбластейМакета.Добавить("Строка");
		МассивОбластейМакета.Добавить("Подвал");
		МассивОбластейМакета.Добавить("Итоги");
		МассивОбластейМакета.Добавить("СуммаПрописью");
		МассивОбластейМакета.Добавить("Подписи");

		Для Каждого ИмяОбласти Из МассивОбластейМакета Цикл
			ОбластьМакета = Макет.ПолучитьОбласть(ИмяОбласти);
			Если ИмяОбласти <> "Строка" Тогда
				ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, ДанныеПечати);
				ТабличныйДокумент.Вывести(ОбластьМакета);
			ИначеЕсли ИмяОбласти = "Строка" Тогда 
				Для Каждого СтрокаТаблицы Из ТаблицаСтрокиТаблицы Цикл
					ОбластьМакета.Параметры.Заполнить(СтрокаТаблицы);
					ТабличныйДокумент.Вывести(ОбластьМакета);
				КонецЦикла;
			КонецЕсли;	
		КонецЦикла;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

// Функция формирует табличный документ с печатной формой НалоговаяВыверка
//
// Возвращаемое значение:
//  Табличный документ - печатная форма
//
Функция ПечатьНалоговойВыверки(МассивОбъектов, ОбъектыПечати)
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ЗакрытиеМесяца.Ссылка КАК Ссылка,
		|	ЗакрытиеМесяца.Номер КАК Номер,
		|	ЗакрытиеМесяца.Дата КАК Дата,
		|	ЗакрытиеМесяца.Организация КАК Организация,
		|	ЗакрытиеМесяца.Организация.НаименованиеПолное КАК ОрганизацияНаименованиеПолное,
		|	ЗакрытиеМесяца.НалоговаяВыверка.(
		|		Ссылка КАК Ссылка,
		|		НомерСтроки КАК НомерСтроки,
		|		Сумма КАК Сумма,
		|		НаименованиеСтроки КАК НаименованиеСтроки
		|	) КАК НалоговаяВыверка
		|ИЗ
		|	Документ.ЗакрытиеМесяца КАК ЗакрытиеМесяца
		|ГДЕ
		|	ЗакрытиеМесяца.Ссылка В(&СписокДокументов)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";		
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("СписокДокументов", МассивОбъектов);
	РезультатЗапроса = Запрос.Выполнить();
	
	Шапка = РезультатЗапроса.Выбрать();
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ЗакрытиеМесяца_НалоговаяВыверка";

	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ЗакрытиеМесяца.ПФ_MXL_НалоговаяВыверка");
	
	Пока Шапка.Следующий() Цикл
		Если ТабличныйДокумент.ВысотаТаблицы > 0 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Подготовка данных
		ДанныеПечати = Новый Структура;
		
		ТекстЗаголовка = СформироватьЗаголовокДокумента(Шапка, НСтр("ru = 'Налоговая выверка'"));
		ДанныеПечати.Вставить("ТекстЗаголовка", ТекстЗаголовка);
		ДанныеПечати.Вставить("ОрганизацияПредставление", Шапка.ОрганизацияНаименованиеПолное);
		
		Руководители = БухгалтерскийУчетСервер.ОтветственныеЛицаОрганизацийРуководители(Шапка.Организация, Шапка.Дата);
		ДанныеПечати.Вставить("ФИОРуководителя", 			Руководители.Руководитель);
		ДанныеПечати.Вставить("ФИОГлавногоБухгалтера", 		Руководители.ГлавныйБухгалтер);
		
		ТаблицаСтрокиТаблицы = Шапка.НалоговаяВыверка.Выгрузить();
		
		// Области
		МассивОбластейМакета = Новый Массив;
		МассивОбластейМакета.Добавить("Заголовок");
		МассивОбластейМакета.Добавить("ШапкаТаблицы");
		МассивОбластейМакета.Добавить("Строка");
		МассивОбластейМакета.Добавить("Подвал");
		МассивОбластейМакета.Добавить("Подписи");

		Для Каждого ИмяОбласти Из МассивОбластейМакета Цикл
			ОбластьМакета = Макет.ПолучитьОбласть(ИмяОбласти);
			Если ИмяОбласти <> "Строка" Тогда
				ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, ДанныеПечати);
				ТабличныйДокумент.Вывести(ОбластьМакета);
			ИначеЕсли ИмяОбласти = "Строка" Тогда 
				Для Каждого СтрокаТаблицы Из ТаблицаСтрокиТаблицы Цикл
					ОбластьМакета.Параметры.Заполнить(СтрокаТаблицы);
					ТабличныйДокумент.Вывести(ОбластьМакета);
				КонецЦикла;
			КонецЕсли;	
		КонецЦикла;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "НалоговаяАмортизация") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, 
		"НалоговаяАмортизация", 
		НСтр("ru = 'Налоговая амортизация'"), 
		ПечатьНалоговойАмортизации(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.ЗакрытиеМесяца.ПФ_MXL_НалоговаяАмортизация");
	КонецЕсли;	
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "НалоговаяВыверка") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, 
		"НалоговаяВыверка", 
		НСтр("ru = 'Налоговая выверка'"), 
		ПечатьНалоговойВыверки(МассивОбъектов, ОбъектыПечати),
		,
		"Документ.ЗакрытиеМесяца.ПФ_MXL_НалоговаяВыверка");
	КонецЕсли;
	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "НалоговаяАмортизация";
	КомандаПечати.Представление = НСтр("ru = 'Налоговая амортизация'");
	КомандаПечати.СписокФорм = "ФормаДокумента,ФормаСписка";
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Обработчик = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Порядок = 1;
	ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(КомандаПечати, "РасчетНалогаНаПрибыль", Истина);
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "НалоговаяВыверка";
	КомандаПечати.Представление = НСтр("ru = 'Налоговая выверка'");
	КомандаПечати.СписокФорм = "ФормаДокумента,ФормаСписка";
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Обработчик = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Порядок = 2;
	ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(КомандаПечати, "РасчетНалогаНаПрибыль", Истина);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает заголовок документа для печатной формы.
//
// Параметры:
//  Шапка - любая структура с полями:
//           Номер         - Строка или Число - номер документа;
//           Дата          - Дата - дата документа;
//           Представление - Строка - (необязательный) платформенное представление ссылки на документ.
//                                    Если параметр НазваниеДокумента не задан, то название документа будет вычисляться
//                                    из этого параметра.
//  НазваниеДокумента - Строка - название документа (например, "Счет на оплату").
//
// Возвращаемое значение:
//  Строка - заголовок документа.
//
Функция СформироватьЗаголовокДокумента(Шапка, Знач НазваниеДокумента = "")
	
	ДанныеДокумента = Новый Структура("Номер,Дата,Представление");
	ЗаполнитьЗначенияСвойств(ДанныеДокумента, Шапка);
	
	// Если название документа не передано, получим название по представлению документа.
	Если ПустаяСтрока(НазваниеДокумента) И ЗначениеЗаполнено(ДанныеДокумента.Представление) Тогда
		ПоложениеНомера = СтрНайти(ДанныеДокумента.Представление, ДанныеДокумента.Номер);
		Если ПоложениеНомера > 0 Тогда
			НазваниеДокумента = СокрЛП(Лев(ДанныеДокумента.Представление, ПоложениеНомера - 1));
		КонецЕсли;
	КонецЕсли;

	Возврат СтрШаблон(НСтр("ru = '%1 за %2 г.'"),
		НазваниеДокумента, Формат(Год(ДанныеДокумента.Дата), "ЧГ=0")); //Формат(ДанныеДокумента.Дата, "ДЛФ=DD"));
	
КонецФункции

#КонецОбласти

#Область ВерсионированиеОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
//@skip-warning
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#КонецЕсли
