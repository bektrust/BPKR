#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ЗаполненияДокумента

Процедура ЗаполнитьПоСтруктуре(ДанныеЗаполнения) Экспорт
	
	Если ДанныеЗаполнения.Свойство("ВедомостьЗаработнойПлаты") Тогда 
		ЗаполнитьПоВедомостиЗП(ДанныеЗаполнения);
	ИначеЕсли ДанныеЗаполнения.Свойство("Командировка") Тогда
		ЗаполнитьДокументПоДокументуКомандировка(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

// Процедура заполнения документа на основании.
//
// Параметры:
//	ДанныеЗаполнения - Структура - Данные заполнения документа.
//	
Процедура ЗаполнитьПоВедомостиЗП(ДанныеЗаполнения) Экспорт
	// Общий 	
	Если ДанныеЗаполнения.Свойство("ТаблицаЗначений") Тогда 
		ДокументОснование = ДанныеЗаполнения.ВедомостьЗаработнойПлаты;
		ВидОперации = Перечисления.ВидыОперацийРКО.ВыплатаЗП;
		КурсВзаиморасчетов = ДанныеЗаполнения.КурсВыплаты;
		КратностьВзаиморасчетов = ДанныеЗаполнения.КратностьВыплаты;
		
		// Заполнение шапки
		Организация = ДанныеЗаполнения.Организация;
		СтатьяДвиженияДенежныхСредств = ДанныеЗаполнения.СтатьяДвиженияДенежныхСредств;
		Касса = ДанныеЗаполнения.Касса;
		ВалютаДокумента = ДанныеЗаполнения.Касса.ВалютаДенежныхСредств;
		СчетРасчетов = ПланыСчетов.Хозрасчетный.НачисленнаяЗаработнаяПлата;
		Выдать = НСтр("ru = 'На зарплату'");
		
		Если ДанныеЗаполнения.ТаблицаЗначений.Количество() = 1 Тогда
			СведенияОФизЛице = БухгалтерскийУчетСервер.ПолучитьСведенияОФизЛице(Организация, ДанныеЗаполнения.ТаблицаЗначений[0].ФизЛицо, ДанныеЗаполнения.Дата); 
			СтрокаПоДокументу = СтрШаблон(НСтр("ru = '%1 Серия %2 №%3 выдан %4 от %5'"),
			"паспорт",
			СведенияОФизЛице.ПаспортСерия,
			СведенияОФизЛице.ПаспортНомер,
			СведенияОФизЛице.ПаспортКемВыдан,
			Формат(СведенияОФизЛице.ПаспортДатаВыдачи, "ДЛФ=DD"));
			ПоДокументу = СтрокаПоДокументу;
		КонецЕсли;
			
		НазваниеДокумента = НСтр("ru = 'Ведомость ЗП'");
		НомерНаПечать = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеЗаполнения.НомерВыплаты);
		Основание = СтрШаблон(НСтр("ru = '%1 № %2 от %3'"),НазваниеДокумента, НомерНаПечать, Формат(ДанныеЗаполнения.Дата, "ДЛФ=DD"));
		
		ОбщаяСуммаДокумента = 0;
		// Заполнение Табличной части
		ВыплатаЗаработнойПлаты.Очистить();
		Для Каждого СтрокаТаблицы Из ДанныеЗаполнения.ТаблицаЗначений Цикл 
			СтрокаТабличнойЧасти = ВыплатаЗаработнойПлаты.Добавить();
			СтрокаТабличнойЧасти.ФизЛицо = СтрокаТаблицы.ФизЛицо;
			СтрокаТабличнойЧасти.СуммаПлатежа = СтрокаТаблицы.СуммаПоКассе;
			ОбщаяСуммаДокумента = ОбщаяСуммаДокумента + СтрокаТаблицы.СуммаПоКассе;
		КонецЦикла;
		СуммаДокумента = ОбщаяСуммаДокумента;
		                  
	// Индивидуальные	
	Иначе 
		ДокументОснование = ДанныеЗаполнения.ВедомостьЗаработнойПлаты;
		ВидОперации = Перечисления.ВидыОперацийРКО.ВыплатаЗП;
		ИндивидуальныйРКО = Истина;
		КурсВзаиморасчетов = ДанныеЗаполнения.КурсВыплаты;
		КратностьВзаиморасчетов = ДанныеЗаполнения.КратностьВыплаты;
		
		// Заполнение шапки
		ФизЛицо = ДанныеЗаполнения.ФизЛицо;
		Организация = ДанныеЗаполнения.Организация;
		СтатьяДвиженияДенежныхСредств = ДанныеЗаполнения.СтатьяДвиженияДенежныхСредств;
		Касса = ДанныеЗаполнения.Касса;
		СуммаДокумента = ДанныеЗаполнения.СуммаПоКассе;
		ВалютаДокумента = ДанныеЗаполнения.Касса.ВалютаДенежныхСредств;
		СчетРасчетов = ПланыСчетов.Хозрасчетный.НачисленнаяЗаработнаяПлата;		
		Выдать = ФизЛицо.Наименование;
		
		СведенияОФизЛице = БухгалтерскийУчетСервер.ПолучитьСведенияОФизЛице(Организация, ФизЛицо, ДанныеЗаполнения.Дата); 
		СтрокаПоДокументу = СтрШаблон(НСтр("ru = '%1 Серия %2 №%3 выдан %4 от %5'"),
			"паспорт",
			СведенияОФизЛице.ПаспортСерия,
			СведенияОФизЛице.ПаспортНомер,
			СведенияОФизЛице.ПаспортКемВыдан,
			Формат(СведенияОФизЛице.ПаспортДатаВыдачи, "ДЛФ=DD"));
			
		ПоДокументу = СтрокаПоДокументу;		
		
		НазваниеДокумента = НСтр("ru = 'Ведомость ЗП'");
		НомерНаПечать = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеЗаполнения.НомерВыплаты);
		Основание = СтрШаблон(НСтр("ru = '%1 № %2 от %3'"),НазваниеДокумента, НомерНаПечать, Формат(ДанныеЗаполнения.Дата, "ДЛФ=DD"));
		
		Выдать = ДанныеЗаполнения.ФизЛицо;
		
		ВыплатаЗаработнойПлаты.Очистить();
		СтрокаТабличнойЧасти = ВыплатаЗаработнойПлаты.Добавить();
		СтрокаТабличнойЧасти.ФизЛицо = ДанныеЗаполнения.ФизЛицо;
		СтрокаТабличнойЧасти.СуммаПлатежа = ДанныеЗаполнения.СуммаПоКассе;
	КонецЕсли;	
КонецПроцедуры

// Процедура заполнения документа на основании.
//
// Параметры:
//	ДанныеЗаполнения - Структура - Данные заполнения документа.
//	
Процедура ЗаполнитьПоВыплатеЗП(ДанныеЗаполнения) Экспорт
	// Общий 	
	Если ДанныеЗаполнения.Свойство("ТаблицаЗначений") Тогда 
		ДокументОснование = ДанныеЗаполнения.ВыплатаЗП;
		ВидОперации = Перечисления.ВидыОперацийРКО.ВыплатаЗП; 
		
		// Заполнение шапки
		Организация = ДанныеЗаполнения.Организация;
		Касса = ДанныеЗаполнения.Касса;
		СчетРасчетов = ПланыСчетов.Хозрасчетный.НачисленнаяЗаработнаяПлата;
		Выдать = НСтр("ru = 'На зарплату'");
		
		НазваниеДокумента = НСтр("ru = 'Ведомость ЗП'");
		НомерНаПечать = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеЗаполнения.НомерВыплаты);
		Основание = СтрШаблон(НСтр("ru = '%1 № %2 от %3'"),НазваниеДокумента, НомерНаПечать, Формат(ДанныеЗаполнения.Дата, "ДЛФ=DD"));
		
		ОбщаяСуммаДокумента = 0;
		// Заполнение Табличной части
		ВыплатаЗаработнойПлаты.Очистить();
		Для Каждого СтрокаТаблицы Из ДанныеЗаполнения.ТаблицаЗначений Цикл 
			СтрокаТабличнойЧасти = ВыплатаЗаработнойПлаты.Добавить();
			СтрокаТабличнойЧасти.ФизЛицо = СтрокаТаблицы.ФизЛицо;
			СтрокаТабличнойЧасти.СуммаПлатежа = СтрокаТаблицы.СуммаКВыплате;
			ОбщаяСуммаДокумента = ОбщаяСуммаДокумента + СтрокаТаблицы.СуммаКВыплате;
		КонецЦикла;
		СуммаДокумента = ОбщаяСуммаДокумента;
		                  
	// Индивидуальные	
	Иначе 
		ДокументОснование = ДанныеЗаполнения.ВыплатаЗП;
		ВидОперации = Перечисления.ВидыОперацийРКО.ВыплатаЗП;
		
		// Заполнение шапки
		ФизЛицо = ДанныеЗаполнения.ФизЛицо;
		Организация = ДанныеЗаполнения.Организация;
		Касса = ДанныеЗаполнения.Касса;
		СуммаДокумента = ДанныеЗаполнения.СуммаКВыплате;
		СчетРасчетов = ПланыСчетов.Хозрасчетный.НачисленнаяЗаработнаяПлата;
		
		НазваниеДокумента = НСтр("ru = 'Ведомость ЗП'");
		НомерНаПечать = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеЗаполнения.НомерВыплаты);
		Основание = СтрШаблон(НСтр("ru = '%1 № %2 от %3'"),НазваниеДокумента, НомерНаПечать, Формат(ДанныеЗаполнения.Дата, "ДЛФ=DD"));
		
		Выдать = ДанныеЗаполнения.ФизЛицо;
		
		ВыплатаЗаработнойПлаты.Очистить();
		СтрокаТабличнойЧасти = ВыплатаЗаработнойПлаты.Добавить();
		СтрокаТабличнойЧасти.ФизЛицо = ДанныеЗаполнения.ФизЛицо;
		СтрокаТабличнойЧасти.СуммаПлатежа = ДанныеЗаполнения.СуммаКВыплате;
	КонецЕсли;	
КонецПроцедуры

// Процедура заполнения документа на основании.
//
// Параметры:
//	ДанныеЗаполнения - ДокументСсылка.Командировка - Данные заполнения документа.
//	
Процедура ЗаполнитьДокументПоДокументуКомандировка(ДанныеЗаполнения) Экспорт
	ДокументОснование = ДанныеЗаполнения;
	
	// Шапка
	ВидОперации = Перечисления.ВидыОперацийРКО.ВыдачаПодотчетнику;

	// Реквизиты организации.
	Организация = ДанныеЗаполнения.Организация;
	СуммаДокумента = ДанныеЗаполнения.СуммаДокумента;
	
	Если ДанныеЗаполнения.Сотрудники.Количество() > 0 Тогда 
		ФизЛицо = ДанныеЗаполнения.Сотрудники[0].ФизЛицо;	
	КонецЕсли;	
	
	Для Каждого СтрокаКомандировка Из ДанныеЗаполнения.Сотрудники Цикл
		СтрокаТабличнойЧасти = РасшифровкаПлатежа.Добавить();
		СтрокаТабличнойЧасти.СуммаПлатежа = СтрокаКомандировка.СуммаВсего;	
		СтрокаТабличнойЧасти.СуммаВзаиморасчетов = СтрокаКомандировка.СуммаВсего;	
	КонецЦикла;
КонецПроцедуры

// Процедура заполнения документа на основании.
//
// Параметры:
//	ДанныеЗаполнения - ДокументСсылка.ПоступлениеТоваровУслуг - Данные заполнения документа.
//	
Процедура ЗаполнитьПоПоступлениюТоваровУслуг(ДанныеЗаполнения) Экспорт
	ДокументОснование = ДанныеЗаполнения;

	// Шапка
	ВидОперации = Перечисления.ВидыОперацийРКО.ОплатаПоставщику;			
	
	// Реквизиты организации.
	Организация = ДанныеЗаполнения.Организация;
	Если НЕ ДанныеЗаполнения.ВалютаДокумента = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета() Тогда
		Касса = ПолучитьКассу(ДанныеЗаполнения.ВалютаДокумента);
	Иначе 
		Касса = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьОсновнуюКассуОрганизации(Организация);
	КонецЕсли;
	ВалютаДокумента = Касса.ВалютаДенежныхСредств;
	
	ДатаДокумента = ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса());
	КурсСтруктура = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаДокумента, ДатаДокумента);
	Если ЗначениеЗаполнено(КурсСтруктура.Курс) Тогда
		Курс = КурсСтруктура.Курс;
		Кратность = КурсСтруктура.Кратность; 
	Иначе
		Курс = 1;
		Кратность = 1;
	КонецЕсли;		
	
	// Реквизиты контрагента.
	Контрагент 	= ДанныеЗаполнения.Контрагент;
	ДоговорКонтрагента = ДанныеЗаполнения.ДоговорКонтрагента;
	ВалютаРасчетов = ДоговорКонтрагента.ВалютаРасчетов;
	
	СчетаРасчетовСКонтрагентом = БухгалтерскийУчетСервер.ПолучитьСчетаРасчетовСКонтрагентом(Организация, Контрагент, ДоговорКонтрагента);
	СчетРасчетов = СчетаРасчетовСКонтрагентом.СчетАвансовПоставщика;
	
	СуммаДокумента = ДанныеЗаполнения.Товары.Итог("Всего") + ДанныеЗаполнения.Услуги.Итог("Всего") + ДанныеЗаполнения.ОС.Итог("Всего");

	// Расшифровка платежа.
	РасшифровкаПлатежа.Очистить();
	СтрокаТабличнойЧасти = РасшифровкаПлатежа.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ДанныеЗаполнения);
	
	Если ВалютаРасчетов = ВалютаДокумента Тогда 
		КурсВзаиморасчетов = Курс;
		КратностьВзаиморасчетов = Кратность;
		СтрокаТабличнойЧасти.СуммаПлатежа = СуммаДокумента;
		СтрокаТабличнойЧасти.СуммаВзаиморасчетов = СуммаДокумента;
	Иначе 
		ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
		КурсСтруктура = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаРасчетов, ДатаДокумента);

		ДанныеДокумента = Новый Структура();
		ДанныеДокумента.Вставить("Валюта", 		ВалютаДокумента);
		ДанныеДокумента.Вставить("Курс", 		Курс);
		ДанныеДокумента.Вставить("Кратность", 	Кратность);
		ДанныеДокумента.Вставить("ПрямойКурс", 	Ложь);
		
		ДанныВзаиморасчетов = Новый Структура("Валюта, Курс, Кратность", ВалютаРасчетов, КурсСтруктура.Курс, КурсСтруктура.Кратность);
		УчетДенежныхСредствКлиентСервер.УстановитьКурсыВзаиморасчетов(ЭтотОбъект, ДанныеДокумента, ДанныВзаиморасчетов);
			
		СтрокаТабличнойЧасти.СуммаПлатежа = ?(ВалютаДокумента = ВалютаРегламентированногоУчета,
			СуммаДокумента * КурсВзаиморасчетов / КратностьВзаиморасчетов,
			СуммаДокумента * КратностьВзаиморасчетов / КурсВзаиморасчетов);			
			
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуВзаиморасчетовТабличнойЧасти(
			ЭтотОбъект, "РасшифровкаПлатежа", ДанныеДокумента, ДанныВзаиморасчетов, ВалютаРегламентированногоУчета);
	КонецЕсли;
		
КонецПроцедуры

// Процедура заполнения документа на основании.
//
// Параметры:
//	ДанныеЗаполнения - ДокументСсылка.ДополнительныеРасходы - Данные заполнения документа.
//	
Процедура ЗаполнитьПоДополнительнымРасходам(ДанныеЗаполнения) Экспорт
	
	ДокументОснование = ДанныеЗаполнения;
	
	// Шапка
	ВидОперации = Перечисления.ВидыОперацийРКО.ОплатаПоставщику;			
	
	// Реквизиты организации.
	Организация = ДанныеЗаполнения.Организация;
	Касса = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьОсновнуюКассуОрганизации(Организация);
	ВалютаДокумента = Касса.ВалютаДенежныхСредств;

	ДатаДокумента = ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса());
	КурсСтруктура = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаДокумента, ДатаДокумента);
	Если ЗначениеЗаполнено(КурсСтруктура.Курс) Тогда
		Курс = КурсСтруктура.Курс;
		Кратность = КурсСтруктура.Кратность; 
	Иначе
		Курс = 1;
		Кратность = 1;
	КонецЕсли;		
	
	// Реквизиты контрагента.
	Контрагент 	= ДанныеЗаполнения.Контрагент;
	ДоговорКонтрагента = ДанныеЗаполнения.ДоговорКонтрагента;
	ВалютаРасчетов = ДоговорКонтрагента.ВалютаРасчетов;
	
	СчетаРасчетовСКонтрагентом = БухгалтерскийУчетСервер.ПолучитьСчетаРасчетовСКонтрагентом(Организация, Контрагент, ДоговорКонтрагента);
	СчетРасчетов = СчетаРасчетовСКонтрагентом.СчетАвансовПоставщика;
	
	СуммаДокумента = ДанныеЗаполнения.СуммаДопРасходов;

	// Расшифровка платежа.
	РасшифровкаПлатежа.Очистить();
	СтрокаТабличнойЧасти = РасшифровкаПлатежа.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ДанныеЗаполнения);
	
	Если ВалютаРасчетов = ВалютаДокумента Тогда 
		КурсВзаиморасчетов = Курс;
		КратностьВзаиморасчетов = Кратность;
		СтрокаТабличнойЧасти.СуммаПлатежа = СуммаДокумента;
		СтрокаТабличнойЧасти.СуммаВзаиморасчетов = СуммаДокумента;
	Иначе 
		ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
		КурсСтруктура = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаРасчетов, ДатаДокумента);

		ДанныеДокумента = Новый Структура();
		ДанныеДокумента.Вставить("Валюта", 		ВалютаДокумента);
		ДанныеДокумента.Вставить("Курс", 		Курс);
		ДанныеДокумента.Вставить("Кратность", 	Кратность);
		ДанныеДокумента.Вставить("ПрямойКурс", 	Ложь);
		
		ДанныВзаиморасчетов = Новый Структура("Валюта, Курс, Кратность", ВалютаРасчетов, КурсСтруктура.Курс, КурсСтруктура.Кратность);
		УчетДенежныхСредствКлиентСервер.УстановитьКурсыВзаиморасчетов(ЭтотОбъект, ДанныеДокумента, ДанныВзаиморасчетов);
			
		СтрокаТабличнойЧасти.СуммаПлатежа = ?(ВалютаДокумента = ВалютаРегламентированногоУчета,
			СуммаДокумента * КурсВзаиморасчетов / КратностьВзаиморасчетов,
			СуммаДокумента * КратностьВзаиморасчетов / КурсВзаиморасчетов);			
			
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуВзаиморасчетовТабличнойЧасти(
			ЭтотОбъект, "РасшифровкаПлатежа", ДанныеДокумента, ДанныВзаиморасчетов, ВалютаРегламентированногоУчета);
	КонецЕсли;
	
КонецПроцедуры

// Процедура заполнения документа на основании.
//
// Параметры:
//	ДанныеЗаполнения - ДокументСсылка.ВозвратТоваровОтПокупателя - Данные заполнения документа.
//	
Процедура ЗаполнитьПоВозвратуОтПокупателя(ДанныеЗаполнения) Экспорт
	
	ДокументОснование = ДанныеЗаполнения;
	
	// Шапка
	ВидОперации = Перечисления.ВидыОперацийРКО.ВозвратПокупателю;
	
	// Реквизиты организации.
	Организация = ДанныеЗаполнения.Организация;
	Касса = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьОсновнуюКассуОрганизации(Организация);
	ВалютаДокумента = Касса.ВалютаДенежныхСредств;

	ДатаДокумента = ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса());
	КурсСтруктура = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаДокумента, ДатаДокумента);
	Если ЗначениеЗаполнено(КурсСтруктура.Курс) Тогда
		Курс = КурсСтруктура.Курс;
		Кратность = КурсСтруктура.Кратность; 
	Иначе
		Курс = 1;
		Кратность = 1;
	КонецЕсли;		

	// Реквизиты контрагента.
	Контрагент 	= ДанныеЗаполнения.Контрагент;
	ДоговорКонтрагента = ДанныеЗаполнения.ДоговорКонтрагента;
	ВалютаРасчетов = ДоговорКонтрагента.ВалютаРасчетов;
	
	СчетаРасчетовСКонтрагентом = БухгалтерскийУчетСервер.ПолучитьСчетаРасчетовСКонтрагентом(Организация, Контрагент, ДоговорКонтрагента);
	СчетРасчетов = СчетаРасчетовСКонтрагентом.СчетРасчетовПокупателя;
	
	СуммаДокумента = ДанныеЗаполнения.СуммаДокумента;

	// Расшифровка платежа.
	РасшифровкаПлатежа.Очистить();
	СтрокаТабличнойЧасти = РасшифровкаПлатежа.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ДанныеЗаполнения);
	
	Если ВалютаРасчетов = ВалютаДокумента Тогда 
		КурсВзаиморасчетов = Курс;
		КратностьВзаиморасчетов = Кратность;
		СтрокаТабличнойЧасти.СуммаПлатежа = СуммаДокумента;
		СтрокаТабличнойЧасти.СуммаВзаиморасчетов = СуммаДокумента;
	Иначе 
		ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
		КурсСтруктура = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаРасчетов, ДатаДокумента);

		ДанныеДокумента = Новый Структура();
		ДанныеДокумента.Вставить("Валюта", 		ВалютаДокумента);
		ДанныеДокумента.Вставить("Курс", 		Курс);
		ДанныеДокумента.Вставить("Кратность", 	Кратность);
		ДанныеДокумента.Вставить("ПрямойКурс", 	Ложь);
		
		ДанныВзаиморасчетов = Новый Структура("Валюта, Курс, Кратность", ВалютаРасчетов, КурсСтруктура.Курс, КурсСтруктура.Кратность);
		УчетДенежныхСредствКлиентСервер.УстановитьКурсыВзаиморасчетов(ЭтотОбъект, ДанныеДокумента, ДанныВзаиморасчетов);
			
		СтрокаТабличнойЧасти.СуммаПлатежа = ?(ВалютаДокумента = ВалютаРегламентированногоУчета,
			СуммаДокумента * КурсВзаиморасчетов / КратностьВзаиморасчетов,
			СуммаДокумента * КратностьВзаиморасчетов / КурсВзаиморасчетов);			
			
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуВзаиморасчетовТабличнойЧасти(
			ЭтотОбъект, "РасшифровкаПлатежа", ДанныеДокумента, ДанныВзаиморасчетов, ВалютаРегламентированногоУчета);
	КонецЕсли;
	
КонецПроцедуры

// Процедура заполнения документа на основании.
//
// Параметры:
//	ДанныеЗаполнения - ДокументСсылка.ВозвратТоваровОтПокупателя - Данные заполнения документа.
//	
Процедура ЗаполнитьПоКорректировкеСтоимостиРеализации(ДанныеЗаполнения) Экспорт
	
	ДокументОснование = ДанныеЗаполнения;
	
	// Шапка
	ВидОперации = Перечисления.ВидыОперацийРКО.ВозвратПокупателю;
	
	// Реквизиты организации.
	Организация = ДанныеЗаполнения.Организация;
	Касса = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьОсновнуюКассуОрганизации(Организация);
	ВалютаДокумента = Касса.ВалютаДенежныхСредств;

	ДатаДокумента = ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса());
	КурсСтруктура = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаДокумента, ДатаДокумента);
	Если ЗначениеЗаполнено(КурсСтруктура.Курс) Тогда
		Курс = КурсСтруктура.Курс;
		Кратность = КурсСтруктура.Кратность; 
	Иначе
		Курс = 1;
		Кратность = 1;
	КонецЕсли;		

	// Реквизиты контрагента.
	Контрагент 	= ДанныеЗаполнения.Контрагент;
	ДоговорКонтрагента = ДанныеЗаполнения.ДоговорКонтрагента;
	ВалютаРасчетов = ДоговорКонтрагента.ВалютаРасчетов;
	
	СчетаРасчетовСКонтрагентом = БухгалтерскийУчетСервер.ПолучитьСчетаРасчетовСКонтрагентом(Организация, Контрагент, ДоговорКонтрагента);
	СчетРасчетов = СчетаРасчетовСКонтрагентом.СчетРасчетовПокупателя;
	
	Если НЕ ДанныеЗаполнения.БезналичныйРасчет Тогда
		ТабличныеЧасти = Новый Массив();
		ТабличныеЧасти.Добавить(ДанныеЗаполнения.Товары);
		ТабличныеЧасти.Добавить(ДанныеЗаполнения.Услуги);
		ТабличныеЧасти.Добавить(ДанныеЗаполнения.ОС);
		
		Для Каждого ТабличнаяЧасть Из ТабличныеЧасти Цикл
			
			Для Каждого СтрокаТабличнойЧасти Из ТабличнаяЧасть Цикл	
				
				Если СтрокаТабличнойЧасти.Всего < 0 Тогда
					СуммаДокумента = СуммаДокумента + СтрокаТабличнойЧасти.Всего;	
				КонецЕсли;	
			КонецЦикла;
		КонецЦикла;	
		
		СуммаДокумента = -СуммаДокумента;
	КонецЕсли;

	// Расшифровка платежа.
	РасшифровкаПлатежа.Очистить();
	СтрокаТабличнойЧасти = РасшифровкаПлатежа.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ДанныеЗаполнения);
	
	Если ВалютаРасчетов = ВалютаДокумента Тогда 
		КурсВзаиморасчетов = Курс;
		КратностьВзаиморасчетов = Кратность;
		СтрокаТабличнойЧасти.СуммаПлатежа = СуммаДокумента;
		СтрокаТабличнойЧасти.СуммаВзаиморасчетов = СуммаДокумента;
	Иначе 
		ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
		КурсСтруктура = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаРасчетов, ДатаДокумента);

		ДанныеДокумента = Новый Структура();
		ДанныеДокумента.Вставить("Валюта", 		ВалютаДокумента);
		ДанныеДокумента.Вставить("Курс", 		Курс);
		ДанныеДокумента.Вставить("Кратность", 	Кратность);
		ДанныеДокумента.Вставить("ПрямойКурс", 	Ложь);
		
		ДанныВзаиморасчетов = Новый Структура("Валюта, Курс, Кратность", ВалютаРасчетов, КурсСтруктура.Курс, КурсСтруктура.Кратность);
		УчетДенежныхСредствКлиентСервер.УстановитьКурсыВзаиморасчетов(ЭтотОбъект, ДанныеДокумента, ДанныВзаиморасчетов);
			
		СтрокаТабличнойЧасти.СуммаПлатежа = ?(ВалютаДокумента = ВалютаРегламентированногоУчета,
			СуммаДокумента * КурсВзаиморасчетов / КратностьВзаиморасчетов,
			СуммаДокумента * КратностьВзаиморасчетов / КурсВзаиморасчетов);			
			
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуВзаиморасчетовТабличнойЧасти(
			ЭтотОбъект, "РасшифровкаПлатежа", ДанныеДокумента, ДанныВзаиморасчетов, ВалютаРегламентированногоУчета);
	КонецЕсли;
	
КонецПроцедуры

// Процедура заполнения документа на основании.
//
// Параметры:
//	ДанныеЗаполнения - Структура - Данные заполнения документа.
//	
Процедура ЗаполнитьПоВедомостиДепонированнойЗарплаты(ДанныеЗаполнения) Экспорт

	ДокументОснование = ДанныеЗаполнения;
	ВидОперации = Перечисления.ВидыОперацийРКО.ВыплатаДепонированнойЗП;
	КурсВзаиморасчетов = 1;
	КратностьВзаиморасчетов = 1;
	
	// Заполнение шапки
	Организация = ДанныеЗаполнения.Организация;
	СтатьяДвиженияДенежныхСредств = ДанныеЗаполнения.СтатьяДвиженияДенежныхСредств;
	СчетРасчетов = ДанныеЗаполнения.СчетУчета;
	Выдать = НСтр("ru = 'На зарплату'");
	
	Если ДанныеЗаполнения.Зарплата.Количество() = 1 Тогда
		СведенияОФизЛице = БухгалтерскийУчетСервер.ПолучитьСведенияОФизЛице(Организация, ДанныеЗаполнения.Зарплата[0].ФизЛицо, ДанныеЗаполнения.Дата); 
		СтрокаПоДокументу = СтрШаблон(НСтр("ru = '%1 Серия %2 №%3 выдан %4 от %5'"),
		"паспорт",
		СведенияОФизЛице.ПаспортСерия,
		СведенияОФизЛице.ПаспортНомер,
		СведенияОФизЛице.ПаспортКемВыдан,
		Формат(СведенияОФизЛице.ПаспортДатаВыдачи, "ДЛФ=DD"));
		ПоДокументу = СтрокаПоДокументу;
	КонецЕсли;
		
	НазваниеДокумента = НСтр("ru = 'Ведомость депонированной зарплаты'");
	НомерНаПечать = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеЗаполнения.Номер);
	Основание = СтрШаблон(НСтр("ru = '%1 № %2 от %3'"),НазваниеДокумента, НомерНаПечать, Формат(ДанныеЗаполнения.Дата, "ДЛФ=DD"));
	
	ОбщаяСуммаДокумента = 0;
	// Заполнение Табличной части
	ВыплатаЗаработнойПлаты.Очистить();
	Для Каждого СтрокаТаблицы Из ДанныеЗаполнения.Зарплата Цикл 
		СтрокаТабличнойЧасти = ВыплатаЗаработнойПлаты.Добавить();
		СтрокаТабличнойЧасти.ФизЛицо = СтрокаТаблицы.ФизЛицо;
		СтрокаТабличнойЧасти.СуммаПлатежа = СтрокаТаблицы.Сумма;
		ОбщаяСуммаДокумента = ОбщаяСуммаДокумента + СтрокаТаблицы.Сумма;
	КонецЦикла;
	СуммаДокумента = ОбщаяСуммаДокумента;
КонецПроцедуры

// Процедура заполнения документа на основании.
//
// Параметры:
//	ДанныеЗаполнения - ДокументСсылка.ОтчетОРозничныхПродажах - Данные заполнения документа.
//	
Процедура ЗаполнитьПоОтчетОРозничныхПродажах(ДанныеЗаполнения) Экспорт
	ДокументОснование = ДанныеЗаполнения;
	
	ВидОперации = Перечисления.ВидыОперацийРКО.ВозвратРозничномуПокупателю;
	
	// Реквизиты организации.
	Организация = ДанныеЗаполнения.Организация;
	Касса = ДанныеЗаполнения.Касса;	
	ВалютаДокумента = Касса.ВалютаДенежныхСредств;
	
	// Реквизиты контрагента.
	Контрагент 	= ДанныеЗаполнения.Контрагент;
	ДоговорКонтрагента = ДанныеЗаполнения.ДоговорКонтрагента;
	ВалютаРасчетов = ДоговорКонтрагента.ВалютаРасчетов;
	
	СчетРасчетов = ДанныеЗаполнения.СчетРасчетовДт;
	
	ДатаДокумента = ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса());
	КурсСтруктура = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаДокумента, ДатаДокумента);
	Если ЗначениеЗаполнено(КурсСтруктура.Курс) Тогда
		Курс = КурсСтруктура.Курс;
		Кратность = КурсСтруктура.Кратность; 
	Иначе
		Курс = 1;
		Кратность = 1;
	КонецЕсли;		
	
	ИтогСуммаБезналичные = ДанныеЗаполнения.ВозвратОплаты.Итог("СуммаОплаты");
	СуммаДокумента = ДанныеЗаполнения.Возвраты.Итог("Всего")
		+ ДанныеЗаполнения.ВозвратУслуг.Итог("Всего")
		- ИтогСуммаБезналичные;
				
	// Рашифровка платежа.
	РасшифровкаПлатежа.Очистить();
	СтрокаТабличнойЧасти = РасшифровкаПлатежа.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ДанныеЗаполнения);
	
	Если ВалютаРасчетов = ВалютаДокумента Тогда 
		КурсВзаиморасчетов = Курс;
		КратностьВзаиморасчетов = Кратность;
		СтрокаТабличнойЧасти.СуммаПлатежа = СуммаДокумента;
		СтрокаТабличнойЧасти.СуммаВзаиморасчетов = СуммаДокумента;
		СтрокаТабличнойЧасти.СтатьяДвиженияДенежныхСредств = ДанныеЗаполнения.СтатьяДвиженияДенежныхСредств;
	Иначе 
		ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
		КурсСтруктура = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаРасчетов, ДатаДокумента);

		ДанныеДокумента = Новый Структура();
		ДанныеДокумента.Вставить("Валюта" , 	ВалютаДокумента);
		ДанныеДокумента.Вставить("Курс" , 		Курс);
		ДанныеДокумента.Вставить("Кратность" , 	Кратность);
		ДанныеДокумента.Вставить("ПрямойКурс" , Ложь);
		
		ДанныВзаиморасчетов = Новый Структура("Валюта, Курс, Кратность", ВалютаРасчетов, КурсСтруктура.Курс, КурсСтруктура.Кратность);
		УчетДенежныхСредствКлиентСервер.УстановитьКурсыВзаиморасчетов(ЭтотОбъект, ДанныеДокумента, ДанныВзаиморасчетов);
			
		СтрокаТабличнойЧасти.СуммаПлатежа = ?(ВалютаДокумента = ВалютаРегламентированногоУчета,
			СуммаДокумента * КурсВзаиморасчетов / КратностьВзаиморасчетов,
			СуммаДокумента * КратностьВзаиморасчетов / КурсВзаиморасчетов);			
			
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуВзаиморасчетовТабличнойЧасти(
			ЭтотОбъект, "РасшифровкаПлатежа", ДанныеДокумента, ДанныВзаиморасчетов, ВалютаРегламентированногоУчета);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

// Процедура - обработчик события ОбработкаЗаполнения объекта.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка) Экспорт
	
	СтратегияЗаполнения = Новый Соответствие;
	СтратегияЗаполнения[Тип("Структура")] = "ЗаполнитьПоСтруктуре";
	СтратегияЗаполнения[Тип("ДокументСсылка.ВедомостьЗаработнойПлаты")] = "ЗаполнитьПоВедомостиЗП";
	СтратегияЗаполнения[Тип("ДокументСсылка.ПоступлениеТоваровУслуг")] = "ЗаполнитьПоПоступлениюТоваровУслуг";
	СтратегияЗаполнения[Тип("ДокументСсылка.Командировка")] = "ЗаполнитьДокументПоДокументуКомандировка";
	СтратегияЗаполнения[Тип("ДокументСсылка.ДополнительныеРасходы")] = "ЗаполнитьПоДополнительнымРасходам";
	СтратегияЗаполнения[Тип("ДокументСсылка.ВозвратТоваровОтПокупателя")] = "ЗаполнитьПоВозвратуОтПокупателя";
	СтратегияЗаполнения[Тип("ДокументСсылка.КорректировкаСтоимостиРеализации")] = "ЗаполнитьПоКорректировкеСтоимостиРеализации";
	СтратегияЗаполнения[Тип("ДокументСсылка.ВедомостьДепонированнойЗарплаты")] = "ЗаполнитьПоВедомостиДепонированнойЗарплаты";
	СтратегияЗаполнения[Тип("ДокументСсылка.ОтчетОРозничныхПродажах")] = "ЗаполнитьПоОтчетОРозничныхПродажах";
	
	ЗаполнениеОбъектовБП.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения, СтратегияЗаполнения);

	Если ЗначениеЗаполнено(Касса) 
		И НЕ ЗначениеЗаполнено(ВалютаДокумента) Тогда
		ВалютаДокумента = Касса.ВалютаДенежныхСредств;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(КурсВзаиморасчетов)  
		ИЛИ НЕ ЗначениеЗаполнено(КратностьВзаиморасчетов) Тогда 
		
		КурсВзаиморасчетов = 1;
		КратностьВзаиморасчетов = 1;
	КонецЕсли;	
	          	
КонецПроцедуры

// Процедура - обработчик события ОбработкаПроверкиЗаполнения объекта.
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
		
	ПроверяемыеРеквизиты.Добавить("РасшифровкаПлатежа");
	ПроверяемыеРеквизиты.Добавить("Касса.Префикс");

	МассивНепроверяемыхРеквизитов = Новый Массив;	
	
	Если ВидОперации = Перечисления.ВидыОперацийРКО.ПрочийРасход Тогда 
		МассивНепроверяемыхРеквизитов.Добавить("Контрагент");
		МассивНепроверяемыхРеквизитов.Добавить("ДоговорКонтрагента");
		МассивНепроверяемыхРеквизитов.Добавить("ВалютаРасчетов");
		МассивНепроверяемыхРеквизитов.Добавить("СчетРасчетов");
		МассивНепроверяемыхРеквизитов.Добавить("БанковскийСчет");
		МассивНепроверяемыхРеквизитов.Добавить("ФизЛицо");
		МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа");
		МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа.СуммаПлатежа");
		МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа.СуммаВзаиморасчетов");
		
		ПроверяемыеРеквизиты.Добавить("ПрочиеПлатежи");
		
		// Контроль заполнения субконто Договор.
		ОписаниеТиповДоговор = Новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов");
		Для Каждого СтрокаТабличнойЧасти Из ПрочиеПлатежи Цикл
			
			СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаТабличнойЧасти.СчетРасчетов);
			Если СвойстваСчета.ВидСубконто1ТипЗначения = ОписаниеТиповДоговор
					И НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Субконто1)
				Или СвойстваСчета.ВидСубконто2ТипЗначения = ОписаниеТиповДоговор
					И НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Субконто2)	
				Или СвойстваСчета.ВидСубконто3ТипЗначения = ОписаниеТиповДоговор
					И НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Субконто3) Тогда 
					
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("КОЛОНКА",, НСтр("ru = 'Субконто'"), СтрокаТабличнойЧасти.НомерСтроки, "Прочие платежи");
				ПолеСообщения = СтрШаблон("Объект.ПрочиеПлатежи[%1].Субконто2", СтрокаТабличнойЧасти.НомерСтроки-1);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, ПолеСообщения,, Отказ);
			
			// Валюта документа и договора должны быть одинаковы.
			ИначеЕсли СвойстваСчета.ВидСубконто1ТипЗначения = ОписаниеТиповДоговор
					И НЕ СтрокаТабличнойЧасти.Субконто1.ВалютаРасчетов = ВалютаДокумента
				Или СвойстваСчета.ВидСубконто2ТипЗначения = ОписаниеТиповДоговор
					И НЕ СтрокаТабличнойЧасти.Субконто2.ВалютаРасчетов = ВалютаДокумента	
				Или СвойстваСчета.ВидСубконто3ТипЗначения = ОписаниеТиповДоговор
					И НЕ СтрокаТабличнойЧасти.Субконто3.ВалютаРасчетов = ВалютаДокумента Тогда 	
			
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("КОЛОНКА", 
					"КОРРЕКТНОСТЬ", 
					НСтр("ru = 'Субконто'"), 
					СтрокаТабличнойЧасти.НомерСтроки, 
					"Прочие платежи",
					НСтр("ru = 'Валюта договора должна совпадать с валютой документа.'"));
				ПолеСообщения = СтрШаблон("Объект.ПрочиеПлатежи[%1].Субконто2", СтрокаТабличнойЧасти.НомерСтроки-1);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, ПолеСообщения,, Отказ);		
			КонецЕсли;	
		КонецЦикла;
		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРКО.ОплатаПоставщику
		Или ВидОперации = Перечисления.ВидыОперацийРКО.ВозвратПокупателю
		Или ВидОперации = Перечисления.ВидыОперацийРКО.ВозвратРозничномуПокупателю
		Или ВидОперации = Перечисления.ВидыОперацийРКО.РасчетыПоЗаймам Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ФизЛицо");
		МассивНепроверяемыхРеквизитов.Добавить("БанковскийСчет");
		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРКО.ВыплатаЗП
		ИЛИ ВидОперации = Перечисления.ВидыОперацийРКО.ВыплатаДепонированнойЗП Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Контрагент");
		МассивНепроверяемыхРеквизитов.Добавить("ДоговорКонтрагента");
		МассивНепроверяемыхРеквизитов.Добавить("ВалютаРасчетов");
		МассивНепроверяемыхРеквизитов.Добавить("БанковскийСчет");
		МассивНепроверяемыхРеквизитов.Добавить("ФизЛицо");
		МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа");
		МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа.СуммаПлатежа");
		МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа.СуммаВзаиморасчетов");
		
		Если ВидОперации = Перечисления.ВидыОперацийРКО.ВыплатаЗП Тогда
			МассивНепроверяемыхРеквизитов.Добавить("СчетРасчетов");
		КонецЕсли;	

		ПроверяемыеРеквизиты.Добавить("ВыплатаЗаработнойПлаты");
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРКО.ВыдачаПодотчетнику Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Контрагент");
		МассивНепроверяемыхРеквизитов.Добавить("ДоговорКонтрагента");
		МассивНепроверяемыхРеквизитов.Добавить("ВалютаРасчетов");
		МассивНепроверяемыхРеквизитов.Добавить("БанковскийСчет");
		МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа");
		МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа.СуммаВзаиморасчетов");
		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРКО.ВзносВБанк Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Контрагент");
		МассивНепроверяемыхРеквизитов.Добавить("ДоговорКонтрагента");
		МассивНепроверяемыхРеквизитов.Добавить("ВалютаРасчетов");
		МассивНепроверяемыхРеквизитов.Добавить("СчетРасчетов");
		МассивНепроверяемыхРеквизитов.Добавить("ФизЛицо");
		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРКО.Инкассация Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Контрагент");
		МассивНепроверяемыхРеквизитов.Добавить("ДоговорКонтрагента");
		МассивНепроверяемыхРеквизитов.Добавить("ВалютаРасчетов");
		МассивНепроверяемыхРеквизитов.Добавить("СчетРасчетов");
		МассивНепроверяемыхРеквизитов.Добавить("БанковскийСчет");
		МассивНепроверяемыхРеквизитов.Добавить("ФизЛицо");
	КонецЕсли;
	
	// Вид деятельности.
	ВидыОперацийЕдиныйНалог = ОбщегоНазначенияБПСервер.ВидыОперацийЕдиныйНалог();
	Если ВидыОперацийЕдиныйНалог.Найти(ВидОперации) = Неопределено Тогда 
		МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа.ВидДеятельности");
	КонецЕсли;	
	
	ОбщегоНазначенияБПСервер.ДобавитьНепроверяемыеРеквизитыПоПараметрамФункциональныхОпций(МассивНепроверяемыхРеквизитов, Организация, Дата);
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
КонецПроцедуры

// Процедура - обработчик события ПередЗаписью объекта.
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ВидОперации = Перечисления.ВидыОперацийРКО.ПрочийРасход
		И НЕ (РасшифровкаПлатежа.Количество() = 0
			И ВыплатаЗаработнойПлаты.Количество() = 0) Тогда 
		РасшифровкаПлатежа.Очистить();
		ВыплатаЗаработнойПлаты.Очистить();
	ИначеЕсли (ВидОперации =  Перечисления.ВидыОперацийРКО.ВыплатаЗП
		ИЛИ ВидОперации =  Перечисления.ВидыОперацийРКО.ВыплатаДепонированнойЗП)
		И НЕ (РасшифровкаПлатежа.Количество() = 0
			И ПрочиеПлатежи.Количество() = 0) Тогда 
		РасшифровкаПлатежа.Очистить();
		ПрочиеПлатежи.Очистить();
	ИначеЕсли НЕ (ВидОперации = Перечисления.ВидыОперацийРКО.ПрочийРасход
			ИЛИ ВидОперации =  Перечисления.ВидыОперацийРКО.ВыплатаЗП
			ИЛИ ВидОперации =  Перечисления.ВидыОперацийРКО.ВыплатаДепонированнойЗП)
			И НЕ (ВыплатаЗаработнойПлаты.Количество() = 0
			И ПрочиеПлатежи.Количество() = 0) Тогда 
		ВыплатаЗаработнойПлаты.Очистить();
		ПрочиеПлатежи.Очистить();
	КонецЕсли;
	
	СуммаДокумента = РасшифровкаПлатежа.Итог("СуммаПлатежа") + ВыплатаЗаработнойПлаты.Итог("СуммаПлатежа") + ПрочиеПлатежи.Итог("СуммаПлатежа");
КонецПроцедуры

// Процедура - обработчик события ОбработкаПроведения объекта.
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ИнициализироватьДанные(Отказ, РежимПроведения);
	
	ОтразитьДвижения(Отказ, РежимПроведения);

	// Запись наборов записей.
	БухгалтерскийУчетСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
	
КонецПроцедуры // ОбработкаПроведения()

// Процедура - обработчик события ОбработкаУдаленияПроведения объекта.
//
Процедура ОбработкаУдаленияПроведения(Отказ)
	
	// Инициализация дополнительных свойств для проведения документа
	БухгалтерскийУчетСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	БухгалтерскийУчетСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Запись наборов записей
	БухгалтерскийУчетСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
КонецПроцедуры // ОбработкаУдаленияПроведения()

// Процедура - обработчик события ПриУстановкеНовогоНомера объекта.
//
Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	Если ЗначениеЗаполнено(Префикс) Тогда 
		Возврат;
	КонецЕсли;

	Если ПолучитьФункциональнуюОпцию("ИспользоватьПрефиксКассыИБанковскогоСчета") Тогда
		Префикс = ?(Касса.Префикс = "", "0", Касса.Префикс);
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункцииОбработкаПроведения

// Процедура инициализирует данные документа
// и подготавливает таблицы для движения в регистры
//
Процедура ИнициализироватьДанные(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа.
	БухгалтерскийУчетСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа.
	Документы.РасходныйКассовыйОрдер.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей.
	БухгалтерскийУчетСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
КонецПроцедуры
	
// Процедура заполняет регистры данными
//
Процедура ОтразитьДвижения(Отказ, РежимПроведения)

	// Отражение в разделах учета.
	БухгалтерскийУчетСервер.ОтразитьХозрасчетный(ДополнительныеСвойства, Движения, Отказ);
	УчетДенежныхСредств.СформироватьДвиженияОперационнаяКурсоваяРазница(ДополнительныеСвойства, Движения, Отказ, Ложь);
	БухгалтерскийУчетСервер.ОтразитьДанныеРеестраГТД(ДополнительныеСвойства, Движения, Отказ);
	БухгалтерскийУчетСервер.ОтразитьАвансыПодотчетника(ДополнительныеСвойства, Движения, Отказ);
	БухгалтерскийУчетСервер.ОтразитьВыплаченнаяЗарплата(ДополнительныеСвойства, Движения, Отказ);
	БухгалтерскийУчетСервер.ОтразитьОборотыПоДаннымЕдиногоНалога(ДополнительныеСвойства, Движения, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция - получение валютной кассы.
//
// Параметры:
//	Валюта - СправочникСсылка.Валюты - валюта документа основания.
//
// Возвращаемое значение:
//	Ссылка - СправочникСсылка.Кассы - найденная касса по валюте и организации или пустая ссылка.
//
Функция ПолучитьКассу(Валюта)

	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Кассы.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Кассы КАК Кассы
		|ГДЕ
		|	Кассы.Владелец = &Владелец
		|	И Кассы.ВалютаДенежныхСредств = &ВалютаДенежныхСредств";
	Запрос.УстановитьПараметр("Владелец", Организация);
	Запрос.УстановитьПараметр("ВалютаДенежныхСредств", Валюта);
	Результат = Запрос.Выполнить().Выбрать();
	
	Если Результат.Следующий() Тогда
		Возврат Результат.Ссылка;	
	Иначе	
		Возврат Справочники.Кассы.ПустаяСсылка();
	КонецЕсли;	

КонецФункции 

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли