#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьДоступныеСтилиПриложения();
	
	// Заполнение списка выбора. 
	Элементы.ИмяСтиляПриложения.СписокВыбора.Добавить("ПоУмолчанию", "<По умолчанию>");
	Для Каждого Стиль Из Метаданные.Стили Цикл
		Если СтрНайти(Стиль.Имя, "ЦветоваяСхема") = 0 Тогда
			Продолжить;
		КонецЕсли; 
		Элементы.ИмяСтиляПриложения.СписокВыбора.Добавить(Стиль.Имя, Стиль.Синоним);
	КонецЦикла; 	
	
	// Определение текущей схемы.
	ИмяСтиляПриложения = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(ВРег("ИмяСтиляПриложения"), "ЗначениеПоУмолчанию", "ПоУмолчанию");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьКартинкуСтиля();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИмяСтиляПриложенияПриИзменении(Элемент)
	УстановитьКартинкуСтиля();	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИПерезапустить(Команда)
	Если ЗаписатьДанные() Тогда
		Закрыть();
		ЗавершитьРаботуСистемы(Ложь, Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть()
	Если ЗаписатьДанные() Тогда
		Закрыть();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДоступныеСтилиПриложения()
	ДоступныеСтилиПриложения = Новый Структура;
	ДоступныеСтилиПриложения.Вставить("ПоУмолчанию", БиблиотекаКартинок.СтильПриложенияПоУмолчанию);
	ДоступныеСтилиПриложения.Вставить("ЦветоваяСхемаСтильПриложенияЗеленый", БиблиотекаКартинок.СтильПриложенияЗеленый);
	ДоступныеСтилиПриложения.Вставить("ЦветоваяСхемаСтильПриложенияСиний", БиблиотекаКартинок.СтильПриложенияСиний);
	ДоступныеСтилиПриложения.Вставить("ЦветоваяСхемаСтильПриложенияРозовый", БиблиотекаКартинок.СтильПриложенияРозовый);
	ДоступныеСтилиПриложения.Вставить("ЦветоваяСхемаСтильПриложенияТемный", БиблиотекаКартинок.СтильПриложенияТемный);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьКартинкуСтиля()
	Элементы.Предпросмотр.Картинка = ДоступныеСтилиПриложения[ИмяСтиляПриложения];
КонецПроцедуры // УстановитьКартинкуСтиля()

&НаСервере
Функция ЗаписатьДанные()
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(ВРег("ИмяСтиляПриложения"), "ЗначениеПоУмолчанию", ИмяСтиляПриложения);
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти
