
#Область ОбработчикиСобытийФормы

// Процедура - обработчик события ПриСозданииНаСервере.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СкрытьВыполненныеЗадачи = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("СкрытьВыполненныеЗадачи");
	ОбновитьСписокЗадачБухгалтера();
	ОбработатьЗначениеРеквизита_СкрытьВыполненныеЗадачи();
	
КонецПроцедуры

// Процедура - обработчик события ПриЗакрытии.
//
&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если НЕ ЗавершениеРаботы Тогда
		СохранитьНастройкиСервер();
		Оповестить("СписокЗадачБухгалтера_Изменение");	
	КонецЕсли;
КонецПроцедуры

// Процедура - обработчик события ОбработкаОповещения.
//
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьСписокЗадачБухгалтера" Тогда
		ОбновитьСписокЗадачБухгалтера();	
	КонецЕсли;	
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаЗадач

// Процедура - обработчик события ПриИзменении таблицы ТаблицаЗадач.
//
&НаКлиенте
Процедура ТаблицаЗадачПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.ТаблицаЗадач.ТекущиеДанные;
	
	СтруктураСтроки = Новый Структура();
	СтруктураСтроки.Вставить("Срок", 		СтрокаТабличнойЧасти.Срок);
	СтруктураСтроки.Вставить("Задача", 		СтрокаТабличнойЧасти.Задача);
	СтруктураСтроки.Вставить("ДатаПлана", 	СтрокаТабличнойЧасти.ДатаПлана);
	СтруктураСтроки.Вставить("Выполнено", 	СтрокаТабличнойЧасти.Выполнено);
	СтруктураСтроки.Вставить("Комментарий", СтрокаТабличнойЧасти.Комментарий);
	
	ТаблицаЗадачПриИзмененииНаСервере(СтруктураСтроки);	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Процедура - обработчик события ПриИзменении реквизита СкрытьВыполненныеЗадачи.
//
&НаКлиенте
Процедура СкрытьВыполненныеЗадачиПриИзменении(Элемент)
	
	ОбработатьЗначениеРеквизита_СкрытьВыполненныеЗадачи();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбработатьЗначениеРеквизита_СкрытьВыполненныеЗадачи()

	Если СкрытьВыполненныеЗадачи Тогда
		Элементы.ТаблицаЗадач.ОтборСтрок = Новый ФиксированнаяСтруктура(Новый Структура("Выполнено", Ложь));
	Иначе
		Элементы.ТаблицаЗадач.ОтборСтрок = Неопределено;
	КонецЕсли;
	
	Элементы.СкрытьВыполненныеЗадачи.Заголовок = ?(СкрытьВыполненныеЗадачи, 
													НСтр("ru = 'Показать все'"),
													НСтр("ru = 'Скрыть выполненные'"));	

КонецПроцедуры

// Процедура заполнения реквизита формы ТаблицаЗадач.
//
&НаСервере
Процедура ОбновитьСписокЗадачБухгалтера()
	ТаблицаЗадач.Загрузить(ОбщегоНазначенияБПСервер.ЗадачиБухгалтера(Истина));	
КонецПроцедуры

// Процедура обновления/создания записей РС "ЗадачиБухгалтера".
//
&НаСервере
Процедура ТаблицаЗадачПриИзмененииНаСервере(СтруктураСтроки)
		
	НаборЗаписей = РегистрыСведений.СписокЗадачБухгалтера.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Срок.Установить(СтруктураСтроки.Срок);
	НаборЗаписей.Отбор.Задача.Установить(СтруктураСтроки.Задача);
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() = 0 Тогда
		Запись = НаборЗаписей.Добавить();
	ИначеЕсли НаборЗаписей.Количество() = 1 Тогда
		Запись = НаборЗаписей[0];
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Запись, СтруктураСтроки);
	
	НаборЗаписей.Записать();
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиСервер()

	ОбщегоНазначенияБПВызовСервера.УстановитьЗначениеПоУмолчанию("СкрытьВыполненныеЗадачи", СкрытьВыполненныеЗадачи);	

КонецПроцедуры

#КонецОбласти
