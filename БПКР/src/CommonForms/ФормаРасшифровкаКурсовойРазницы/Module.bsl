
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗаполнитьПоляДляРасчетаОперационнойКурсовойРазницы(Параметры);
	УстановитьВидимостьДоступностьЭлементов();	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
// Процедура - обработчик события нажатия кнопки ОК.
//
Процедура КомандаОК(Команда)
	Закрыть();	
КонецПроцедуры // КомандаОК()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимостьДоступностьЭлементов()
	ИмяРеглВалюты = ВалютаРегламентированногоУчета.Наименование;
	
	ЭтаФорма.Элементы.СуммаДтСом.Заголовок = СтрШаблон(НСтр("ru = 'Сумма Дт (%1)'"), ИмяРеглВалюты);
	ЭтаФорма.Элементы.СуммаКтСом.Заголовок = СтрШаблон(НСтр("ru = 'Сумма Кт (%1)'"), ИмяРеглВалюты);
КонецПроцедуры 

&НаСервере
Процедура ЗаполнитьПоляДляРасчетаОперационнойКурсовойРазницы(Данные) 
	ВалютаРегламентированногоУчета = Данные.ВалютаРегламентированногоУчета; 
	
	СчетУчетаДоходыОКР = ПланыСчетов.Хозрасчетный.ДоходыОтОперационныхКурсовыхРазниц;
	СчетУчетаУбыткиОКР = ПланыСчетов.Хозрасчетный.УбыткиОтОперационныхКурсовыхРазниц;
	
	ДанныеРасчета = УчетДенежныхСредств.ДанныеРасчетаСуммыКурсовойРазницы(Данные, Данные.ЭтоПриходДенежныхСредств);
	СуммаКурсовойРазницы 	= ДанныеРасчета.СуммаКурсовойРазницы;
	Доход 					= ДанныеРасчета.Доход;
	ДенежныйСчетВалютный 	= ДанныеРасчета.ДенежныйСчетВалютный;
	ОбаСчетаВалютные 		= ДанныеРасчета.ОбаСчетаВалютные;
		
	Если Доход Тогда
		СчетКт = СчетУчетаДоходыОКР;		
		
		Если ОбаСчетаВалютные ИЛИ ДенежныйСчетВалютный Тогда
			СчетДт = Данные.ДенежныйСчет;
			ВалютаДт = Данные.ВалютаДокумента;
			ВалютаКт = Данные.ВалютаРасчетов;
		Иначе
			СчетДт = Данные.СчетРасчетов;
			ВалютаДт = Данные.ВалютаРасчетов;
			ВалютаКт = Данные.ВалютаДокумента;
		КонецЕсли;
			
		ВалютаДтСом = ВалютаДт = Данные.ВалютаРегламентированногоУчета;
		ВалютаКтСом = ВалютаКт = Данные.ВалютаРегламентированногоУчета;
		
		Если ОбаСчетаВалютные ИЛИ ДенежныйСчетВалютный Тогда
			СуммаДт = Данные.СуммаПлатежа;		
			СуммаКт = Данные.СуммаВзаиморасчетов;
		 	СуммаДтСом = Данные.СуммаПлатежа * ?(ВалютаДтСом, 1, Данные.КурсДокумента / Данные.КратностьДокумента);
			СуммаКтСом = Данные.СуммаВзаиморасчетов * ?(ВалютаКтСом, 1, Данные.КурсВзаиморасчетовПоНацБанку / Данные.КратностьВзаиморасчетовПоНацБанку);
			КурсДт = ?(ВалютаДтСом, 1, Данные.КурсДокумента);
			КурсКт = ?(ВалютаКтСом, 1, Данные.КурсВзаиморасчетовПоНацБанку);
			КратностьДт = ?(ВалютаДтСом, 1, Данные.КратностьДокумента);
			КратностьКт = ?(ВалютаКтСом, 1, Данные.КратностьВзаиморасчетовПоНацБанку);
		Иначе
			СуммаДт = Данные.СуммаВзаиморасчетов;		
			СуммаКт = Данные.СуммаПлатежа;
		 	СуммаДтСом = Данные.СуммаВзаиморасчетов * Данные.КурсВзаиморасчетовПоНацБанку / Данные.КратностьВзаиморасчетовПоНацБанку;
			СуммаКтСом = Данные.СуммаПлатежа * Данные.КурсДокумента / Данные.КратностьДокумента;
			КурсДт = Данные.КурсВзаиморасчетовПоНацБанку;
			КурсКт = Данные.КурсДокумента;
			КратностьДт = Данные.КратностьВзаиморасчетовПоНацБанку;
			КратностьКт = Данные.КратностьДокумента;
		КонецЕсли;		
				
		Содержание = НСтр("ru = 'Доходы от операционных курсовых разниц'");
					
	Иначе 
		СчетДт = СчетУчетаУбыткиОКР;
		
		Если ОбаСчетаВалютные ИЛИ ДенежныйСчетВалютный Тогда
			СчетКт = Данные.ДенежныйСчет;
			ВалютаДт = Данные.ВалютаРасчетов;
			ВалютаКт = Данные.ВалютаДокумента;
		Иначе
			СчетКт = Данные.СчетРасчетов;
			ВалютаДт = Данные.ВалютаДокумента;
			ВалютаКт = Данные.ВалютаРасчетов;
		КонецЕсли;
		
		ВалютаДтСом = ВалютаДт = Данные.ВалютаРегламентированногоУчета;
		ВалютаКтСом = ВалютаКт = Данные.ВалютаРегламентированногоУчета;
		
		Если ОбаСчетаВалютные ИЛИ ДенежныйСчетВалютный Тогда
			СуммаДт = Данные.СуммаВзаиморасчетов;		
			СуммаКт = Данные.СуммаПлатежа;
		 	СуммаДтСом = Данные.СуммаВзаиморасчетов * ?(ВалютаДтСом, 1, Данные.КурсВзаиморасчетовПоНацБанку / Данные.КратностьВзаиморасчетовПоНацБанку);
			СуммаКтСом = Данные.СуммаПлатежа * ?(ВалютаКтСом, 1, Данные.КурсДокумента / Данные.КратностьДокумента);
			КурсДт = ?(ВалютаДтСом, 1, Данные.КурсВзаиморасчетовПоНацБанку);
			КурсКт = ?(ВалютаКтСом, 1, Данные.КурсДокумента);
			КратностьДт = ?(ВалютаДтСом, 1, Данные.КратностьВзаиморасчетовПоНацБанку);
			КратностьКт = ?(ВалютаКтСом, 1, Данные.КратностьДокумента);
		Иначе			
			СуммаДт = Данные.СуммаПлатежа;		
			СуммаКт = Данные.СуммаВзаиморасчетов;
		 	СуммаДтСом = Данные.СуммаПлатежа * Данные.КурсДокумента / Данные.КратностьДокумента;
			СуммаКтСом = Данные.СуммаВзаиморасчетов * Данные.КурсВзаиморасчетовПоНацБанку / Данные.КратностьВзаиморасчетовПоНацБанку;
			КурсДт = Данные.КурсДокумента;
			КурсКт = Данные.КурсВзаиморасчетовПоНацБанку;
			КратностьДт = Данные.КратностьДокумента;
			КратностьКт = Данные.КратностьВзаиморасчетовПоНацБанку;			
		КонецЕсли;		
		
		Содержание = НСтр("ru = 'Убытки от операционных курсовых разниц'");
	
	КонецЕсли;
	
	Сумма = ?(СуммаКурсовойРазницы > 0, СуммаКурсовойРазницы, СуммаКурсовойРазницы * (-1));;
		
КонецПроцедуры

#КонецОбласти
