
#Область ОбработчикиСобытийФормы

// Процедура - обработчик события ПриСозданииНаСервере.
// В процедуре осуществляется
// - инициализация реквизитов формы,
// - установка параметров функциональных опций формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДанныеОМестеРождения = ОбщегоНазначенияБПКлиентСервер.РазложитьМестоРождения(Параметры.Представление, Ложь);
	
	Город 		        = ДанныеОМестеРождения.НаселенныйПункт;
	Район               = ДанныеОМестеРождения.Район;
	Область             = ДанныеОМестеРождения.Область;
	Страна              = ДанныеОМестеРождения.Страна;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность Тогда 
		ТекстПредупреждения = НСтр("ru = 'Данные были изменены, внесенные изменения будут отменены.
										 |Отменить и закрыть?'");
		ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияПроизвольнойФормы(ЭтотОбъект, Отказ, ЗавершениеРаботы, ТекстПредупреждения, "ДействиеВыбрано");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	ДействиеВыбрано = Истина;
	Закрыть(ПолучитьМестоРожденияСтрокой());
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	ДействиеВыбрано = Истина;
	Закрыть(Неопределено);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПолучитьМестоРожденияСтрокой()
	Возврат	""+СокрЛП(Город)+","+СокрЛП(Район)+","+СокрЛП(Область)+","+СокрЛП(Страна);
КонецФункции	



#КонецОбласти
