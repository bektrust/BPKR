
#Область ОбработчикиСобытийФормы

// Процедура - обработчик события ПриСозданииНаСервере.
// В процедуре осуществляется
// - инициализация реквизитов формы,
// - установка параметров функциональных опций формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СправочникСсылкаОтчеты = Справочники.ИдентификаторыОбъектовМетаданных.НайтиПоНаименованию("Отчеты",Истина,ПредопределенноеЗначение("Справочник.ИдентификаторыОбъектовМетаданных.ПустаяСсылка"));
	
	Выборка = Справочники.ИдентификаторыОбъектовМетаданных.Выбрать(СправочникСсылкаОтчеты);
	
	Пока Выборка.Следующий() Цикл
		ВидыОбъектовОтчетов.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Если Параметры.Отбор.Свойство("ОбъектОтчет") Тогда
		НайденноеЗначение	= ВидыОбъектовОтчетов.НайтиПоЗначению(Параметры.Отбор.ОбъектОтчет);
		Если НЕ НайденноеЗначение = Неопределено Тогда
			Элементы.ВидыОбъектовОтчетов.ТекущаяСтрока	= НайденноеЗначение.ПолучитьИдентификатор();
		КонецЕсли;
		Параметры.Отбор.Удалить("ОбъектОтчет");
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицФормы

&НаКлиенте
Процедура ВидыОбъектовОтчетовПриАктивизацииСтроки(Элемент)
	ОбъектОтчет = Элемент.ТекущиеДанные.Значение;
	
	РаботаСОтборамиКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "ОбъектОтчет", ОбъектОтчет, Истина);
	
	// Восстановление настройки отчета
	НастройкиОтчета = ПолучитьНастройкиОтчета(ОбъектОтчет);
	ВыводитьМестоДляШтампа = НастройкиОтчета.ВыводитьМестоДляШтампа;	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода ВыводитьМестоДляШтампа.
//
&НаКлиенте
Процедура ВыводитьМестоДляШтампаПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ВидыОбъектовОтчетов.ТекущиеДанные;
	
	// не удалось получить- возвращаемся
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ОбъектОтчет = ТекущиеДанные.Значение;

	НастройкиОтчета = Новый Структура;
	НастройкиОтчета.Вставить("ВыводитьМестоДляШтампа", ВыводитьМестоДляШтампа);

	ВыводитьМестоДляШтампаПриИзмененииНаСервере(ОбъектОтчет, НастройкиОтчета);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура ВыводитьМестоДляШтампаПриИзмененииНаСервере(ОбъектОтчет, НастройкиОтчета)
	НаборЗаписей = РегистрыСведений.НастройкиОтчетов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ОбъектОтчет.Установить(ОбъектОтчет);
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();
	
	Если НастройкиОтчета.ВыводитьМестоДляШтампа Тогда 
		НастройкаОтчета = НаборЗаписей.Добавить();
		НастройкаОтчета.ОбъектОтчет = ОбъектОтчет;
		НастройкаОтчета.ВыводитьМестоДляШтампа = Истина;		
	КонецЕсли;	
	
	НаборЗаписей.Записать();
	
	ОбновитьПовторноИспользуемыеЗначения();
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьНастройкиОтчета(ОбъектОтчет)
	Возврат БухгалтерскиеОтчетыПовтИсп.ПолучитьНастройкиОтчета(ОбъектОтчет);
КонецФункции 

#КонецОбласти
