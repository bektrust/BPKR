#Область ОбработчикиСобытийФормы

// Процедура - обработчик события ПриСозданииНаСервере.
// В процедуре осуществляется
// - инициализация реквизитов формы,
// - установка параметров функциональных опций формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Это старая запись.
	Если ЗначениеЗаполнено(Параметры.Ключ.Организация)
		Или ЗначениеЗаполнено(Параметры.Ключ.Период) Тогда 
		ТолькоПросмотр = Истина;
	КонецЕсли;	
	
	УстановитьПараметрыВыбора();

	// Установить видимость и доступность элементов формы
	УстановитьВидимостьДоступностьЭлементов();
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Процедура - обработчик события ПриИзменении поля ввода ПределыПоПодразделениям.
//
&НаКлиенте
Процедура ПределыПоПодразделениямПриИзменении(Элемент)
	// Установить видимость и доступность элементов формы
	УстановитьВидимостьДоступностьЭлементов();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВключитьВозможностьРедактирования(Команда)
	
	ТолькоПросмотр = Ложь;
	Элементы.ФормаВключитьВозможностьРедактирования.Доступность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписокПределов(Команда)
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Отбор", Новый Структура("Организация", Запись.Организация));
	ОткрытьФорму("РегистрСведений.ПределыПоПодразделениям.ФормаСписка", ПараметрыФормы);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура - Установить параметры выбора
//
&НаСервере
Процедура УстановитьПараметрыВыбора()
	
	// Выбираются все счета группы 3400, за исключением 3420.	
	СчетаУчета = БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.НалогиКОплате);

	ИсключенныеСчета = Новый Массив;
	ИсключенныеСчета.Добавить(ПланыСчетов.Хозрасчетный.НалогиКОплате); // 3400
	ИсключенныеСчета.Добавить(ПланыСчетов.Хозрасчетный.ПодоходныйНалогНаДоходыСотрудников); // 3420
	СчетаУчета = ОбщегоНазначенияКлиентСервер.РазностьМассивов(СчетаУчета, ИсключенныеСчета); 
	
	// Установка параметров выбора.
	ФиксированныйМассивСчетов = Новый ФиксированныйМассив(СчетаУчета);
	НовыйПараметр = Новый ПараметрВыбора("Отбор.Ссылка", ФиксированныйМассивСчетов);
	НовыйМассив = Новый Массив();
	НовыйМассив.Добавить(НовыйПараметр);
	НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
	Элементы.СчетУчетаПоПНДляПрочихФизическихЛиц.ПараметрыВыбора = НовыеПараметры;
	
КонецПроцедуры // УстановитьПараметрыВыбора()

// Процедура устанавливает видимость и доступность элементов.
//
&НаСервере
Процедура УстановитьВидимостьДоступностьЭлементов()
	Элементы.ОткрытьСписокПределов.Видимость = Запись.ПределыПоПодразделениям;
КонецПроцедуры 

#КонецОбласти
