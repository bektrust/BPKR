#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция возвращает ключ записи регистра.
//
Функция ПолучитьКлючЗаписи(СтруктураПараметров) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СоответствияНоменклатурыЭСФ.Номенклатура КАК Номенклатура
		|ИЗ
		|	РегистрСведений.СоответствияНоменклатурыЭСФ КАК СоответствияНоменклатурыЭСФ
		|ГДЕ
		|	СоответствияНоменклатурыЭСФ.Контрагент = &Контрагент
		|	И СоответствияНоменклатурыЭСФ.НоменклатураНаименованиеПолное = &НоменклатураНаименованиеПолное";
	
	Запрос.УстановитьПараметр("Контрагент", СтруктураПараметров.Контрагент);
	Запрос.УстановитьПараметр("НоменклатураНаименованиеПолное", СтруктураПараметров.НоменклатураНаименованиеПолное);
	
	СтруктураВозврата = Новый Структура("ЗаписьСуществует, Контрагент, НоменклатураНаименованиеПолное", Ложь);
	ЗаполнитьЗначенияСвойств(СтруктураВозврата, СтруктураПараметров);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда 
		СтруктураВозврата.ЗаписьСуществует = Истина;
	КонецЕсли;	

	Возврат СтруктураВозврата;

КонецФункции // ПолучитьКлючЗаписи()

// Функция возвращает соответствие номенклатуры.
//
Функция ПолучитьСоответствиеНоменклатуры(СтруктураПараметров) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СоответствияНоменклатурыЭСФ.Номенклатура КАК Номенклатура
		|ИЗ
		|	РегистрСведений.СоответствияНоменклатурыЭСФ КАК СоответствияНоменклатурыЭСФ
		|ГДЕ
		|	СоответствияНоменклатурыЭСФ.Контрагент = &Контрагент
		|	И СоответствияНоменклатурыЭСФ.НоменклатураНаименованиеПолное = &НоменклатураНаименованиеПолное";
	
	Запрос.УстановитьПараметр("Контрагент", СтруктураПараметров.Контрагент);
	Запрос.УстановитьПараметр("НоменклатураНаименованиеПолное", СтруктураПараметров.НоменклатураНаименованиеПолное);
	
	СтруктураВозврата = Новый Структура("Номенклатура, Контрагент, НоменклатураНаименованиеПолное", Справочники.Номенклатура.ПустаяСсылка());
	ЗаполнитьЗначенияСвойств(СтруктураВозврата, СтруктураПараметров);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда 
		СтруктураВозврата.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;	
	КонецЕсли;
	
	Возврат СтруктураВозврата;

КонецФункции // ПолучитьСоответствиеНоменклатуры()

#КонецОбласти

#КонецЕсли