#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Процедура - Заполнить по умолчанию
// Выполняет первоночальное заполнение
Процедура ЗаполнитьПоУмолчанию() Экспорт 
	СправочникМенеджер = Справочники.СтавкиНДС;
	
	КлассификаторXML = РегистрыСведений.СтавкиНДС.ПолучитьМакет("МакетЗаполнения").ПолучитьТекст();
	КлассификаторТаблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;
	
	НаборЗаписей = РегистрыСведений.СтавкиНДС.СоздатьНаборЗаписей();
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();                 
	
	Для Каждого СтрокаТаблицыЗначений Из КлассификаторТаблица Цикл
		Период = Дата(СокрЛП(СтрокаТаблицыЗначений.Период));
		Ставка = Число(СокрЛП(СтрокаТаблицыЗначений.Ставка));
		СтавкаНДС = СправочникМенеджер[СокрЛП(СтрокаТаблицыЗначений.СтавкаНДС)];
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Период = Период;
		НоваяЗапись.СтавкаНДС = СтавкаНДС;
		НоваяЗапись.Ставка = Ставка;
	КонецЦикла;
	
	Попытка
		НаборЗаписей.Записать();
	Исключение
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось завершить запись ставок НДС по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка'"), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
	КонецПопытки;
КонецПроцедуры // ЗаполнитьПоУмолчанию()	

#КонецОбласти

#КонецЕсли