
#Область ОбработчикиСобытийФормы

// Процедура - обработчик события ПриСозданииНаСервере.
// В процедуре осуществляется
// - инициализация реквизитов формы,
// - установка параметров функциональных опций формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Это старая запись.
	Если ЗначениеЗаполнено(Параметры.Ключ.Организация)
		Или ЗначениеЗаполнено(Параметры.Ключ.Период) Тогда 
		ТолькоПросмотр = Истина;
	Иначе 
		//Запись.СпособОценкиТМЗ = Перечисления.СпособыОценки.ПоСредней;
	КонецЕсли;
	
	// Отображение метода расчета ЕН.
	Если Запись.ПлательщикЕН Тогда 
		МетодРасчетаЕН = ?(Запись.ПлательщикЕНМетодНачисления, 1, 0);
	КонецЕсли;	
	
	// Установить видимость и доступность элементов формы
	УстановитьВидимостьДоступностьЭлементов();	

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Запись.НеПлательщикНДС = НЕ Запись.ПлательщикНДС;
	
	Запись.ПлательщикЕНКассовыйМетод = ?(Запись.ПлательщикЕН И МетодРасчетаЕН = 0, Истина, Ложь);
	Запись.ПлательщикЕНМетодНачисления = ?(Запись.ПлательщикЕН И МетодРасчетаЕН = 1, Истина, Ложь);
	
	// Добавление признака Модифицированность для обновления интерфейса после записи.
	ПараметрыЗаписи.Вставить("Модифицированность", Модифицированность);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Если ПараметрыЗаписи.Модифицированность Тогда 
		ОбновитьИнтерфейс();	
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Процедура - обработчик события ПриИзменении поля ввода ПлательщикНДС.
//
&НаКлиенте
Процедура ПлательщикНДСПриИзменении(Элемент)
	ПриИзмененииНалоговогоРежима();
	
	// Установить видимость и доступность элементов формы
	УстановитьВидимостьДоступностьЭлементов();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода ПлательщикНСП.
//
&НаКлиенте
Процедура ПлательщикНСППриИзменении(Элемент)
	
	Если Запись.ПлательщикНСП Тогда
		Запись.ПлательщикЕН = Ложь;
	КонецЕсли;	
	
	ПриИзмененииНалоговогоРежима();
	
	// Установить видимость и доступность элементов формы
	УстановитьВидимостьДоступностьЭлементов();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода ПлательщикНП.
//
&НаКлиенте
Процедура ПлательщикНППриИзменении(Элемент)
	Если Запись.ПлательщикНП Тогда
		Запись.ПлательщикЕН = Ложь;
	КонецЕсли;	
	
	ПриИзмененииНалоговогоРежима();
	
	// Установить видимость и доступность элементов формы
	УстановитьВидимостьДоступностьЭлементов();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода ПлательщикЗН.
//
&НаКлиенте
Процедура ПлательщикЗНПриИзменении(Элемент)
	// Установить видимость и доступность элементов формы
	УстановитьВидимостьДоступностьЭлементов();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода ПлательщикЕН.
//
&НаКлиенте
Процедура ПлательщикЕНПриИзменении(Элемент)
	
	// Установка значений по умолчанию
	Если Запись.ПлательщикЕН Тогда
		Запись.ПлательщикНСП = Ложь;
		Запись.ПлательщикНП = Ложь;
	КонецЕсли;
	
	ПриИзмененииНалоговогоРежима();

	// Установить видимость и доступность элементов формы
	УстановитьВидимостьДоступностьЭлементов();
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Период.
//
&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	УстановитьВидимостьДоступностьЭлементов();
	
	Если Запись.Период >= Дата(2022, 01, 01) Тогда
		Запись.Контракт = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВключитьВозможностьРедактирования(Команда)
	
	ТолькоПросмотр = Ложь;
	Элементы.ФормаВключитьВозможностьРедактирования.Доступность = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура устанавливает видимость и доступность элементов.
//
&НаСервере
Процедура УстановитьВидимостьДоступностьЭлементов()
	
	Элементы.УчетНДСНаАвансы.Видимость = Запись.ПлательщикНДС;
	Элементы.УказыватьПризнакЗачетаНДСПриПоступлении.Видимость = Запись.ПлательщикНДС;
	Элементы.СтавкаНСПДляРасчетаНДСНаАвансы.Видимость = Запись.ПлательщикНДС;
	Элементы.НеУчитыватьЗакупкиБезНДС.Видимость = Запись.ПлательщикНДС;
	
	Элементы.СтавкаНалогаНаПрибыль.Видимость = Запись.ПлательщикНП;
	Элементы.ПроцентНП.Видимость = Запись.ПлательщикНП;
	
	Элементы.ПороговыйПроцентОсвобожденныхПоставок.Видимость = Запись.ПлательщикНДС;
	Элементы.ПроцентОП.Видимость = Запись.ПлательщикНДС;
	
	Элементы.Контракт.Видимость = Запись.ПлательщикНДС;
	
	Элементы.СтавкаНСПДляРасчетаНДСНаАвансы.Видимость = Запись.ПлательщикНСП;
	Элементы.СтавкаНСПРеализацииТовары.Видимость = Запись.ПлательщикНСП;
	Элементы.СтавкаНСПРеализацииУслуги.Видимость = Запись.ПлательщикНСП;
	
	// Единый налог.
	Элементы.МетодРасчетаЕН.Видимость = Запись.ПлательщикЕН;
	
	// Земельный налог.
	Элементы.СчетУчетаЗН.Видимость = Запись.ПлательщикЗН;
	Элементы.СчетРасходовЗН.Видимость = Запись.ПлательщикЗН;
	Элементы.КоэффициентИнфляции.Видимость = Запись.ПлательщикЗН;
	
	Если Запись.Период >= Дата(2022, 01, 01) Тогда
		Элементы.Контракт.Видимость = Ложь;
	КонецЕсли;	
	
КонецПроцедуры 

&НаКлиенте
Процедура ПриИзмененииНалоговогоРежима()

	// НДС.
	Если НЕ Запись.ПлательщикНДС Тогда 
		Запись.Контракт = Ложь;
		Запись.УчетНДСНаАвансы = Ложь;
		Запись.УказыватьПризнакЗачетаНДСПриПоступлении = Ложь;
		Запись.НеУчитыватьЗакупкиБезНДС = Ложь;
		Запись.ОтчетПоНДСПоПоставке = Ложь;
		Запись.ПороговыйПроцентОсвобожденныхПоставок = 0;
	КонецЕсли;	
	
	// НСП.
	Если НЕ Запись.ПлательщикНСП Тогда 
		Запись.СтавкаНСПРеализацииТовары = Неопределено;
		Запись.СтавкаНСПРеализацииУслуги = Неопределено;
		Запись.СтавкаНСПДляРасчетаНДСНаАвансы = Неопределено;
	КонецЕсли;	
	
	// НП.
	Если НЕ Запись.ПлательщикНП Тогда 
		Запись.СтавкаНалогаНаПрибыль = 0;
	КонецЕсли;

КонецПроцедуры // ПриИзмененииНалоговогоРежима()

#КонецОбласти        
