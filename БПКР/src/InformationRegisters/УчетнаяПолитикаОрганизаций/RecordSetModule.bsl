#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события ОбработкаЗаполнения набора записей.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	Справочники.Организации.ДополнитьДанныеЗаполненияПриОднофирменномУчете(ДанныеЗаполнения);
КонецПроцедуры

// Процедура - обработчик события ПриЗаписи набора записей.
//
Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда            
		Возврат;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;		
	КонецЕсли;
	
	Если ВыгрузитьКолонку("СпособОценкиТМЗ").Найти(Перечисления.СпособыОценки.ФИФО) <> Неопределено Тогда
		ВключитьПартионныйУчет(Отказ);
	КонецЕсли;
	
	ОбновитьПовторноИспользуемыеЗначения();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура отвечает за включение партионного учета на плане счетов
//
Процедура ВключитьПартионныйУчет(Отказ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПараметрыУчета = БухгалтерскийУчетСервер.ОпределитьПараметрыУчета();
	Если НЕ ПараметрыУчета.ВестиПартионныйУчет Тогда
		ПараметрыУчета.ВестиПартионныйУчет = Истина;
		ДополнительныеПараметры = ОбщегоНазначенияБПКлиентСервер.ПолучитьСтруктуруДополнительныхПараметров();
		ДополнительныеПараметры.УчетТМЗ = Истина;
		ОбщегоНазначенияБПСервер.ПрименитьПараметрыУчета(ПараметрыУчета, ДополнительныеПараметры,, Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли