#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Процедура - Заполнить по умолчанию
// Выполняет первоночальное заполнение
Процедура ЗаполнитьПоУмолчанию() Экспорт 
	ОсновнаяОрганизация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");

	СправочникМенеджер = Справочники.СтавкиНСП;
	
	КлассификаторXML = РегистрыСведений.СтавкиНСП.ПолучитьМакет("МакетЗаполнения").ПолучитьТекст();
	КлассификаторТаблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;
	
	НаборЗаписей = РегистрыСведений.СтавкиНСП.СоздатьНаборЗаписей();
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();                 
	
	Для Каждого СтрокаТаблицыЗначений Из КлассификаторТаблица Цикл
		Период = Дата(СокрЛП(СтрокаТаблицыЗначений.Период));
		Ставка = Число(СокрЛП(СтрокаТаблицыЗначений.Ставка));
		СтавкаНСП = СправочникМенеджер[СокрЛП(СтрокаТаблицыЗначений.СтавкаНСП)];
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Период = Период;
		НоваяЗапись.Организация = ОсновнаяОрганизация;
		НоваяЗапись.СтавкаНСП = СтавкаНСП;
		НоваяЗапись.Ставка = Ставка;
	КонецЦикла;
	
	Попытка
		НаборЗаписей.Записать();
	Исключение
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось завершить запись ставок НСП по причине: %1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка'"), УровеньЖурналаРегистрации.Ошибка,,, ТекстСообщения);
	КонецПопытки;
КонецПроцедуры // ЗаполнитьПоУмолчанию()	

#КонецОбласти

#КонецЕсли